# Definition of the class which handles revision control using RCS.
#
# Authors: Joel King and Edward A. Lee
#
# Version: $Id$
#
# Copyright (c) 1990-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

option add *RevControlRCS.versionColor [ptkColor blue black] widgetDefault
option add *RevControlRCS.commentColor [ptkColor firebrick black] widgetDefault

#######################################################################
#### RevControlRCS
# This class defines how revision control will be handled when using 
# RCS, and is intended to be contained within objects that use such 
# facilities.
class ::tycho::RevControlRCS {
    inherit ::tycho::RevControl

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                      public methods                       ####

    #### checkIn
    # Checkin the file associated with this class.
    method checkIn {}

    #### checkOut
    # Checkout the file associated with this class.
    method checkOut {}

    #### getRlog
    # Get rlog information about the current file and check for the 
    # existance of the locks field.
    method getRlog {}

    #### initializeRevControl
    # Put the file under revision control.
    method initializeRevControl {}

    #### retrieveVersion
    # Get the specified version and open the file.
    method retrieveVersion {version}

    #### unedit
    # Unedit the file associated with this class.
    method unedit {}

    #### viewHistory
    # View or close the version control history.
    method viewHistory {}

    ###################################################################
    ####                   protected methods                       ####

    #### getComments
    # Get the users comments.
    protected method getComments {} 

    #### isCheckedOut
    # Return 1 if the file is checked out, 0 otherwise.
    protected method isCheckedOut {}

    #### isUnderRevControl
    # Return 1 if the file is under revision control, 0 otherwise.
    protected method isUnderRevControl {}

    ###################################################################
    ####                   private methods                         ####

    #### parseHistory
    # Parse the history information and insert a synopsis into the text window.
    private method parseHistory {history text}

    #### updateHistory
    # Update the history information, if it is on display.
    private method updateHistory {}
}

###################################################################
# constructor
#
body ::tycho::RevControlRCS::constructor {args} {
    eval itk_initialize $args 
    configure -text "RCS revision control for:\n\n$file"
}

#####################################################################
#### checkIn
# Check in the file associated with this class.
# 
body ::tycho::RevControlRCS::checkIn {} {
    ::tycho::RevControl::checkIn 
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    set scanout [getRlog]
    regexp -nocase {([a-z]+):} $scanout garbage lockowner
    if {[info exists lockowner]} {
        catch {exec whoami} currentuser
        if {$currentuser != $lockowner} {
	    error "The file \"$basefile\" is checked out by: $lockowner."
        }
    } else {
        error "The file \"$basefile\" is not checked out."
    }
    cdDir $dir
    set comment [$this getComments]
    if {$comment == ""} {
	# cancelled
	return
    }
    set comment [format "%s\n.\n" $comment]
    catch {exec echo $comment | ci -u $basefile} output
    set scanout [getRlog]
    if {[regexp -nocase {([a-z]+):} $scanout]} {
	error "Checkin failed: $output"
    }
    cd $olddir
    $object reload
    enableButton checkout

    updateHistory
}

#####################################################################
#### getRlog
# Get rlog information about the current file and check for the 
# existance of the locks field.
# 
body ::tycho::RevControlRCS::getRlog {} {
    catch {exec rlog $file} results
    set ind1 [string first "locks:" $results]
    set ind2 [string first "access list:" $results]
    set ind1 [expr $ind1 + 6]
    set ind2 [expr $ind2 - 1]
    if {$ind1 == -1 || $ind2 == -1} {
	error "An error occurred while obtaining file information with rlog: $results."
	return ""
    } else {
        return [string range $results $ind1 $ind2]
    }
}

#####################################################################
#### checkOut
# Check out the file associated with this class.
# 
body ::tycho::RevControlRCS::checkOut {} {
    ::tycho::RevControl::checkOut    
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    set scanout [getRlog]
    regexp -nocase {([a-z]+):} $scanout garbage lockowner
    if {[info exists lockowner]} {
        catch {exec whoami} currentuser
	error "The file \"$basefile\" is checked out by: $lockowner."
    } 
    cdDir $dir
    catch {exec co -l $basefile} output
    set scanout [getRlog]
    regexp -nocase {([a-z]+):} $scanout garbage lockowner
    if {![info exists lockowner]} {
	error "Checkout failed: $output."
    }
    cd $olddir
    $object reload
    enableButton checkin

    updateHistory
}

#####################################################################
#### initializeRevControl
# Put the file under revision control.
# 
body ::tycho::RevControlRCS::initializeRevControl {} {
    ::tycho::RevControl::initializeRevControl
    set dir [file dirname $file]
    set revdir [format "%s/RCS" $dir]
    set basefile [file tail $file]
    set olddir [eval pwd]
    if {![file exists $revdir]} {
	if {![file writable $dir]} {
	    error "The directory \"$revdir\" doesn't exist, and you do not\
		    have write permission to create it."
	}   
	exec mkdir $revdir
    }
    cdDir $dir
    set comment [format "First revision.\n.\n"]
    catch {exec echo $comment | ci -u $basefile} output
    if {![regexp "initial revision: 1.1" $output]} {
	error "Failed to place the file under revision\
		control: $output."
    }   
    $object reload
    cd $olddir
}

#####################################################################
#### retrieveVersion
# Retrieve the specified version and display.
# 
body ::tycho::RevControlRCS::retrieveVersion {version} {
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    cdDir $dir

    catch {exec co -p$version $file > #tycho#rcsrevcontroltempfile} output
    set fd [open #tycho#rcsrevcontroltempfile r]
    set text [read $fd]
    close $fd
    catch {exec rm -f #tycho#rcsrevcontroltempfile}
    ::tycho::File::displayData $text $file 
    cd $olddir
}

#####################################################################
#### unedit
# Unedit the file associated with this class.
# 
body ::tycho::RevControlRCS::unedit {} {
    ::tycho::RevControl::unedit
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    set scanout [getRlog]
    if {![regexp -nocase {([a-z]+):} $scanout]} {
        error "The file \"$basefile\" is not checked out."
    }
    cdDir $dir
    catch {exec rcs -u $basefile} output 
    set scanout [getRlog]
    if {[regexp -nocase {([a-z]+):} $scanout]} {
	error "Unedit failed: $output"
    }    
    catch {exec rm -f $basefile} output
    catch {exec co -u $basefile} output
    cd $olddir
    $object reload
    enableButton checkout

    updateHistory
}

#####################################################################
#### viewHistory
# View or close the version control history.
# 
body ::tycho::RevControlRCS::viewHistory {} {
    if {$file == {}} {
	error "Revision control: no file specified"
    }
    if {[info exists itk_component(history)] && \
	    [winfo exists $itk_component(history)]} {
	delete object $itk_component(history)
	return
    } {
	set dir [file dirname $file]
	set basefile [file tail $file]
	set olddir [eval pwd]
	
	if {![file exists [format "%s/RCS/$basefile,v" $dir]]} {
	    error "The file \"$file\" is not under RCS control."
	}
	cdDir $dir
	catch {exec sccs prs $basefile} history

	catch {exec rlog $file} results
	set ind1 [string first "----------------------------" $results]
	if {$ind1 == -1} {
	    # No logs yet.
	    set history ""
	} else {
            set history [string range $results $ind1 end]
	}
	# Create the display
	itk_component add history {
	    ::tycho::EditText $itk_component(hull).history -height 30
	} {
	    keep -readonly -width -background \
		    -highlightbackground -highlightcolor -selectforeground \
		    -cursor -insertbackground -textforeground -textfont \
		    -textbackground
	}
	pack $itk_component(history) -expand yes -fill both
	$itk_component(history) configure -readonly 1

	# Parse the history
	parseHistory $history [$itk_component(history) textWinName]

	# Delay or the focus command is ignored.  Tk bug?
	after 500 "focus [$itk_component(history) textWinName]"

	cd $olddir
    }
}

    ###################################################################
    ####                   protected methods                       ####

#####################################################################
#### getComments
# Open a dialog box to query the user for comments.
# 
body ::tycho::RevControlRCS::getComments {} {
    set list [DialogWindow::newModal EntryQuery [autoName .getcomment] \
	    -queries {{body {Comment:} {} 5}}  \
	    -entrywidth 60]
    set comments [lindex [lindex $list 0] 1]
    return [join [split $comments "\n"] "\\\n"]
}

#####################################################################
#### isCheckedOut
# Return 1 if the file is checked out, 0 otherwise.
# Here we assume that if the file is read-only, then it is not checked out.
# 
body ::tycho::RevControlRCS::isCheckedOut {} {
    if [$object getReadonly] {
	return 0
    } {
	return 1
    }
}

#####################################################################
#### isUnderRevControl
# Check whether the file associated with this class is under revision 
# control, and return a 1 if it is or a 0 otherwise.
# 
body ::tycho::RevControlRCS::isUnderRevControl {} {
    if {$file == {}} {
	error "Revision control: no file specified"
    }
    set dir [file dirname $file]
    set revdir [format "%s/RCS" $dir]
    set basefile [file tail $file]
    if {![file exists [format "%s/%s,v" $revdir $basefile]]} {
	return 0
    }
    return 1
}

    ###################################################################
    ####                   private methods                         ####

#####################################################################
#### parseHistory
# Parse the history information and insert a synopsis into the text window.
# 
body ::tycho::RevControlRCS::parseHistory {history text} {
    set lines [split $history "\n"]

    if {$history == ""} {
	$text insert insert "This file does not yet have any log messages."
	return
    }

    if [isCheckedOut] {
	$text insert insert "The file is currently checked out\n\n"
    }

    set commentColor \
	    [option get $itk_component(hull) commentColor RevControlRCS]
    set versionColor \
	    [option get $itk_component(hull) versionColor RevControlRCS]

    foreach line [lrange $lines 0 end] {
	if {[regexp {^date:} $line] && [regexp {^revision } $prevline]} {
	    regexp -nocase {^date: ([0-9/]+) ([0-9:]+);  author: ([a-z]*);} \
		$line junk date time user
	    set start [$text index insert]
	    $text insert insert "$version $date, checked in by $user\n"
	    $text tag add v$version $start insert
	    $text tag configure v$version -foreground \
		    $versionColor -underline 1
	    $text tag bind v$version <Double-Button-1> \
		    [code "$this retrieveVersion $version"]
	    set prevline $line
	} elseif {[regexp {^revision} $line] && 
	    $prevline == "----------------------------"} {
	    regexp {^revision ([0-9.]*)} $line junk version
	    set prevline $line
	} elseif [regexp {^----------------------------} $line] {
	    set prevline $line	    
	} elseif [regexp {^=============================================================================} $line] {
	    # we are done parsing
	} else {
	    set start [$text index insert]
	    $text insert insert "$line\n"
	    $text tag add comment $start insert
	    $text tag configure comment -foreground $commentColor
	    $text tag configure comment -lmargin1 0.5c
	    $text tag configure comment -lmargin2 0.5c
	    set prevline $line	    
	}
    }
}

#####################################################################
#### updateHistory
# Update the history information, if it is on display.
# 
body ::tycho::RevControlRCS::updateHistory {} {
    if {[info exists itk_component(history)] && \
	    [winfo exists $itk_component(history)]} {
	delete object $itk_component(history)
	viewHistory
    }
}
