#! /bin/csh -f
# script to make directories and links for an architecture
# $Id$

if (! $?ARCH ) then
	echo The environment variable ARCH must be set.
	exit 1
endif

# optmkdir -- make a directory if it does not exist
alias optmkdir \
'if ( ! -d \!:1 ) echo making directory $cwd/\!:1 ; if ( ! -d \!:1 ) mkdir \!:1'

# optlink -- make a symbolic link if it does not exist, from a file in a
# different directory to the same name in this directory.  args: dir filename

alias optlink \
'if ( ! -e \!:2 ) echo making link \!:1/\!:2 ; if ( ! -e \!:2 ) ln -s \!:1/\!:2 .'

# Make the top-level directories
optmkdir bin.$ARCH
optmkdir lib.$ARCH
optmkdir obj.$ARCH
optmkdir octtools/obj.$ARCH
optmkdir octtools/lib.$ARCH
optmkdir octtools/bin.$ARCH

# Now, make stuff under obj.$ARCH

cd obj.$ARCH
optlink ../src makefile
foreach x (domains interp ptcl kernel pigiRpc pigilib libgantt ptlang xgraph ptklib vem)
	optmkdir $x
	cd $x
	optlink ../../src/$x makefile
	if (-r ../../src/$x/make.template) then
		optlink ../../src/$x make.template
	endif
	cd ..
end

foreach x (filters)
	optmkdir $x
	cd $x
	optlink ../../src/$x Makefile
	cd ..
end

# Make subdirectories of filters
cd filters
foreach x (optfir wfir)
	optmkdir $x
	cd $x
	optlink ../../../src/filters/$x Makefile
	cd ..
end
cd ..

# Make link for xgraph bitmaps
cd xgraph
optlink ../../src/xgraph bitmaps
cd ..

# EXPDOMAINS = experimental domains
# EXPDOMAINS = capsim bdf
set EXPDOMAINS=(sproc silage cg-ddf cg56 cgc cg96 mq)
set EXPEXTRAS=(sproc/targets cg56/targets cg96/targets)

cd domains
foreach x (sdf ddf de thor bdf cg $EXPDOMAINS)
	optmkdir $x
	cd $x
	optlink ../../../src/domains/$x makefile
	foreach y (kernel stars)
		optmkdir $y
		cd $y
		optlink ../../../../src/domains/$x/$y make.template
		optlink ../../../../src/domains/$x/$y makefile
		cd ..
	end
	cd ..
end

# At this point, we are in obj.$ARCH/domains.

# create extra directories under domain-specific directories
# everything must have one slash (be two levels down)

foreach x (thor/pepp cg/targets sdf/image sdf/targets \
	sdf/loopScheduler sdf/tcltk de/tcltk cg/parScheduler thor/analyzerX11 \
	cg/dlScheduler cg/HuScheduler cg/dcScheduler cgc/targets cgc/tcltk \
	cg56/dsp cg96/dsp sdf/dsp sdf/image sdf/matrix mq/gals sdf/atm \
	de/atm $EXPEXTRAS )
	optmkdir $x
	cd $x
	optlink ../../../../src/domains/$x make.template
	optlink ../../../../src/domains/$x makefile
	cd ../..
end

# create Bitmaps for analyzerX11
cd thor/analyzerX11
optlink ../../../../src/domains/thor/analyzerX11 Bitmaps
cd ../..

# create extra subsub dirs in each domain
# everything must have two slashs (be three levels down)

foreach x ( cg56/dsp/stars cg96/dsp/stars \
	sdf/dsp/stars sdf/image/kernel sdf/image/stars sdf/tcltk/stars \
	sdf/matrix/stars \
	sdf/atm/stars \
	de/atm/stars de/atm/kernel \
	de/tcltk/stars \
	cgc/targets/main cgc/targets/cm5 cgc/tcltk/stars cgc/tcltk/targets )
	optmkdir $x
	cd $x
	optlink ../../../../../src/domains/$x make.template
	optlink ../../../../../src/domains/$x makefile
	cd ../../..
end

# Start of vem section
# Handle vem and octtools
cd ../vem

foreach x (include vem-bin.mk vem-lib.mk)
	optlink ../../src/vem	$x
end

foreach x ( Packages Xpackages attache vem)
	optmkdir $x
	cd $x
	#optlink ../../../src/vem/$x make.template
	optlink ../../../src/vem/$x makefile
	cd ..
end

foreach x ( oct2ps octmvlib)
	optmkdir $x
	cd $x
	optlink ../../../src/vem/$x make.template
	optlink ../../../src/vem/$x makefile
	cd ..
end


cd Packages

foreach x (  avl cp da errtrap fang fc gu harpoon kd lel list mm nle oct \
	octmm oh options pep port pq region st symbolic symlib tap th timer \
	tr uprintf utility vov vulcan)
	optmkdir $x
	cd $x
	optlink ../../../../src/vem/Packages/$x make.template
	optlink ../../../../src/vem/Packages/$x makefile
	cd ..
end
cd ../Xpackages
foreach x (Maxport Table Tgl dds fb iv rpc ts xg xpa)
	optmkdir $x
	cd $x
	optlink ../../../../src/vem/Xpackages/$x make.template
	optlink ../../../../src/vem/Xpackages/$x makefile
	cd ..
end
cd ../vem

optlink ../../../src/vem/vem include
optlink ../../../src/vem/vem bitmaps
foreach x (bags buffers dialogs dispatch doc drawing drs lib main \
	options patterns physical properties rpc schematic select strings \
	symbolic utility windows)
	optmkdir $x
	cd $x
	optlink ../../../../src/vem/vem/$x make.template
	optlink ../../../../src/vem/vem/$x makefile
	cd ..
end

cd lib
foreach x (Vem checkoff.bm checkoffhi.bm checkon.bm checkonhi.bm \
	folder.bm hisetDot hisetMark hiunsetDot hiunsetMark \
	newcell.bm other.bm physical.bm schematic.bm setDot setMark \
	symbolic.bm unsetDot unsetMark vem.bindings) 
	optlink ../../../../src/vem/vem/lib $x
end

cd ../../
# End of vem section

# This is for the makefile
cd ../..
touch obj.$ARCH
