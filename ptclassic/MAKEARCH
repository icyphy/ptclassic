#! /bin/csh -f
# script to make directories and links for an architecture
# $Id$

if (! $?ARCH ) then
	echo The environment variable ARCH must be set.
	exit 1
endif

# optmkdir -- make a directory if it does not exist
alias optmkdir \
'if ( ! -d \!:1 ) echo making directory $cwd/\!:1 ; if ( ! -d \!:1 ) mkdir \!:1'

# optlink -- make a symbolic link if it does not exist, from a file in a
# different directory to the same name in this directory.  args: dir filename

alias optlink \
'if ( ! -e \!:2 ) echo making link \!:1/\!:2 ; if ( ! -e \!:2 ) ln -s \!:1/\!:2 .'

# optdifflink -- make a symbolic link if it does not exist, with a different
# name

alias optdifflink \
'if ( ! -e \!:2 ) echo `pwd`: making link \!:1 \!:2 ; if ( ! -e \!:2 ) ln -s \!:1 \!:2 '

# Quiet versions of pushd and popd.
alias pushd 'pushd \!* > /dev/null'
alias popd 'popd \!* > /dev/null'

# Make the top-level directories
optmkdir bin.$ARCH
optmkdir lib.$ARCH
optmkdir obj.$ARCH
optmkdir octtools
optmkdir octtools/include
optmkdir octtools/lib.$ARCH
optmkdir octtools/bin.$ARCH

cd bin.$ARCH
optdifflink ../octtools/bin.$ARCH/attache attache
optdifflink ../octtools/bin.$ARCH/installColors installColors
optdifflink ../octtools/bin.$ARCH/masters masters
optdifflink ../octtools/bin.$ARCH/oct2ps oct2ps
optdifflink ../octtools/bin.$ARCH/octfix octfix
optdifflink ../octtools/bin.$ARCH/octls octls
optdifflink ../octtools/bin.$ARCH/vem vem
cd ..

# Now, make stuff under obj.$ARCH

cd obj.$ARCH
optlink ../src makefile

# interp used to be in the line below, as of 0.5.1, it is no longer.
foreach x (domains ptcl kernel galileo pigiRpc pigilib libgantt ptlang pxgraph ptklib octtools)
	optmkdir $x
	cd $x
	optlink ../../src/$x makefile
	if (-r ../../src/$x/make.template) then
		optlink ../../src/$x make.template
	endif
	cd ..
end


foreach x (filters utils)
	optmkdir $x
	cd $x
	optlink ../../src/$x Makefile
	cd ..
end

# Make subdirectories of filters
cd filters
foreach x (optfir wfir)
	optmkdir $x
	cd $x
	optlink ../../../src/filters/$x make.template
	optlink ../../../src/filters/$x makefile
	cd ..
end
cd ..

cd utils
foreach x (libgslider ptplay)
	optmkdir $x
	cd $x
	optlink ../../../src/utils/$x makefile
	if (-r ../../../src/utils/$x/make.template) then
		optlink ../../../src/utils/$x make.template
	endif
	cd ..
end
cd ..

# Make posix thread and PN directories.
# Currently specific to SunOS and Solaris.
if ($ARCH =~ sun?* || $ARCH =~ sol?*) then
	mkarch thread thread/posix
	mkarch domains/pn{,/kernel,/stars}
endif

# Make thread and CP directories (currently specific to sun)
if ($ARCH =~ sun?*) then
	mkarch thread/lwp
	mkarch domains/cp{,/kernel,/stars,/infopad{,/stars}}
endif

# Make link for pxgraph bitmaps
cd pxgraph
optlink ../../src/pxgraph bitmaps
cd ..

# EXPDOMAINS = experimental domains
# EXPDOMAINS = capsim bdf

# cg-ddf should be removed from any released versions of this file.
set EXPDOMAINS=(sproc silage cg-ddf cg56 cgc cg96 mq vhdlf vhdlb mdsdf hof)
set EXPEXTRAS=(cg56/targets cg96/targets silage/targets)
set ALLDOMAINS=(sdf ddf de thor bdf cg $EXPDOMAINS)

# If the tst directory is present, then populate it
if ( -d ../src/tst ) then
	optmkdir tst
	cd tst
	optlink ../../src/tst makefile
	optlink ../../src/tst makefile.tst
	optlink ../../src/tst makefile.tst2
	foreach x ($ALLDOMAINS)
		optmkdir $x
		cd $x
		optlink ../../../src/tst/$x makefile
		cd ..
	end
	cd ..
endif

cd domains
foreach x ($ALLDOMAINS)
	optmkdir $x
	cd $x
	optlink ../../../src/domains/$x makefile
	foreach y (kernel stars)
		optmkdir $y
		cd $y
		optlink ../../../../src/domains/$x/$y make.template
		optlink ../../../../src/domains/$x/$y makefile
		cd ..
	end
	cd ..
end

# At this point, we are in obj.$ARCH/domains.

# create extra directories under domain-specific directories
# everything must have one slash (be two levels down)

foreach x (thor/pepp cg/targets sdf/image sdf/targets \
	sdf/loopScheduler sdf/tcltk de/tcltk cg/parScheduler thor/analyzerX11 \
	cg/dlScheduler cg/HuScheduler cg/dcScheduler cgc/targets cgc/tcltk \
	cg56/dsp cg96/dsp \
	sdf/atm sdf/contrib sdf/dsp sdf/image sdf/matrix \
	cgc/rtlib \
	mdsdf/tcltk de/atm cg/multiScheduler $EXPEXTRAS )
	optmkdir $x
	cd $x
	optlink ../../../../src/domains/$x make.template
	optlink ../../../../src/domains/$x makefile
	cd ../..
end

# create Bitmaps for analyzerX11
cd thor/analyzerX11
optlink ../../../../src/domains/thor/analyzerX11 Bitmaps
cd ../..

# create extra subsub dirs in each domain
# everything must have two slashs (be three levels down)

foreach x ( cg56/dsp/stars cg96/dsp/stars \
	sdf/atm/kernel sdf/atm/stars sdf/contrib/stars \
	sdf/dsp/stars sdf/image/kernel sdf/image/stars sdf/tcltk/stars \
	sdf/matrix/stars \
	mdsdf/tcltk/stars \
	de/atm/stars \
	de/tcltk/stars \
	cgc/targets/main cgc/targets/cm5 cgc/tcltk/stars cgc/tcltk/targets )
	optmkdir $x
	cd $x
	optlink ../../../../../src/domains/$x make.template
	optlink ../../../../../src/domains/$x makefile
	cd ../../..
end

# Start of octtools section
cd ../octtools

foreach x (include vem-bin.mk vem-lib.mk)
	optlink ../../src/octtools	$x
end

foreach x ( Packages Xpackages attache vem)
	optmkdir $x
	cd $x
	#optlink ../../../src/octtools/$x make.template
	optlink ../../../src/octtools/$x makefile
	cd ..
end

foreach x ( installColors masters octfix octls oct2ps)
	optmkdir $x
	cd $x
	optlink ../../../src/octtools/$x make.template
	optlink ../../../src/octtools/$x makefile
	cd ..
end

# If tkoct directory exists, then make dirs and do links
if ( -d $PTOLEMY/src/octtools/tkoct ) then
	optmkdir tkoct
	cd tkoct
	optlink ../../../src/octtools/tkoct makefile
	foreach x (oct2ptcl tkgraph tkoct top)
		optmkdir $x
		cd $x
		optlink ../../../../src/octtools/tkoct/$x make.template
		optlink ../../../../src/octtools/tkoct/$x makefile
		cd ..
	end
	cd ..
endif

cd Packages

foreach x (  avl cp da errtrap fang fc gu harpoon kd lel list nle oct \
	octmm oh options pep port pq region st symbolic symlib tap th timer \
	tr uprintf utility vov vulcan)
	optmkdir $x
	cd $x
	optlink ../../../../src/octtools/Packages/$x make.template
	optlink ../../../../src/octtools/Packages/$x makefile
	cd ..
end
cd ../Xpackages
foreach x (Maxport Table Tgl dds fb iv rpc ts xg xpa)
	optmkdir $x
	cd $x
	optlink ../../../../src/octtools/Xpackages/$x make.template
	optlink ../../../../src/octtools/Xpackages/$x makefile
	cd ..
end
cd ../vem

optlink ../../../src/octtools/vem include
optlink ../../../src/octtools/vem bitmaps
foreach x (bags buffers dialogs dispatch doc drawing drs lib main \
	options patterns physical properties rpc schematic select strings \
	symbolic utility windows)
	optmkdir $x
	cd $x
	optlink ../../../../src/octtools/vem/$x make.template
	optlink ../../../../src/octtools/vem/$x makefile
	cd ..
end

cd lib
foreach x (Vem checkoff.bm checkoffhi.bm checkon.bm checkonhi.bm \
	folder.bm hisetDot hisetMark hiunsetDot hiunsetMark \
	newcell.bm other.bm physical.bm schematic.bm setDot setMark \
	symbolic.bm unsetDot unsetMark vem.bindings) 
	optlink ../../../../src/octtools/vem/lib $x
end

cd ../../
# End of octtools section

# This is for the makefile
cd ../..
touch obj.$ARCH
