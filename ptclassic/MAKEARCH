#! /bin/csh -f
# script to make directories and links for an architecture
# $Id$

if (! $?ARCH ) then
	echo The environment variable ARCH must be set.
	exit 1
endif

# Make the top-level directories
mkdir bin.$ARCH lib.$ARCH obj.$ARCH

# Now, make stuff under obj.$ARCH

cd obj.$ARCH
ln -s ../src/makefile .
foreach x (domains interp kernel pigiRpc pigilib tests preproc)
	mkdir $x
	cd $x
	ln -s ../../src/$x/makefile .
	if (-r ../../src/$x/make.template) ln -s ../../src/$x/make.template .
	cd ..
end

foreach x (xgraph filters)
	mkdir $x
	cd $x
	ln -s ../../src/$x/Makefile .
	cd ..
end

# Make subdirectories of filters
cd filters
foreach x (optfir wfir)
	mkdir $x
	cd $x
	ln -s ../../../src/filters/$x/Makefile .
	cd ..
end
cd ..

# EXPDOMAINS = experimental domains
# EXPDOMAINS = capsim bdf
set EXPDOMAINS=cg

cd domains
foreach x (sdf ddf de thor $EXPDOMAINS)
	mkdir $x
	cd $x
	ln -s ../../../src/domains/$x/makefile .
	foreach y (kernel stars)
		mkdir $y
		cd $y
		ln -s ../../../../src/domains/$x/$y/{make.template,makefile} .
		cd ..
	end
	cd ..
end

# At this point, we are in obj.$ARCH/domains.

#make subdirectories of cg.
mkdir cg; cd cg
foreach x (loopScheduler gilsScheduler topologies)
    mkdir $x
    cd $x
    ln -s ../../../../src/domains/cg/$x/{make.template,makefile} .
    cd ..
end
cd ..

# back in obj.$ARCH/domains

# special case: thor/pepp
mkdir thor/pepp
cd thor/pepp
ln -s ../../../../src/domains/thor/pepp/{make.template,makefile} .
cd ../..

#special case: cg/architectures
mkdir cg/architectures
cd cg/architectures
ln -s ../../../../src/domains/cg/architectures/{make.template,makefile} .
cd ../..

#special case: sdf/expgraph
mkdir sdf/expgraph
cd sdf/expgraph
ln -s ../../../../src/domains/sdf/expgraph/{make.template,makefile} .
cd ../..

# This is for the makefile
cd ../..
touch obj.$ARCH
