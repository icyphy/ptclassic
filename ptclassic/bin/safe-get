#! /bin/csh -f
# safe-get: arg is the name of the SCCS file
# always does cd to proper place before doing the get.
# This is invoked by Gnu make because the makefiles
# set the variable $GET to equal "safe-get".
# Note that Gnu make adds a "-G filename" option
# to the end, so typical usage is:
# 	safe-get SCCS/s.filename -G filename
#
# CAVEAT: This assumes the filename to be got is the first
# argument.  This may not be the case.  The first argument
# may be an option to pass to get.  Any options that come
# after the filename are discarded.
#
# $Id$

set fullname = $1
# get is in /usr/sccs on some systems
if (-r /usr/sccs/get) set path = (/usr/sccs $path)

if (! -r $fullname) then
	echo safe-get: no such file: $fullname
	exit 1
endif

# remove the trailing pathname component, leaving the head
set sccsdir = $fullname:h

# use the tail of the pathname only, I.e., the filename
set root = $fullname:t

if ("$sccsdir" == "SCCS") then
	echo "get SCCS/$root"
else
	set dir = $sccsdir:h
	echo "cd $dir; get SCCS/$root"
	cd $dir
endif

get SCCS/$root

