#!/bin/csh -f

# $Id$
# Print index for Ptolemy Almagest.

if ($#argv != 1) then
	echo "usage: $0 index_file"
	exit 1
endif

# On interrupt, remove temporary files
onintr catch

set dircmds = ~ptolemy/doc/bin/
set dirheader = ~ptolemy/doc/headers/

# Cull out the interesting entries from the input file, sort them,
# and then change spaces into |'s so subsequent awk script can
# do the right thing with them.
egrep index $1 | \
	sort -fd +2 +1.2 -2n | \
	sed -e 's/ /\|/' -e 's/ /\|/' \
	> /tmp/ptc$$

# Format the index.
awk -f ${dircmds}awk.index /tmp/ptc$$ > /tmp/ptinf$$

# Print it.
ditroff -me -P$PRINTER ${dirheader}index /tmp/ptinf$$
# (for previewing)
#ditroff -me -t -P$PRINTER ${dirheader}index /tmp/ptinf$$ | xditview -

/bin/rm /tmp/ptc$$ /tmp/ptinf$$
exit 0

catch:
	# prevent the "no match" error message if there are no temporary files
	set nonomatch
	/bin/rm /tmp/ptc$$ /tmp/ptinf$$
	exit 1

