#! /bin/csh -f
# $Id$

# Print documentation for a Ptolemy domain

# Command uses printer defined by environmental variable $PRINTER
# User .cshrc file must contain "setenv PRINTER whatever"

# The optional argument is the directory of the domain documentation
# The -index option generates an index only, printing nothing.
# An implicit header file, ~ptolemy/doc/headers/shared, is prepended.

# The following files are assumed to present in the documentation directory:
#	overall	  - The main documentation section.
#		    It may source other files with .so.
#	startitle - The title page for the stars section.
#	stars	  - A directory with star documentation
#	demotitle - The title page for the demo section
#	demos	  - A directory with demo documentation.

# Index information is appended to the file $HOME/Almagest.index,
# or to whatever ALAMAGEST_INDEX is set to.

# On interrupt, remove temporary files
onintr catch

# File structure information:
set dirheader = ~ptolemy/doc/headers
set printcommand = ditroff
set printoptions = "-me -tbl -eqn -P$PRINTER"
set contentscmd = ~ptolemy/doc/bin/contents
set indexonly = 0

if (!($?ALMAGEST_INDEX)) then
	setenv ALMAGEST_INDEX $HOME/Almagest.index
endif

while ($#argv)
	switch ($argv[1])
		case -index:
			set indexonly = 1
			shift
			breaksw
		case -*:
			set printoptions = ($printoptions $argv[1])
			shift
			breaksw
		default:
			cd $1
			shift
			breaksw
	endsw
end

soelim $dirheader/shared $dirheader/domain \
	init overall > /tmp/pt$$
if ( -d stars ) then
	if (-e starstitle) then
		# set type for this section
		echo '.ds TY "star' >> /tmp/pt$$
		cat starstitle >> /tmp/pt$$
	endif
	cd stars
	# print all stars
	cat -s * >> /tmp/pt$$
	cd ..
endif
if ( -d demos ) then
	if (-e demotitle) then
		# set type for this section
		echo '.ds TY "demo' >> /tmp/pt$$
		cat demotitle >> /tmp/pt$$
	endif
	cd demos
	# print all demos
	cat -s * >> /tmp/pt$$
	cd ..
endif

if ($indexonly) then
	# if only generating index, add -t option and throw away the output
	($printcommand $printoptions -t /tmp/pt$$ > /dev/null) >& /tmp/index$$
else
	$printcommand $printoptions /tmp/pt$$ >& /tmp/index$$
	$contentscmd /tmp/index$$
	$printcommand $printoptions $dirheader/shared title
endif
cat /tmp/index$$ >>! $ALMAGEST_INDEX
/bin/rm /tmp/pt$$ /tmp/index$$
exit 0

catch:
        # prevent the "no match" error message if there are no temporary files
        set nonomatch
        /bin/rm /tmp/pt$$ /tmp/index$$
        exit 1
