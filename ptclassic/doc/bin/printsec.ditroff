#! /bin/csh -f
# $Id$

# Command uses printer defined by environmental variable $PRINTER
# User .cshrc file must contain "setenv PRINTER whatever"

# The argument(s) are the files to be printed.
# An implicit header file, $PTOLEMY/doc/headers/shared, is prepended.

# Index information is appended to the file $HOME/Almagest.index,
# or to whatever ALAMAGEST_INDEX is set to.

if ($#argv == 0) then
	echo "Usage: printsec [-options] files"
	exit 0
endif

# On interrupt, remove temporary files
onintr catch

if ( ! $?PTOLEMY ) setenv PTOLEMY ~ptolemy

# File structure information:
set dirheader = $PTOLEMY/doc/headers
set printcommand = ditroff
set printoptions = "-me -tbl -eqn -P$PRINTER"
set contentscmd = $PTOLEMY/doc/bin/contents
set files = ""

if (!($?ALMAGEST_INDEX)) then
	setenv ALMAGEST_INDEX $HOME/Almagest.index
endif

#make sure permissions are OK
umask 2

while ($#argv)
	switch ($argv[1])
		case -*:
			set printoptions = ($printoptions $argv[1])
			shift
			breaksw
		default:
			set files = ($files $argv[1])
			shift
			breaksw
	endsw
end

soelim $dirheader/shared $files > /tmp/pt$$
$printcommand $printoptions /tmp/pt$$ >& /tmp/index$$
$contentscmd /tmp/index$$
cat /tmp/index$$ >>! $ALMAGEST_INDEX
/bin/rm /tmp/pt$$ /tmp/index$$
exit 0

catch:
        # prevent the "no match" error message if there are no temporary files
        set nonomatch
        /bin/rm /tmp/pt$$ /tmp/index$$
        exit 1
