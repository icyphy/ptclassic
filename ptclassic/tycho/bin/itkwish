#! /bin/csh -f
# This script is used to run itkwish from prebuilt binaries
# Author: Christopher Hylands
# Version: $Id$
# Copyright (c) 1996 The Regents of the University of California.
# 	All Rights Reserved.

# PTOLEMY and PTARCH are part of the Ptolemy project,  If you don't
# have Ptolemy installed, you can comment out the two lines below and
# then set $PTARCH by hand to either:
# Solaris2.4
# setenv PTARCH sol2
# SunOS4.1.3
# setenv PTARCH sun4
# HPUX10.01
# setenv PTARCH hppa
if (! $?PTOLEMY) setenv PTOLEMY /usr/eesww/share/ptolemy
if (! $?PTARCH) setenv PTARCH ` $PTOLEMY/bin/ptarch`

# ITCLDIR should be the location of the untar'd itcl files
if (! $?ITCLDIR) setenv ITCLDIR /usr/eesww/share/tycho0.1.foo

# TYCHO should be the location of the tycho distribution
if (! $?TYCHO) setenv TYCHO /usr/eesww/share/tycho0.1/tycho0.1

if ( -f $ITCLDIR/itcl/lib/itcl/itcl/itcl.tcl ) then
	if ($PTARCH =~ hppa?*) then
		if ($?SHLIB_PATH) then
			setenv SHLIB_PATH $ITCLDIR/itcl.$PTARCH/lib/itcl:${SHLIB_PATH}
		else
			setenv SHLIB_PATH $ITCLDIR/itcl.$PTARCH/lib/itcl
		endif
	else
		if ($?LD_LIBRARY_PATH) then
			setenv LD_LIBRARY_PATH $ITCLDIR/itcl.$PTARCH/lib/itcl:${LD_LIBRARY_PATH}
		else
			setenv LD_LIBRARY_PATH $ITCLDIR/itcl.$PTARCH/lib/itcl
		endif
	endif

	# Itcl2.1
	setenv TCL_LIBRARY $ITCLDIR/itcl/lib/itcl/tcl
	setenv TK_LIBRARY $ITCLDIR/itcl/lib/itcl/tk
	setenv ITCL_LIBRARY $ITCLDIR/itcl/lib/itcl/itcl
	setenv ITK_LIBRARY $ITCLDIR/itcl/lib/itcl/itk
	setenv IWIDGETS_LIBRARY $ITCLDIR/itcl/lib/itcl/iwidgets

	$ITCLDIR/itcl.$PTARCH/bin/itkwish $*
else
	if ( -f $PTOLEMY/tcltk/tcl/lib/tcl/init.tcl) then
		# Itcl2.0, which was shipped with Ptolemy0.6
		setenv TCL_LIBRARY $PTOLEMY/tcltk/tcl/lib/tcl
		setenv TK_LIBRARY $PTOLEMY/tcltk/tk/lib/tk
		setenv ITCL_LIBRARY $PTOLEMY/tcltk/itcl/lib/itcl
		setenv ITK_LIBRARY $PTOLEMY/tcltk/itcl/lib/itk
		setenv IWIDGETS_LIBRARY $PTOLEMY/tcltk/itcl/lib/iwidgets
	endif
	$PTOLEMY/bin.$PTARCH/itkwish $*
endif


