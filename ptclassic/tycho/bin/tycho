#! /bin/csh -f
# tycho - Ptolemy Tycho Brahe Graphical Interface
#
# Author: Edward Lee
# Version: $Id$
#
# Copyright (c) %Q% The Regents of the University of California.
# All rights reserved.
# See the file $PTOLEMY/copyright for copyright notice,
# limitation of liability, and disclaimer of warranty provisions.

# usage: tycho [-debug] [-ptiny] [script_file]

# If 'tycho.ptiny' is a link to this script, then we just use tycho.ptiny
set progname = `basename $0`

if ( ! $?PTOLEMY ) setenv PTOLEMY ~ptolemy
if ( ! $?PTARCH ) then
    setenv PTARCH `$PTOLEMY/bin/arch`
endif
if ( ! $?USER ) then
    setenv USER $LOGNAME
endif

# FIXME: Hardwired in the beta version of Tcl/Tk
# See also $PTOLEMY/src/tycho/kernel/make.template
#	   $PTOLEMY/tycho/kernel/Lib.tcl
setenv TCL_LIBRARY $PTOLEMY/tcltk/tcl7.4/lib/tcl7.4
setenv TK_LIBRARY $PTOLEMY/tcltk/tk4.0/lib/tk4.0

# Not all shells and platforms set $PWD, so we use our own
setenv PTPWD `pwd`

# To get tcl/tk man pages before corresponding unix man pages
setenv MANPATH $PTOLEMY/tcltk/tk/man:$PTOLEMY/tcltk/tcl/man:$MANPATH

while ($#argv)
	switch ($argv[1])
		case -help:
			echo "usage: tycho [-debug] [-ptiny] [script_file]"
			exit 0
		case -debug:
			set tychodebug
			breaksw
		case -ptiny:
			set tychoptiny
			breaksw
		case -*:
			echo Bad option: $argv[1]
			echo "usage: tycho [-debug] [-ptiny] [script_file]"
			exit 1
			breaksw
		case *:
			set script=$1
			breaksw
	endsw
	shift
end

if ( ! $?DISPLAY ) then
    echo "${0}: Your DISPLAY environment variable must be set"
    exit 1
endif

# For now, look for the executable in obj.$PTARCH, rather than bin.$ARCH.
set bindir = $PTOLEMY/obj.$PTARCH

if ( "$progname" == "tycho.ptiny" || $?tychoptiny ) then
    set prog = $PTOLEMY/obj.$PTARCH/tycho/kernel/tycho.ptiny
else
    set prog = $PTOLEMY/obj.$PTARCH/tycho/kernel/tycho
endif

if ( $?tychodebug ) then
    set prog = $prog.debug
endif

if ( "$1" =~ "-*" ) then
    echo "${0}: Bad option: $1"
    exit 1
endif

# FIXME: Should check starting palette/facet for validity.
# FIXME: Should make the starting facet if needed.

set path = ( $PTOLEMY/bin.$PTARCH $PTOLEMY/bin $path )

if (! -x $prog) then
    echo "Program does not exist: $prog"
else
    $prog
endif
