##########################################################################
# Definition of a scrollable slate view
# 
# Author: Farhana Sheikh
# Version: @(#)SlateView.itcl	1.1 6/10/96
# 
# Copyright (c)  The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
#
#                                                COPYRIGHTENDKEY

# KNOWN BUGS:
#
# 1. If the window is resized from the left, the user is can scroll
#    to negative coordinates.  Need a fix in scrollSlate method.

# LIST OF FEATURES TO BE ADDED:
#


if {[info class ::tycho::SlateView] != ""} {
    delete class ::tycho::SlateView
}

##########################################################################
#### SlateView
# This class defines a class for a scrollable slate.  It inherits from the
# the abstract View class.
#
# FIXME: Fill in the documentation here
#
# A Help menu contains pointers to key Tycho documentation
# <p>
# Here is an example:
# <tcl><pre>
# tycho::Displayer .d
# tycho::SlateView .d.sv
# .d.sv insertInDisplay 
# wm deiconify .d
# </pre></tcl>

# DEFAULT RESOURCES:
#
option add *SlateView.scrollMargin 2 widgetDefault
option add *SlateView.vscrollMode static widgetDefault
option add *SlateView.hscrollMode static widgetDefault
option add *SlateView.width 200 widgetDefault
option add *SlateView.height 230 widgetDefault
option add *SlateView.autoResize 1 widgetDefault

class ::tycho::SlateView {
    inherit ::tycho::View

    constructor {args} {}
    destructor {}

    #####################################################################
    ####                          options                            ####
    
    itk_option define -scrollmargin scrollMargin ScrollMargin 2
    itk_option define -vscrollmode vscrollMode VScrollMode static
    itk_option define -hscrollmode hscrollMode HScrollMode static
    itk_option define -width width Width 30
    itk_option define -height height Height 30 
    itk_option define -autoresize autoResize AutoResize 1

    #####################################################################
    ####                        public methods                       ####

    # methods to manipulate subviews
    
    # methods to manipulate my menus in my displayer
    public method addMyMenus {}

    # Methods to operate on a slate
    public method childSite {} {return $childsite}
 
    # methods to manipulate my menus in my displayer
    public method insertInDisplay {}
 
    # Methods specific to slate Menus
    # print
    # save
    # help
    # zoom
    # redraw
    # exit
    # 

    public method scMapEventHandler {}
    public method scrollSlate {wid first last}

    #####################################################################
    ####                        slate methods                        ####

    # Refer to slate documentation for details
    # The following methods are wrappers around the slate methods

    public method addtag {args} {
	eval "$itk_component(slateview) addtag $args"
    }

    public method bbox {args} {
	return [eval "$itk_component(slateview) bbox $args"]
    }

    public method bind {args} {
	return [eval "$itk_component(slateview) bind $args"]
    }

    public method create {args} {
	return [eval "$itk_component(slateview) create $args"]
	update idletasks
	$this configure -autoresize $itk_option(-autoresize)
    }

    public method delete {args} {
	eval "$itk_component(slateview) delete $args"]
	update idletasks
	$this configure -autoresize $itk_option(-autoresize)
    }

    public method find {args} {
	return [eval "$itk_component(slateview) find $args"]
    }

    public method lower {args} {
	eval "$itk_component(slateview) lower $args"
    }

    public method move {args} {
	eval "$itk_component(slateview) move $args"
	update idletasks
	$this configure -autoresize $itk_option(-autoresize)
     }

    public method raise {args} {
	eval "$itk_component(slateview) raise $args"
    }

    public method roots {args} {
	return [eval "$itk_component(slateview) roots $args"]
    }

    public method xview {args} {
	eval "$itk_component(slateview) xview $args"
    }

    public method yview {args} {
	eval "$itk_component(slateview) yview $args"
    }

    #####################################################################
    ####                  protected and private methods              ####

    private method fillerWidth {} {return $width}
    private method verticalScrollbarDisplay {mode} 
    private method horizontalScrollbarDisplay {mode}
    
    #####################################################################
    ####                       private variables                     ####

}

# -----------------------------------------------------------------------
#                        CONSTRUCTOR
# -----------------------------------------------------------------------
body ::tycho::SlateView::constructor {args} {
    
    component hull configure -borderwidth 0
    pack propagate $itk_component(hull) no

    # create frames to hold the scrollbars and the slate
    itk_component add main {
	frame $itk_interior.main
    } {
	keep -background -cursor -width -height
    }
    pack $itk_component(main) -fill both -expand true
    
    # create frame to hold slate and vertical scrollbar
    itk_component add top {
	frame $itk_component(main).top
    } {
	keep -background -cursor
    }
    pack $itk_component(top) -fill both -expand true

    # frame for horizontal margin between scrolled area and scrollbar
    itk_component add hmargin {
	frame $itk_component(main).hmargin
    } {
	keep -background -cursor
    }
    pack $itk_component(hmargin)
    
    # frame for horizontal scrollbar
    itk_component add bottom {
	frame $itk_component(main).bottom
    } {
	keep -background -cursor
    }
    pack $itk_component(bottom) -fill x 

    # create a basic slate
    itk_component add slateview {
	slate $itk_component(top).slateview  \
		-height 30 \
		-width 30 \
		-scrollregion "0 0 30 30" \
		-borderwidth 2 \
		-relief raised \
		-yscrollcommand \
		"$this scrollSlate $itk_component(top).vsb" \
		-xscrollcommand \
		"$this scrollSlate $itk_component(bottom).hsb"
    } {
    }
    pack $itk_component(slateview) -expand true -fill both -side left
    set itk_interior $itk_component(slateview)

    # create frame for vertical margin between scrolled area and 
    # scrollbar
    itk_component add vmargin {
	frame $itk_component(top).vmargin
    } {
	keep -background -cursor
    }
    pack $itk_component(vmargin) -side left

    # create vertical scrollbar
    itk_component add vsb {
	scrollbar $itk_component(top).vsb \
		-orient vertical \
		-command "$itk_component(slateview) yview"
    } {
	keep -activebackground -activerelief -background \
		-borderwidth -cursor -highlightcolor -highlightthickness \
		-relief -repeatdelay -repeatinterval -troughcolor
	rename -width -sbwidth sbWidth SbWidth
    }
    pack $itk_component(vsb) -side right -fill y

    # create horizontal scrollbar
    itk_component add hsb {
	scrollbar $itk_component(bottom).hsb \
		-orient horizontal \
		-command "$itk_component(slateview) xview"
    } {
	keep -activebackground -activerelief -background \
		-borderwidth -cursor -highlightcolor -highlightthickness \
		-relief -repeatdelay -repeatinterval -troughcolor
	rename -width -sbwidth sbWidth SbWidth
    }
    pack $itk_component(hsb) -side left -fill x -expand true

    # create filler frame and figure out its width
    itk_component add filler {
	frame $itk_component(bottom).filler -width [fillerWidth]
    } {
	keep -background -cursor
    }
    pack $itk_component(filler) -side right

    ::bind $itk_component(hull) <Map> "$this scMapEventHandler"
    ::bind $itk_component(hull) <Configure> "$this scMapEventHandler"

    # evaluate specified options
    eval itk_initialize $args
}

# -----------------------------------------------------------------------
#                        DESTRUCTOR
# -----------------------------------------------------------------------
body ::tycho::SlateView::destructor {} {
}

# ------------------------------------------------------------------
#                             OPTIONS
# ------------------------------------------------------------------
# --------------------------------------------------------------
# OPTION: -scrollmargin
#
# Size of margin between scrolled region and scrollbar 
# --------------------------------------------------------------
configbody ::tycho::SlateView::scrollmargin {

    # Set the distance between the slate and the scrollbars
    set pixels [winfo pixels $itk_component(hmargin) \
	    $itk_option(-scrollmargin)]
    set itk_option(-scrollmargin) $pixels

    # if the horizontal scrollbar is mapped then set the margin
    if {[winfo ismapped $itk_component(hsb)]} {
	$itk_component(hmargin) configure -width 1 -height $pixels
    }

    # if the vertical scrollbar is mapped then set the margin
    if {[winfo ismapped $itk_component(vsb)]} {
	$itk_component(vmargin) configure -width $pixels -height 1
    }

    # Set the filler size appropriately
    $itk_component(filler) configure -width [fillerWidth] -height 1
}

# --------------------------------------------------------------
# OPTION: -vscrollmode
#
# Enable or disable the display and change the mode of the 
# vertical scrollbar 
# --------------------------------------------------------------
configbody ::tycho::SlateView::vscrollmode {
    
    # Check mode and display scrollbar accordingly
    switch $itk_option(-vscrollmode) {
	static {
	    verticalScrollbarDisplay on
	}
	
	dynamic -
	none {
	    verticalScrollbarDisplay off
	}
	
	default {
	    error "Bad vscrolloption \"$itk_option(-vscrollmode)\": \
		    should be static, dynamic, or none."
	}
    }
	    
}

# --------------------------------------------------------------
# OPTION: -hscrollmode
#
# Enable or disable the display and change the mode of the 
# horizontal scrollbar 
# --------------------------------------------------------------
configbody ::tycho::SlateView::hscrollmode {

    # Check mode and display scrollbar accordingly
    switch $itk_option(-hscrollmode) {
	static {
	    horizontalScrollbarDisplay on
	}
	dynamic -
	none {
	    horizontalScrollbarDisplay off
	}
	default {
	    error "Bad hscrolloption \"$itk_option(-hscrollmode)\": \
		    should be static, dynamic, or none."
	}
    }
}

# --------------------------------------------------------------
# OPTION: -width
#
# Set the width of scrolled slate
# --------------------------------------------------------------
configbody ::tycho::SlateView::width {

    # get the width specified width and set it
    set pixels [winfo pixels $itk_component(hull) $itk_option(-width)]
    component hull configure -width $pixels
}

# --------------------------------------------------------------
# OPTION: -height
#
# Set the height of scrolled slate
# --------------------------------------------------------------
configbody ::tycho::SlateView::height {

    # get the height specified height and set it
    set pixels [winfo pixels $itk_component(hull) $itk_option(-height)]
    component hull configure -height $pixels
}

# --------------------------------------------------------------
# OPTION: -autoresize
#
# Automatically adjusts the scrolled region to be the bounding
# box covering all the items in the slate.  As new items are
# added or removed, the scrollbars adjust accordingly.
# --------------------------------------------------------------
configbody ::tycho::SlateView::autoresize {

    # if the slate is mapped and autoresizing is on, then
    # cover all items in the bounding box.
    if {[winfo ismapped $itk_component(hull)]} {
	if {$itk_option(-autoresize)} {
	    set slatebbox [$itk_component(slateview) bbox all]
	    if {$slatebbox == {}} {
		set swidth [winfo width $itk_component(slateview)]
		set sheight [winfo height $itk_component(slateview)]
	    } else {
		if {[lindex $slatebbox 2] > \
			[winfo width $itk_component(slateview)]} {
		    set swidth [lindex $slatebbox 2]
		} else {
		    set swidth [winfo width $itk_component(slateview)]
		}
		if {[lindex $slatebbox 3] > \
			[winfo height $itk_component(slateview)]} {
		    set sheight [lindex $slatebbox 3]
		} else {
		    set sheight [winfo height $itk_component(slateview)]
		}
	    }
	    $itk_component(slateview) configure \
		    -scrollregion "0 0 $swidth $sheight"
	}
    }
}


# -----------------------------------------------------------------------
#                        PUBLIC METHODS
# -----------------------------------------------------------------------

# -----------------------------------------------------------------------
# METHOD: childSite {}
#
# RETURNS: the path name of the child site widget
# -----------------------------------------------------------------------
body ::tycho::SlateView::childSite {} {

    return $itk_component(slateview)
}

# -------------------------------------------------------------------
# METHOD: insertInDisplay
#
# Insert view in the display
# -------------------------------------------------------------------
body ::tycho::SlateView::insertInDisplay {} {
    
   # insert the view into the displayer
   $myDisplayer setView $itk_component(hull)
   # FIXME: this is a temporary fix to get the scrollbars to adjust
   #        properly to the size of the slate once it is packed into
   #        the displayer.  Not sure why this is required.
   update idletasks
   $this scMapEventHandler
}

# -----------------------------------------------------------------------
# METHOD: addMyMenus {}
#
# Add menus specific to a slate
# -----------------------------------------------------------------------
body ::tycho::SlateView::addMyMenus {} {

    # First add commands to the existing File menu which is standard on
    # the displayer
    $myDisplayer addMenuItem {Open} File \
	    -command {error "Sorry not implemented yet"} \
	    -state normal 
    $myDisplayer addMenuItem {Save} File \
	    -command {error "Sorry not implemented yet"} \
	    -state normal 
    $myDisplayer addMenuItem {Save As...} File \
	    -command {error "Sorry not implemented yet"} \
	    -state normal 
    $myDisplayer addMenuItem {Close} File \
	    -command {error "Sorry not implemented yet"} \
	    -state normal
    $myDisplayer addMenuItem {Quit} File \
	    -command "$myDisplayer nextWindow; delete object $myDisplayer" \
	    -state normal 
}

# -----------------------------------------------------------------------
# METHOD: scMapEventHandler {}
#
# Performs the necessary operations following a map even, including
# configuring the scroll margin appropriately.
# -----------------------------------------------------------------------
body ::tycho::SlateView::scMapEventHandler {} {
    
    configure -vscrollmode $itk_option(-vscrollmode)
    configure -hscrollmode $itk_option(-hscrollmode)
    configure -scrollmargin $itk_option(-scrollmargin)
    configure -autoresize $itk_option(-autoresize)
}

# -----------------------------------------------------------------------
# METHOD: scrollSlate {}
#
# ARGUMENTS: 
# wid: widget id - either vsb or hsb
# first: first automatic scroll parameter 
# last: last automatic scroll parameter
#
# Performs scrolling and display of scrollbars based on total and
# maximum frame size as well as the current -vscrollmode and 
# -hscrollmode settings. 
# -----------------------------------------------------------------------
body ::tycho::SlateView::scrollSlate {wid first last} {

    # Set the scrollbar parameters
    puts "scrollSlate: $first $last"
    $wid set $first $last

    if {$wid == $itk_component(vsb)} {
	if {$itk_option(-vscrollmode) == "dynamic"} {
	    if {($first == 0) && ($last == 1)} {
		verticalScrollbarDisplay off
	    } else {
		verticalScrollbarDisplay on
	    }
	} elseif {$wid == $itk_component(hsb)} {
	    if {$itk_option(-hscrollmode) == "dynamic"} {
		if {($first == 0) && ($last == 1)} {
		    horizontalScrollbarDisplay off 
		} else {
		    horizontalScrollbarDisplay on
		}
	    }
	}
    }
}

# -----------------------------------------------------------------------
#                        PRIVATE METHODS
# -----------------------------------------------------------------------

# -----------------------------------------------------------------------
# METHOD: fillerWidth {}
#
# Figures out the width of the filler frame based on the vertical
# scrollbar width and the scrollmargin
# -----------------------------------------------------------------------
body ::tycho::SlateView::fillerWidth {} {

    # figure out the width and return it. 
    set width [expr [winfo reqwidth $itk_component(vsb)] + \
	    [winfo reqwidth $itk_component(vmargin)]]
    puts "fillerWidth: $width"
    return $width
}

# -----------------------------------------------------------------------
# METHOD: verticalScrollbarDisplay {}
#
# ARGUMENTS: 
# mode: on or off
#
# Enable or disable display of vertical scrollbar
# -----------------------------------------------------------------------
body ::tycho::SlateView::verticalScrollbarDisplay {mode} {

    switch $mode {
	on {
	    $itk_component(vmargin) configure \
		    -width $itk_option(-scrollmargin) \
		    -height 1
	    pack $itk_component(vsb) -side right -fill y
	    $itk_component(filler) configure \
		    -width [fillerWidth] \
		    -height 1
	}
	off {
	    pack forget $itk_component(vsb)
	    
	    $itk_component(vmargin) configure -width 1 -height 1
	    $itk_component(filler) configure -width 1 -height 1
	}
	default {
	    error "Invalid argument \"$mode\": should be on or off."
	}
    }
}

# -----------------------------------------------------------------------
# METHOD: horizontalScrollbarDisplay {}
#
# ARGUMENTS: 
# mode: on or off
#
# Enable or disable display of horizontal scrollbar
# -----------------------------------------------------------------------
body ::tycho::SlateView::horizontalScrollbarDisplay {mode} {

    switch $mode {
	on {
	    $itk_component(hmargin) configure \
		    -width $itk_option(-scrollmargin) \
		    -height 1
	    pack $itk_component(hsb) -side left -fill x -expand yes
	    $itk_component(filler) configure \
		    -width [fillerWidth] \
		    -height 1
	}
	off {
	    pack forget $itk_component(hsb)
	    
	    $itk_component(hmargin) configure -width 1 -height 1
	    $itk_component(filler) configure -width 1 -height 1
	}
	default {
	    error "Invalid argument \"$mode\": should be on or off."
	}
    }
}







