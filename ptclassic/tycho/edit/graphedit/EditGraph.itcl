##########################################################################
# Version: $Id$
# Author: John Reekie
#
# Copyright (c) 1996-1997 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
#                                                         COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### EditGraph
#
# _EditGraph_ is an editor for the _GraphLayout_ model. It edits
# directed graphs.
#
class ::tycho::EditGraph {
    inherit ::tycho::TCanvas

    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####

    # The layout model
    itk_option define -layout layout Layout {}
    
    # The graph model
    itk_option define -graph graph Graph {}
    
    #################################################################
    ####                     public methods                      ####

    # Remove an item from the selection
    method deselect {id}

    # Load a new layout
    method loadLayout {}

    # Add a vertex to the selection
    method select {id}

    # Clear the selection
    method selectionclear {}

    # Create another editor.
    method clone {}

    # Call the model
    method model {method args}

    # Handle change notification from the graph layout model
    method notify {method args}

    # Update the layout model
    method updateLayout {}

    # Make a new vertex
    method vertex {name x y}

    # Start moving a vertex
    method vertexclick {tag x y args}

    # Continue moving a vertex item
    method vertexdrag {tag x y}

    # Stop moving a vertex
    method vertexrelease {tag x y}

    # Return the vertices and edges with a given tag.
    method withtag {tag}

    #################################################################
    ####                  protected methods                      ####
    
    # Add a tag to all indicated vertices and edges.
    protected method _addtag {tag args}

    # Clear data from this editor (Note: not the viewer)
    method _clear {}

    # Remove an item from the selection
    protected method _deselect {name}

    # Delete a tag from all items.
    protected method _dtag {tag}

    # Move an item
    protected method _move {tag x y}

    # Start moving an item
    protected method _moveclick {tag x y args}

    # Continue moving an item
    protected method _movedrag {tag x y}

    # Stop moving an item
    protected method _moverelease {tag x y}

    # Reshape an item
    protected method _reshape {name x y args}

    # Start reshaping an item
    protected method _reshapeclick {name x y args}

    # Continue reshaping an item
    protected method _reshapedrag {name x y args}

    # Stop reshaping an item
    protected method _reshaperelease {name x y args}

    # Add a vertex to the selection
    protected method _select {name}

    # Clear the selection
    protected method _selectionclear {foo}

    # Add a new vertex to the view
    protected method _vertex {name coords args}

    # Configure a vertex
    protected method _vertexconfigure {name args}

    #################################################################
    ####                 protected variables                     ####
    
    # The mapping from vertex and edge names to item IDs
    protected variable _itemID

    # The mapping from item IDs to vertex and edge names
    protected variable _itemname

    #################################################################
    ####                   private methods                       ####

}

#####################################################################
#### -layout configuration
# Subscribe to the layout model. Note: there will be a problem here
# if the model is changed, since the old one is not unsubscribed...
#
configbody ::tycho::EditGraph::layout {
    if { $itk_option(-layout) != "" } {
        $itk_option(-layout) subscribe $this all
    }
    # If the graph model already exists, load the layout
    if { $itk_option(-graph) != "" } {
        loadLayout
    }
}

########################################################################
#### constructor
#
body ::tycho::EditGraph::constructor {args} {
    # Add to menus
    menubar addMenu Devel
    menubar command {Foo} Devel -command "$this vertex foo 70 70"
    menubar command {Bar} Devel -command "$this vertex bar 200 120"
    menubar command {Baz} Devel -command "$this vertex baz 100 200"

    menubar addSeparator Devel

    menubar command {Clone} Devel \
            -command "$this clone"

    menubar addSeparator Devel

    menubar command {Show layout} Devel \
	    -command "::tycho::post \[\[$this cget -layout\] describe\]"
    menubar command {Show graph} Devel \
            -command "::tycho::post \[\[$this cget -graph\] describe\]"

    # Evaluate options
    eval itk_initialize $args

    # If there is not already a graph model, create one
    if { $itk_option(-graph) == "" } {
	configure -graph \
                [uplevel #0 ::tycho::KeyedDigraph [::tycho::autoName graph]]
    }

    # If there is not already a layout model, create one
    if { $itk_option(-layout) == "" } {
	configure -layout \
                [uplevel #0 ::tycho::GraphLayout \
                [::tycho::autoName layout] \
                -primaryview $this \
                -graph $itk_option(-graph)]
    }

    # Make a more pronounced highlight
    $slate configure -highlightcolor cyan3 -highlightwidth 3

    # Enable selection with button 1; shift-click toggles
    $slate selector bind click vertex -button 1
    $slate selector bind click vertex -button 1 -modifiers shift -toggle

    # Enable drag-selection with button 1; shift-click toggles
    $slate selector bind drag "#background" -button 1
    $slate selector bind drag "#background" -button 1 -modifiers shift -toggle

    # Create the interactor to move selected items
    set follower [$slate interactor Follower]
    $slate selector delegate immediate selected $follower vertex \
	    -button 1 
    $slate selector delegate immediate selected $follower vertex \
	    -button 1 -modifiers shift

    # Now for some tricky stuff: we want the follower to send
    # the interaction to the model, rather than to the slate.
    # Give it the right "prefixes":
    $follower configure \
            -clickprefix "$this vertexclick" \
            -dragprefix "$this vertexdrag" \
            -releaseprefix "$this vertexrelease"

    # More tricky stuff: the selector sends selection events
    # to the model:
    $slate selector configure \
	    -clearprefix "$this selectionclear" \
	    -deselectprefix "$this deselect" \
	    -selectprefix "$this select"

    # Try this:
    $slate configure -exportselection 0
}

########################################################################
#### clone
#
# Create another editor -- for demos only.
#
body ::tycho::EditGraph::clone {} {
    set clone [::tycho::view EditGraph \
            -layout $itk_option(-layout) \
            -graph $itk_option(-graph)]
    $clone loadLayout
}

########################################################################
#### deselect
#
body ::tycho::EditGraph::deselect {id} {
    $itk_option(-layout) publish deselect $_itemname($id)
}

########################################################################
#### loadLayout
#
# Load a new layout into the editor.
#
body ::tycho::EditGraph::loadLayout {} {
    # Clear all data here
    _clear

    # Make sure the layout is up to date
    updateLayout

    # Get vertices and edges and create
    foreach vertex [$itk_option(-graph) vertices] {
        eval [list _vertex \
                [$itk_option(-layout) vertexget $vertex]] \
                [$itk_option(-layout) vertexget $vertex]
    }
}

########################################################################
#### model
#
# Call the model. Note that this does not call the command to
# the model via Model::publish{}, so commands that are to be published
# should explitly be called via publish{}.
#
body ::tycho::EditGraph::model {method args} {
    if { $itk_option(-layout) !=  "" } {
	eval $itk_option(-layout) $method $args
    }
}

########################################################################
#### notify
#
body ::tycho::EditGraph::notify {method args} {
    eval _$method $args
}

########################################################################
#### select
#
body ::tycho::EditGraph::select {id} {
    $itk_option(-layout) publish select $_itemname($id)
}

########################################################################
#### selectionclear
#
body ::tycho::EditGraph::selectionclear {} {
    $itk_option(-layout) publish selectionclear foo
}

########################################################################
#### updateLayout
#
#  Update the layout with current coordinates.
#
body ::tycho::EditGraph::updateLayout {} {
    foreach vertex [array names _itemID] {
        set coords [$slate coords $_itemID($vertex)]
        eval [list $itk_option(-layout) vertexcoords $vertex] $coords
    }
}

########################################################################
#### vertexclick
#
body ::tycho::EditGraph::vertex {name x y} {
    # Add a vertex to the graph
    $itk_option(-graph) publish vertex $name

    # Add a vertex to the layout model
    if { $name == "foo" } {
	$this model publish vertex foo [list IconFrame $x $y] \
		-inputs {0 50 50 0} \
		-outputs {100 50 50 100} \
		-text Foo \
                -anchor nw \
                -graphics {Frame 10 10 50 50 -color red; Frame 50 50 90 90 -color green -relief sunken} \
		-color green \
		-tags vertex \
		-intype {Terminal -type input -style blob -fill red -tags input} \
		-outtype {Terminal -type output -style doublearrow -fill blue -tags output}
    } elseif { $name == "bar" } {
	$this model publish vertex bar [list IconFrame $x $y] \
		-inputs 2 \
		-outputs 1 \
		-text Bar \
		-color lightsteelblue \
		-tags vertex \
		-intype {Terminal -type input} \
		-outtype {Terminal -type output -fill green2}
    } else {
        $this model publish vertex baz [list IconOval $x $y] \
                -inputs {0 50 50 0} \
                -outputs 1 \
                -fill green \
                -graphics {line 20 50 80 50 -width 4; \
                    line 50 20 50 80 -width 4} \
                -tags vertex \
                -intype {Terminal -type input -style blob -fill red} \
                -outtype {Terminal -type output -style doublearrow -fill blue}
    }
}

########################################################################
#### vertexclick
#
body ::tycho::EditGraph::vertexclick {tag x y args} {
    eval $itk_option(-layout) vertexclick $tag $x $y $args
}

########################################################################
#### vertexdrag
#
body ::tycho::EditGraph::vertexdrag {tag x y} {
    $itk_option(-layout) vertexdrag $tag $x $y
}

########################################################################
#### vertexrelease
#
body ::tycho::EditGraph::vertexrelease {tag x y} {
    $itk_option(-layout) vertexrelease $tag $x $y
}

########################################################################
#### withtag
#
# Return the vertice and edges with a given tag.
#
body ::tycho::EditGraph::withtag {tag} {
    set result {}
    foreach id [$slate find withtag $tag] {
	lappend result $_itemname($id)
    }
    return $result
}

###################################################################
###################################################################
####                      protected methods                    ####

########################################################################
#### _addtag
#
# Add a tag to all indicated vertices and names.
#
body ::tycho::EditGraph::_addtag {tag args} {
    foreach name $args {
	$slate addtag $tag withtag $_itemID($name)
    }
}

########################################################################
#### _clear
#
# Celar the displayed graph.
#
body ::tycho::EditGraph::_clear {} {
    catch {unset _itemname}
    catch {unset _itemID}
    $slate delete all
}

########################################################################
#### _deselect
#
# Remove an item from the selection.
#
body ::tycho::EditGraph::_deselect {name} {
    $slate select remove $_itemID($name)
}

########################################################################
#### _dtag
#
# Delete a tag from all items.
#
body ::tycho::EditGraph::_dtag {tag} {
    $slate dtag $tag
}

########################################################################
#### _move
#
body ::tycho::EditGraph::_move {tag x y} {
    $slate move $tag $x $y
}

########################################################################
#### _moveclick
#
body ::tycho::EditGraph::_moveclick {tag x y} {
    eval $slate moveclick $tag $x $y args
}

########################################################################
#### _movedrag
#
body ::tycho::EditGraph::_movedrag {tag x y} {
    $slate movedrag $tag $x $y
}

########################################################################
#### _moverelease
#
body ::tycho::EditGraph::_moverelease {tag x y} {
    $slate moverelease $tag $x $y
}

########################################################################
#### _reshape
#
body ::tycho::EditGraph::_reshape {name x y args} {
    eval $slate reshape $name $x $y $args
}

########################################################################
#### _reshapeclick
#
body ::tycho::EditGraph::_reshapeclick {name x y args} {
    eval $slate reshapeclick $name $x $y $args
}

########################################################################
#### _reshapedrag
#
body ::tycho::EditGraph::_reshapedrag {name x y args} {
    $slate reshapedrag $name $x $y
}

########################################################################
#### _reshaperelease
#
body ::tycho::EditGraph::_reshaperelease {name x y args} {
    $slate reshaperelease $name $x $y
}

########################################################################
#### _select
#
# Add an item to the selection.
#
body ::tycho::EditGraph::_select {name} {
    $slate select add $_itemID($name)
}

########################################################################
#### _selectionclear
#
# Clear the selection.
#
body ::tycho::EditGraph::_selectionclear {foo} {
    $slate select clear
}

########################################################################
#### _vertex
#
# Add a new vertex to the view. This method must be called only
# by the model.
#
body ::tycho::EditGraph::_vertex {name spec args} {
    set id [eval $slate create $spec $args]
    set _itemID($name) $id
    set _itemname($id) $name
}

########################################################################
#### _vertexconfigure
#
# Configure a vertex. This method must be called only
# by the model.
#
body ::tycho::EditGraph::_vertexconfigure {name args} {
    eval $slate itemconfigure $_itemID($name) $args
}

