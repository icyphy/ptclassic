##########################################################################
# Version: $Id$
# Author: John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABL TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### IconLibrary
#
# <b>This class is under development.</b>
#
# The _IconLibrary_ model is a hierarchical model for a library
# of "icons" for dataflow graphs and such.
#
# In addition to those in the <a href="Library.html">Library</a>
# superclass, this model has the following types:
# <ul>
# <li><b>icon</b> _name_ _type_: An icon. The _type_ is the
# actual type of Slate item that will be used to draw the icon.
# The name cannot contain spaces.
# Icons can have any attributes that are valid for that Slate
# type, as well as the following:
#   <ul>
#   <li><b>-label</b>: The label used to display the icon in
#    menu bars and the like. This is used because the icon name
#    cannot contain spaces. By default, this attribute will be
#    set to the icon name.
#   </ul>
#
# <li><b>port</b> _name_ _type_: A port of an icon. The _type_ is the
# actual type of Slate item that will be used to draw the port.
# Ports can have any attributes that are valid for that Slate
# type. Ports can only appear inside icons. The name cannot contain
# spaces.
#
# </ul>
#
# Here is an example of an icon model:
# <pre><tcl>
#     catch {delete object $icons}
#     set icons [::tycho::IconLibrary [::tycho::autoName icons]]
#     $icons parse {
#         group sample {
#             -label {Some sample icons}
#             icon Add IconOval {
#                 -fill white
#                 -graphics {
#                     line 20 50 80 50 -width 4
#                     line 50 20 50 80 -width 4
#                 }
#                 port input-0 Terminal -anchor {0 50} &#92
#                       -direction {-1 0} &#92
#                       -type input -style blob -fill red
#                 port input-1 Terminal -anchor {50 0} &#92
#                         -direction {0 -1} &#92
#                         -type input -style blob -fill red
#                 port output Terminal -anchor {100 50} &#92
#                         -direction {1 0} &#92
#                         -type output -style arrow -fill blue
#             }
#             icon Ramp IconFrame {
#                 -label {Ramp}
#                 -text Ramp
#                 -color lightgreen
#                 -graphics {
#                     polygon 20 80 80 80 80 20 -fill orange -outline ""
#                 }
#                 port output Terminal -anchor {100 50} &#92
#                         -direction {1 0} &#92
#                         -type output -fill blue
#             }
#         }
#     }
#     $icons describe
# </tcl></pre>
#
# Create a menubar and add this index to it:
# <tcl quiet><pre>
#    catch {delete object .mb}
#    ::tycho::MenuBar .mb
#    pack .mb
#    raise .
#    wm deiconify .
#    .mb addMenu Foo
#    $icons menu .mb Foo ::tycho::inform
# </pre></tcl>
#
# Create a blank slate and draw the icons on it:
# <tcl><pre>
#     source $TYCHO/editors/slate/doc/internals/newslate.itcl
#     $icons draw sample.Add $slate 100 100 -tags moveable
#     $icons draw sample.Ramp $slate 200 100 -tags moveable
# </pre></tcl>
# 
class ::tycho::IconLibrary {
    inherit ::tycho::Library

    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####


    #################################################################
    ####                     public methods                      ####

    # Draw an icon on a given slate
    method draw {icon slate x y args}

    # Add an icon
    method icon {name value args}

    # Add a port to an icon
    method port {icon name value args}
    
    # Get the Slate type of an icon
    method slatetype {name}
}

########################################################################
#### constructor
#
body ::tycho::IconLibrary::constructor {args} {
    # Evaluate options
    eval configure $args

    # The icon type
    type define icon -label ""

    # The port type
    type define port -leafonly 1 -type input
}

########################################################################
#### draw
#
# Draw an icon on a slate at the given coordinates. Additional
# argument are given to the icon item when created
#
body ::tycho::IconLibrary::draw {icon slate x y args} {
    set type  [slatetype $icon]
    set attrs [attributes icon $icon]
    set iconid [eval $slate create $type $x $y \
            $attrs $args -name $icon]

    foreach port [names port $icon] {
        set type  [slatetype $icon.$port]
        set attrs [attributes port $icon.$port]
        eval $slate call $iconid terminal \
                $icon $type 0 0 $attrs \
                -tags [list [_itemcget port $icon $port -type]]

 #       eval $slate createrootchild $iconid $type 0 0 $attrs \
#                -tags [list [_itemcget port $icon $port -type]] \
#		-name $icon.$port
    }
}

########################################################################
#### icon
#
# Add a new icon. Flag an error if the icon
# already exists. Additional arguments are the initial values
# of icon attributes. Return the inverse command. If the
# *-label* attribute is null, set it to the icon name.
# Note that is the icon is in a group, it's name is
# in the form "group.iconname".
#
body ::tycho::IconLibrary::icon {name value args} {
    set splut [split $name $separator]
    set cntxt [join [lreplace $splut end end] "."]
    set local [lindex $splut end]

    _verifynot icon $cntxt $local $name
    _addentity icon $cntxt $local $name $value $args
    if { [_itemcget icon $cntxt $local -label] == "" } {
	_itemconfigure icon $cntxt $local [list -label $local]
    }
    list delete icon $name
}

########################################################################
#### port
#
# Add a new port to an icon. Flag an error if the port
# already exists. Additional arguments are the initial values
# of port attributes. Return the inverse command.
#
body ::tycho::IconLibrary::port {icon port value args} {
    _verifynot port $icon $port $icon$separator$port
    _addentity port $icon $port $icon$separator$port $value $args
    list delete port $icon$separator$name
}

########################################################################
#### type
#
# Get the slate type of an icon. Flag an error if the icon does
# not exist.
#
body ::tycho::IconLibrary::slatetype {name} {
    set splut [split $name $separator]
    set cntxt [join [lreplace $splut end end] "."]
    set local [lindex $splut end]

    set t [_type $cntxt $local]
    _verify $t $cntxt $local
    _get $t $cntxt $local
}
