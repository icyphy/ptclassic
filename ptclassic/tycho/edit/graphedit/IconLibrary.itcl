##########################################################################
# Version: $Id$
# Author: John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABL TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### IconLibrary
#
# The _IconLibrary_ model is a hierarchical model for a library
# of "icons" for dataflow graphs and such.
#
# In addition to those in the <a href="Library.html">Library</a>
# superclass, this model has the following types:
# This model has the following types:
# <dl>
# <dt><b>icon</b>
# <dd>
# A description of a graphical icon. An item of this type can
# be drawn on a Slate with the <b>draw</b> method. Items of type
# <b>icon</b> have the following predefined attributes; in
# addition, they can have any attributes that are legal for the
# Slate type that will be used to draw the icon (see the
# <b>-slatetype</b> attribute):
#   <dl>
#   <dt><b>-label</b> _string_
#   <dd>
#    The label used to display the icon in menu bars and the like.
#    (Note that the text displayed in the icon itself is usually
#    set by the <b>-text</b> option.)
#
#   <dt><b>-slatetype</b> _slatetype_
#   <dd>
#    The Slate class that will be used to draw the icon. The default
#    is IconRect, which is a simple rectangle with a text
#    label and optional graphics. Other choices include IconOval
#    and IconFrame.
#
#   <dt><b>-termstyles</b> _stylelist_
#   <dd>
#    A list of terminal styles that the icon will accept. When the icon
#    is drawn, it can be drawn with any of the termstyles given in
#    this list. The listed termstyles must be within the same IconLibrary
#    model.
#
#   </dl>
#
# <dt><b>terminal</b>
# <dd>
# A description of a terminal of an icon. An item of this type
# can be drawn on a Slate as a child item of an <b>icon</b> item.
# The <b>port</b> item type can be contained within an <b>icon</b>
# item, in which cas it will be drawn as part of the icon, or
# within a <b>termstyle</b> item, in which case it will be draw as
# part of any icon that selects the containing termstyle. Items of type
# <b>terminal</b> have the following predefined attributes; in
# addition, they can have any attributes that are legal for the
# Slate type that will be used to draw the terminal (see the
# <b>-slatetype</b> attribute):
# 
#   <dl>
#   <dt><b>-slatetype</b> _slatetype_
#   <dd>
#    The Slate class that will be used to draw the icon. The default
#    is Terminal.
#
#   <dt><b>-coords</b> {_x y_}
#   <dd>
#    The coordinates of the terminal, relative to the icon that
#    it become parts of.
#
#   <dt><b>-tags</b> _taglist_
#   <dd>
#    The tags which are given to this terminal. This is typically used to
#    enable various forms of connection. The default is "input."
#
#   </dl>
#
# <dt><b>termstyle</b>
# <dd>
# A set of terminals. Termstyles allow a single icon to have different
# terminal appearances, and allow different icons to share definitions
# of terminal appearance. A termstyle can appear anywhere in an IconLibrary.
# Its name can be referred to from within any <b>-termstyle</b>
# list. If the termstyle appears within an <b>icon</b>, then that termstyle
# is implicitly prepended to the <b>-termstyle</b> option of the icon
# (if it has one). A termstyle has the following attributes:
#   <dl>
#   <dt><b>-label</b> _string_
#   <dd>
#    The label used to display the termstyle in menu bars and
#    selection lists.
#
#   </dl>
#
#
# </dl>
#
# Here is an example of an icon library:
# <pre><tcl>
#     catch {delete object $icons}
#     set icons [::tycho::IconLibrary [::tycho::autoName icons]]
#     $icons parse {
#         termstyle 1-0-east -label "One output, east" {
#             terminal in -coords {0 50} -direction {-1 0} &#92
#                         -style circle -tags output
#         }
#         
#         termstyle 1-0-west -label "One output, west" {
#             terminal in -coords {100 50} -direction {1 0} &#92
#                         -style rectangle -fill magenta -tags output
#         }
#         
#         group sample -label {Sample Icons} {
#             icon add -label Add \
#                     -slatetype IconOval \
#                     -fill white \
#                     -text "" \
#                     -termstyles {1-0-east 1-0-west} \
#                     -graphics {
#                 line 20 50 80 50 -width 4
#                 line 50 20 50 80 -width 4
#             }
#             
#             icon foo \
#                     -slatetype IconFrame \
#                     -color blue \
#                     -label {The Foo Icon} \
#                     -text Foo \
#                     -textanchor nw \
#                     -termstyles {1-0-east 1-0-west} \
#                     -graphics {
#                 Frame 10 10 50 50 -color red
#                 Frame 50 50 90 90 -color green -relief sunken
#             }
#         }
#     }
#     $icons describe
# </tcl></pre>
#
# Create a menubar and add this index to it:
# <tcl quiet><pre>
#    proc select {library item} {
#        ::tycho::inform "item $item: [$library describe $item]"
#    }
#    catch {delete object .mb}
#    ::tycho::MenuBar .mb
#    pack .mb
#    raise .
#    wm deiconify .
#    .mb addMenu foo -label Foo
#    $icons makemenu {icon termstyle} .mb foo "select $icons"
# </pre></tcl>
#
# Create a blank slate and draw the icons on it:
# <tcl><pre>
#     source $TYCHO/edit/slate/doc/internals/newslate.itcl
#     $icons draw {sample add} $slate 100 100 -tags moveable
#     $icons draw {sample foo} $slate 200 100 -tags moveable &#92
#            -termstyle 1-0-west
# </pre></tcl>
# 
class ::tycho::IconLibrary {
    inherit ::tycho::Library

    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####


    #################################################################
    ####                     public methods                      ####

    # Draw an icon on a given slate
    method draw {icon slate x y args}

    # Instantiate an icon into a layout model
    method instantiate {item layout args}
}

########################################################################
#### constructor
#
body ::tycho::IconLibrary::constructor {args} {
    # Evaluate options
    eval configure $args

    # The icon type
    define icon \
	    -label "" \
            -slatetype IconRect \
            -termstyles {}

    # The terminal type
    define terminal \
	    :leafonly 1 \
            -slatetype Terminal \
            -coords {0 0} \
	    -tags input

    # The termstyle type
    define termstyle \
            -label ""
}

########################################################################
#### draw
#
# Draw an icon on a slate at the given coordinates, and return the slate
# id of the slate item. Optional arguments can be any of the following:
# <dl>
# <dt><b>-termstyle</b> _style_
# <dd>
# The terminal style to draw the icon in. The given style must be
# one in the icons <b>termstyle</b> list or a termstyle that is
# a child of the icon. If not supplied, the terminals drawn will be
# either the first termstyle found, or, if the icon contains
# <b>terminal</b> items within it, then those terminals will be
# drawn.
#
# <dt><b>-_attribute_</b> _value_
# <dd>
# Any other attribute will be added to the attribute list of the
# created slate item.
#
# </dl>
#
body ::tycho::IconLibrary::draw {icon slate x y args} {
    if ![exists $icon] {
        error "Unknown icon \"$icon\""
    }
    if { [type get $icon] != "icon" } {
        error "\"$icon\" is not an icon"
    }
    array set opts {-termstyle {}}
    array set opts $args
    set termstyle $opts(-termstyle)
    unset opts(-termstyle)

    # Check and get attributes
    array set attrs [attrget $icon]
    set slatetype $attrs(-slatetype)
    set termstyles {}
    foreach t [match termstyle $icon] {
        lappend termstyles [concat $icon $t]
    }
    set termstyles [concat $termstyles $attrs(-termstyles)]
    unset attrs(-slatetype)
    unset attrs(-termstyles)

    # Create the icon
    set id [eval $slate create $slatetype $x $y \
            [array get attrs] \
            [array get opts]]

    # Check and get termstyle and terminals
    if { $termstyle == "" } {
        # Try for nested terminals
        set terminals [match terminal $icon]
        if { $terminals != "" } {
            set termstyle $icon
        } else {
            # Try for a default termstyle
            set termstyle [lindex $termstyles 0]
            if { $termstyle != "" } {
                set terminals [match terminal $termstyle]
            }
        }
    } else {
        if { [lsearch -exact $termstyles $termstyle] == -1 } {
            error "Unknown termstyle \"$termstyle\": must be one\
                    of $termstyles"
        }
        set terminals [match terminal $termstyle]
    }

    # Create the terminals. If there were no termstyles or terminals
    # found for the icon, then terminals will be empty and none
    # will be drawn 
    foreach term $terminals {
        array set a [attrget [concat $termstyle $term]]
        set termtype $a(-slatetype)
        unset a(-slatetype)
        set x [lindex $a(-coords) 0]
        set y [lindex $a(-coords) 1]
        unset a(-coords)
        eval $slate call $id terminal \
                $term $termtype $x $y [array get a]
        unset a
    }
    return $id
}

########################################################################
#### instantiate
#
# Instantiate an icon into a layout model. This method overrides the
# method in ::tycho::Library to:
# <ul>
# <li>Remove the <b>-termstyles</b> attributes, which Layout
# doesn't understand.
# <li>Choose the right terminals to create as subitems, based on
# the termstyle.
# </ul>
#
# Optional arguments accepted are the following:
#
# <dl>
# <dt><b>-itemname</b> _name_
# <dd>
# The name of the item to instantiate in the layout model. If not
# given, a name will be generated.
#
# <dt><b>-itemcontext</b> _context_
# <dd>
# The context within which to generate the new item. If not given,
# the item will be generated at the top level. Note, however, that
# <b>-itemname</b> may be a list -- in this case, the full name of
# the item is the concatenation of <b>-context</b> and <b>-itemname</b>.
# 
# <dt><b>-termstyle</b> _style_
# <dd>
# The terminal style to draw the icon in. The given style must be
# one in the icons <b>termstyle</b> list or a termstyle that is
# a child of the icon. If not supplied, the terminals drawn will be
# either the first termstyle found, or, if the icon contains
# <b>terminal</b> items within it, then those terminals will be
# drawn.
#
# <dt><b>-_attribute_</b> _value_
# <dd>
# Any other attribute will be added to the attribute list of the
# created item.
#
# </dl>
#
# The return value is the name of the top-level item. Each item is added
# via the publish{} method of the Model class, so that subscribers will
# be notified of the new data.
#
body ::tycho::IconLibrary::instantiate {icon layout args} {
    if ![exists $icon] {
        error "unknown item \"$icon\""
    }
    # Extract options
    array set opts {-termstyle {} -itemname {} -itemcontext {}}
    array set opts $args

    set termstyle $opts(-termstyle)
    unset opts(-termstyle)

    set itemname $opts(-itemname)
    unset opts(-itemname)

    set itemcontext $opts(-itemcontext)
    unset opts(-itemcontext)

    # Get the full name
    if { $itemname == "" } {
	set itemname [uniqueid [concat $itemcontext $itemname]]
    }

    # Check and get attributes
    array set attrs [attrget $icon]
    set slatetype $attrs(-slatetype)
     set slatetype $attrs(-slatetype)
    set termstyles {}
    foreach t [match termstyle $icon] {
        lappend termstyles [concat $icon $t]
    }
    set termstyles [concat $termstyles $attrs(-termstyles)]
    unset attrs(-termstyles)

    # Add the icon item
    eval [list $layout publish add item $itemname] \
            [array get attrs] \
            [array get opts]

    # Check and get termstyle and terminals
    if { $termstyle == "" } {
        # Try for nested terminals
        set terminals [match terminal $icon]
        if { $terminals != "" } {
            set termstyle $icon
        } else {
            # Try for a default termstyle
            set termstyle [lindex $termstyles 0]
            if { $termstyle != "" } {
                set terminals [match terminal $termstyle]
            }
        }
    } else {
        if { [lsearch -exact $termstyles $termstyle] == -1 } {
            error "Unknown termstyle \"$termstyle\": must be one\
                    of $termstyles"
        }
        set terminals [match terminal $termstyle]
    }

    # Create the terminals. If there were no termstyles or terminals
    # found for the icon, then terminals will be empty and none
    # will be drawn 
    foreach term $terminals {
        eval [list $layout publish add item [concat $itemname $term]] \
                [attrget [concat $termstyle $term]]
    }
    
    # Return the item name
    return $itemname
}
