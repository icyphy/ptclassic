#########################################################################
# Version: $Id$
# Author: John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### Layout
#
# The _Layout_ model is a model for graphical items drawn onto a
# slate. Its methods provide a model-like interface for slate
# modifications, so that any view based on a slate can communicate
# with other views.
#
# _Layout_ has the following types:
# <dl>
# <dt><b>item</b> _name_
# <dd>
# A graphical item on the slate. Items can be nested arbitrarily, and
# views should interpret nested items as active child items on the slate.
# Items have the following attributes:
#   <dl>
#   <dt><b>-coords</b> <b>{</b> _x_ _y_ ?_x_ _y_ ...? <b>}</b>
#   <dd>
#   The coordinates of the item. The coordinates are not always kept
#   up to date, simply because most of the work of maintaining them
#   is done by the Slate, and duplicating it in this mdoel would be
#   pointless. Instead, this attribute is updated automatically when
#   it is read. This item must be supplied when the item is created.
#
#   <dt><b>-slatetype</b> _type_
#   <dd>
#   The slate type of the item. This attribute must be supplied when
#   an item is created, and cannot be changed afterwards.
#
#   <dt>_Item option_
#   <dd>
#   Any other attributes must be legal options to the slate type
#   given by </b>-slatetype</b>. 
#
#   </dl>
#
# _Layout_ supports several events that have no method, but are used
# by views to communicate information with each other. These events
# deal with tagging and selection, which is information that is not
# recorded in the model itself. The event names correspond directly
# to the methods of the same names in _IncrCanvas_ or _Slate_. The
# events are:
# <dl>
# <dt><b>addtag</b> _tag_ ?_item_ _item_...?
# <dd>
# Add the specified _tag_ to each _item_.
#
# <dt><b>dtag</b> _tag_ ?_delete_...?
# <dd>
# Delete _tag_. If _delete_ is given, delete from matching items,
# oherwise delete from all items.
# 
# <dt><b>selection</b> _mode_ ?_arg_ _arg...?
# <dd>
# Manipulate the selection.
# 
# </dl>
#
class ::tycho::Layout {
    inherit ::tycho::DataModel
    
    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####
    
    #################################################################
    ####                     public methods                      ####
    
    # Assign the values an item 
    method assign {item value}
    
    # Add an item 
    method add {type item args}
    
    # Get or set the coordinates of an item
    method coords {item args}
    
    # Forward a query to a subscriber view
    method forward {method name args}
    
    # Get an attribute of an item
    method itemcget {item args}
    
    # Configure an item
    method itemconfigure {item args}
    
    # Move an item
    method move {tag x y}
    
    # Start moving an item
    method moveclick {tag x y args}
    
    # Continue moving an item
    method movedrag {tag x y}
    
    # Stop moving an item
    method moverelease {tag}
    
    # Reshape an item
    method reshape {item x y args}
    
    # Start reshaping an item
    method reshapeclick {item x y args}
    
    # Continue reshaping an item
    method reshapedrag {item x y}
    
    # Stop reshaping an item
    method reshaperelease {item}
    
    # Subscribe to this model
    method subscribe {type {subscriber {}} {command {}}}
    
    # Unsubscribe from this model
    method unsubscribe {type subscriber}
    
    #################################################################
    ####                   protected methods                     ####
    
    # Add an item
    protected method _additem {args} {eval add $args}
    
    # Return a description of a single item
    protected method _describeitem {item {prefix {}}}
    
    # Find items using a subscribed view
    protected method find {mode args}
    
    #################################################################
    ####                   private variables                     ####
    
    # Flag to mark "dirty" coordinates
    private variable _dirty
    
    # Storage used during interaction sequences
    private variable _scratch
    
    # A list of subscribed slate views
    private variable _views {}
}

########################################################################
#### constructor
#
body ::tycho::Layout::constructor {args} {
    # Evaluate options
    eval configure $args
    
    # The "item" type
    type define item
}

########################################################################
#### assign
#
# Assign an attribute list to an item. This method overrides
# the inherited method to deal with the <b>-slatetype</b> and
# <b>-coords</b> attributes.
#
body ::tycho::Layout::assign {item value} {
    array set opts $value
    if ![info exists opts(-slatetype)] {
        error "attribute \"-slatetype\" must be supplied"
    }
    if { $opts(-slatetype) != [itemcget $item -slatetype] } {
        error "attribute \"-slatetype\" cannot be changed"
    }
    if ![info exists opts(-coords)] {
        error "attribute \"-coords\" must be supplied"
    }
    chain $item $value
}

########################################################################
#### add
#
# Add an item. This method overrides the inherited method to deal
# with the <b>-slatetype</b> and <b>-coords</b> attributes.
#
body ::tycho::Layout::add {type item args} {
    array set opts $args
    if ![info exists opts(-slatetype)] {
        error "attribute \"-slatetype\" must be supplied"
    }
    if ![info exists opts(-coords)] {
        error "attribute \"-coords\" must be supplied"
    }
    eval [list chain $type $item] $args
}

########################################################################
#### coords
#
# With no arguments, get the coordinates of an item, with arguments,
# set the coordinates and add to the history if recording. 
#
body ::tycho::Layout::coords {item args} {
    if { $args == "" } {
        itemcget $item -coords
    } else {
        if $recording {
            history append \
                    "coords $item $args" \
                    "coords $item [itemcget $item -coords]"
        }
        norecord itemconfigure $item -coords $args
    }
}

#######################################################################
#### forward
#
# Call a method on a slate. The first widget in the list of widgets
# subscribed with <b>subscribe view</b> must be able to respond to
# the <b>slateid</b> method and the <b>component slate</b>
# method.
#
body ::tycho::Layout::forward {method name args} {
    if { $_views == "" } {
        error "$modelname has no subscribed views"
    }
    set view [lindex $_views 0]
    set id [$view slateid $name]
    set slate [$view component slate]
    eval $slate $method $id $args
}

########################################################################
#### itemcget
#
# Get an attribute or attributes of an item. This method overrides
# the inherited method to deal with the <b>-slatetype</b> and
# <b>-coords</b> attributes.
#
body ::tycho::Layout::itemcget {item {attribute {}}} {
    # Update coordinates
    if { [::info exists _dirty($item)] && $_views != "" } {
        itemconfigure $item -coords [[lindex $_views 0] coords $item]
        unset _dirty($item)
    }
    chain $item $attribute
}

########################################################################
#### itemconfigure
#
# Configure an attribute or attributes of an item. This method overrides
# the inherited method to deal with the <b>-slatetype</b> and
# <b>-coords</b> attributes.
#
body ::tycho::Layout::itemconfigure {item args} {
    if { [lsearch -exact -- $args -slatetype] != -1 } {
        error "Attribute \"-slatetype\" is read-only"
    }
    if { [lsearch -exact -- $args -coords] != -1 } {
        catch {unset _dirty($item)}
    }
    eval chain $item $args	
}

########################################################################
#### move
#
# Move an item by name, or a set of items by tag. Record a single move
# command in the history if currently recording.
#
body ::tycho::Layout::move {tag x y} {
    # If not recording, set the dirty flag of each item.
    if !$recording {
        if [exists $tag] {
            set _dirty($tag) 1
        } else {
            foreach item [find withtag $tag] {
                set _dirty($item) 1
            }
        }
    } else {
        # If recording, add to the history as well
        if [exists $tag] {
            set _dirty($tag) 1
            history append \
                    "move $tag $x $y" \
                    "move $tag [expr -$x] [expr -$y]"
        } else {
            set redo _compound
            set undo _compound
            foreach item [find withtag $tag] {
                set _dirty($item) 1
                lappend redo "move $item $x $y"
                lappend undo "move $item [expr -$x] [expr -$y]"
            }
            history append $redo $undo
        }
    }
}

########################################################################
#### moveclick
#
# Start moving an item specified by name, or a set of items 
# specified by tag. Don't record anything in the history.
#
body ::tycho::Layout::moveclick {tag x y} {
    set _scratch($tag.startx) $x
    set _scratch($tag.starty) $y
    set _scratch($tag.currentx) $x
    set _scratch($tag.currenty) $y
}

########################################################################
#### movedrag
#
# Continue moving an item specified by name, or a set of items 
# specified by tag. Don't record anything in the history.
#
body ::tycho::Layout::movedrag {tag x y} {
    set _scratch($tag.currentx) $x
    set _scratch($tag.currenty) $y
}

########################################################################
#### moverelease
#
# Terminate movement of an item specified by name, or a set of items 
# specified by tag. Record a single move command in the history
# if currently recording.
#
body ::tycho::Layout::moverelease {tag} {
    set deltax [expr $_scratch($tag.currentx) - $_scratch($tag.startx)]
    set deltay [expr $_scratch($tag.currenty) - $_scratch($tag.starty)]
    unset _scratch($tag.startx)
    unset _scratch($tag.starty)
    unset _scratch($tag.currentx)
    unset _scratch($tag.currenty)
    
    # Call the move method to construct history
    move $tag $deltax $deltay
}

########################################################################
#### reshape
#
# Reshape an item by name. Record a single reshape
# command in the history if currently recording.
#
body ::tycho::Layout::reshape {item x y args} {
    if ![exists $item] {
        error "Cannot reshape \"$item\""
    }
    set _dirty($item) 1
    if $recording {
        history append \
                "reshape $item $x $y $args" \
                "reshape $item [expr -$x] [expr -$y] $args"
    }
}

########################################################################
#### reshapeclick
#
# Start moving an item specified by name. Don't record anything in
# the history.
#
body ::tycho::Layout::reshapeclick {item x y args} {
    if ![exists $item] {
        error "Cannot reshape \"$item\""
    }
    set _scratch($item.startx) $x
    set _scratch($item.starty) $y
    set _scratch($item.currentx) $x
    set _scratch($item.currenty) $y
    
    array set opts $args
    set _scratch($item.aspects) $opts(-aspects)
}

########################################################################
#### reshapedrag
#
# Continue reshaping an item specified by name. Don't record anything in
# the history.
#
body ::tycho::Layout::reshapedrag {item x y} {
    if ![exists $item] {
        error "Cannot reshape \"$item\""
    }
    set _scratch($item.currentx) $x
    set _scratch($item.currenty) $y
}

########################################################################
#### reshaperelease
#
# Terminate reshaping of an item specified by name. Record a reshape
# command in the history if currently recording.
#
body ::tycho::Layout::reshaperelease {item} {
    if ![exists $item] {
        error "Cannot reshape \"$item\""
    }
    set deltax [expr $_scratch($item.currentx) - $_scratch($item.startx)]
    set deltay [expr $_scratch($item.currenty) - $_scratch($item.starty)]
    set aspects $_scratch($item.aspects)
    
    unset _scratch($item.startx)
    unset _scratch($item.starty)
    unset _scratch($item.currentx)
    unset _scratch($item.currenty)
    unset _scratch($item.aspects)
    
    # Call the reshape method to construct history
    eval reshape $item $deltax $deltay $aspects
}

########################################################################
#### subscribe
#
# Subscribe to this model. This command has several forms, depending
# on the value of the _mode_ argument.
#
# <dl>
# <dt><b>data</b> _subscriber_ ?_command_?
# <dd>
# As inherited from the _Model_ superclass.
#
# <dt><b>event</b> _subscriber_ ?_command_?
# <dd>
# As inherited from the _Model_ superclass.
#
# <dt><b>view</b> _subscriber_
# <dd>
# The _subscriber_ must be a view that understands the following
# methods with model item names as arguments: <b>coords</b>,
# and <b>find</b>.
#
# </dl>
#
body ::tycho::Layout::subscribe {mode {subscriber {}} {command {}}} {
    if { $mode == "view" } {
        lappend _views $subscriber
    } else {
        chain $mode $subscriber $command
    }
}

########################################################################
#### unsubscribe
#
# Unsubscribe from this model. This command has several forms, depending
# on the value of the _mode_ argument.
#
# <dl>
# <dt><b>data</b> _subscriber_
# <dd>
# As inherited from the _Model_ superclass.
#
# <dt><b>event</b> _subscriber_
# <dd>
# As inherited from the _Model_ superclass.
#
# <dt><b>view</b> _subscriber_
# <dd>
# The _subscriber_ must be a view previously subscribed with
# <b>subscribe view</b>.
#
# </dl>
#
body ::tycho::Layout::unsubscribe {mode subscriber} {
    if { $mode == "view" } {
        set _views [::tycho::ldelete $_views $subscriber]
    } else {
        chain $mode $subscriber
    }
}

#####################################################################
#####################################################################
####                     protected methods                       ####

########################################################################
#### _describeitem
#
# Return a string describing an item. This method overrides
# the default method in order to make sure that the item's
# <b>-coords</b> attribute is correctly updated.
#
body ::tycho::Layout::_describeitem {item {prefix {}}} {
    itemcget $item -coords
    _describe $item $prefix
}

########################################################################
#### find
#
# Perform a "find" command, using one of the subscribed slate views.
#
body ::tycho::Layout::find {mode args} {
    if { $_views != "" } {
        eval [lindex $_views 0] find $mode $args
    } else {
        return ""
    }
}
