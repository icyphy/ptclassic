##########################################################################
# Version: $Id$
# Author: John Reekie
#
# @Copyright (c) 1996-1998 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### PtVertexController
#
# _PtVertexController_ is a controller object used by the
# _DFGEditor_ view. It is responsible for managing vertices
# of the graph.
#
class ::tycho::PtVertexController {
    inherit ::tycho::ViewController
	
    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####

    # The corresponding edge controller
    public variable edgecontroller ""

    # The simulation object
    public variable simulation ""
	
    #################################################################
    ####                     public methods                      ####

    # Get the follower
    method _follower {} {return $_follower}

    # Add a vertex and icon
    method add {libname starname {uniqueName {}} {coords {}}}

    # Process a click event
    method click {tag x y args}

    # Configure the context-sensitive popup menu
    method configurePopup {name}

    # Delete a vertex
    method delete {name}

    # Process a motion event
    method drag {tag x y}

    # Edit parameters of the given vertices
    method editparameters {tag}

    # Given a vertex, get the icon library
    method iconlib {vertex}

   # Given a vertex, get the icon item
    method iconname {vertex}

    # Initialize the popup menu
    method initPopup {name}

    # Add an icon at the specified location
    method place {iconlib iconname starname x y}

    # Process a release event
    method release {tag}

    # Set the termstyle of an icon
    method termstyle {vertex {style {}}}

    #################################################################
    ####                  protected methods                      ####

    # Reset all variables
    protected method _cleanup {}

    #################################################################
    ####                 protected variables                     ####
    
    # The interactor to move vertexes
    protected variable _follower

    # Input edges to the subgraph
    protected variable _inputedges ""
    
    # Edges within the subgraph
    protected variable _moveedges ""
    
    # The tag of items being moved
    protected variable _movetag
    
    # The vertices being moved
    protected variable _vertices
    
    # Output edges from the subgraph
    protected variable _outputedges ""
    
    #################################################################
    ####                   private variables                     ####
    
    # The starting coordinates of the vertex
    private variable _initcoords

}

########################################################################
#### constructor
#
body ::tycho::PtVertexController::constructor {args} {
    # Evaluate options
    eval configure $args

    set _follower [$slate interactor Follower]

    # Make the follower send moves here
    $_follower configure \
            -clickprefix "$this click" \
            -dragprefix "$this drag" \
            -releaseprefix "$this release"
}

########################################################################
#### add
#
# Add a vertex given the library name and star name
#
body ::tycho::PtVertexController::add {libname iconname \
	{uniqueName {}} {coords {}}} {
    puts "PtVertexController::add $libname $iconname"
    set iconlib [::tycho::PtIconLibrary::getLibrary $libname]

    set icon [$iconlib getIcon $iconname]
    # puts [$icon toString]

    set type [$icon getChildElement entitytype]
    # puts [$type toString]
    set realname [$type getAttribute "name"]
    set etype [$type getAttribute "type"]

    if { $uniqueName == "" } {
	set uniqueName [::tycho::autoName $realname]
    }
    switch -exact $etype {
	"actor" {
	    set actor [$simulation addActor $realname $uniqueName]
	    if { $actor == "" } {
		::tycho::inform "Unable to create actor $realname \
                        in domain [$simulation getDomain]."
		return
	    }
	}
	"port" {
	    $simulation addPort $realname $uniqueName
	}
	"composite" {
	    $simulation addCompositeActor $realname $uniqueName
            set actor [$simulation getActor $uniqueName]
	    puts "Create actor $uniqueName ([$actor toString])"

            # Create ports
            puts [$icon toString]
	    set iterator [$icon ports]
	    while { [$iterator hasMoreElements] } {
		set p [$iterator nextElement]
		set port [java::new ptolemy.actor.TypedIOPort \
			$actor \
			[$p getAttribute name]]
		# puts "Created port $p ([$port toString])"

		if { [$p getAttribute input] == "false" } {
		    puts "Set [$p getAttribute name] to output"
		    $port setOutput 1
		} else {
		    puts "Set [$p getAttribute name] to input"
		    $port setInput 1
		}
	    }
	}
    }

    # Mess with the history
    $graph mark set temp
    $graph publish add "vertex" $uniqueName \
	    -library $libname \
	    -class $realname \
	    -icon $libname.$iconname

    switch -exact $etype {
	"actor" {
	    $graph attrset $uniqueName \
		    -type actor
	}
	"port" {
	    $graph attrset $uniqueName \
		    -type port
	}
	"composite" {
	    $graph attrset $uniqueName \
		    -type composite \
		    -url [$type getAttribute location]
	}
    }

    # Place the icon
    if { $coords == "" } {
	set coords [$slateview place]
	set coords [list \
		[expr [lindex $coords 0] + 50] \
		[expr [lindex $coords 1] + 40]]
    }    
    eval place [list $iconlib $iconname $uniqueName] $coords

    # Notify views that the data has been modified
    $layout notify event "modified"
    $graph notify event "modified"
}

########################################################################
#### click
#
# Start a move interaction on vertices. The argument is a tag which
# must be attached only to vertices to be moved. Get the names of
# edges that are fully connected to these vertices and tag
# all vertices and those edges. Also get the names of edges
# that have one end connected to one of these vertices and
# remember who they are.
#
body ::tycho::PtVertexController::click {tag x y args} {
    # Get the vertices, and start a move sequence on them
    set _vertices [$slateview find withtag $tag]

    # Get the item name or the same tag
    set name [itemname $tag]
    set _movetag $name

    # Remember the coordinates of the first vertex
    set _initcoords [$slateview coords [lindex $_vertices 0]]

    # Notify views that these items are about to move
    $layout publish moveclick $name $x $y

    # Get completely-connected and partially-connected edges
    set _moveedges [eval $graph subgraphEdges $_vertices]
    set _inputedges [eval $graph subgraphInputs $_vertices]
    set _outputedges [eval $graph subgraphOutputs $_vertices]

    # Start a move or reshape sequence on found edges
    if { $_moveedges != "" } {
	eval $layout notify data addtag $this.edge $_moveedges
	$layout publish moveclick $this.edge $x $y
    }
    foreach edge $_inputedges {
        $layout publish reshapeclick $edge $x $y -features end
    }
    foreach edge $_outputedges {
        $layout publish reshapeclick $edge $x $y -features start
    }
}

#########################################################################
#### configurePopup
#
# Initialize the context-sensitive pop-up menu. 
#
body ::tycho::PtVertexController::configurePopup {name} {
    set v [$slateview find withtag current]

    # Get the terminal styles
    # FIXME: Need a shorter way of doing this!
    set OLDSTUFF {
        set icon [$graph attrget $v -icon]
        set split [split $icon #]
        set iconlib [lindex $split 0]
        set iconname [split [lindex $split 1] .]
    
        set proxy [::tycho::model locate $iconlib]
        set lib [::tycho::model dereference $proxy]
        set termstyles [$lib attrget $iconname -termstyles]

        # Clear and rebuild the terminal style cascade menu
        $slateview popup clear $name termstyle
        foreach s $termstyles {
            if [$lib exists $s -label] {
                set label [$lib attrget $s -label]
                if { $label == "" } {
                    set label $s
                }
            } else {
                set label $s
            }
            $slateview popup add $name command $s termstyle \
                    -label $label \
                    -command "$this termstyle $v $s"
        }
    }
}

########################################################################
#### delete
#
# Delete a vertex and icon by name
#
body ::tycho::PtVertexController::delete {vertex} {
    # Mark the histories
    $layout mark set vertex
    $graph mark set vertex

    # Disconnect input edges
    foreach edge [concat \
            [$graph edges * [list $vertex *]] \
            [$graph edges * $vertex]] {
        $edgecontroller disconnect $edge -to
    }
    # Disconnect output edges
    foreach edge [concat \
            [$graph edges [list $vertex *] *] \
            [$graph edges $vertex *]] {
        $edgecontroller disconnect $edge -from
    }
    # Delete the vertex and icon, making sure it's clean first
    $layout clean $vertex
    $layout publish delete $vertex
    $graph publish delete $vertex

    # Compact the histories
    $layout history compact vertex current
    $graph history compact vertex current

    # Notify views that the data has modified
    $layout notify event "modified"
    $graph notify event "modified"
}

########################################################################
#### drag
#
# Continue a move interaction on vertices. The vertices and
# edges are moved and reshaped as appropriate.
#
body ::tycho::PtVertexController::drag {tag x y} {
    # Move the vertices and fully-connected edges
    $layout publish movedrag $_movetag $x $y
    if { $_moveedges != "" } {
	$layout publish movedrag $this.edge $x $y
    }
    # Reshape the partially-connected edges.
    foreach edge $_inputedges {
	$layout publish reshapedrag $edge $x $y
    }
    foreach edge $_outputedges {
	$layout publish reshapedrag $edge $x $y
    }
}

########################################################################
#### editparameters
#
body ::tycho::PtVertexController::editparameters {tag} {
    # puts "editparameters $tag"

    # FIXME: The parameter editing window must include the whole
    # path name of the model
    set items [$slateview find withtag $tag]
    # puts ###$items
    foreach i $items {
	# Filter out non-vertices
        # FIXME FIXME FIXME (access to slate)
        set id [$slateview slateid $i]
        set sl [$slateview slate]
	if ![$sl hastag $id "vertex"] {
	    continue
	}
        # Get the name of the item
        # FIXME FIXME FIXME
        puts "id = $id, slate = $slate"

        # Produce a name for the window
        set dialogname [$slateview component hull].[string tolower $i]

        if { [infoWhichCommand $dialogname] != "" } {
            # Already exists
            wm deiconify $dialogname
            raise $dialogname
        } else {
            # Create the dialog box
            uplevel #0 [list ::tycho::PtParameterDialog $dialogname \
                    -command "::tycho::post \[$dialogname get\]"]

            # Edit the object
            set namedObj [$simulation getActor $i]
            $dialogname edit $namedObj
            wm deiconify $dialogname
        }
    }
}


########################################################################
#### iconlib
#
# Get the icon library of a vertex.
#
body ::tycho::PtVertexController::iconlib {v} {
    # Find the icon of the vertex
    if ![$graph exists $v -icon] {
        # FIXME
        error "No icon for $starname"
    }
    set iconpath [$graph attrget $v -icon]
    set iconlib [lindex [split $iconpath #] 0]

    # Get the icon library
    set lib [::tycho::model locate $iconlib]
    set iconmodel [::tycho::model dereference $lib]

    return $iconmodel
}

########################################################################
#### iconname
#
# Get the icon name of a vertex.
#
body ::tycho::PtVertexController::iconname {v} {
    # Find the icon of the vertex
    if ![$graph exists $v -icon] {
        # FIXME
        error "No icon for $starname"
    }
    set iconpath [$graph attrget $v -icon]
    set iconname [split [lindex [split $iconpath #] 1] "."]
    return $iconname
}

########################################################################
#### initPopup
#
# Initialise popup menu for vertices.
#
body ::tycho::PtVertexController::initPopup {name} {
    # Command entry for parameter editing
    $slateview popup add $name command editparameters \
	    -command "$this editparameters current" \
	    -label "Parameters..."

    # A cascade for selecting the terminal style
    $slateview popup add $name cascade termstyle \
	    -label "Terminal style"
    
    # Add a cascade for debugging type stuff
    $slateview popup add $name separator sepdebug
    $slateview popup add $name cascade debug -label "Debug"
    $slateview popup add $name command describeItem debug \
	    -command "::tycho::post \[$slateview describe graph current\]" \
	    -label "Describe item"
    $slateview popup add $name command describeLayout debug \
	    -command "::tycho::post \[$slateview describe layout current\]" \
	    -label "Describe layout"
}

########################################################################
#### place
#
# Place an icon at the specified location.
#
body ::tycho::PtVertexController::place {iconlib iconname starname x y} {
    # Mark the history
    $layout mark set vertex

    # Place the icon into the layout model, giving it the
    # same name as the vertex in the graph.
    $iconlib instantiate $iconname $starname $layout \
            -itemname $starname \
            -coords [list $x $y] \
            -tags vertex \
            -text $starname

    # Move its nw corner to the right position. Find the position
    # from my slate, and then move by publishing to the model
    # $_slate moveto $id $iconx $icony "nw" -shape rectangle
    #set current [$layout forward feature $starname "nw" -shape "rectangle"]
    #set dx [expr $x - [lindex $current 0]]
    #set dy [expr $y - [lindex $current 1]]
    #$layout publish move $starname $dx $dy

    # Compact the history and replace the undo command
    $layout history compact vertex current
    $layout history undo current "delete $starname"
}

########################################################################
#### release
#
# Terminate a move interaction on vertices and add an undo and
# redo command to the history of the layout model.
#
body ::tycho::PtVertexController::release {tag} {
    # Mark the history
    $layout mark set vertex

    # Terminate move of vertices
    $layout publish moverelease $_movetag

    # Terminate move of fully-connected edges
    if { $_moveedges != "" } {
        $layout publish moverelease $this.edge
        $layout notify data dtag $this.edge
    }

    # Terminate the reshape of partially-connected edges
    foreach edge $_inputedges {
	$layout publish reshaperelease $edge
    }
    foreach edge $_outputedges {
	$layout publish reshaperelease $edge
    }
    # Now merge all those commands into a single one
    $layout history compact vertex current

    # Finally (and importantly!), add a null command to the graph
    # model to match this command
    $graph history append {} {}

    # Even more finally, figure out if the vertex actually moved.
    # and if it did, tell all views that the layout was modified
    if { $_initcoords != [$slateview coords [lindex $_vertices 0]] } {
	$layout notify event "modified"
    }
}

########################################################################
#### termstyle
#
# Get or set the terminal style of an icon. If the _style_ argument
# is not given, return teh current terminal style. Otherwise change
# the terminal style to the given style, and adjust any connected
# edges to match.
#
body ::tycho::PtVertexController::termstyle {vertex {style {}}} {
    if { $style == "" } {
        return ???
    }
    set iconlib [iconlib $vertex]
    set iconname [iconname $vertex]

    # Iterate through the terminals in the termstyle
    foreach t [$iconlib match terminal $style] {
        # If the terminal exists, reconfigure it
        if [$layout exists [concat $vertex $t]] {
            eval $layout publish attrset \
                    [list [concat $vertex $t]] \
                    [$iconlib attrget [concat $style $t]]
            # Adjust connected edges
            foreach edge [$graph edges * [list $vertex $t]] {
                $edgecontroller redraw $edge -to
            }
            foreach edge [$graph edges [list $vertex $t] *] {
                $edgecontroller redraw $edge -from
            }
        } else {
            # Otherwise create a new one
            eval $layout publish add item \
                    [list [concat $vertex $t]] \
                    [$iconlib attrget [concat $style $t]]
        }
    }
    # If there are any terminals that shouldn't be there, delete them
    foreach t [::tycho::lsubtract \
            [$layout match item $vertex] \
            [$iconlib match terminal $style]] {
        # Disconnect edges
        foreach edge [$graph edges * [list $vertex $t]] {
            $edgecontroller disconnect $edge -to
        }
        foreach edge [$graph edges [list $vertex $t] *] {
            $edgecontroller disconnect $edge -from
        }
        # Delete terminal
        $layout publish delete [concat $vertex $t]
    }
}

###################################################################
###################################################################
####                      protected methods                    ####

########################################################################
#### _cleanup
#
body ::tycho::PtVertexController::_cleanup {} {
    ;
}
