#########################################################################
# @Version: $Id$
# @Author: H. John Reekie
#
# @Copyright (c) 1996 The Regents of the University of California.
# All rights reserved.
#  
# Permission is	hereby granted,	without	written	agreement and without
# license or royalty fees, to use, copy, modify, and distribute	this
# software and its documentation for any purpose, provided that	the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO	EVENT SHALL THE	UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT,	SPECIAL, INCIDENTAL, OR	CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN	ADVISED	OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES	OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.	THE SOFTWARE
# PROVIDED HEREUNDER IS	ON AN "AS IS" BASIS, AND THE UNIVERSITY	OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT,	UPDATES,
# ENHANCEMENTS,	OR MODIFICATIONS.
#							  COPYRIGHTENDKEY
##########################################################################


# Explicitly load the superclass. I don't really know why this is
# needed, but the common arrays get upset otherwise.
namespace ::tycho {
    if { [::info classes ComplexItem] == "" } {
	uplevel #0 {source $tychoslate/ComplexItem.itcl}
    }
}

##########################################################################
## Frame
#
# <i>Frame</i> is a pseudo-3D rectangle, with a border and a relief.
# <i>Frame</i> implements its scaling code so that the borderwidth remains 
# constant when the picture is resized. In all other respects, it can be 
# treated like any other rectangular item.
#
# <p><b>Options</b>
#
# <dl>
# <dt><dd>
# <dl>
# <dt><b>-borderwidth</b>
# <dd>The width of the border of the item. By default, it is two. For
# various reasons, the borderwidth is actually twice this value if the
# <code>-relief</code> option is <i>groove</i> or <i>ridge</i>.
#
# <dt><b>-color</b>
# <dd>The color of the item. This is a "generic" color as implemented by
# the <a href="../codeDoc/Shades.html">Shades</a> class -- this will
# soon be replaced by an enhanced version of <a
# href="../../../../kernel/doc/codeDoc/ColorManager.html">ColorManager</a>. By
# default, the color is the same as the standard Tk background color
# ("grey85").
#
# <dt><b>-relief</b>
# <dd>The "relief" of the pseudo-3D item. This can be one of:
# <i>raised</i>, <i>sunken</i>, <i>groove</i>, <i>ridge</i>, or
# <i>flat</i>. The meanings of these are the same as for the Tk
# <code>frame</code> widget. The default is <i>raised</i>.
#
# </dl></dl>
#
#
# <p><b>Bugs</b>
#
# With the -ridge option, some polygons are not tagged properly and get
# "left behind" when the item is moved.
#
# <p><b>Enhancements</b>
#
# In an earlier version of the Slate, the Frame
# had an additional option,<code>-fill</code>, which could take
# values <i>none</i>,  <i>gloss</i>,  and <i>matt</i>. In the first 
# case, the Frame had no "surface" -- that is, it was transparent, which 
# was particularly useful if <code>-relief</code> was <i>ridge</i>. This 
# should be reimplemented.
#
# <p><b>Example</b>
# 
# Create some Frames with various options. These can be moved around
# with the mouse:
# <tcl quiet><pre>
#   source $tycho/editors/slate/doc/internals/newslate.itcl
#   $slate create Frame 30 30 70 50 -color green -tags moveable
#   $slate create Frame 100 20 120 40 -color lightblue &#92
# 	-relief ridge -tags moveable
#   $slate create Frame 30 130 70 150 -relief sunken  &#92
# 	-borderwidth 4 -tags moveable
# </pre></tcl>
#
# <p><b>Links</b>
# 
# <ul>
# <li><a href="../internals/howtoread.html">Reading <i>ComplexItem</i>
# documentation</a>.
# <li><a href="../internals/items.html">Complex item classes</a>.
# </ul>
#
class ::tycho::Frame {
    inherit ::tycho::ComplexItem

    # Create a new item
    proc construct {id canvas slate tags x0 y0 x1 y1 args}

    ###################################################################
    ####                            options                        ####

    # The width of the relief border
    common _borderwidth

    # The color of the frame
    common _color

    # The relief: raised, sunken, groove, ridge, or flat
    common _relief

    ###################################################################
    ####                     option update procs                   ####

    # Update the *-borderwidth* option
    proc _borderwidth {id canvas slate borderwidth}
    
    # Update the *-color* option
    proc _color {id canvas slate color}
    
    # Update the *-relief* option
    proc _relief {id canvas slate relief}

    ###################################################################
    ####                         public procs                      ####

    # Set or get the item's coordinates
    proc coords {id canvas slate args}

    ###################################################################
    ####                         picture components                ####

    # The polygon that is "lit"
    common lit
    
    # The polygon in the inside shadow (ridge and groove reliefs only)
    common notlit
    
    # The polygon that is "shaded"
    common shaded
    
    # The polygon in the inside lit region (ridge and groove reliefs only)   
    common notshaded
    
    # The rectangular surface
    common surface

    ###################################################################
    ####                         protected variables               ####

    # The object used to get shades of color
    common colorShadesObject

    # The virtual "method" table
    common methodtable

    # Default values of this item's options
    common optiondefault

    # Frames are rectangular in shape
    common shape "rectangle"

    ###################################################################
    ####                       class initialization                ####

    #### Set method table
    array set methodtable [array get ::tycho::ComplexItem::methodtable]

    set methodtable(_borderwidth) ::tycho::Frame::_borderwidth
    set methodtable(_color)       ::tycho::Frame::_color
    set methodtable(_relief)      ::tycho::Frame::_relief

    set methodtable(construct)   ::tycho::Frame::construct
    set methodtable(coords)      ::tycho::Frame::coords

    #### Set option defaults
    array set optiondefault [array get ::tycho::ComplexItem::optiondefault]

    set optiondefault(-color)       background
    set optiondefault(-borderwidth) 2
    set optiondefault(-relief)      raised
}


##########################################################################
## -borderwidth option configuration
#
body ::tycho::Frame::_borderwidth {id canvas slate borderwidth} {
    set _borderwidth($id) $borderwidth

    set coords [$canvas	coords $primary($id)]

    set x0 [lindex $coords 0]
    set y0 [lindex $coords 1]
    set x1 [expr [lindex $coords 2] + 1]
    set y1 [expr [lindex $coords 3] + 1]

    set	a0 [expr $x0 + $borderwidth]
    set	b0 [expr $y0 + $borderwidth]
    set	a1 [expr $x1 - $borderwidth]
    set	b1 [expr $y1 - $borderwidth]

    $canvas coords $lit($id) \
	    $x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1
    $canvas coords $shaded($id) \
	    $x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1

    if { $_relief($id) == "ridge" || $_relief($id) == "groove" } {
	set x0 $a0
	set y0 $b0
	set x1 $a1
	set y1 $b1

	set a0 [expr $x0 + $borderwidth]
	set b0 [expr $y0 + $borderwidth]
	set a1 [expr $x1 - $borderwidth]
	set b1 [expr $y1 - $borderwidth]
	
	$canvas coords $notlit($id) \
		$x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1
	$canvas coords $notshaded($id) \
		$x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1
    }

    if { [::info exists surface($id)] } {
	$canvas coords $surface($id) $a0 $b0 $a1 $b1
    }
}

##########################################################################
## -color option configuration
#
# FIXME: Use a new procedural color manager instead of color objects.
#
body ::tycho::Frame::_color {id canvas slate color} {
    set _color($id) $color
    set colorShadesObject($id) [::tycho::Shades::new $color]

    # FIXME: quick and dirty
    _relief $id $canvas $slate $_relief($id)
    $canvas itemconfigure $surface($id) -fill [$colorShadesObject($id) matt]
}

##########################################################################
## -relief option configuration
#
# Update the relief of the Frame. Can be one of: flat, raised,
# sunken. ridge, or groove. The coloring for the raised and ridge
# options is the same: the coloring for the sunken and grooved options
# is the same -- the difference is only in whether the notlit and
# notshaded components exist.
#
body ::tycho::Frame::_relief {id canvas slate relief} {
    set _relief($id) $relief
    if { $relief == "ridge" || $relief == "groove" } {
	
	set light [$colorShadesObject($id) light]
	set dark  [$colorShadesObject($id) dark]

	if { $relief == "groove" } {
	    set light $dark
	    set dark [$colorShadesObject($id) light]
	}

	# Change the color of the lit and shaded components
	$canvas itemconfigure $lit($id) -fill $light
	$canvas itemconfigure $shaded($id) -fill $dark

	# If the relief has changed to ridge or groove, then we
	# have to create the inside border polygons, and move
	# the surface if it exists.
	if { ! [info exists notlit($id)] } {

	    set coords [$canvas coords $primary($id)]
	    set tags [$slate gettags $id]

	    set x0 [expr [lindex $coords 0] + $_borderwidth($id)]
	    set y0 [expr [lindex $coords 1] + $_borderwidth($id)]
	    set x1 [expr [lindex $coords 2] - $_borderwidth($id) + 1]
	    set y1 [expr [lindex $coords 3] - $_borderwidth($id) + 1]

	    set a0 [expr $x0 + $_borderwidth($id)]
	    set b0 [expr $y0 + $_borderwidth($id)]
	    set a1 [expr $x1 - $_borderwidth($id)]
	    set b1 [expr $y1 - $_borderwidth($id)]

	    set notlit($id) [$canvas create polygon \
		    $x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1 \
		    -fill $dark -tags $tags]

	    set notshaded($id) [$canvas create polygon \
		    $x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1 \
		    -fill $light -tags $tags]

	    if { [::info exists surface($id)] } {
		$canvas coords $surface($id) $a0 $b0 $a1 $b1
	    }
	} else {
	    $canvas itemconfigure $notshaded($id) -fill $light
	    $canvas itemconfigure $notlit($id) -fill $dark
	}
    } else {
	set light [$colorShadesObject($id) light]
	set dark  [$colorShadesObject($id) dark]

	if { $relief == "sunken" } {
	    set light $dark
	    set dark [$colorShadesObject($id) light]
	} elseif { $relief == "flat" } {
	    set light [$colorShadesObject($id) matt]
	    set dark $light
	}

	# Change the color of the various components
	$canvas itemconfigure $lit($id) -fill $light
	$canvas itemconfigure $shaded($id) -fill $dark

	# If the relief has changed from ridge or groove, then we
	# delete the inside border polygons and move the surface.
	if {  [::info exists notlit($id)] } {

	    $canvas delete $notlit($id)
	    $canvas delete $notshaded($id)
	    unset notlit($id)
	    unset notshaded($id)

	    if { [::info exists surface($id)] } {
		set coords [$canvas coords $primary($id)]

		set x0 [lindex $coords 0]
		set y0 [lindex $coords 1]
		set x1 [expr [lindex $coords 2] + 1]
		set y1 [expr [lindex $coords 3] + 1]

		set a0 [expr $x0 + $_borderwidth($id)]
		set b0 [expr $y0 + $_borderwidth($id)]
		set a1 [expr $x1 - $_borderwidth($id)]
		set b1 [expr $y1 - $_borderwidth($id)]

		$canvas coords $surface($id) $a0 $b0 $a1 $b1
	    }
	}
    }
}

##########################################################################
#### construct
#
body ::tycho::Frame::construct {id canvas slate tags x0 y0 x1 y1 args} {
    # Initialize the options
    foreach {option value} [concat [array get optiondefault] $args] {
	set _[string trimleft $option -]($id) $value
    }

    # This object is used to get the shades of color
    set colorShadesObject($id) [::tycho::Shades::new $_color($id)]
    
    # Create the primary component for the outline coordinates.
    # Note that this has an outline, but it will be obscured
    # by other components. DO NOT give this rectangle a blank
    # outline, because then it will respond to mouse events on
    # its inside (even though invisible), which makes see-through
    # frames impossible.
    set primary($id) [$canvas create rectangle $x0 $y0 $x1 $y1 \
	    -tags $tags]

    # The way that the polygon co-ordinates is set up in the Tk
    # canvas seems somewhat bogus, so we have to futz with the
    # coordinates here.
    set	y1 [expr $y1+1]
    set	x1 [expr $x1+1]

    # Create the borders and the fill. This is set for the default
    # _relief (raised).
    set	a0 [expr $x0 + $_borderwidth($id)]
    set	b0 [expr $y0 + $_borderwidth($id)]
    set	a1 [expr $x1 - $_borderwidth($id)]
    set	b1 [expr $y1 - $_borderwidth($id)]

    set lit($id) [$canvas create polygon \
 	    $x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1 \
 	    -fill [$colorShadesObject($id) light] -tags $tags]

    set shaded($id) [$canvas create polygon \
 	    $x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1 \
 	    -fill [$colorShadesObject($id) dark] -tags $tags]

    set surface($id) [$canvas create rectangle \
            $a0 $b0 $a1 $b1 \
            -fill [$colorShadesObject($id) matt] \
            -outline {} -tags $tags]

    # Adjust the relief if not the default
    if { $_relief($id) != "raised" } {
	_relief $id $canvas $slate $_relief($id)
    }
}


##########################################################################
#### coords
#
# Set or read the Frame's coordinates
#
body ::tycho::Frame::coords {id canvas slate args} {
    # No args: just return coordinates
    if { $args == "" } {
	return [$canvas coords $primary($id)]
    }

    # Move the primary
    set coords $args
    eval $canvas coords $primary($id) $coords

    # Change the co-ordinates of the outside borders.
    set x0 [lindex $coords 0]
    set y0 [lindex $coords 1]
    set x1 [expr [lindex $coords 2] + 1]
    set y1 [expr [lindex $coords 3] + 1]

    set a0 [expr $x0 + $_borderwidth($id)]
    set b0 [expr $y0 + $_borderwidth($id)]
    set a1 [expr $x1 - $_borderwidth($id)]
    set b1 [expr $y1 - $_borderwidth($id)]

    $canvas coords $lit($id) \
	    $x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1

    $canvas coords $shaded($id) \
	    $x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1

    # If the relief is "ridge" or "groove," change the coordinates
    # of the inside borders.
    if { $_relief($id) == "ridge" || $_relief($id) == "groove" } {
	set x0 $a0
	set y0 $b0
	set x1 $a1
	set y1 $b1

	set a0 [expr $x0 + $_borderwidth($id)]
	set b0 [expr $y0 + $_borderwidth($id)]
	set a1 [expr $x1 - $_borderwidth($id)]
	set b1 [expr $y1 - $_borderwidth($id)]

	$canvas coords $notlit($id) \
		$x0 $y1 $x0 $y0 $x1 $y0 $a1 $b0 $a0 $b0 $a0 $b1
	
	$canvas coords $notshaded($id) \
		$x0 $y1 $x1 $y1 $x1 $y0 $a1 $b0 $a1 $b1 $a0 $b1
    }

    # If there is a surface component, change its coordinates
    if { [::info exists surface($id)] } {
	$canvas coords $surface($id) $a0 $b0 $a1 $b1
    }
}
