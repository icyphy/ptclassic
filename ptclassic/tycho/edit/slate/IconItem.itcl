#########################################################################
# @Version: $Id$
# @Author: H. John Reekie
#
# @Copyright (c) 1996 The Regents of the University of California.
# All rights reserved.
#  
# Permission is	hereby granted,	without	written	agreement and without
# license or royalty fees, to use, copy, modify, and distribute	this
# software and its documentation for any purpose, provided that	the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO	EVENT SHALL THE	UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT,	SPECIAL, INCIDENTAL, OR	CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN	ADVISED	OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES	OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.	THE SOFTWARE
# PROVIDED HEREUNDER IS	ON AN "AS IS" BASIS, AND THE UNIVERSITY	OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT,	UPDATES,
# ENHANCEMENTS,	OR MODIFICATIONS.
#							  COPYRIGHTENDKEY
##########################################################################


# Explicitly load the superclass. I don't really know why this is
# needed, but the common arrays get upset otherwise.
namespace ::tycho {
    if { [::info classes ComplexItem] == "" } {
	uplevel #0 {source $tychoslate/ComplexItem.itcl}
    }
}


########################################################################
## Node
#
# A "node" class for dataflow diagrams and the like.
# By default, this class provides a reasonable representation
# of a graph node, but it can be parameterized for different
# "icons" and "terminals." More complex control over appearance
# can be achieved by subclassing.
#
# Note: Because this class is highly parameterized, it is not very
# efficient. A more efficient version with hardwired icon and
# terminal types could be created if this becomes a problem.
#
# <a href="../internals/howtoread.html">Reading <i>ComplexItem</i> documentation</a>.
#
# <b>Note</b>: This is an experimental class.
#
class ::tycho::Node {
    inherit ::tycho::ComplexItem

	# Create a new item
    proc construct {id canvas slate tags x0 y0 x1 y1 args}

    ###################################################################
    ####                            options                        ####

    # The font to use to display the icon name
    common _font

    # The type of item to use for the icon
    common _icontype

    # The positions of the input ports
    common _inputs

    # The type of item and options to use for the input ports
    common _intype

    # The name to display on the icon
    common _name

    # The positions of the output ports
    common _outputs

    # The type of item and options to use for the output ports
    common _outtype

    ###################################################################
    ####                     option update procs                   ####

	# Update the *-font* option
    proc _font {id canvas slate font}
    
	# Update the *-icontype* option
    proc _icontype {id canvas slate icontype}
    
	# Update the *-inputs* option
    proc _inputs {id canvas slate inputs}
    
	# Update the *-intype* option
    proc _intype {id canvas slate intype}
    
	# Update the *-name* option
    proc _name {id canvas slate name}
    
	# Update the *-outputs* option
    proc _outputs {id canvas slate outputs}
    
	# Update the *-outtype* option
    proc _outtype {id canvas slate outtype}

    ###################################################################
    ####                         public procs                      ####

	# Get an aspect
    proc aspect {id canvas slate args}
    
    # Get or set the coordinates of the Node's icon
    proc coords {id canvas slate args}
    
    # Access a component
    proc component {id canvas slate {name {}}}

    ###################################################################
    ####                         picture components                ####

    # The item used as the node's icon
    common iconitem

    # The label attached to the icon
    common textitem
    
    # The input ports
    common inports

    # The output ports
    common outports

    ###################################################################
    ####                         protected variables               ####

    # All methods are looked up through a table
    common methodtable

    # All options have a default value
    common optiondefault

    # The "direction" of the input ports
    common indirn

    # The "direction" of the output ports
    common outdirn

    ###################################################################
    ####                           private procs                   ####

    proc redrawinputs  {id canvas slate positions}
    proc redrawoutputs {id canvas slate positions}

    ###################################################################
    ####                       class initialization                ####

    #### Set method table
    array set methodtable [array get ::tycho::ComplexItem::methodtable]

    set methodtable(_font)       ::tycho::Node::_font
    set methodtable(_icontype)   ::tycho::Node::_icontype
    set methodtable(_inputs)     ::tycho::Node::_inputs
    set methodtable(_intype)     ::tycho::Node::_intype
    set methodtable(_name)       ::tycho::Node::_name
    set methodtable(_outputs)    ::tycho::Node::_outputs
    set methodtable(_outtype)    ::tycho::Node::_outtype

    set methodtable(aspect)      ::tycho::Node::aspect
    set methodtable(construct)   ::tycho::Node::construct
    set methodtable(component)   ::tycho::Node::component
    set methodtable(coords)      ::tycho::Node::coords
    set methodtable(update)      ::tycho::Node::update

    #### Set option defaults
    array set optiondefault [array get ::tycho::ComplexItem::optiondefault]

    set optiondefault(-font)     [::tycho::font Helvetica 12]
    set optiondefault(-icontype) {Frame -color green}
    set optiondefault(-inputs)   1
    set optiondefault(-intype)   {Terminal -type input}
    set optiondefault(-name)     "NoName"
    set optiondefault(-outtype)  {Terminal -type output}
    set optiondefault(-outputs)  1
}


##########################################################################
#### -font option configuration
#
body ::tycho::Node::_font {id canvas slate font} {
    set _font($id) $font
    $canvas itemconfigure $textitem($id) -font $font
}

##########################################################################
#### -icontype option configuration
#
body ::tycho::Node::_icontype {id canvas slate icontype} {
    set _icontype($id) $icontype

    $slate delete $iconitem($id)
    set iconitem($id) [eval $slate create [lindex $_icontype($id) 0] \
	    [$canvas coords $primary($id)] [lrange $_icontype($id) 1 end] \
	    -tags [list [$slate gettags $id]]]

    $slate lower $iconitem($id) $textitem($id)
}

##########################################################################
#### -inputs option configuration
#
# The option representing the positions of the input connections.
# Each of these is a list of connection positions, in % and relative
# to the top-left corner of the picture's outline. Each position
# is a pair, with each number the percentage distance along
# the horizontal and vertical axis respectively. For example,
# a single point in the center of the left edge is *{0 50}*;
# two points along the bottom edge could be *{25 100 75 100}*.
#
# By default, there is one input on the left edge and one output
# on the right edge.
#
# Setting this configuration option supports a handy shortcut:
# A single number sets the list to evenly spaced positions
# along the left (input) or right (output) edges. For example,
# the option *-inputs 2* to the constructor will result in
# the -inputs variable being set to *{0 25 0 75}*.
#
body ::tycho::Node::_inputs {id canvas slate inputs} {
    # Set up coordinates
    assign x0 y0 x1 y1 [$canvas coords $primary($id)]
    set xscale [expr ($x1-$x0)/100.0]
    set yscale [expr ($y1-$y0)/100.0]
    set positions {}

    # If the argument is an integer, generate evenly-spaced points
    # along the left axis.
    if { [llength $inputs] == 1 && [string match {[0-9]*} $inputs] } {
	set _inputs($id) {}
	set indirn($id) {}

	# If it's zero, then delete ports and we're done
	if { $inputs == 0 } {
	    if { [::info exists inports($id)] } {
		foreach p $inports($id) {
		    $slate delete $p
		}
	    }
	    return
	}
	foreach y [spread $inputs 0 100 -indented] {
	    lappend _inputs($id) 0 $y
	    lappend indirn($id) -1 0
	    lappend positions $x0 [expr $y0 + $yscale * $y]
	}
    } else {
	# The argument has the correct format
	set _inputs($id) $inputs
	set indirn($id) {}
	foreach {x y} $inputs {
	    if { $x == 0 } {
		lappend indirn($id) -1 0
		lappend positions $x0 [expr $y0 + $yscale * $y]
	    } elseif { $x == 100 } {
		lappend indirn($id) 1 0
		lappend positions $x1 [expr $y0 + $yscale * $y]
	    } elseif { $y == 0 } {
		lappend indirn($id) 0 -1
		lappend positions [expr $x0 + $xscale * $x] $y0
	    } elseif { $y == 100 } {
		lappend indirn($id) 0 1
		lappend positions [expr $x0 + $xscale * $x] $y1
	    } else {
		error "Invalid coordinates $x $y"
	    }
	}
    }
    # Now move/draw the terminals
    redrawinputs $id $canvas $slate $positions
}

##########################################################################
#### -intype option configuration
#
body ::tycho::Node::_intype {id canvas slate intype} {
    set _intype($id) $intype
    foreach p $inports($id) {
	$slate delete $p
    }
    set inports($id) {}

    _inputs $id $canvas $_inputs($id)
}

##########################################################################
#### -name option configuration
#
body ::tycho::Node::_name {id canvas slate name} {
    set _name($id) $name
    $canvas itemconfigure $textitem($id) -text $name
}

##########################################################################
#### -outputs option configuration
#
# See _inputs{} for comments.
#
body ::tycho::Node::_outputs {id canvas slate outputs} {
    # Set up coordinates
    assign x0 y0 x1 y1 [$canvas coords $primary($id)]
    set xscale [expr ($x1-$x0)/100.0]
    set yscale [expr ($y1-$y0)/100.0]
    set positions {}
    
    # If the argument is an integer, generate evenly-spaced points
    # along the right axis.
    if {  [llength $outputs] == 1 && [string match {[0-9]*} $outputs] } {
	set _outputs($id) {}
	set outdirn($id) {}

	# If it's zero, then delete ports and we're done
	if { $outputs == 0 } {
	    if { [::info exists outports($id)] } {
		foreach p $outports($id) {
		    $slate delete $p
		}
	    }
	    return
	}
	foreach y [spread $outputs 0 100 -indented] {
	    lappend _outputs($id) 100 $y
	    lappend outdirn($id) 1 0
	    lappend positions $x1 [expr $y0 + $yscale * $y]
	}
    } else {
	# The argument has the correct format
	set _outputs($id) $outputs
	set outdirn($id) {}
	foreach {x y} $outputs {
	    if { $x == 0 } {
		lappend outdirn($id) -1 0
		lappend positions $x0 [expr $y0 + $yscale * $y]
	    } elseif { $x == 100 } {
		lappend outdirn($id) 1 0
		lappend positions $x1 [expr $y0 + $yscale * $y]
	    } elseif { $y == 0 } {
		lappend outdirn($id) 0 -1
		lappend positions [expr $x0 + $xscale * $x] $y0
	    } elseif { $y == 100 } {
		lappend outdirn($id) 0 1
		lappend positions [expr $x0 + $xscale * $x] $y1
	    } else {
		error "Invalid coordinates $x $y"
	    }
	}
    }
    # Now move/draw the terminals
    redrawoutputs $id $canvas $slate $positions
}

##########################################################################
#### -outtype option configuration
#
body ::tycho::Node::_outtype {id canvas slate outtype} {
    set _outtype($id) $outtype
    foreach p $outports($id) {
	$slate delete $p
    }
    set outports($id) {}
    _outputs $id $canvas $_outputs($id)
}

##########################################################################
#### construct
#
body ::tycho::Node::construct {id canvas slate tags x0 y0 x1 y1 args} {
    # Initialize the options
    foreach {option value} [concat [array get optiondefault] $args] {
	set _[string trimleft $option -]($id) $value
    }

    # Create the primary component for the outline coordinates.
    set primary($id) [$canvas create rectangle $x0 $y0 $x1 $y1 \
	    -tags $tags]

    # Create the iconitem
    set iconitem($id) [eval $slate create [lindex $_icontype($id) 0] \
	    $x0 $y0 $x1 $y1 [lrange $_icontype($id) 1 end] \
	    -tags [list $tags]]

    # Create the label (should it be underneath?)
    set point [rectAspect [list $x0 $y0 $x1 $y1] "c"]
    set textitem($id) [eval $canvas create text $point \
	    -text $_name($id) -font $_font($id) \
	    -tags [list $tags]]

    # Create the input and output ports
    _inputs  $id $canvas $slate $_inputs($id)
    _outputs $id $canvas $slate $_outputs($id)
}


##########################################################################
#### aspect
#
# Return aspects of a node. Aspects are of the form *input-*_n_
# or $output-*_n_.
#
# FIXME: Make more efficient.
#
# FIXME: Do we really need this? Terminal positions can be found
# more easily by using component and then calling aspects on those.
# Plus it doesn't assume that the ports have an aspect called
# "terminal."
#
body ::tycho::Node::aspect {id canvas slate args} {
    set result {}

    # If there are no args, return a list of aspect names
    if { $args == "" } {
	loop [llength $inports($id)] -counter n {
	    lappend result input-$n
	}
	loop [llength $outports($id)] -counter n {
	    lappend result output-$n
	}
	return $result
    }
    
    # If there are args, return the aspect coordinates
    foreach aspect $args {
	set splut [split $aspect "-"]
	switch -exact [lindex $splut 0] {
	    "input" {
		set result [concat $result \
			[$slate aspect \
			[lindex $inports($id) [lindex $splut 1]] "terminal"]]
	    }
	    "output" {
		set result [concat $result \
			[$slate aspect \
			[lindex $outports($id) [lindex $splut 1]] "terminal"]]
	    }
	    default {
		error "unknown aspect $aspect"
	    }
	}
    }
    return $result
}


##########################################################################
#### component
#
# Return a component of a node. Component names are of the form *input-*_n_
# or $output-*_n_.
#
body ::tycho::Node::component {id canvas slate {name {}}} {
    # If there is no component argument, return a list of components
    if { $name == "" } {
	return [aspect $id $canvas]
    }
    
    # Otherwise, get the specified component
    set splut [split $name "-"]
    switch -exact [lindex $splut 0] {
	"input" {
	    return [lindex $inports($id) [lindex $splut 1]]
	}
	"output" {
	    return [lindex $outports($id) [lindex $splut 1]]
	}
	default {
	    error "unknown component $name"
	}
    }
}

##########################################################################
#### coords
#
body ::tycho::Node::coords {id canvas slate args} {
    # No args: just return coordinates
    if { $args == "" } {
	return [$canvas coords $primary($id)]
    }

    # Move the primary
    set coords $args
    eval $canvas coords $primary($id) $coords

    # Move the iconitem
    eval $slate coords $iconitem($id) $coords

    # Perform a generic update
    update $id $canvas

    # Set up coordinates for moving inputs and outputs
    assign x0 y0 x1 y1 $coords

    set xscale [expr ($x1-$x0)/100.0]
    set yscale [expr ($y1-$y0)/100.0]

    # Move inputs
    set positions {}
    foreach {x y} $_inputs($id) {
	if { $x == 0 } {
	    lappend positions $x0 [expr $y0 + $yscale * $y]
	} elseif { $x == 100 } {
	    lappend positions $x1 [expr $y0 + $yscale * $y]
	} elseif { $y == 0 } {
	    lappend positions [expr $x0 + $xscale * $x] $y0
	} elseif { $y == 100 } {
	    lappend positions [expr $x0 + $xscale * $x] $y1
	}  
    }
    redrawinputs $id $canvas $slate $positions

    # Move outputs
    set positions {}
    foreach {x y} $_outputs($id) {
	if { $x == 0 } {
	    lappend positions $x0 [expr $y0 + $yscale * $y]
	} elseif { $x == 100 } {
	    lappend positions $x1 [expr $y0 + $yscale * $y]
	} elseif { $y == 0 } {
	    lappend positions [expr $x0 + $xscale * $x] $y0
	} elseif { $y == 100 } {
	    lappend positions [expr $x0 + $xscale * $x] $y1
	}  
    }
    redrawoutputs $id $canvas $slate $positions
}


##########################################################################
#### redrawinputs
#
body ::tycho::Node::redrawinputs {id canvas slate positions} {
    if { ! [::info exists inports($id)] } {
	set inports($id) {}
    }
    set tags [$canvas gettags $id]
    foreach {x y} $positions {a b} $indirn($id) p $inports($id) {
	if { $p == "" } {
	    lappend inports($id) [eval $slate create \
		    [lindex $_intype($id) 0] \
		    $x $y \
		    [lrange $_intype($id) 1 end] \
		    -direction [list [list $a $b]] \
		    -tags [list $tags]]
	} else {
	    $slate coords $p $x $y
	    # Should be able to optimise this out
	    $slate itemconfigure $p \
		    -direction [list $a $b]
	}
    }
}

##########################################################################
#### redrawoutputs
#
body ::tycho::Node::redrawoutputs {id canvas slate positions} {
    if { ! [::info exists outports($id)] } {
	set outports($id) {}
    }
    set tags [$canvas gettags $id]
    foreach {x y} $positions {a b} $outdirn($id) p $outports($id) {
	if { $p == "" } {
	    lappend outports($id) [eval $slate create \
		    [lindex $_outtype($id) 0] \
		    $x $y \
		    [lrange $_outtype($id) 1 end] \
		    -direction [list [list $a $b]] \
		    -tags [list $tags]]
	} else {
	    $slate coords $p $x $y
	    # Should be able to optimise this out
	    $slate itemconfigure $p \
		    -direction [list $a $b]
	}
    }    
}

##########################################################################
#### update
#
# FIXME!!!
#
#body ::tycho::Node::update {id canvas slate} {
#    # Figure out where to put the text item
#    set point [rectAspect [$canvas coords $primary($id)] "c"]
#
#    # Move it there
#    eval $canvas coords $textitem($id) $point
#}
