#########################################################################
# @Version: $Id$
# @Author: John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


# Explicitly load the superclass. I don't really know why this is
# needed, but the common arrays get upset otherwise.
namespace ::tycho {
    if { [::info classes ComplexItem] == "" } {
        uplevel #0 {source $tychoslate/ComplexItem.itcl}
    }
}

##########################################################################
## LabeledItem
#
# An item with a label attached to it, and optional graphics drawn in it.
# This class is the abstract superclass of the LabeledOval, 
# LabeledRect, and LabeledFrame items.
#
# <p><b>Options</b>
#
# <ul>
# <li><b>-anchor</b>:
# The anchor position of the text label relative to the region
# occupied by the item. The anchor can be any of the compass points --
# <i>n</i>, <i>s</i>, <i>e</i>, <i>w</i>, <i>ne</i>, <i>se</i>,
# <i>nw</i>, <i>sw</i> -- and <i>c</i> or <i>center</i>. If it is
# <i>c</i> or <i>center</i> and there are graphics drawn on the item,
# the label is drawn underneath the item (as though the anchor was
# <i>s</i>).
#
# <li><b>-font</b>:
# The font to use for the label. Currently, this must be a valid X font.
#
# <li><b>-graphics</b>:
# The graphics to draw on the item. This option consists of zero or
# more item specs, where an item spec is the item type, coordinates, and
# options. The coordinates are specified as a percentage of the item
# size. Item specs are separates by newlines or semi-colons.
#
# <li><b>-text</b>:
# The label of the item. If there are graphics also displayed, the
# label is placed outside the item; if not, the label is displayed
# outside the item.  By default, this option is "", which means that
# nothing is displayed.
#
# </ul>
#
# <p><b>Components</b>
#
# <ul>
# <li><b>label</b>:
# The editable text label.
# </ul>
#
# <p><b>Bugs</b>
#
# If there are graphics and the anchor is "e" or "w," the label
# is displayed inside the icon not outside.
#
# <p><b>Enhancements</b>
#
# There are four possible positions for the label (on the left and right
# sides, near the top and bottom of that edge) that cannot be specified.
#
# <p><b>Links</b>
# 
# <ul>
# <li><a href="../internals/howtoread.html">Reading <i>ComplexItem</i>
# documentation</a>.
# <li><a href="../internals/items.html">Complex item classes</a>.
# </ul>
#
class ::tycho::LabeledItem {
    inherit ::tycho::ComplexItem

    ###################################################################
    ####                            options                        ####

    # Update the *-anchor* option
    proc _anchor {id canvas slate anchor}
    
    # Update the *-font* option
    proc _font {id canvas slate font}

    # Update the *-graphics* option
    proc _graphics {id canvas slate graphics}

    # Update the *-text* option
    proc _text {id canvas slate text}

    ###################################################################
    ####                           private variables               ####

    # The virtual "method" table
    private common methodtable

    # Default values of this item's options
    private common optiondefault

    ###################################################################
    ####                       class initialization                ####

    #### Set method table
    array set methodtable [array get ::tycho::ComplexItem::methodtable]

    set methodtable(_anchor)   ::tycho::LabeledItem::_anchor
    set methodtable(_font)     ::tycho::LabeledItem::_font
    set methodtable(_graphics) ::tycho::LabeledItem::_graphics
    set methodtable(_text)     ::tycho::LabeledItem::_text

    #### Set option defaults
    array set optiondefault [array get ::tycho::ComplexItem::optiondefault]

    set optiondefault(-anchor)		"center"
    set optiondefault(-font)		[::tycho::font Helvetica 12]
    set optiondefault(-graphics)	""
    set optiondefault(-text)		""
}

##########################################################################
## -anchor option configuration
#
body ::tycho::LabeledItem::_anchor {id canvas slate anchor} {
    set option(anchor$id) $anchor

    # If there are no graphics, place inside the item, else place
    # outside the item
    if { $option(text$id) != "" } {
        eval $canvas coords $component(label$id) \
		[$slate aspect $id $anchor -region]
        if { $option(graphics$id) == "" } {
            $canvas itemconfigure $component(label$id) -anchor $anchor
        } else {
            if { $anchor == "center" || $anchor == "c" } {
                set anchor "s"
                eval $canvas coords $component(label$id) \
			[$slate aspect $id s -region]
            }
            $canvas itemconfigure $component(label$id) \
                    -anchor [::tycho::Shape::compass $anchor "ud"]
        }
    }
}

##########################################################################
## -font option configuration
#
body ::tycho::LabeledItem::_font {id canvas slate font} {
    set option(font$id) $font
    $canvas itemconfigure $component(label$id) -font $font
}

##########################################################################
## -graphics option configuration
#
# Draw the specified items specified using coordinates in
# percent of the parent item's size. <i>Do not specify tags.</i>
#
body ::tycho::LabeledItem::_graphics {id canvas slate graphics} {
    set option(graphics$id) $graphics

    # Delete existing graphics
    $slate delete graphics.$id

    # If graphics are empty, do nothing
    if { $graphics == "" } {
        return
    }

    # Get my hierarchy tags
    set htags [$canvas gettags $primary($id)]

    # Draw each item at full scale
    set tags [concat graphics.$id [$canvas gettags $primary($id)]]
    foreach spec [split $graphics ";\n"] {
	if { [string trim $spec " \t"] != "" } {
	    eval [list $slate createchild* $id $htags graphics.$id] $spec
	}
    }

    # Move and scale them to fit into this item
    ::tycho::assign x0 y0 x1 y1 [$slate region $id]
    $slate move graphics.$id $x0 $y0
    $slate scale graphics.$id $x0 $y0 \
    		[expr ($x1-$x0)/100.0] \
    		[expr ($y1-$y0)/100.0]
    
    # Move the label outside the item if necessary
    if { $graphics != "" && $option(text$id) != "" } {
    	 _anchor $id $canvas $slate $option(anchor$id)
    }
}

##########################################################################
## -text option configuration
#
body ::tycho::LabeledItem::_text {id canvas slate text} {
    if { $text != "" } {
        if { ! [::info exists component(label$id)] } {
	    ::tycho::assign x0 y0 x1 y1 [$canvas coords $primary($id)]
	    set component(label$id) [$canvas create text \
                    [expr ($x0+$x1)/2] \
		    [expr ($y0+$y1)/2] \
                    -text $text \
		    -font $option(font$id) \
		    -tags  [$canvas gettags $primary($id)]]
            _anchor $id $canvas $slate $option(anchor$id)
        } else {
	    $canvas itemconfigure $component(label$id) -text $text
	}
    } else {
        catch {$canvas delete $component(label$id)}
    }
    set option(text$id) $text
}
