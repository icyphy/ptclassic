##########################################################################
# @Version: $Id$
# @Author: H. John Reekie
#
# @Copyright (c) 1996 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
#                                                         COPYRIGHTENDKEY
##########################################################################


#######################################################################
#### AsLine
#
# A shape for lines.
#
class ::tycho::AsLine {
    inherit ::tycho::AsPolygon

    ###################################################################
    ####                         public procs                      ####

    # Get one or more aspects of an item
    proc aspect {id canvas slate args}

    # Attach a set of grapples to an item
    proc grapple {id canvas slate interactor args}

    # Reshape one or more aspects of an item
    proc reshape {id canvas slate x y args}

    # Start a reshape interaction sequence
    proc reshapeclick {id canvas slate x y args}

    # Continue a reshape interaction sequence
    proc reshapedrag {id canvas slate x y}

    # Terminate a reshape interaction sequence
    proc reshaperelease {id canvas slate x y}
}


#######################################################################
#### aspect
#
# Return the value of an aspect. Valid aspects are "start,"
# "end," "length," or <code>vertex-</code><i>n</i>, where _n_ is
# between 0 and _length_.
#
body ::tycho::AsLine::aspect {id canvas slate args} {
    set coords [$slate coords $id]
    set result {}

    if { $args == "" } {
	lappend result length start end
	foreach n [interval 0 \
		[expr [llength $coords] / 2 - 1]] {
	    lappend result "vertex-$n"
	}
    } else {
	foreach aspect $args {
	    set a [lindex [split $aspect "-"] 0]
	    switch -exact $a {
		"length" {
		    lappend result [expr [llength $coords] / 2 - 1]
		}
		"start" {
		    lappend result [lindex $coords 0] [lindex $coords 1]
		}
		"end" {
		    lappend result \
			    [lindex $coords [expr [llength $coords] - 2]] \
			    [lindex $coords end]
		}
		"vertex" {
		    set n [lindex [split $aspect "-"] 1]
		    lappend result \
			    [lindex $coords [expr 2*$n]] \
			    [lindex $coords [expr 2*$n + 1]]
		}
		default {
		    error "Invalid aspect \"$aspect\" of line shape"
		}
	    }
	}
    }
    return $result
}

#######################################################################
#### reshape
#
# Reshape one or more aspects of the item by the specified amount.
# Reshape each aspect by the specified _x_ and _y_ relative
# values. Aspects are only affected by the appropriate _x_ or _y_
# value -- for example, if the "e" (east) aspect is specified, it
# is moved only by the _x_ amount.
#
body ::tycho::AsLine::reshape {id canvas slate x y args} {
    # Get the coordinates of the line into an efficient-to-process form
    set i 0
    set indexes {}
    foreach {a b} [$slate coords $id] {
		set _x($i) $a
		set _y($i) $b
		lappend indexes $i
		incr i
    }

    # Process all aspects to change coordinates
    foreach aspect $args {
	assign a n [split $aspect "-"]
	switch -exact $a {
	    "start" {
		set _x(0) [expr $_x(0) + $x]
		set _y(0) [expr $_y(0) + $y]
	    }
	    "end" {
		set n [lindex $indexes end]
		set _x($n) [expr $_x($n) + $x]
		set _y($n) [expr $_y($n) + $y]
	    }
	    "length" {
		error "Cannot set the \"length\" aspect of a line"
	    }
	    "vertex" {
		set _x($n) [expr $_x($n) + $x]
		set _y($n) [expr $_y($n) + $y]
	    }
	    default {
		error "Invalid aspect \"$aspect\" of line shape"
	    }
	}
    }

    # Rebuild the coordinates list and change coordinates
    set coords {}
    foreach i $indexes {
	lappend coords $_x($i) $_y($i)
    }
    eval $slate coords $id $coords
}

#######################################################################
#### reshapeclick
#
body ::tycho::AsLine::reshapeclick {id canvas slate x y args} {
    getflag ghost args

    # Remember how I am now.
    set scratch($id.prevX) $x
    set scratch($id.prevY) $y

    # Get the coordinates of the line into an efficient-to-process form
    set i 0
    foreach {a b} [$slate coords $id] {
		set scratch($id.$i.x) $a
		set scratch($id.$i.y) $b
		lappend scratch($id.indexes) $i
		incr i
    }

    # Figure out the vertices that will be changed
    foreach aspect $args {
		assign a n [split $aspect "-"]
		switch -exact $a {
	    	"start" {
				lappend scratch($id.vertices) 0
			}
		    "end" {
				lappend scratch($id.vertices) [lindex $scratch($id.indexes) end]
		    }
	    	"vertex" {
	    		lappend scratch($id.vertices) $n
	    	}
		    "length" {
				error "Cannot set the \"length\" aspect of a line"
	    	}
		    default {
				error "Invalid aspect \"$aspect\" of line shape"
			}
		}
    }
    
    # Make sure there are no duplicates
    set scratch($id.vertices) [lnub scratch($id.vertices)]

    # If the -ghost flag is set, draw the ghost line.
    if { $ghost } {
		set scratch($id.ghost) [eval $canvas create line \
			$scratch($id.coords) \
			-fill grey]
    }
}

#######################################################################
#### reshapedrag
#
# This just calls the "parent" class -- we should probably
# implement "inheritance."
#
body ::tycho::AsLine::reshapedrag {id canvas slate x y} {
	::tycho::AsPolygon::reshapedrag $id $canvas $slate $x $y
}

#######################################################################
#### reshaperelease
#
# This just calls the "parent" class -- we should probably
# implement "inheritance."
#
body ::tycho::AsLine::reshaperelease {id canvas slate x y} {
	::tycho::AsPolygon::reshaperelease $id $canvas $slate $x $y
}
