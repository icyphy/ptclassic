# Definition of an editor for editing source code in various languages.
#
# @Author: Edward A. Lee
#
# @Version: $Id$
#
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### EditProgram
# This class defines a window that provides an emacs-like editor
# specialized for editing program files. It colorizes the code,
# identifying comments, automatically indents the code, and fills
# blocks of comments. Derived classes are specialized to particular
# programming languages. This class provides a common infrastructure.
# In particular, it provides a common mechanism for handling
# line-oriented comments, and indentation driven by brackets, braces,
# and parentheses. It also tags and colors colors comments and the
# strings "FIXME" and "NOTE", provides infrastructure for tagging and
# coloring keywords, and provides a method for commenting out a region.
#
class ::tycho::EditProgram {
    inherit ::tycho::Edit

    ###################################################################
    ####                     public methods                        ####

    constructor {args} {}
    destructor {}

    # Open a help window.  This should be redefined in derived classes.
    method help {}

    # Specify whether or not modifications are allowed in the displayed text.
    method setReadOnly {bool}

    ###################################################################
    ####                  protected methods                        ####

    # Return the name of the type of text widget to use (EditProgramText).
    protected method textWidget {} {return ::tycho::EditProgramText}
}

###################################################################
#
body ::tycho::EditProgram::constructor {args} {
    
    eval itk_initialize $args

    # Add to the format menu commands to comment and uncomment
    # the regions defined by the selection.
    addMenuSeparator Format
    
    addMenuItem "Comment Out Region" Format -underline 0 \
	    -command "$this component text commentSelection"
    
    addMenuItem "Uncomment Region" Format -underline 0 \
	    -command "$this component text uncommentSelection"
    
    addMenuSeparator Format
    
    addMenuItem "Recolorize" Format -underline 0 -accelerator "S-C-l" \
	    -command "$this component text parseRegion 1.0 end"
    
    addMenuItem "Indent Line" Format -underline 0 -accelerator "Tab" \
	    -command "$this component text indentLine"

    addMenuItem "Indent Region" Format -underline 1 -accelerator "Esc C-\\" \
	    -command "$this component text busy indentSelection"
    
    addMenuItem "Tab" Format -underline 0 -accelerator "C-q Tab" \
	    -command "$this component text insertString insert {\t}"
}

###################################################################
#### help
# Open a window with help on the current widget.
#
body ::tycho::EditProgram::help {} {
    global ::TYCHO
    ::tycho::File::openContext \
	    $TYCHO/editors/textedit/doc/usersGuides/EditProgramHelp.html
}

#####################################################################
#### setReadOnly
# Specify whether or not modifications are allowed in the displayed text.
# Note that this is independent of whether the associated file is readonly.
# It merely determines whether the buffer can be modified, not whether the
# file can be modified.  To control the file, use <code>toggleReadOnly</code>.
#
body ::tycho::EditProgram::setReadOnly {bool} {
    ::tycho::Edit::setReadOnly $bool
    if [winfo exists $prefix] {
	if {$bool} {
            disableMenuItem {Comment Out Region} Format
            disableMenuItem {Uncomment Region} Format
            disableMenuItem {Indent Line} Format
            disableMenuItem {Indent Region} Format
            disableMenuItem {Tab} Format
	} {
            enableMenuItem {Comment Out Region} Format
            enableMenuItem {Uncomment Region} Format
            enableMenuItem {Indent Line} Format
            enableMenuItem {Indent Region} Format
            enableMenuItem {Tab} Format
	}
    }
}
