# Definition of a Mathematica console widget.
#
# @Authors: Joel R. King and Edward A. Lee
#
# @Version: $Id$
#
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### Mathematica
# A Mathematica widget is a Console widget that accepts Mathematica 
# commands instead of Tcl commands.
#
class ::tycho::Mathematica {
    inherit ::tycho::TclShell

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                         public methods                    ####

    # Evaluate a command. Return the result.
    method evalCommand {com}

    # Evaluate a the current command in the text widget.
    method evalTextEntry {}

    # Open a help window.
    method help {}

    ###################################################################
    ####                        protected methods                  ####

    # Initialize the menu/status/tool bars associated with this widget.
    protected method initialize {{menubar {}} {statusbar {}} {toolbar {}}}

    ##########################################################################
    ####                          common members                          ####
    
    # Keep a count on the number of Consoles.
    private common consoleCount 0

}

###################################################################
#
body ::tycho::Mathematica::constructor {args} {

    if {[uplevel #0 info commands mathematica] == {}} {
	error "Command mathematica not defined. \
		Perhaps you are not running Ptolemy?"
    }

    # Start mathematica, if necessary
    if {[mathematica status] != 0} {
	uplevel #0 [list \
		::tycho::Message .patience -text \
		{Starting Mathematica.  Please be patient.}]
	.patience centerOnScreen
	update
	uplevel #0 mathematica start Mathematica
	if [winfo exists .patience] {
	    delete object .patience
	}
    }
    if {[mathematica status] != 0} {
	error "Failed to start Mathematica!"
    }
  
    incr consoleCount 
  
    eval itk_initialize $args
}

###################################################################
#
body ::tycho::Mathematica::destructor {} {

    incr consoleCount -1

    if {$consoleCount == 0} {
	mathematica end Mathematica
    }

}

    ###################################################################
    ###################################################################
    ####                      public methods                       ####

#########################################################################
#### evalCommand
# Evaluate a command. Return the result.
#
body ::tycho::Mathematica::evalCommand {com} {
    # To be sure a prompt gets printed even if an error occurs,
    # catch errors here
    if {[catch {mathematica eval $com} retval] != 0} {
	tkerror $retval
    }
    return $retval
}

#########################################################################
#### evalTextEntry
# Evaluate the current command in the text widget.
# There seems to be no way to tell whether a Mathematica command is
# complete.
#
body ::tycho::Mathematica::evalTextEntry {} {

    append command [$itk_component(text) get "promptEnd + 1 chars" end]
    
    # Add it to history in the first position.
    set com [string trim $command " \n\t"]
    if {$com != ""} {lappend commandList $com}
    set historypos 0
	
    set result [evalCommand $command]
    set command ""
	
    # Note: cannot use displayResult here because it will only
    # conditionally display the result.
    if {$result != ""} {
	insertWithTags $result\n result
	$itk_component(text) tag config result \
		-foreground \
		[option get $itk_component(hull) answerForeground \
		Mathematica]
    }
    printPrompt $result
}

###################################################################
#### help
# Open a help window.
#
body ::tycho::Mathematica::help {} {
    global ::TYCHO
    ::tycho::File::openContext \
        [file join $TYCHO editors textedit doc usersGuides \
	    EditMathematicaHelp.html]
}

    ###################################################################
    ###################################################################
    ####                     protected methods                     ####

#####################################################################
#### initialize
# Set myDisplayer if I'm in a displayer, and initialize the menu, status, 
# and toolbars associated with this widget.
#
# This functionality used to be in the constructor, however that design
# was not flexible and didn't fit with the notion of multiple views.
#
body ::tycho::Mathematica::initialize {{menubar {}} \
        {statusbar {}} {toolbar {}}} {
    chain $menubar $statusbar $toolbar
   
    displayer configure -title "Mathematica Shell"
}

