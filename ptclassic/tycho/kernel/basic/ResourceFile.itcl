# The class representing files on a local file system
# 
# @Author: John Reekie
#
# @Version: $Id$
# 
# @Copyright (c) 1997 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### ResourceFile
#
# The ResourceFile class abstracts the notion of files and (t some extent)
# a file system. A given ResourceFile object can be used as a directory
# or as a file, making it very convenient for both moving around
# file systems and accessing files within it. The data accessed by a
# resource could be a local file system, an HTTP server, a database,
# or anywhere else. Streamable resources act like regular files;
# non-streamable resource act like data repositories. In all cases,
# file and directory paths use a URL-like naming scheme.
#
class ::tycho::ResourceFile {
    inherit ::tycho::Resource

    constructor {args} {}
    destructor {}

    #####################################################################
    ####                          options                            ####

    ###################################################################
    ####                         public methods                    ####

    # Close the resource
    method close {}

    # Return the content type of the resource
    method contenttype {}

    # Return the end of file status
    method eof {}

    # Flush buffered output to a streamable resource 
    method flush {}

    # Read a string from a streamable resource 
    method gets {{varName {}}}

    # Test if the file is currently open
    method isopen {}

    # Open the resource
    method open {{mode "r"}}

    # Write a string to a streamable resource 
    method puts {string}

    # Return a string containing the data in a streamable resource 
    method read {}

    # Return 1 if this resource is streamable
    method streamable {}

    # Write a string to a streamable resource
    method write {data}

    ###################################################################
    ####                           public procs                    ####

    # Change the permissions of a resource
    proc chmod {path permissions}

    # Delete a resource
    proc delete {path}

    # Return a directory listing
    proc dirlist {path}

    # Test if a given resource exists
    proc exists {path}

    # Create a new directory
    proc mkdir {path}

    # Create a new resource object
    proc new {path}

    # Query properties of a file
    proc query {path option}

    # Source the file as Tcl
    proc source {path}

    # Get the status of a resource
    proc stat {path varName}

    # Return the type of a given file
    proc type {path}

    ###################################################################
    ####                         protected methods                 ####

    ###################################################################
    ####                        protected variables                ####

    ###################################################################
    ####                          private variables                ####
    
    # The current channel (file) ID
    private variable fileid ""
    
    # True if the file is open
    private variable fileIsOpen 0
}


#######################################################################
#### constructor
#
body ::tycho::ResourceFile::constructor {args} {
    eval configure $args
}

#######################################################################
#### destructor
#
body ::tycho::ResourceFile::destructor {} {
    $this close
}

#########################################################################
#########################################################################
####                      public methods                             ####

#####################################################################
#### close
body ::tycho::ResourceFile::close {} {
    if { $fileid != "" } {
	::close $fileid
    }
    set fileid ""
    set fileIsOpen 0
}

#####################################################################
#### contenttype
# Return the content type of the open file.
body ::tycho::ResourceFile::contenttype {} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    # For now, just return "text/plain". Later on, look at the
    # file extension and/or contents.
    return "text/plain"
}

#####################################################################
#### flush
body ::tycho::ResourceFile::flush {} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    ::flush $fileid
}

#####################################################################
#### eof
body ::tycho::ResourceFile::eof {} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    ::eof $fileid
}

#####################################################################
#### gets
body ::tycho::ResourceFile::gets {{varName {}}} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    if { $varName == "" } {
	::gets $fileid
    } else {
	upvar $varName v
	uplevel ::get $fileid v
    }
}

#####################################################################
#### open
body ::tycho::ResourceFile::open {{mode "r"}} {
    if $fileIsOpen {
	$this close
    }
    set path [path]
    regsub {^file:} $path {} path
    set fileid [::open $path $mode]
    set fileIsOpen 1
    return 1
}

#####################################################################
#### isopen
body ::tycho::ResourceFile::isopen {} {
    return $fileIsOpen
}

#####################################################################
#### puts
body ::tycho::ResourceFile::puts {string} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    ::puts $fileid $string
}

#####################################################################
#### read
body ::tycho::ResourceFile::read {} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
	::read $fileid
}

#####################################################################
#### streamable
body ::tycho::ResourceFile::streamable {} {
    return 1
}

#####################################################################
#### write
body ::tycho::ResourceFile::write {data} {
    if !$fileIsOpen {
	error "File \"$this\" is not open"
    }
    ::puts -nonewline $fileid $data
}


#########################################################################
#########################################################################
####                          public procedures                      ####

#####################################################################
#### chmod
body ::tycho::ResourceFile::chmod {path permissions} {
    # NOTE: Unix-dependent implementation
    regsub {^file:} $path {} path
    exec chmod $permissions $path
}

#####################################################################
#### delete
body ::tycho::ResourceFile::delete {path} {
    # FIXME: Check if file is open?
    regsub {^file:} $path {} path
    ::file delete $path
}

#####################################################################
#### dirlist
# Get a "listing" of files in a "directory." This parses
# the received index text to extract names from it. The
# list is sorted, with directory names ending in "/",
# in a similar fashion to ls -F.
#
body ::tycho::ResourceFile::dirlist {path} {
    regsub {^file:} $path {} path
    set result {}
    foreach f [::glob [file join $path *]] {
	if [::file isdirectory $f] {
	    lappend result [file tail $f]/
	} else {
	    lappend result [file tail $f]
	}
    }
    # Sort and return
    lsort $result
}

#####################################################################
#### exists
body ::tycho::ResourceFile::exists {path} {
    regsub {^file:} $path {} path
    ::file exists $path
}

#####################################################################
#### mkdir
body ::tycho::ResourceFile::mkdir {path} {
    regsub {^file:} $path {} path
    ::file mkdir $path
}

#####################################################################
#### new
body ::tycho::ResourceFile::new {path} {
    uplevel #0 ::tycho::ResourceFile [::tycho::autoName resourcefile]
}

#####################################################################
#### query
body ::tycho::ResourceFile::query {path option} {
    # Check for "isremote"
    if { $option == "isremote" } {
	return 0
    }
    # Check for valid option
    if { [lsearch {executable exists isdirectory \
	    isfile readable writable} $option] >= 0 } {
	regsub {^file:} $path {} path
	::file $option $path
    }
}

#####################################################################
#### source
body ::tycho::ResourceFile::source {path} {
    regsub {^file:} $path {} path
    ::source $path
}

#####################################################################
#### stat
body ::tycho::ResourceFile::stat {path varName} {
    regsub {^file:} $path {} path
    if [::file exists $path] {
	upvar $varName v
	::file stat $path v
    }
}
