# Simple color browser.
#
# @Author: Edward A. Lee
#
# @Version: $Id$
#
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

##########################################################################
#### querycolor
# Query the user for a color and return it.  This procedure brings up
# a simple color browser, and returns only when that browser is dismissed.
# The returned string is the name of a color.  If the user cancels the
# dialog, the given default (or null if not given) is returned.
# This name should be passed
# to <code>::tycho::color</code> before being used, to convert it to a standard
# color format recognized by all installations.  One argument is required:
# a string that labels the query.  If a second argument is given,
# it is the default color in the entry box.
# <p>
# This procedure indirectly calls <code>update</code>, a Tk utility
# that processes pending events, including mouse events. If this
# procedure is called from within an Itcl object, directly or
# indirectly, it may be necessary to take precautions to prevent the
# calling object from being deleted during the call. Because of a
# defect in at least some versions of Itcl, failure to do so can result
# in catastrophic failure of the application (a core dump). In Tycho
# objects, the <code>safeEval</code> method should be used to evaluate
# this procedure or any method or procedure that in turn calls this
# procedure. If the procedure is called from the top level or from a
# Tcl procedure that is not itself called from within an Itcl object,
# then there is no cause for worry.
#
proc ::tycho::querycolor {string {default {}}} {
    set name [autoName .color]
    uplevel #0 [list ::tycho::ColorBrowser $name -text $string]
    if {$default != {}} {
        $name configure -default $default
    }
    set color [::tycho::Dialog::wait $name]
    if { $color == "" } {
	return $default
    } else {
	return $color
    }
}

##########################################################################
#### ColorBrowser
# A simple color browser.  The list of colors recognized by Tycho by name
# are displayed in a list box.  Clicking on any color (or using the standard
# mechanism in Tycho list browsers to scroll through pattern matches) causes
# the selected color to be displayed.  For example, to scroll through all
# the greens, enter "*green*" in the entry box and repeatedly hit <Tab>.
# Typing <Return> dismisses the window. Note that the display may not be
# perfect, since it relies on Tk's colormap handling.  If you run out of
# colors in your colormap, Tk selects the nearest color in the colormap.
#
class ::tycho::ColorBrowser {
    inherit ::tycho::ListBrowser

    constructor {args} {}
    destructor {}

    ##################################################################
    ####                     public methods                       ####

    # Open a help window.
    method help {}

    ##################################################################
    ####                   protected methods                      ####

    # Set the contents of the entry box and display the color.
    protected method setEntry {string}

    # Update the listbox with colors.
    protected method updateListbox {}

    ##################################################################
    ####                   protected variables                    ####

    # The directory of the index file.
    protected variable CWD {}

    # The entries in the index.
    protected variable entries
}

######################################################################
#
body ::tycho::ColorBrowser::constructor {args} {
    # Frame for displaying colors
    itk_component add color {
	frame $itk_component(hull).color -height 1c -relief raised \
                -borderwidth 5
    } {
	keep -cursor
    }
    pack $itk_component(color) -side top -anchor nw\
	    -expand yes -fill x -after $itk_component(querysite)

    # Process any unevaluated arguments.
    eval itk_initialize $args

    # Subscribe to the same preferences as an index browser
    preference subscribeoptions $this \
	    -width  indexBrowserWidth \
	    -height indexBrowserHeight

    # Load the colors
    updateListbox
}


    ##################################################################
    ##################################################################
    ####                     public methods                       ####

######################################################################
#### help
# Open a help window.
#
body ::tycho::ColorBrowser::help {} {
    global ::TYCHO
    ::tycho::File::openContext \
	    [file join $TYCHO kernel doc usersGuides ColorBrowserHelp.html]
}

    ##################################################################
    ##################################################################
    ####                   protected methods                      ####

######################################################################
#### updateListbox
# Construct the list box contents from the color manager.
#
body ::tycho::ColorBrowser::updateListbox {} {
    set colors [lsort [::tycho::ColorManager::colorList]]
    clearListbox
    appendItems $colors
}

######################################################################
#### setEntry
# This method clears the current contents of the entry box and replaces
# them with the string passed as an argument.  In addition, it displays
# the color named in the entry box.
#
body ::tycho::ColorBrowser::setEntry {string} {
    chain $string
    $itk_component(color) configure -background [::tycho::color $string]
}
