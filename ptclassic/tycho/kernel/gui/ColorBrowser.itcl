# Simple color browser.
#
# @Author: Edward A. Lee
#
# @Version: $Id$
#
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

##########################################################################
#### querycolor
# Query the user for a color and return it.  This procedure brings up
# a simple color browser, and returns only when that browser is dismissed.
# The returned string is the name of a color.  This name should be passed
# to <code>ptkColor</code> before being used, to convert it to a standard
# color format recognized by all installations.  If an argument is given,
# it is the default color in the entry box.
# <p>
# This procedure indirectly calls <code>update</code>, a Tk utility
# that processes pending events, including mouse events. If this
# procedure is called from within an Itcl object, directly or
# indirectly, it may be necessary to take precautions to prevent the
# calling object from being deleted during the call. Because of a
# defect in at least some versions of Itcl, failure to do so can result
# in catastrophic failure of the application (a core dump). In Tycho
# objects, the <code>safeEval</code> method should be used to evaluate
# this procedure or any method or procedure that in turn calls this
# procedure. If the procedure is called from the top level or from a
# Tcl procedure that is not itself called from within an Itcl object,
# then there is no cause for worry.
#
proc ::tycho::querycolor {{default {}}} {
    set name [autoName .color]
    uplevel #0 ::tycho::ColorBrowser $name
    if {$default != {}} {
        $name configure -default $default
    }
    return [::tycho::Dialog::wait $name]
}

##########################################################################
#### ColorBrowser
# A simple color browser.  The list of colors recognized by Tycho by name
# are displayed in a list box.  Clicking on any color (or using the standard
# mechanism in Tycho list browsers to scroll through pattern matches) causes
# the selected color to be displayed.  For example, to scroll through all
# the greens, enter "*green*" in the entry box and repeatedly hit <Tab>.
# Typing <Return> dismisses the window. Note that the display may not be
# perfect, since it relies on Tk's colormap handling.  If you run out of
# colors in your colormap, Tk selects the nearest color in the colormap.
#
class ::tycho::ColorBrowser {
    inherit ::tycho::ListBrowser

    constructor {args} {}
    destructor {}

    ##################################################################
    ####                     public methods                       ####

    # Open a help window.
    method help {}

    ##################################################################
    ####                   protected methods                      ####

    # Set the contents of the index.
    protected method setContents {}

    # Set the contents of the entry box and display the color.
    protected method setEntry {string}

    ##################################################################
    ####                   protected variables                    ####

    # The directory of the index file.
    protected variable CWD {}

    # The entries in the index.
    protected variable entries
}

######################################################################
#
body ::tycho::ColorBrowser::constructor {args} {
    # Frame for displaying colors
    itk_component add color {
	frame $itk_component(hull).color -height 1c -relief raised \
                -borderwidth 5
    } {
	keep -cursor
    }
    pack $itk_component(color) -side top -anchor nw\
	    -expand yes -fill x -after $itk_component(radiosite)

    # Process any unevaluated arguments.
    eval itk_initialize $args

    # Subscribe to the same preference set as an index browser
    preference subscribe indexbrowser $this \
	    -width width \
	    -height height

    setContents
}

###################################################################
#### destructor
# Unsubscribe from all preferences
#
body ::tycho::ColorBrowser::destructor {} {
    # Unsubscribe from all preferences
    preference unsubscribe indexbrowser $this
}

    ##################################################################
    ##################################################################
    ####                     public methods                       ####

######################################################################
#### help
# Open a help window.
#
body ::tycho::ColorBrowser::help {} {
    global ::TYCHO
    ::tycho::File::openContext \
	    [file join $TYCHO kernel doc usersGuides ColorBrowserHelp.html]
}

    ##################################################################
    ##################################################################
    ####                   protected methods                      ####

######################################################################
#### setContents
# Construct the list box contents from the ptkColorList array.
#
body ::tycho::ColorBrowser::setContents {} {
    global ::ptkColorList
    set colors [lsort [array names ptkColorList]]
    configure -items $colors
}

######################################################################
#### setEntry
# This method clears the current contents of the entry box and replaces
# them with the string passed as an argument.  In addition, it displays
# the color named in the entry box.
#
body ::tycho::ColorBrowser::setEntry {string} {
    ::tycho::ListBrowser::setEntry $string
    $itk_component(color) configure -background [::ptkColor $string]
}
