# Stack display for Tcl errors.
#
# @Author: Edward A. Lee
#
# @Version: $Id$
#
# @Copyright (c) %Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### options
# NOTE: Unfortunately, itcl appears to require that all options
# from the base class be repeated here with the new class name.
# Otherwise, they are not recognized.
option add *EditStack.textWidth 80 widgetDefault
option add *EditStack.textHeight 30 widgetDefault
option add *EditStack.parenColor [ptkColor lightskyblue white] widgetDefault
option add *EditStack.targetColor [ptkColor lightskyblue white] widgetDefault
option add *EditStack.fillMaxLineLength 70 widgetDefault
option add *EditStack.textBackground \
	[ptkColor antiqueWhite white] widgetDefault
option add *EditStack.textfont [.tychoFonts defaultFont fixed]
option add *EditStack.relief raised widgetDefault
option add *EditStack.selectColor [ptkColor yellow white] widgetDefault

# new options

option add *EditStack.errorColor \
	[ptkColor red black] widgetDefault
option add *EditStack.commentColor \
	[ptkColor forestGreen black] widgetDefault
option add *EditStack.titleColor \
	[ptkColor black black] widgetDefault
option add *EditStack.linkColor \
	[ptkColor blue black] widgetDefault

#######################################################################
#### EditStack
# This class defines a text widget for displaying Tcl stack trace information.
#
class ::tycho::EditStack {
    inherit ::tycho::Edit

    constructor {args} {}
    destructor {}

    ##########################################################################
    ####                         public methods                           ####

    # Open a help window.
    method help {}

    # Color the stack information in a region.
    method parseNow {start stop}
}

###################################################################
#
body ::tycho::EditStack::constructor {args} {

    eval itk_initialize $args

# FIXME:
#    wm iconname $prefix "Stack Trace"
#    wm title $prefix "Stack Trace"

    markUnmodified

    # Remove irrelevant capabilities from the menus.
    menubar disableMenuItem  "Switch File..." File
    menubar disableMenuItem  "Insert..." File
    menubar disableMenuItem  "Save" File
    menubar disableMenuItem  Reload File
    menubar disableMenuItem  "Revision Control..." File

    menubar disableMenuItem "Undo/Redo" Edit
    menubar disableMenuItem "Cut" Edit
    menubar disableMenuItem "Paste" Edit

    menubar disableMenuItem "Fill Region" Format
    menubar disableMenuItem "Right Fill Column" Format
    menubar disableMenuItem "Set Fill Prefix" Format
    
    # The default fill prefix works for Tcl and shell scripts.
    set fillPrefix "#"
}

###################################################################
#### help
# Open a help window.
#
body ::tycho::EditStack::help {} {
    global ::TYCHO
    ::tycho::File::openContext \
	    $TYCHO/editors/textedit/doc/usersGuides/EditStackHelp.html
}

#####################################################################
#### parseNow
# Parse the stack information, coloring it for better readability.
# Eventually, this should insert hyperlinks to the code referenced in
# the stack.
#
body ::tycho::EditStack::parseNow {start stop} {

    set hull $itk_component(hull)
    set text $itk_component(text)

    # The first line is always a title
    $text tag add title 1.0 "0.0 lineend"
    $text tag configure title -foreground \
	    [option get $hull titleColor EditStack]

    set comments {while executing|invoked from within|\(while constructing [^)]*\)|\(command bound to event\)}
    set point $start
    set first {}
    while {[set st [$text search -count cnt -regexp $comments $point $stop]] \
	    != {}} {
	set end [$text index "$st + $cnt chars"]
	if {$first == {}} {
	    # Color the error message
	    $text tag add error 2.0 "$st -1 chars"
	    set first 0
	}
	$text tag add comment $st "$st + $cnt chars"
	set point $end
	# The following appears to be needed to prevent infinite loops.
	if [$text compare $point >= end] {break}
    }

    # Eventually the following will be hyperlinks, I hope.
    set links {\([^\)]* body line [0-9]*\)|\(procedure [^\)]*\)}
    set point $start
    while {[set st [$text search -count cnt -regexp $links $point $stop]] \
	    != {}} {
	set end [$text index "$st + $cnt chars"]
	$text tag add link $st "$st + $cnt chars"
	set point $end
    }
    
    # Set the visual characteristics of the tagged text
    $text tag configure error -foreground \
	    [option get $hull errorColor EditStack]
    $text tag configure comment -foreground \
	    [option get $hull commentColor EditStack]
    $text tag configure link -foreground \
	    [option get $hull linkColor EditStack]
}
