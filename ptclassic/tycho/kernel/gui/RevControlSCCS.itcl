# Definition of the class which handles revision control using SCCS.
#
# Authors: Joel King and Edward A. Lee
#
# Version: $Id$
#
# Copyright (c) 1990-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

option add *RevControlSCCS.versionColor [ptkColor blue black] widgetDefault
option add *RevControlSCCS.commentColor [ptkColor firebrick black] widgetDefault

#######################################################################
#### RevControlSCCS
# This class defines how revision control will be handled when using 
# SCCS, and is intended to be contained within objects that use such 
# facilities.
class ::tycho::RevControlSCCS {
    inherit ::tycho::RevControl

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                      public methods                       ####

    #### checkIn
    # Checkin the file associated with this class.
    method checkIn {}

    #### checkOut
    # Checkout the file associated with this class.
    method checkOut {}

    #### initializeRevControl
    # Put the file under revision control.
    method initializeRevControl {}

    #### retrieveVersion
    # Get the specified version and open the file.
    method retrieveVersion {version}

    #### unedit
    # Unedit the file associated with this class.
    method unedit {}

    #### viewHistory
    # View or close the version control history.
    method viewHistory {}

    ###################################################################
    ####                   protected methods                       ####

    #### getComments
    # Get the users comments.
    protected method getComments {} 

    #### isCheckedOut
    # Return 1 if the file is checked out, 0 otherwise.
    protected method isCheckedOut {}

    #### isUnderRevControl
    # Return 1 if the file is under revision control, 0 otherwise.
    protected method isUnderRevControl {}

    ###################################################################
    ####                   private methods                         ####

    #### parseHistory
    # Parse the history information and insert a synopsis into the text window.
    private method parseHistory {history text}

    #### updateHistory
    # Update the history information, if it is on display.
    private method updateHistory {}
}

###################################################################
# constructor
#
body ::tycho::RevControlSCCS::constructor {args} {
    eval itk_initialize $args 
    configure -text "SCCS revision control for: $file"
}

#####################################################################
#### checkIn
# Check in the file associated with this class.
# 
body ::tycho::RevControlSCCS::checkIn {} {
    ::tycho::RevControl::checkIn 
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    if {![file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "The file \"$basefile\" is not checked out."
    }
    cdDir $dir
    set comment [$this getComments]
    if {$comment == ""} {
	# cancelled
	return
    }
    catch {exec echo $comment | sccs delget $basefile} output
    if {[file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "Checkin failed: $output"
    }
    cd $olddir
    $object reload
    enableButton checkout
 
    updateHistory
}

#####################################################################
#### checkOut
# Check out the file associated with this class.
# 
body ::tycho::RevControlSCCS::checkOut {} {
    ::tycho::RevControl::checkOut
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]

    if {[file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "The file \"$basefile\" is already checked out."
    }
    cdDir $dir
    catch {exec sccs edit $basefile} output
    if {![file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "Checkout failed: $output."
    }
    cd $olddir
    $object reload
    enableButton checkin

    updateHistory
}

#####################################################################
#### initializeRevControl
# Put the file under revision control.
# 
body ::tycho::RevControlSCCS::initializeRevControl {} {
    ::tycho::RevControl::initializeRevControl
    set dir [file dirname $file]
    set revdir [format "%s/SCCS" $dir]
    set basefile [file tail $file]
    set olddir [eval pwd]
    if {![file exists $revdir]} {
	if {![file writable $dir]} {
	    error "The directory \"$revdir\" doesn't exist, and you do not\
		    have write permission to create it."
	}   
	exec mkdir $revdir
    }
    cdDir $dir
    catch {exec sccs create $basefile} output
    if {![regexp "^\[1-9\]+.\[0-9\]+" $output]} {
	error "Failed to place the file under revision\
		control: $output."
    }   
    $object reload
    cd $olddir
}

#####################################################################
#### retrieveVersion
# Retrieve the specified version and display.
# 
body ::tycho::RevControlSCCS::retrieveVersion {version} {
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]
    cdDir $dir

    # SCCS redirects the version info to stderr, so we redirect stderr.
    # Unfortunately, this means there is no way to report errors.
    set text [exec sccs get -p -r$version $basefile 2> /dev/null]

    ::tycho::File::displayData $text $file 
    cd $olddir
}

#####################################################################
#### unedit
# Unedit the file associated with this class.
# 
body ::tycho::RevControlSCCS::unedit {} {
    ::tycho::RevControl::unedit
    set dir [file dirname $file]
    set basefile [file tail $file]
    set olddir [eval pwd]

    if {![file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "The file \"$basefile\" is not checked out."
    }
    cdDir $dir
    catch {exec sccs unedit $basefile} output 
    if {[file exists [format "%s/SCCS/p.$basefile" $dir]]} {
	error "Unedit failed: $output"
    }    
    cd $olddir
    $object reload

    updateHistory
}

#####################################################################
#### viewHistory
# View or close the version control history.
# 
body ::tycho::RevControlSCCS::viewHistory {} {
    if {$file == {}} {
	error "Revision control: no file specified"
    }
    if {[info exists itk_component(history)] && \
	    [winfo exists $itk_component(history)]} {
	delete object $itk_component(history)
	return
    } {
	set dir [file dirname $file]
	set basefile [file tail $file]
	set olddir [eval pwd]
	
	if {![file exists [format "%s/SCCS/s.$basefile" $dir]]} {
	    error "The file \"$file\" is not under SCCS control."
	}
	cdDir $dir
	catch {exec sccs prs $basefile} history

	# Create the display
	itk_component add history {
	    ::tycho::EditText $itk_component(hull).history -height 30
	} {
	    keep -readonly -width -background \
		    -highlightbackground -highlightcolor -selectforeground \
		    -cursor -insertbackground -textforeground -textfont \
		    -textbackground
	}
	pack $itk_component(history) -expand yes -fill both
	$itk_component(history) configure -readonly 1

	# Parse the history
	parseHistory $history [$itk_component(history) textWinName]

	# Delay or the focus command is ignored.  Tk bug?
	after 500 "focus [$itk_component(history) textWinName]"

	cd $olddir
    }
}

    ###################################################################
    ####                   protected methods                       ####

#####################################################################
#### getComments
# Open a dialog box to query the user for comments.
# 
body ::tycho::RevControlSCCS::getComments {} {
    set list [DialogWindow::newModal EntryQuery [autoName .getcomment] \
	    -queries {{body {Comment:} {} 5}}  \
	    -entrywidth 60]
    set comments [lindex [lindex $list 0] 1]
    return [join [split $comments "\n"] "\\\n"]
}

#####################################################################
#### isCheckedOut
# Return 1 if the file is checked out, 0 otherwise.
# Here we assume that if the file is read-only, then it is not checked out.
# 
body ::tycho::RevControlSCCS::isCheckedOut {} {
    if [$object getReadonly] {
	return 0
    } {
	return 1
    }
}

#####################################################################
#### isUnderRevControl
# Check whether the file associated with this class is under revision 
# control, and return a 1 if it is or a 0 otherwise.
# 
body ::tycho::RevControlSCCS::isUnderRevControl {} {
    if {$file == {}} {
	error "Revision control: no file specified"
    }
    set dir [file dirname $file]
    set revdir [format "%s/SCCS" $dir]
    set basefile [file tail $file]
    if {![file exists [format "%s/s.%s" $revdir $basefile]] && \
	    ![file exists [format "%s/p.%s" $revdir $basefile]]} {
	return 0
    }
    return 1
}

    ###################################################################
    ####                   private methods                         ####

#####################################################################
#### parseHistory
# Parse the history information and insert a synopsis into the text window.
# 
body ::tycho::RevControlSCCS::parseHistory {history text} {
    set lines [split $history "\n"]

    if {[llength $lines] < 2} {
	$text insert insert $lines
	return
    }

    $text insert insert "Double click to view versions. "
    if [isCheckedOut] {
	$text insert insert "The file is currently checked out"
    }
    $text insert insert "\n\n"

    set commentColor \
	    [option get $itk_component(hull) commentColor RevControlSCCS]
    set versionColor \
	    [option get $itk_component(hull) versionColor RevControlSCCS]

    # Skip first two lines
    foreach line [lrange $lines 2 end] {
	if [regexp -nocase {D ([0-9.]*) ([0-9/]*) ([0-9:]*) ([a-z]*)} \
		$line junk version date time user] {
	    set start [$text index insert]
	    $text insert insert "$version $date, checked in by $user\n"
	    $text tag add v$version $start insert
	    $text tag configure v$version -foreground $versionColor -underline 1
	    $text tag bind v$version <Double-Button-1> \
		    [code "$this retrieveVersion $version"]
	} elseif [regexp {MRs:|COMMENTS:} $line] {
	    # ignore
	} else {
	    set start [$text index insert]
	    $text insert insert "$line\n"
	    $text tag add comment $start insert
	    $text tag configure comment -foreground $commentColor
	    $text tag configure comment -lmargin1 0.5c
	    $text tag configure comment -lmargin2 0.5c
	}
    }
}

#####################################################################
#### updateHistory
# Update the history information, if it is on display.
# 
body ::tycho::RevControlSCCS::updateHistory {} {
    if {[info exists itk_component(history)] && \
	    [winfo exists $itk_component(history)]} {
	delete object $itk_component(history)
	viewHistory
    }
}
