##########################################################################
# @Version: $Id$
# @Author: John Reekie
#
# @Copyright (c)  The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
#                                                         COPYRIGHTENDKEY
##########################################################################

#######################################################################
## preferencedialog
#
# Open a preferences dialog box. If there already is one, bring it
# to the top.
#
proc ::tycho::preferencedialog {} {
    if { [::info which -command .tychoPreferences] != "" } {
	raise .tychoPreferences
    } else {
	::tycho::PreferenceDialog .tychoPreferences
    }
}


#######################################################################
#### PreferenceDialog
#
# A window that controls a set of preference editors.
#
class ::tycho::PreferenceDialog {
    # FIXME: this should be a subclass of ::tycho::TopLevel but it
    # hangs - ?
    inherit itk::Toplevel

    constructor {args} {}
    destructor {}


    ###################################################################
    ####                            options                        ####



    ###################################################################
    ####                         public methods                    ####

    # Open an editor for the specified preference set
    method edit {set}

    ###################################################################
    ####                       private variables                   ####

    # The current preferences editor
    private variable editor {}
}


#######################################################################
#### constructor
#
body ::tycho::PreferenceDialog::constructor {args} {

    foreach set [preference info] {
	set description [preference cget $set -description]
	if { $description != "" } {
	    itk_component add $set {
		button $itk_interior.$set -text $description \
			-command "$this edit $set" \
			-highlightthickness 0
	    } {
		keep -background -cursor
	    }
	    grid $itk_interior.$set -sticky "ew"
	}
    }

    itk_component add space {
	frame $itk_interior.space -height 6
    } {
	keep -background -cursor
    }
    grid $itk_component(space)

    itk_component add close {
	button $itk_interior.close -text Close -command "delete object $this"
    } {
	keep -background -cursor
    }
    grid $itk_component(close) -sticky e

    # Initialise options
    eval itk_initialize $args
}

#######################################################################
#### constructor
#
body ::tycho::PreferenceDialog::destructor {} {
    if { $editor != "" } {
	delete object $editor
    }
}

#######################################################################
#### edit
#
body ::tycho::PreferenceDialog::edit {set} {
    if { $editor != "" } {
	delete object $editor
	set editor {}
    }

    set editor [::tycho::PreferenceEditor .$set -preferences $set]
}
