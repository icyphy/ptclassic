# Definition of a window that displays HTML.
# This uses the HTML library by Stephen Uhler.
#
# Author: Edward A. Lee
#
# Version: $Id$
#
# Copyright (c) 1990-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

# FIXME:  Known bugs
#  - Many of the File menu commands do not do anything reasonable.

#######################################################################
#### HTML
# This window contains a text widget that displays HTML using the
# library by Stephen Uhler.
# 
class ::tycho::HTML {
    inherit ::tycho::File

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                       options                             ####

    #### -sizeadjustment option
    # Offset from 14 points for the font sizes.
    itk_option define -sizeadjustment sizeAdjustment SizeAdjustment 2

    ###################################################################
    ####                       public methods                      ####

    #### openSource
    # Open the HTML source
    method openSource {}

    #### raiseWindow
    # raise the HTML broswer window and the source window (if any).
    method raiseWindow {}

    #### reload
    # Clear the current text and reload from the source file.
    method reload {}

    #### save
    # Save HTML source to a file.
    method save {{name ""}}

    #### seeName
    # Follow a hypertext link within the file.
    method seeName {name}

    #### switchFile
    # Switch to a different HTML file.
    method switchFile {filename}

    #########################################################################
    ####                   protected methods                             ####

    #### textWidget
    # Return the name of the type of text widget to use.
    protected method textWidget {} {return ::tycho::HTMLText}

    #########################################################################
    ####              private methods and variables                      ####

    # Keep track of the requested size adjustment.
    private common sizeName

    # Keep track of the current source window for the current file.
    private variable sourcewin

    #### sourcewinIsMine
    # Return 1 if the sourcewin window is a valid source window.
    private method sourcewinIsMine {}
}

#########################################################################
#### -sizeadjustment configuration
# The size of normal text will be 14 points plus the size adjustment
# set with this option.  Note that this does not affect the selected
# size name in the Format menu.
#
configbody ::tycho::HTML::sizeadjustment {
    HMset_state [$itk_component(text) textWinName] \
	    -size $itk_option(-sizeadjustment)
}

###################################################################
#
body ::tycho::HTML::constructor {args} {

    # A frame for accelerator buttons
    itk_component add acc {
	frame $itk_component(childsite).acc -relief sunken
    } {
	keep -background -cursor
    }
    pack $itk_component(acc) -expand no -fill x

    # The text widget.
    itk_component add text {
	[textWidget] $itk_component(childsite).text
    } {
	keep -text -readonly -textfont -width -height -background \
		-highlightbackground -highlightcolor -selectforeground \
		-cursor -insertbackground -textfont \
		-textbackground -textforeground
    }
    pack $itk_component(text) -expand yes -fill both

    # A button to move back in the stack
    itk_component add back {
	global TYCHO
	button $itk_component(acc).back \
		-text Back \
		-bitmap @$TYCHO/kernel/img/back.xbm \
		-command "$itk_component(text) back"
    } {
	keep -background -font -activebackground -cursor \
		-highlightthickness -activeforeground \
		-foreground -highlightcolor -highlightbackground
    }
    pack $itk_component(back) -side left

    itk_component add formatMenuButton {
	menubutton $itk_component(menuFrame).format \
		-text Format \
		-underline 1 \
		-menu $itk_component(menuFrame).format.menu
    } {
	keep -font -activebackground -cursor -activeforeground \
		-background -foreground -highlightbackground \
		-highlightcolor
    }

    pack $itk_component(formatMenuButton) -side left

    itk_component add formatMenu {
	menu $itk_component(formatMenuButton).menu
    } {
	keep -font -activebackground -cursor -activeforeground \
		-background -foreground
    }

    eval itk_initialize $args

    foreach size {{tiny -2} {small 0} {medium 2} {large 4} {huge 8}} {
	$itk_component(formatMenu) add radio \
		-label [lindex $size 0] \
		-variable [scope sizeName($this)] \
		-command "$this configure -sizeadjustment [lindex $size 1]; \
		::HTMLText::render $itk_component(text)"
    }
    set sizeName($this) medium

    # Modify the menu commands appropriately.
    $itk_component(fileMenu) entryconfigure 2 \
	    -label "Open Source" \
	    -underline 1 \
	    -accelerator {} \
	    -command "$this openSource"

    if {$itk_option(-file) != "NoName"} {
	# In order to get reasonably interactive response, this is delayed.
	# Not doing this seems to send the program into an infinite wait.
	after 1 $itk_component(text) readFile $itk_option(-file)
    }

    $itk_component(text) configure -readonly 1
}

###################################################################
# The destructor needs to ensure that after this object is gone, if the
# corresponding source file is still open, that it will appear in the
# Window menu.
#
body ::tycho::HTML::destructor {} {
    if [sourcewinIsMine] {
	windowMenuRemoveAll $file
	windowMenuAddAll $sourcewin $file
	set filesOpen($file) $sourcewin
    }
    catch {unset sizeName($this)}
}

    ###################################################################
    ###################################################################
    ####                       public methods                      ####

#####################################################################
#### openSource
# Clear the current text and reload from the source file.
#
body ::tycho::HTML::openSource {} {
    # If a source window already exists, just raise it.
    if [sourcewinIsMine] {
	$sourcewin raiseWindow
	return
    }
    set win [autoName .edit]
    set obj [uplevel #0 ::tycho::EditHTML $win -file $file]
    wm deiconify $win
    set sourcewin "$obj"
}

#####################################################################
#### raiseWindow
# Overload the base class raiseWindow to raise the source window also,
# if it is open.
#
body ::tycho::HTML::raiseWindow {} {
    if [sourcewinIsMine] {
	$sourcewin raiseWindow
    }
    ::tycho::TopLevel::raiseWindow
}

#####################################################################
#### reload
# Clear the current text and reload from the source file.
#
body ::tycho::HTML::reload {} {
    HMreset_win [$itk_component(text) textWinName]
    $itk_component(text) deleteRange 1.0 end
    $itk_component(text) readFile $file
}

#####################################################################
#### save
# Save HTML source to a file. If no argument is given, the filename is
# taken from the -file option for the class. If the value of the
# -file option is "NoName" (the default), then saveAs is invoked.
# Remove backup files (auto-save or crash-recovery). If an argument
# is supplied, save the current data to a file with the given name
# and do not remove the backup files. In either case, if the
# specified file is not writable, trigger an error. Return 1 if the
# write succeeds, 0 otherwise.
#
body ::tycho::HTML::save {{name ""}} {
    if {$name == ""} {
	set filename $file
    } {
	set filename $name
    }
    if {$filename != "" && $filename != "NoName"} {
	set fd [open $filename w+]
	puts -nonewline $fd [$itk_component(text) source]
	close $fd
    } {	
	# The file has not been specified.  Invoke saveAs
	if {[saveAs] == 0} {return 0}
    }
    return 1
}

#####################################################################
#### seeName
# Follow a hypertext link.  If the hypertext link is in this same file,
# defer to the HTMLText widget.  Otherwise, check to see whether
# the other referenced file is an HTML file.  If it is, read the other
# file into this same text widget.  Otherwise, open a new window.
#
body ::tycho::HTML::seeName {name} {
    if {![regexp {([^#]*)#(.+)} $name dummy fileref fragment]} {
	set fileref $name
	set fragment {}
    }

    if {$fileref == {} && $fragment != {}} {
	# A local reference within the same file
	$itk_component(text) seeName $fragment
	return
    }
    # Got a reference to a file.
    # If the filename is not absolute, use the current file directory
    # as a prefix.
    if {![string match /* $fileref]} {
	set fileref [$itk_component(text) lastDirectory]/$file
    }

    # If the file is another html file, we open it in this same window.
    # Otherwise, we open a new window.
    if {[file extension $fileref] == {.html}} {
	configure -file $fileref
	switchFile $fileref
    } {
	::tycho::File::openContext $fileref
    }
}

#####################################################################
#### switchFile
# Switch to displaying another HTML file.  Special care is required
# here because there might be a source file open for either the old
# or the new file. If there is a source file for the old HTML file,
# it should now appear in the Window menu.  If there is one for the
# new file, it should now be removed from the Window menu (the HTML
# browser widget will take its place).  This method essentially
# accomplishes the same functions as configuring the -file option,
# but specialized to this circustance.
#
body ::tycho::HTML::switchFile {filename} {

    # If the destination file is already open, raise it.
    if {[info exists filesOpen($filename)] && \
	    [$filesOpen($filename) isa ::tycho::HTML]} {
	$filesOpen($filename) raiseWindow
	return
    }

    # Remove the old file from the list of open files, and
    # add its source file, if open, to the Window menu.
    if [info exists filesOpen($file)] {
	windowMenuRemoveAll $file
	if [sourcewinIsMine] {
	    windowMenuAddAll $sourcewin $file
	    set filesOpen($file) $sourcewin
	} {
	    unset filesOpen($file)
	}
    }
    if [info exists sourcewin] {
	unset sourcewin
    }

    # Reclaim the source window, if it is open.
    if {[info exists filesOpen($filename)]} {
	windowMenuRemoveAll $filename
	set sourcewin $filesOpen($filename)
    }

    # Record the new filename in various places.
    set file $filename
    set filesOpen($file) $this
    set previousfile $file
    wm iconname $prefix [file tail $file]
    windowMenuAddAll $this $filename

    # Render the HTML file.
    $itk_component(text) readFile $file
}

#########################################################################
#########################################################################
####                     private methods                             ####

#####################################################################
#### sourcewinIsMine
# Return 1 if the sourcewin window is the source window for the
# currently open HTML text. Otherwise, return 0.
#
body ::tycho::HTML::sourcewinIsMine {} {
    if {[info exists sourcewin] && \
	    [uplevel #0 info objects $sourcewin] != {} && \
	    [$sourcewin isa ::tycho::File] && \
	    [$sourcewin filename] == $file} {
	return 1
    } {
	return 0
    }
}
