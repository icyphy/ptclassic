# Definition of a window that displays HTML.
# 
# @Author: Edward A. Lee
# @Contributor: This uses the HTML library by Stephen Uhler.
#
# @Version: $Id$
#
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

# FIXME: Many features are missing.
# Perhaps the most important and relatively easy to support:
# - forms
# - a forward button
# - hotlist support

#######################################################################
#### HTML
# A window containing a text widget that displays HTML using the
# HTML library by Stephen Uhler.  The HTML may be specified directly
# using the -data option or indirectly using the -file option.
# An interface to an HTML editor is provided through the "Open Source"
# menu command.  Buttons are provided for frequently executed commands.
# <p>
# This class is meant to provide an integrated documentation system,
# not to replace standard Web browsers.  In fact, it does not at this
# time have HTTP capability, so it will not read files off the Web.
# <p>
# This HTML parser is a bit slow,
# and does not include most features of HTML beyond version 2.0.
# Nonetheless, it is small, simple, and easily extended, so it serves
# our purposes well.
# 
class ::tycho::HTML {
    inherit ::tycho::File

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                       options                             ####

    # Offset from the nominal 14 points for the font sizes.
    itk_option define -sizeadjustment sizeAdjustment SizeAdjustment 2

    ###################################################################
    ####                       public methods                      ####

    # Prompt the user for a file name and load in place of the current file.
    method altFile {}

    # Go back to the previous view.
    method back {}

    # Clear the window.
    method clear {}

    # Return the current insertion point in a form usable by seePoint.
    method currentPoint {}

    # Give the text subwindow the focus.
    method focusin {}

    # Open a help window.
    method help {}

    # Open a file and go to its internal point identified by name.
    method hyperJump {filename point {push 1}}

    # Insert the specified data.
    method insertData {data}

    # Open the source file using an HTML editor.
    method openSource {}

    # Raise the HTML browser window and the source window (if any).
    method raiseWindow {}

    # Go to a specified point within the file.
    method seePoint {point}

    # Add a file editor object to the window menus of all open File objects.
    method windowMenuAddAll {label}

    # Remove a filename from the window menus of all open File objects.
    method windowMenuRemoveAll {filename}

    #########################################################################
    ####                   protected methods                             ####

    # Add a button to the toolbar.
    protected method addTool {name label command}

    # Return the name of the type of text widget to use.
    protected method textWidget {} {return ::tycho::HTMLText}

    #########################################################################
    ####              private methods and variables                      ####

    # Keep track of the requested size adjustment.
    private common sizeName

    # Keep track of the current source window for the current file.
    private variable sourcewin {}

    # Return 1 if the sourcewin window is a valid source window.
    private method sourcewinIsMine {}

    # Store the name of our stack object
    private variable stack {}
}

#########################################################################
#### -sizeadjustment configuration
# The size of normal text will be 14 points plus the size adjustment
# set with this option.  Note that this does not affect the selected
# size name in the Format menu.
#
configbody ::tycho::HTML::sizeadjustment {
    HMset_state [$itk_component(text) textWinName] \
	    -size $itk_option(-sizeadjustment)
}

###################################################################
#
body ::tycho::HTML::constructor {args} {

    # A frame for accelerator buttons
    itk_component add acc {
	frame $itk_component(childsite).acc -relief sunken
    } {
	keep -background -cursor
    }
    pack $itk_component(acc) -expand no -fill x

    # The text widget.
    itk_component add text {
	[textWidget] $itk_component(childsite).text -top $this
    } {
	keep -text -readonly -textfont \
		-highlightbackground -highlightcolor -selectforeground \
		-cursor -insertbackground -textfont \
		-textbackground -textforeground
        rename -width -textwidth textWidth TextWidth
        rename -height -textheight textHeight TextHeight
    }
    pack $itk_component(text) -expand yes -fill both

    # FIXME: The HTML object comes up different sizes on different
    # machines.  Below is an attempt to fix this, but unfortunately, it
    # this attempt merely demonstrates that the "wm grid" command
    # does not work.
    #     set grid [wm grid $prefix]
    #     if {$grid != {}} {
    #         set width [lindex $grid 0]
    #         set height [lindex $grid 1]
    #         wm grid $prefix $width $height 7 13
    #     }

    # A toolbar for frequently used functions.
    addTool back Back "$this back"
    # FIXME: need a forward command.
    # FIXME: should the user be allowed to change the home window?
    global ::TYCHO
    addTool home Home \
	    "$this hyperJump $TYCHO/doc/index.html {}"
    addTool reload Reload "$this reload"
    addTool open Open "$this altFile"
    addTool find Find "$itk_component(text) search"
    addTool stop Stop "$itk_component(text) stopRendering"

    addMenu Format left {} -underline 1

    eval itk_initialize $args

    # Configure the reference directory of the widget
    component text configure -directory [file dirname $file]

    foreach size {{tiny -2} {small 0} {medium 2} {large 4} {huge 8}} {
        # Note that the File base class has no method for adding radio
        # buttons to menus.
	$itk_component(menuFormat) add radio \
		-label [lindex $size 0] \
		-variable [scope sizeName($this)] \
		-command \
		"$this configure -sizeadjustment [lindex $size 1]; \
		::tycho::HTMLText::render [winfo command $itk_component(text)]"
    }
    set sizeName($this) medium

    # Modify the menu commands appropriately.
    insertMenuItem "Open Source" "Switch File..." File \
            -underline 1 -accelerator {C-x s} \
	    -command "$this openSource"

    disableMenuItem "Insert..." File
    disableMenuItem "Save" File
    disableMenuItem "SaveAs..." File
    disableMenuItem "Evaluate" File
    disableMenuItem "Revision Control..." File
    disableMenuItem "Toggle Read-Only" File

    bind $prefix <Control-x><Key-s> "$this openSource; break"
    # disable certain bindings
    bind $prefix <Control-x><Control-s> { }
    bind $prefix <Control-x><Control-w> { }
    bind $prefix <Control-x><Key-v> { }
    bind $prefix <Control-x><Key-i> { }
    bind $prefix <Control-x><Control-q> { }

    $itk_component(text) configure -readonly 1
    focusin

    # Create a stack to store where we've been.
    set stack [::tycho::Stack #auto]
}

###################################################################
# Ensure that after this object is gone, if the
# corresponding source file is still open, it will appear in the
# "Window" menu.
#
body ::tycho::HTML::destructor {} {
    if [sourcewinIsMine] {
	# Schedule this for after destruction is complete because the
	# base class destructor removes the menu entry.
	after idle "$sourcewin windowMenuAddAll $file"
    }
    catch {unset sizeName($this)}
    if {[info exists stack] && [info objects $stack] != {}} {
	delete object $stack
    }
}

    ###################################################################
    ###################################################################
    ####                       public methods                      ####

###################################################################
#### altFile
# Query the user for a filename, and if the selected file has the
# extension ".html", ".htm", or "htl", insert its contents in place of the
# currently displayed HTML.  Otherwise, view its contents in a new window.
#
body ::tycho::HTML::altFile {} {
    set filename [::tycho::DialogWindow::newModal FileBrowser \
	    [::tycho::autoName .fileBrowser] -text "Alternate file to load:"]
    if {$filename != {}} {
	hyperJump $filename {}
    }
}

#####################################################################
#### back
# Go back to the previous view, popping it off the stack.
#
body ::tycho::HTML::back {} {
    set ref [$stack pop]
    if {$ref == {}} {return}
    set filename [lindex $ref 0]
    set point [lindex $ref 1]
    # Use the optional third argument to avoid being recorded for
    # backtracking.
    hyperJump $filename $point 0
}

#####################################################################
#### clear
# Clear the current text.
#
body ::tycho::HTML::clear {} {
    HMreset_win [$itk_component(text) textWinName]
    $itk_component(text) deleteRange 1.0 end
}

###################################################################
#### currentPoint
# Return the current insertion point in a form usable by seePoint.
#
body ::tycho::HTML::currentPoint {} {
    set idx [[component text textWinName] yview]
    return [list yview [lindex $idx 0]]
}

###################################################################
#### focusin
# Hand the focus to the text window.
#
body ::tycho::HTML::focusin {} {
    focus [$itk_component(text) textWinName]
}

#########################################################################
#### help
# Open a help window.
#
body ::tycho::HTML::help {} {
    global ::TYCHO
    ::tycho::File::openContext $TYCHO/kernel/doc/usersGuides/HTMLHelp.html
}

#####################################################################
#### hyperJump
# Open a file and go to its internal point identified by name. If the
# filename has the extension ".html", ".htm", or "htl", then open it in
# this same window. If the filename is the empty string, then move the
# view to a location within the same file. If filename is relative
# (does not begin with "/", "~", or "$"), then prepend the directory of
# the file currently being viewed. Then, expand the filename using
# <code>::tycho::expandPath</code> and open the resulting file with
# <code>::tycho::File::openContext</code>, which will choose an editor
# based on the filename extension. Finally, invoke
# <code>seePoint</code> to view the specified point within the file.
# The format for the <i>point</i> argument depends on the type of file
# being opened. For HTML files, it will normally be the name of an
# anchor in the file or specified "yview" location within the file. For
# text files, it will normally be either "{line <i>linenumber</i>}" or
# "{range <i>start</i> <i>end</i>}", where <i>start</i> and <i>end</i>
# have the form <i>linenumber.characternumber</i>. If the third
# argument, which is optional, has value 0, then the jump is not
# recorded for backtracking.
#
body ::tycho::HTML::hyperJump {filename point {push 1}} {
    if {$filename != {}} {
        set char [string index $filename 0]
        if {$char != "/" && $char != "~" && $char != "\$"} {
            set filename [format "%s/%s" [file dirname $file] $filename]
        }
        set filename [::tycho::expandPath $filename]

        # If the filename has the ".html", ".htm", or ".htl" extension, open it
        # in this window (unless it is already open in another window).
        set ext [file extension $filename]
        if {$ext == ".html" || $ext == ".htm" || $ext == ".htl"} {

            # Special care is required here because there might be a
            # source file open for either the old or the new file. If
            # there is a source file for the old HTML file, it will now
            # appear in the "Window" menu. If there is one for the new
            # file, it should now be removed from the Window menu (the
            # HTML browser widget will take its place).

            # If the destination file is already open, raise it.
            if {[info exists filesOpen($filename)] && \
                    [eval $filesOpen($filename) isa ::tycho::HTML]} {
                ::tycho::File::hyperJump $filename $point
                return
            }

            # For backtracking
            if $push {
                $stack push [list $file [currentPoint]]
            }

            component text configure -directory [file dirname $filename]
            configure -file $filename
            if {$point != {}} {
                after 200 [list $this seePoint $point]
            }
            return
        }
    } {
        # For backtracking
        if $push {
            $stack push [list {} [currentPoint]]
        }
    }

    # The file is not an HTML file, or is not specified,
    # so defer to File.
    ::tycho::File::hyperJump $filename $point
}

#####################################################################
#### insertData
# Insert the specified data, which should be HTML.
#
body ::tycho::HTML::insertData {data} {
    component text configure -html $data
}

#####################################################################
#### openSource
# Open the source file using an editor specialized for editing HTML.
#
body ::tycho::HTML::openSource {} {
    # If a source window already exists, just raise it.
    if [sourcewinIsMine] {
	$sourcewin raiseWindow
	return
    }
    set win [::tycho::autoName .edit]
    set obj [uplevel #0 ::tycho::EditHTML $win -file $file]
    wm deiconify $win
    set sourcewin $obj
}

#####################################################################
#### raiseWindow
# Raise this window and also the source window, if it is open.
#
body ::tycho::HTML::raiseWindow {} {
    if [sourcewinIsMine] {
	$sourcewin raiseWindow
    }
    ::tycho::TopLevel::raiseWindow
}

#####################################################################
#### seePoint
# Go to a specified point within the file.
#
body ::tycho::HTML::seePoint {point} {
    $itk_component(text) seePoint $point
}

#####################################################################
#### windowMenuAddAll
# Add me to the window menus of all open File objects.
# This is redefined in order to coordinate the source window, which
# may be open.
#
body ::tycho::HTML::windowMenuAddAll {label} {
    # Reclaim the source window, if it is open.
    if {[info exists filesOpen($label)]} {
        set sourcewin $filesOpen($label)
        $sourcewin windowMenuRemoveAll $label
    } {
        set sourcewin {}
    }
    ::tycho::File::windowMenuAddAll $label
}

#####################################################################
#### windowMenuRemoveAll
# Remove a filename from the window menus of all open File objects.
# This is redefined in order to coordinate the source window, which
# may be open.
#
body ::tycho::HTML::windowMenuRemoveAll {filename} {
    ::tycho::File::windowMenuRemoveAll $filename
    if [sourcewinIsMine] {
        $sourcewin windowMenuAddAll $filename
        set sourcewin {}
    }
}

#########################################################################
#########################################################################
####                     protected methods                           ####

#####################################################################
#### addTool
# Add a button to the toolbar at the top.
# The arguments are: a name for the button and itk_component,
# the text label to use for the button, and the command associated with
# the button.
#
body ::tycho::HTML::addTool {name label command} {
    itk_component add $name {
	button $itk_component(acc).$name \
		-text $label \
		-command $command
    } {
	keep -background -font -activebackground -cursor \
		-highlightthickness -activeforeground \
		-foreground -highlightcolor -highlightbackground
    }
    # To use a bitmap instead, we need before the button command:
    #       global ::TYCHO
    # and as one of the button options:
    #		-bitmap @$TYCHO/kernel/img/back.xbm

    pack $itk_component($name) -side left
}

#########################################################################
#########################################################################
####                     private methods                             ####

#####################################################################
#### sourcewinIsMine
# Return 1 if the sourcewin window is the source window for the
# currently open HTML text. Otherwise, return 0.
#
body ::tycho::HTML::sourcewinIsMine {} {
    if {$sourcewin != {}} {
        # Test to see whether the window still exists.
        # We use catch here because other Itcl methods seem to be too tricky.
        if [catch {$sourcewin isModified}] {
            return 0
        }
	return 1
    } {
	return 0
    }
}
