# Definition of a text editor with facilities for editing programs
# in various languages.
#
# @Author: Kevin Chang
#
# @Version: $Id$
#
# @Copyright (c) 1995-1997 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

#######################################################################
# FIXME: Known bugs
#

#######################################################################
#### EditDiff
# This class defines a widget that provides an emacs-like editor
# specialized for diffing program files. It colorizes the code,
# identifying new and old file lines.
#

class ::tycho::EditDiff {
    inherit ::tycho::Edit

    constructor {args} {}
    destructor {}

    ##########################################################################
    ####                            options                               ####

    # The type of diff we want to perform
    itk_option define -difftype difftype Difftype 0

    itk_option define -filename1 filename1 Filename1 ""
    itk_option define -filename2 filename2 Filename2 ""

    ##########################################################################
    ####                         public methods                           ####

    # Compare files
    method compareFile {{nothing {}}} {}

    # Keep the concurrency between filename and listentry file
    method configureUpdate {} {}

    # This will return the info line entry from the UNIX diff program
    # (ie. 12,32c53,3    3d5,6    etc)
    method diffReport {} {}

    # Open a help window.  This should be redefined in derived classes.
    method help {}

    # Put in the file name in the list entry
    method insertFileName {num nameAndPath} {}

    # insert filename into $numth line
    method insertSelectedFileName {num} {}

    # The attribute for each text could be either "common", "new", or "old".
    method insertString {index str status} {}

    # Jump to the next difference
    method jumpDifference {{incrdecr 1}} {}

    ####################################################################
    ####           protected methods and variables                  ####

    # Keep track of the type of the diff
    private common difftypeName

    # Instance of the Exec which calls UNIX's diff utility
    private variable execDiff {}

    # Color for old and new text
    protected variable linecolor1 {{} {}}
    protected variable linecolor2 {{} {}}

    # This keeps a list of diffs, output from UNIX's diff
    public variable diffResult {}

    public variable diffResultLineCount 0
    
    # Keep track of where exactly differences occured.
    public variable differenceList {}

    # Keep track of where in the error we are now.
    public variable differenceListCount -1

    # Keep track where we can insert in the text. This is added for
    # concurency with seeFragment during insertion.
    public variable textLineCount 1

    # File names that are being diffed.
    public variable filename1
    public variable filename2
}



###################################################################
#
body ::tycho::EditDiff::constructor {args} {
    #global filename1 filename2 
    # FIXME: Currently, we'd like to change the default colors
    set linecolor1 [list blue {}]
    set linecolor2 [list red {}]

    set execDiff ""
    configure -readonly 1

    toolbar button prevdiff \
            -text {Prev Difference} \
            -description {Jump to the previous difference} \
            -command "$this jumpDifference -1"

    toolbar button nextdiff \
            -text {Next Difference} \
            -description {Jump to the next difference} \
            -command "$this jumpDifference 1"

    toolbar button compare \
            -text Compare \
            -description {Read in both files, and start parsing} \
            -command "$this compareFile"

    toolbar button addfile \
            -text "Add File" \
            -description {Read in one more file to compare} \
            -command {} -state disabled

    toolbar button remfile \
            -text "Remove File" \
            -description {Remove one more file entry} \
            -command {} -state disabled
           
    toolbar entry filename1 "File 1 name: " {} "puts " \
            -foreground [lindex $linecolor1 0] \
            -command "$this insertSelectedFileName 1"
           
    toolbar entry filename2 "File 2 name: " {} "$this compareFile" \
            -foreground [lindex $linecolor2 0] \
            -command "$this insertSelectedFileName 2"

    eval itk_initialize $args

    # Initialize the format menu
    array set difftypeList {{line/line diff} 0 {smart diff} 1}
    if {$myMenubar != {}} {
        foreach i {{line/line diff}} {
            $myMenubar add $i Format \
		    -type radiobutton \
                    -variable [scope difftypeName($this)] \
                    -command \
                    "$this configure -difftype $difftypeList($i); \
                    #::tycho::HTML::render $this"
        }
    }
    # initialize this variable to "small", since that has a zero
    # offset.
    set difftypeName($this) "line/line diff"
    configure -difftype $difftypeList($difftypeName($this))

    if {$myMenubar != {}} {
        $myMenubar disable {Compare To...}
        $myMenubar disable {Undo/Redo}
        $myMenubar disable {Cut}
        $myMenubar disable {Paste}
        $myMenubar disable "Font..."
        # Delete all in Edit
        $myMenubar delete "Undo/Redo"
        $myMenubar delete "Cut"
        $myMenubar delete "Paste"
        # Delete all the formats
        $myMenubar delete "Fill Region"
        $myMenubar delete "Right Fill Column"
        $myMenubar delete "Set Fill Prefix"
        $myMenubar delete "Lower Case"
        $myMenubar delete "Upper Case"
        $myMenubar delete "Capitalize"

        $myMenubar delete "Insert..."
        $myMenubar delete "Save"
        $myMenubar delete "SaveAs..."
        $myMenubar delete "Evaluate"
        $myMenubar delete "Revision Control..."
        $myMenubar delete "Toggle Read-Only"
    }

    # Center the screen.
    bind $itk_component(text) <Control-L> "$this compareFile"

    # Configure text color
    set text $itk_component(text)
    # Tag the text to have different colors
    foreach i {1 2} {
        $text tag configure linecolor$i \
                -foreground [lindex [eval set linecolor$i] 0] \
                -background [lindex [eval set linecolor$i] 1]
    }
    
    if {$filename1 != {}} {insertFileName 1 $filename1}
    if {$filename2 != {}} {insertFileName 2 $filename2}
}


###################################################################
#
# This is the entry point to comparing files. It first clears up the
# text buffer, read in UNIX's diff, and then compare.
body ::tycho::EditDiff::compareFile {{nothing {}}} {
    markUnmodified

    set text $itk_component(text)
    $text delete 0.0 end    ;# This replaces clear
    set differenceList {}
    set differenceListCount -1
    set textLineCount 1

    #global filename1 filename2
    global linecolor1 linecolor2
    
    # Make sure that all the files are legal to begin with. Otherwise, exit.
    set flag 1
    foreach i {1 2} {
        if {[eval set filename$i]=="" || [eval set filename$i]=="NoName"} {
            safeEval ::tycho::warn "File name invalid: [eval set filename$i]"
            return
        }

        if {![file exists [eval set filename$i]]} {
            safeEval ::tycho::warn "File does not exist: [eval set filename$i]"
            return
        }
        
        if {![file readable $filename1]} {
            safeEval ::tycho::warn "File is not readable: $filename"
            return
        }
    }

    # Create an exec instance
    if {[info object $execDiff ]==""} {
        # Doesn't exist yet, create one
        set execDiff [::tycho::Exec [::tycho::autoName execDiff]]
    }
    $execDiff execProcess \
            "diff $filename1 $filename2 |& grep -v \"^<\" |& grep -v \"^<\""
    # Wait until execution is done
    $execDiff getStatusWait
    set diffResult [$execDiff getHistory]

    set diffResult [split $diffResult \n]
    # Reset the line to read from beginning
    set diffResultLineCount 0
    

    # Open up files 
    set fd1 [open $filename1 r]
    set fd2 [open $filename2 r]
    set fileLineCount1 1
    set fileLineCount2 1
    
    # Entry point to reading the actual files and outputting to text
    clear
    while {[set diffInfo [diffReport]] != "done"} {

        set info [lindex $diffInfo 0]
        set n1 [lindex $diffInfo 1]
        set n2 [lindex $diffInfo 2]
        set n3 [lindex $diffInfo 3]
        set n4 [lindex $diffInfo 4]
        
        # Output common lines. Also increment
        # file1 and file2 pointer
        while {$fileLineCount1 < $n1} {
            # Increment file1 until difference seen.
            insertString insert [gets $fd1]\n common
            incr fileLineCount1
        }
        while {$fileLineCount2 < $n3} {
            # Increment file2 until difference seen.
            gets $fd2
            incr fileLineCount2
        }
        
        # If changed (character c), delete (d), or added (a)
        set differenceBlock {}
        if {$info=="c" || $info=="d" || $info=="a"} {
            lappend differenceBlock [$text index insert]
        }
        if {$info=="c"} {
            # Output differences
            while {$fileLineCount1 <= $n2} {
                insertString insert [gets $fd1]\n linecolor1
                incr fileLineCount1
            }
            while {$fileLineCount2 <= $n4} {
                insertString insert [gets $fd2]\n linecolor2
                incr fileLineCount2
            }
            # Continue...
        } elseif {$info=="d"} {
            # Output the added lines
            while {$fileLineCount1 <= $n2} {
                insertString insert [gets $fd1]\n linecolor1
                incr fileLineCount1
            }
        } elseif {$info=="a"} {
            while {$fileLineCount2 <= $n4} {
                insertString insert [gets $fd2]\n linecolor2
                incr fileLineCount2
            }
        }
        if {$info=="c" || $info=="d" || $info=="a"} {
            lappend differenceBlock [$text index insert]
            lappend differenceList $differenceBlock
        }
    }

    # Read and output the rest of the common lines until EOF
    while {![eof $fd1]} {
        insertString insert [gets $fd1]\n common
    }
    close $fd1
    close $fd2

    return
}

#####################################################################
#### configureUpdate
# when the file names are changed (from outer interface), change the
# internal listentry.
#
body ::tycho::EditDiff::configureUpdate {} {
    # most recent file should always in filename1.
    insertFileName 1 $filename1
    insertFileName 2 $filename2
}


#####################################################################
#### diffReport
# parse the output of the UNIX's diff utility
body ::tycho::EditDiff::diffReport {} {
    #The normal output contains lines of these forms:
    #n1 a n3,n4
    #n1,n2 d n3
    #n1,n2 c n3,n4

    # Reinitialize n1 through n4 to 0
    foreach i {3 4} {
        set n$i 0
    }
    
    # Keep going until we hit the end of line
    while {$diffResultLineCount < [llength $diffResult]} {
        set line [lindex $diffResult $diffResultLineCount]
        # FIXME:
        # This is an expensive operation! Regex is the bottleneck if
        # there are many differences.
        if {[regexp \
                {(^[0-9]+)(,[0-9]+)?(c|a|d)([0-9]+)(,[0-9]+)?$} $line match \
                n1 n2 command n3 n4]} {
            # Get rid of leading comma for n2 and n4
            regexp {,([0-9]+)} $n2 match n2
            regexp {,([0-9]+)} $n4 match n4
            
            # If it's a single line, make sure the second line (now 0)
            # corresponds to the first line (with a value)
            if {$n2<$n1} {set n2 $n1}
            if {$n4<$n3} {set n4 $n3}
            
            incr diffResultLineCount
            return "$command $n1 $n2 $n3 $n4"
        }
	incr diffResultLineCount
    }
    return done
}



###################################################################
#### help
# Open a window with help on the current widget.
#
body ::tycho::EditDiff::help {} {
    global ::TYCHO

    ::tycho::File::openContext \
        [file join $TYCHO editors textedit doc usersGuides \
	    EditDiffHelp.html]
}

###################################################################
####
body ::tycho::EditDiff::insertString {index str {status {common}}} {
    #configure -readonly 0
    $itk_component(text) insert $textLineCount.0 $str $status
    incr textLineCount
    #configure -readonly 1

    # This is here as an attempt to speed up responce time.
    ::tycho::safeUpdate $this
}

###################################################################
####
# Put in the file name in the list entry
body ::tycho::EditDiff::insertFileName {num nameAndPath} {
    $this toolbar clear filename$num
    $this toolbar insert filename$num $nameAndPath
    set filename$num $nameAndPath
}

###################################################################
####
# insert filename into $numth line
body ::tycho::EditDiff::insertSelectedFileName {num} {
    set nameAndPath [::tycho::queryfilename {File to compare:}]
    # Only change when user explicitly changed. Otherwise, leave as default
    if {$nameAndPath != {}} {
        insertFileName $num $nameAndPath
    }
}


###################################################################
####
# Jump to the next difference
body ::tycho::EditDiff::jumpDifference {{incrdecr 1}} {
    # No difference, return
    if {[llength $differenceList]==0} {return}
    
    incr differenceListCount $incrdecr
    # Overflow! Warning:
    if {$differenceListCount==[llength $differenceList]} {
        incr differenceListCount -1
    }
    if {$differenceListCount==-1} {
        incr differenceListCount 1
    }
    
    # Extract begin and ending block numbers
    set differenceBlock [lindex $differenceList $differenceListCount]
    regexp {([0-9]+).0 ([0-9]+).0} $differenceBlock match \
            differenceBegin differenceEnd
    
    # Highlight from end to beginning to be sure that we always see
    # the beginning of a difference block.
    for {set i $differenceBegin} {$i != $differenceEnd} {incr i} {
        # Indexing is different for seeFragment
        $this seeFragment "line $i"
    }
}


####################################################################
####################################################################
####                    public procedures                       ####



####################################################################
####################################################################
####                    protected methods                       ####

