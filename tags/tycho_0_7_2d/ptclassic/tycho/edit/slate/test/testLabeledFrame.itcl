# Test the LabeledFrame item
#
# @Author: John Reekie
# 
# @Version: @(#)testLabeledFrame.itcl	1.7 07/29/98
#
# @Copyright (c) 1997-1998 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

# Make sure that the test package is loaded
package require slate.test


############################################################################
#### Define a proc that creates a single LabeledFrame on the slate
#
proc _frame {args} {
    global f
    _clean
    set f [.s create LabeledFrame 40 40 80 80 \
	    -color green -tags foo \
	    -text Hello \
	    -graphics {rectangle 20 20 80 80; line 20 20 80 80; line 20 80 80 20} ]
    eval .s itemconfigure $f $args
}

############################################################################
#### Define another proc that creates a single LabeledFrame on the slate
#
proc _emptyframe {args} {
    global f
    _clean
    set f [.s create LabeledFrame 40 40 80 80 \
	    -color green -tags foo \
	    -text Hello]
    eval .s itemconfigure $f $args
}

#######################################################################
#### Creation
#
_slate
test Frame-construct-1 {Create a LabeledFrame item and check coords} {
    _frame
    .s coords [.s primary $f]
} {40.0 40.0 80.0 80.0}

#######################################################################
#### Options
#
_slate
set i 0

set f [.s create LabeledFrame 40 40 80 80]
update
foreach {opt val} {\
        -relief raised \
        -color background \
        -shape rectangle \
        -borderwidth 2 \
        -tags {} \
        -text {} \
        -textanchor center \
        -graphics {} } {
    incr i
    test LabeledFrame-options-$i "Check default option $opt" {
        .s itemcget $f $opt
    } $val
}

_clean
set f [.s create LabeledFrame 140 140 180 180 \
        -relief sunken \
        -color green \
        -borderwidth 4 \
        -tags foo \
        -text Foo \
        -textanchor s \
        -graphics {line 0 0 100 100} ]
update
foreach {opt val} {\
        -relief sunken \
        -color green \
        -borderwidth 4 \
        -tags foo \
        -text Foo \
        -textanchor s \
        -graphics {line 0 0 100 100}} {
    incr i
    test LabeledFrame-options-$i "Check non-default option $opt" {
        .s itemcget $f $opt
    } $val
}

_frame
foreach r {flat raised sunken} \
	c {{green3 green3 green3} {green1 green3 green4} \
	{green4 green3 green1}} {
    incr i
    test LabeledFrame-options-$i "Check coloring for relief $r" {
	.s itemconfigure $f -relief $r
	list \
		[.s component $f lit itemcget -fill] \
		[.s component $f surface itemcget -fill] \
		[.s component $f shaded itemcget -fill]
    } $c
    _update
}
    
_frame
foreach r {ridge groove} \
	c {{green1 green3 green4 green4 green1} \
	{green4 green3 green1 green1 green4}} {
    incr i
    test LabeledFrame-options-$i "Check coloring for relief $r" {
	.s itemconfigure $f -relief $r
	list \
		[.s component $f lit itemcget -fill] \
		[.s component $f surface itemcget -fill] \
		[.s component $f shaded itemcget -fill] \
		[.s component $f notlit itemcget -fill] \
		[.s component $f notshaded itemcget -fill]
    } $c
    _update
}
    
_frame
foreach b {0 1 2} \
	c {{40.0 40.0 81.0 81.0} {41.0 41.0 80.0 80.0} {42.0 42.0 79.0 79.0}} {
    incr i
    test LabeledFrame-options-$i "Check border width $b" {
	.s itemconfigure $f -borderwidth $b
	.s component $f surface coords
    } $c
    _update
}
    
_frame
foreach c {red blue yellow} {
    incr i
    test LabeledFrame-options-$i "Check coloring change to $c" {
	.s itemconfigure $f -color $c
	.s component $f surface itemcget -fill
    } ${c}3
    _update
}

incr i
_emptyframe -text Foo
foreach a {n s e w nw sw ne se c} p {n s e w nw sw ne se center} {
    incr i
    test LabeledFrame-options-$i "Check anchoring, no graphics, $a" {
        .s itemconfigure $f -textanchor $a
        .s component $f label itemcget -anchor
    } $p
    _update
}

_frame -text Bar
foreach a {n s e w nw sw ne se c} p {s n w e sw nw se ne n} {
    incr i
    test LabeledFrame-options-$i "Check anchoring, graphics, $a" {
        .s itemconfigure $f -textanchor $a
        .s component $f label itemcget -anchor
    } $p
    _update
}

#######################################################################
#### Reshaping
#
# Fairly simple tests, just to make sure that coords{} is
# doing what it's supposed to. Doesn't use animate{} because
# round-off error makes testing final coords impossible.
#
_slate
set i 0

# Test that internal graphics are being rescaled properly
_emptyframe -graphics {rectangle 25 25 75 75}
test LabeledFrame-reshape-1 {Reshape a corner} {
    _interactslate reshape $f 1 1 40 -features se
    set result {}
    # Need to remove rounding errors here
    foreach c [.s coords [.s find withtag graphics.$f]] {
        lappend result [expr round($c)]
    }
    set result
} {60 60 100 100}

# Test that text label is being moved correctly
_frame -textanchor ne
test LabeledFrame-reshape-2 {Reshape a corner} {
    _interactslate reshape $f 1 1 40 -features e
    .s component $f label coords
} {120.0 40.0}

