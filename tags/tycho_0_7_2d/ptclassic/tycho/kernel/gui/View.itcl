# Base class for widgets that view and/or edit data or text.
# 
# @Author: Farhana Sheikh and Edward A. Lee
#
# @Version: @(#)View.itcl	1.36 05/29/98
# 
# @Copyright (c) 1995-1998 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

# FIXME:
# FEATURES TO BE ADDED:
# (1) Add subview functionality


#######################################################################
#### View
# This class is the base class for most graphical and textual widgets
# in Tycho. It is designed to be inserted into a <a href="Displayer.html">
# Displayer</a> window, although it can be used alone as an ordinary widget.
# Subclasses of the View class display text or graphics, and may
# choose to contain "subviews" within themselves. (The protocols
# and functionality needed to properly implemented nested views
# has not yet been developed.)
#
# The displayer of a view is specified with the -displayer option:
# if this is set in the constructor, the view attaches itself to
# its displayer. <i>Note that the displayer should be specified
# before other arguments.</i>
#
# The displayer of a view provides it with a menu bar, a status bar,`
# and a tool bar. The View class provides methods that issue commands
# to these bars; if a view widget is used without a displayer, then
# commands issued via these methods to the bars are ignored. A view
# that has been attached to a displayer must only be destroyed by
# calling the <code>destroyView</code> method of the displayer, or
# otherwise the displayer will be unaware that the view is gone.
#
class ::tycho::View {
    inherit ::tycho::TWidget

    constructor {args} {}
    destructor {}

  
    ###################################################################
    ####                            options                        ####

    # Specify whether modifications will be permitted on the data.
    itk_option define -readonly readonly ReadOnly 0

    ###################################################################
    ####                         public methods                    ####

    # Attach the displayer and the menu/status/tool bars
    method attachDisplayer {displayer menubar statusbar toolbar}

    # Invoke a command on the Displayer, if it exists.
    method displayer {{method {}} args}
    
    # Give the window or a subwindow the focus and set the menu bar (if any).
    method focusin {}

    # Cause subviews to be unable to affect their displayer.
    method forgetDisplayer {} {set myDisplayer {}}

    # Return 1 if the data is read-only, 0 otherwise.
    method getReadOnly {} 

    # Insert this view into its Displayer.
    method insertInDisplay {}

    # Invoke a command on the menu bar, if it exists.
    method menubar {args}

    # Print a status message, if possible.
    method putStatus {string}

    # Invoke a command on the status bar, if it exists.
    method statusbar {args}

    # Invoke a command on the tool bar, if it exists.
    method toolbar {args}

    ###################################################################
    ####                         protected methods                 ####

    # Indicate whether or not modifications are allowed in the
    # displayed text.
    protected method indicateReadOnly {bool}

    # Initialize the menu/status/tool bars
    protected method _initializeBars {menubar statusbar toolbar}

    ###################################################################
    ####                         protected variables               ####

    # Indicate whether modifying the text is allowed.
    protected variable readOnly 0

    ###################################################################
    ####                          private variables                ####

    # Name of my displayer
    private variable myDisplayer {}

    # Name of my menubar
    private variable myMenubar {}

    # Name of my status bar
    private variable myStatusbar {}

    # Name of my toolbar
    private variable myToolbar {}
}

#########################################################################
#### -readonly option configuration
# Set whether the data can be modified.  A value of 1 means "no", whereas
# a value of 0 means "yes".  Note that this is independent of whether the
# associated file is read-only.  This determines whether the buffer can
# be modified, not whether the file can be modified.
# 
configbody ::tycho::View::readonly {
    set readOnly $itk_option(-readonly)
    # Defer this call until the widget has been mapped to the screen
    whenMapped hull "$this indicateReadOnly $itk_option(-readonly)"
}

#######################################################################
#### constructor
#
body ::tycho::View::constructor {args} {
    eval itk_initialize $args
}

#######################################################################
#### destructor
#
body ::tycho::View::destructor {} {
    # Cannot do this because then destruction cannot be cancelled
    # in derived classes.
    # displayer hideView $viewName
}


    ###################################################################
    ###################################################################
    ####                      public methods                       ####

#####################################################################
#### attachDisplayer
# Attach the view to a Displayer widget. The method remembers the
# name of given Displayer and initializes the menu, status, 
# and toolbars associated with this widget. Subclasses will need to
# override the protected method *_initializeBars* to set up
# the menu, status, and toolbar for their own needs. Each of
# these can be the null string, in which the View assumes that
# it doesn't exist. This method is intended for calling from
# within the *attachView* method if the Displayer class.
#
body ::tycho::View::attachDisplayer {displayer menubar statusbar toolbar} {
    # Remember the displayer and its bars
    set myDisplayer $displayer
    set myMenubar $menubar
    set myStatusbar $statusbar
    set myToolbar $toolbar

    # Initialize the bars
    _initializeBars $menubar $statusbar $toolbar
}

#########################################################################
#### displayer
# If no arguments are given, return the displayer; if there is none,
# return null. If arguments are given and there is a Displayer, execute
# this command on the displayer and return the result. If there are args
# but no displayer, do nothing and return null.
#
body ::tycho::View::displayer {{method {}} args} {
    if { $method != "" } {
	if {$myDisplayer != {}} {
	    return [eval $myDisplayer $method $args]
	} {
	    return {}
	}
    } else {
	if {$myDisplayer != {}} {
	    return $myDisplayer
	} {
	    return {}
	}
    }
}

#########################################################################
#### focusin
# Grab the focus and change the Displayer menu, if necessary. This
# should be redefined in derived classes to give the focus to the
# appropriate subwindow.
#
body ::tycho::View::focusin {} {
    chain
    displayer setCurrentView $this
}

#####################################################################
#### getReadOnly
# Return 1 if the data is read-only, 0 otherwise.
#
body ::tycho::View::getReadOnly {} {
    return $readOnly
}

#########################################################################
#### menubar
# With no arguments, return this view's menubar. Otherwise,
# if there is a menu bar associated with this view, use the arguments
# as a command to issue to the Menubar, and return whatever is returned
# by the command.  Otherwise, return an empty string.
#
body ::tycho::View::menubar {args} {
    if { $args == "" } {
        return $myMenubar
    } elseif {$myMenubar != {}} {
        return [eval $myMenubar $args]
    } {
        return {}
    }
}

###################################################################
#### putStatus
# Print a status message to the view's status bar.
#
body ::tycho::View::putStatus {string} {
    statusbar putMessage $string
}

#########################################################################
#### statusbar
# With no arguments, return this view's status bar. Otherwise,
# if there is a status bar associated with this view, use the arguments
# as a command to issue to the StatusBar, and return whatever is returned
# by the command.  Otherwise, return an empty string.
#
body ::tycho::View::statusbar {args} {
    if { $args == "" } {
        return $myStatusbar
    } elseif {$myStatusbar != {}} {
        return [eval $myStatusbar $args]
    } {
        return {}
    }
}

#########################################################################
#### toolbar
# With no arguments, return this view's tool bar. Otherwise,
# if there is a tool bar associated with this view, use the arguments
# as a command to issue to the toolbar, and return whatever is returned
# by the command.  Otherwise, return an empty string.
#
body ::tycho::View::toolbar {args} {
    if { $args == "" } {
        return $myToolbar
    } elseif {$myToolbar != {}} {
        return [eval $myToolbar $args]
    } else {
        return {}
    }
}

#####################################################################
#####################################################################
####                   protected methods                         ####

#####################################################################
#### _initializeBars
# Initialize the menubar, toolbar, and statusbar or a view. This
# default method does nothing. Subclasses should override this
# method to properly initialize their bars. If a bar is null,
# then this view does not have one.
#
body ::tycho::View::_initializeBars {menubar statusbar toolbar} {
    ;# Do nothing
}

#####################################################################
#### indicateReadOnly
# Indicate in the status bar (and in derived classes, in the menus)
# whether modifications are allowed on the buffer.
#
body ::tycho::View::indicateReadOnly {bool} {
    if {$bool} {
        statusbar configure -filestatus "readonly"
    } {
        statusbar configure -filestatus "readwrite"
    }
}

