# An unfolding tree of widgets, with a button at each
# node to open and close the node.
#
# A grid of labeled widgets.
# 
# @Author: John Reekie
#
# @Version: @(#)WidgetTree.itcl	1.10 06/09/98
#
# @Copyright (c) 1997-1998 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

#
# Usual options.
#
itk::usual WidgetTree {
    keep -font -background -cursor -disabledforeground -foreground \
	    -highlightthickness -highlightcolor
    rename -labelfont -font font Font
}

####
# NOTE: this file doesn't quite follow the Tycho guidelines
# because it was ported from iwidgets in a hurry.
#

#######################################################################
#### WidgetTree
#
# <b>Summary</b>
#
# The <b>widgettree</b> command creates an unfolding tree of widgets,
# with a button at each node to open and close the node. The widget is
# designed for implementing unfolding preference and dialog boxes as
# well as viewers of tree-like structures.
#
# Each item in the tree is referenced by a unique id, which is constructed
# in the same manner as hierarchical Tk widget names, but without
# the leading period. For example, a node at the top level may
# have the id <code>foo</code>, and a widget contained within that
# node the id <code>foo.bar</code>. Widgets and nodes are added to
# the tree with the <b>add</b> method.
#
# Widgets can be added to the tree either when the Widgettree is
# created or as each node is opened. To add widgets as node are opened,
# supply the <b>-createcommand</b> option. This command will be
# evaluated when a node with no children is opened -- it should
# call <b>add</b> to add children to the node.
#
# For a complete example of the use of this class, see the demonstration
# script <b>demo/widgettree</b> (where the path is relative to the
# directory the <b>tycho.kernel.gui</b> package is installed in).
#
# <b>Tree traversal</b>
#
# If the <b>-traversal</b> option is true, the widget tree can be
# navigated using tab traversal and cursor keys. Each button will accept
# the focus, and can be traversed using the standard Tk traversal
# mechanism. The Return key opens or closes the node with the focus. In
# addition, cursor keys navigate the tree as follows:
# <dl>
# <dt>
# <b>down</b>
# <dd>
# Move to the next button in the same node. If at the bottom,
# stay there.
# <dt>
# <b>up</b>
# <dd>
# Move to the previous button in the same node. If at the top,
# stay there.
# <dt>
# <b>left</b>
# <dd>
# Move up to the parent node, if there is one. If there
# isn't, stay there.
# <dt>
# <b>right</b>
# <dd>
# If the node is closed, open it. If there are child nodes,
# move to the first child. If there aren't any, stay there.
# </dl>
#
# Widgettree does not implement a selection mechanism, but does provides
# some support for it with the <b>locate</b> method. If required,
# selection can be implemented by wrapping or subclassing Widgettree.
#
# <b>Components</b>
#
# <i>Widgettree</i> dynamically creates components as widgets
# and nodes are added to the tree.
# <dl>
# <dt><i>id</i>
# <dd>
# A leaf item of the tree. The <i>id</i> item is the id passed to the
# <b>add</b> method when the item was created.
# <dt><i>nodeid</i>
# <dd>
# A frame containing the children of a tree node.  The frame is not
# created until a child is added to that node with the <b>add</b> method.
# See the "frame" widget manual entry for details.
# <dt>
# <i>id</i><b>button</b>
# <dd>
# The button of a tree node. See the "button" widget manual entry for
# details.
# <dt>
# <i>id</i><b>label</b>
# <dd>
# The label of a tree node. See the "label" widget manual entry for
# details.
# </dl>
#
# Widgettree is inspired by the SubPanel and MultiPanel widgets written by
# Farhana Sheikh and Cliff Cordeiro.
#
class ::tycho::WidgetTree { 
    inherit ::tycho::TWidget

    constructor {args} {}
    destructor {}

    #####################################################################
    ####                          options                            ####

    # Specifies the closed icon image.  
    itk_option define -closedicon closedIcon Icon closedNode

    # Specifies the open icon image.  
    itk_option define -openicon openIcon Icon openNode

    # The command to execute when a node of the tree is closed.
    public variable closecommand ""

    # The command to execute when a node of the tree is opened
    # if the node does not currently contain any widgets
    public variable createcommand ""

    # The command to execute when a node of the tree is opened
    public variable opencommand ""

    # The minimum indent for each level of the tree.
    public variable indent 24

    # The horizontal padding to use when gridding leaf widgets.
    itk_option define -padx padX PadX 0

    # The vertical padding to use when gridding leaf widgets.
    itk_option define -pady padY PadY 0

    # Specify Tab and cursor traversal through the tree. 
    itk_option define -traversal traversal Traversal 1

    # The background color
    itk_option define -background background Background "#ececec"

    # The foreground color propagated to disabled child widgets
    itk_option define -disabledforeground disabledForeground \
	    DisabledForeground #a3a3a3 
    
    # The font
    itk_option define -font font Font \
	    "-Adobe-Helvetica-Bold-R-Normal--*-120-*-*-*-*-*-*"
    
    # The foreground color propagated to child widgets
    itk_option define -foreground foreground Foreground black

    # The highlight color
    itk_option define -highlightcolor highlightColor HighlightColor black

    # The highlight thickness
    itk_option define -highlightthickness highlightThickness \
	    HighlightThickness 2

    ###################################################################
    ####                         public methods                    ####

    public method add      {type id args}
    public method close    {nodeid}
    public method delete   {id}
    public method disable  {nodeid}
    public method enable   {nodeid}
    public method hide     {nodeid}
    public method itemcget {id option}
    public method itemconfigure {id {option {}} args}
    public method locate   {x y {id {}}}
    public method names    {{nodeid {}}}
    public method open     {nodeid}
    public method toggle   {nodeid}
    public method traverse {mode {nodeid {}}}
    public method type     {id}

    ###################################################################
    ####                         private methods                   ####

    private method _mkNodeFrame {id}  ;# Make the containing frame
    private method _verifyNode {id}   ;# Verify that an id is a node
    private method _updateNodes {}  ;# Update all nodes after configuration

    ###################################################################
    ####                         private variables                 ####

    private variable _children  ;# Children of each node
    private variable _type      ;# Type of each widget, or "node"
    private variable _id        ;# Mapping from widget path to id
    private variable _row       ;# Grid row of each item
    private variable _state     ;# State of each node: "opened" or "closed"
    private variable _nextrow   ;# The next available row in each node
}

#
# Use option database to override default resources.
#
# option add *WidgetTree.thickness 3 widgetDefault ???

#
# Provide a lowercase access method for the WidgetTree class
#
proc widgettree { args } {
    uplevel ::::tycho::WidgetTree $args
}

#########################################################################
#### option configuration: -closedicon
#
# Specifies the closed icon image.  
# The default value is an image called closedNode, which is generated
# automatically. Currently we have no pixmap.
#
configbody ::tycho::WidgetTree::closedicon {
    if { $itk_option(-closedicon) == "closedNode" } {
	if {[lsearch [image names] closedNode] == -1} {
	    image create bitmap closedNode -data {
		#define closednode_width 13
		#define closednode_height 13
		static char closednode_bits[] = {
		    0x00, 0x00, 0x00, 0x00, 0x10, 0x10, 0x30, 0x10,
		    0x70, 0x10, 0xf0, 0x10, 0xf0, 0x11, 0xf0, 0x13,
		    0xf0, 0x11, 0xf0, 0x10, 0x70, 0x10, 0x30, 0x10,
		    0x10, 0x10};
	    }
	}
    }
    _updateNodes
}

#########################################################################
#### option configureation:  -closecommand
#
# The command to execute when a node of the tree is closed.
# If this command is null, clicking on the open-close button will
# close the node; if not, the node id is appended
# to the command and the command is executed at the global scope,
# and the node will not close automatically.
#
configbody ::tycho::WidgetTree::closecommand {
}

#########################################################################
#### option configureation:  -createcommand
#
# The command to execute when a node of the tree is opened
# if the node does not currently contain any widgets. The node id is appended
# to the command and the command is executed at the global scope.
# The command should use the <b>add</b> method to create the contents of
# the node. If the command is null, the node is opened but
# nothing will be displayed.
#
configbody ::tycho::WidgetTree::createcommand {
}

########################################################################
#### option configureation:  -disabledforeground
#
# The foreground color propagated to disabled child widgets, including
# node labels.
#
configbody ::tycho::WidgetTree::disabledforeground {
    _updateNodes
}

########################################################################
#### option configureation:  -foreground
#
# The foreground color propagated to child widgets, including node labels.
#
configbody ::tycho::WidgetTree::foreground {
    _updateNodes
}

########################################################################
#### option configureation:  -indent
#
# The minimum indent for each level of the tree. This option is needed
# in order that each level is indented correctly, regardless of the
# presence or not of open-close buttons at each level. It sets the
# minimum indent, not the actual indent: if either <b>-closedbitmap</b>
# or <b>-openedbitmap</b> makes an open-close button wider than the
# specified indent, that width will be used instead. (There is no
# reasonable way to avoid this, since that's how Tk's grid manager
# works.) The default value is 24 pixels.
#
configbody ::tycho::WidgetTree::indent {
    grid columnconfigure $itk_interior 0 -minsize $indent
    foreach id [array names _state] {
	if [::info exists itk_component($id)] {
	    grid columnconfigure $itk_component($id) 0 -minsize $indent
	}
    }
}

#########################################################################
#### -openicon option configuration
#
# Specifies the open icon image.  
# The default value is an image called openNode, which is generated
# automatically. Currently we have no pixmap.
#
configbody ::tycho::WidgetTree::openicon {
    if { $itk_option(-openicon) == "openNode" } {
	if {[lsearch [image names] openNode] == -1} {
	    image create bitmap openNode -data {
		#define opennode_width 13
		#define opennode_height 13
		static char opennode_bits[] = {
		    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xfc, 0x1f,
		    0xf8, 0x0f, 0xf0, 0x07, 0xe0, 0x03, 0xc0, 0x01,
		    0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0xfc, 0x1f,
		    0x00, 0x00};
	    }
	}
    }
    _updateNodes
}

########################################################################
#### option configureation:  -opencommand
#
# The command to execute when a node of the tree is opened
# and the node already has children. The node id is appended
# to the command and the command is executed at the global scope.
# The node is opened after the command returns.
#
configbody ::tycho::WidgetTree::opencommand {
}

########################################################################
#### option configureation:  -padx
#
# The horizontal padding to use when gridding leaf widgets. By default,
# all leaves are packed with the same padding. If needed, the Tk
# \fBgrid configure\fR command can be used to override this
# default -- note, however, that reconfiguring this option after
# widgets have been created will return all widgets to the new
# value of <b>-pady</b>.
#
configbody ::tycho::WidgetTree::padx {
    foreach id [array names _type] {
	if { $_type($id) != "node" } {
	    grid configure $itk_component($id) -padx $itk_option(-padx)
	}
    }
}

########################################################################
#### option configureation:  -pady
#
# The vertical padding to use when gridding leaf widgets. By default,
# all leaves are packed with the same padding. If needed, the Tk
# \fBgrid configure\fR command can be used to override this
# default -- note, however, that reconfiguring this option after
# widgets have been created will return all widgets to the new
# value of <b>-pady</b>.
#
configbody ::tycho::WidgetTree::pady {
    foreach id [array names _type] {
	if { $_type($id) != "node" } {
	    grid configure $itk_component($id) -pady $itk_option(-pady)
	}
    }
}

########################################################################
#### option configureation:  -traversal
#
# Specify whether or not to support Tab and cursor traversal through
# the tree. If true, the tab and cursor keys can be used to
# navigate through the tree (see <i>Tree Traversal</i>. The
# default is true. Note that this option is not called <b>-takefocus</b>
# because that allows the focus to go to the hull, which we don't want.
#
configbody ::tycho::WidgetTree::traversal {
    if !$itk_option(-traversal) {
        # If this widget had the focus, remove it
        if [string match $itk_component(hull)* \
                [focus -lastfor $itk_component(hull)]] {
            focus [winfo toplevel $itk_component(hull)]
        }
    }
}

#######################################################################
#### constructor
#
body ::tycho::WidgetTree::constructor {args} {
    # The counter for top-level items
    set _nextrow() 0

    # Set the grid column options for the top level frame
    grid columnconfigure $itk_interior 0 -minsize $indent -weight 0
    grid columnconfigure $itk_interior 1 -weight 1

    # Add borderwidth and relief options
    itk_option add hull.borderwidth hull.relief

    # Configure options
    eval itk_initialize $args
}


#########################################################################
#########################################################################
####                      public methods                             ####

#####################################################################
#### add type id ?option value...? ?optioncmds?
#
# Add a new node or widget to the tree. The first argument is the widget type,
# and the second is an id to give it. Raise an error if an item with
# _id_ already exists. IDs are hierarchical, as in *foo.bar*. The
# special type "node" creates a new node, in which case options are passed
# directly to the label constructor. Any other _type_ is assumed to be
# the type of the widget to create. Optional arguments are passed to the
# widget constructor, except for the _optioncmds_, if supplied. The
# return value is the id.
#
body ::tycho::WidgetTree::add {type id args} {
    if [::info exists _type($id)] {
	error "Item $id already exists"
    }
    # Decompose the id
    regexp {^((([^\.]+)?.*)?\.)?[^\.]+$} $id _ _ parent

    # If this is a hierarchical id, check that the parent is a
    # node. If the containing frame does not exist, create it.
    if { $parent != "" } {
    	_verifyNode $parent
	if ![::info exists itk_component($parent)] {
	    _mkNodeFrame $parent
	}
    }
    # Process according to type
    if { $type == "node" } {
	# Create the open/close button. Let it take the focus.
	itk_component add ${id}button {
	    button $itk_interior.${id}button \
		    -command "$this toggle $id; $this traverse to $id" \
		    -borderwidth 1 \
		    -image $itk_option(-closedicon)
	} {
	    keep -background -cursor
	    rename -takefocus -traversal traversal Traversal
        }
	grid $itk_component(${id}button) \
		-row $_nextrow($parent) \
		-column 0 \
		-sticky nw

        # Create bindings for traversal and open-close
        bind $itk_component(${id}button) <Key> "$this traverse %K %W"

	# Create the accompanying label
	itk_component add ${id}label {
	    eval label $itk_interior.${id}label -takefocus 0 $args
	}
	grid $itk_component(${id}label) \
		-row $_nextrow($parent) \
		-column 1 \
		-sticky nw
	
	# Remember the parameters of this node
	set _id($itk_component(${id}label)) $id
        set _children($id) {}
	set _nextrow($id) 0
	set _row($id) $_nextrow($parent)
	set _state($id) closed
	set _type($id) node

        lappend _children($parent) $id

	# Update the row counter. Increment it by two so that when the button
	# is opened, the new widget can be placed in the intervening row
	incr _nextrow($parent) 2

    } else {
	# Extract options
	if { [llength $args] & 1 } {
	    set optioncmds [lindex $args end]
	    set args [lreplace $args end end]
	} else {
	    set optioncmds "\nusual\n"
	}
	# Create the widget
	itk_component add $id {
	    eval $type $itk_interior.$id $args
	} $optioncmds    ;# indenting \}
	
	# Grid it
	grid $itk_component($id) \
		-row $_nextrow($parent) \
		-column 1 \
		-padx $itk_option(-padx) \
		-pady $itk_option(-pady) \
		-sticky nw
	
	# Remember the parameters of this item
	set _id($itk_component($id)) $id
	set _row($id) $_nextrow($parent)
	set _type($id) $type
	
	# Update the row counter
	incr _nextrow($parent)
    }
    return $id
}

#####################################################################
#### close nodeid
#
# Close a node of the tree. If the node is already closed or is
# disabled, do nothing.
# If the -closecommand option is not null, append the nodeid and evaluate
# the command in the global scope -- the close command must explicitly close
# the node with the hide{} method. Otherwise, just close the node.
#
body ::tycho::WidgetTree::close {nodeid} {
    _verifyNode $nodeid
    if { $_state($nodeid) == "closed"  \
	    || [$itk_component(${nodeid}button) cget -state] == "disabled" } {
	return
    }

    set _state($nodeid) closed
    if { $closecommand != "" } {
	# If there's a command, execute it
	uplevel #0 $closecommand $nodeid
    } else {
	# Otherwise just close the node
	hide $nodeid
    }
}

#####################################################################
#### delete id
#
# Delete an item from the tree. If the item is a node, then all descendents
# are deleted too. Raise an error if the item does not exist. Grid rows
# occupied by the item are not reclaimed, but this should not be visible.
#
body ::tycho::WidgetTree::delete {id} {
    if ![::info exists _type($id)] {
	error "Unknown item: $id"
    }
    # Decompose the id
    regexp {^((([^\.]+)?.*)?\.)?[^\.]+$} $id _ _ parent

    # Get the items to delete. If a node, get all descendents in
    # bottom-up order, otherwise just the id.
    if { $_type($id) == "node" } {
	set items [lsort -decreasing [array names _type $id.]]
	lappend items $id
    } else {
	set items $id
    }
    # Delete each item
    foreach id $items {
	if { $_type($id) == "node" } {
	    unset _id($itk_component(${id}label))
	    unset _id($itk_component($id))

	    unset _type($id)
	    unset _state($id)
	    unset _row($id)
	    unset _children($id)

	    destroy $itk_component(${id}button)   ;# button
	    destroy $itk_component(${id}label)    ;# label
	    destroy $itk_component($id)           ;# frame

	    unset itk_component(${id}button)   ;# button
	    unset itk_component(${id}label)    ;# label
	    unset itk_component($id)           ;# frame

	} else {
	    unset _id($itk_component($id))
	    unset _type($id)
	    unset _row($id)

	    destroy $itk_component($id)
	    unset itk_component($id)
	}
    }
    # Remove from parent list
    set t [lsearch -exact $_children($parent) $id]
    set _children($parent) [lreplace $_children($parent) $t $t]
}

#####################################################################
#### disable nodeid
#
# Disable a node. The button will be disabled, and the label foreground
# change to the disabled foreground.
#
body ::tycho::WidgetTree::disable {nodeid} {
    _verifyNode $nodeid
    $itk_component(${nodeid}button) configure -state disabled
    $itk_component(${nodeid}label) configure \
	    -foreground $itk_option(-disabledforeground)
}

#####################################################################
#### enable nodeid
#
# Enable a node. The button will be enabled, and the label foreground
# change to the regular foreground.
#
body ::tycho::WidgetTree::enable {nodeid} {
    _verifyNode $nodeid
    $itk_component(${nodeid}button) configure -state normal
    $itk_component(${nodeid}label) configure -foreground $itk_option(-foreground)
}

#####################################################################
#### hide nodeid
#
# Unconditionally close a node of the tree, without checking the
# -closecommand option. If the node is already closed or is disabled,
# do nothing.
#
body ::tycho::WidgetTree::hide {nodeid} {
    _verifyNode $nodeid
    if { $_state($nodeid) != "closed" } {
	return
    } 
    # Hide the widget, making sure it exists first
    if [::info exists itk_component($nodeid)] {
	grid remove $itk_component($nodeid)
    }
    # Change the icon
    $itk_component(${nodeid}button) configure -image $itk_option(-closedicon)

    # Update state
    set _state($nodeid) closed
}

#####################################################################
#### itemcget id option
#
# Get a configuration option of an item. If a node, get an option of
# the node label, otherwise get an option of the corresponding widget.
#
body ::tycho::WidgetTree::itemcget {id option} {
    if ![::info exists _type($id)] {
	error "Unknown item: $id"
    }
    if { $_type($id) == "node" } {
	$itk_component(${id}label) cget $option
    } else {
	$itk_component($id) cget $option
    }
}

#####################################################################
#### itemconfigure id ?option value ...? 
#
# Configure options of an item. If a node, configure the
# node label, otherwise configure the corresponding widget.
#
body ::tycho::WidgetTree::itemconfigure {id {option {}} args} {
    if ![::info exists _type($id)] {
	error "Unknown item: $id"
    }
    if { $_type($id) == "node" } {
	eval $itk_component(${id}label) $option $args
    } else {
	eval $itk_component($id) $option $args
    }
}

#####################################################################
#### locate x y ?id?
#
# Return the id of the item at the given _x_ and _y_ coordinates.
# If the _id_ argument is given, it must be a node, and the _x_ and
# _y_ coordinates are relative to the interior of that node. If _x_
# is negative, then the _x_ coordinate is ignored in figuring out
# which item is under the mouse. If _x_ is non-negative, then an id
# is returned only if the mouse is actually over an item.
#
body ::tycho::WidgetTree::locate {x y {id {}}} {
    if { $id == "" } {
	set master $itk_interior
    } else {
	if ![::info exists itk_component($id)] {
	    error "Unknown item: $id"
	}
	set master $itk_component($id)
    }
    if { $x < 0 } {
	# Get the row in this widget
	set col 0
	set row [lindex [grid location $master 0 $y] 1]
    } else {
	# Get the column and row in this widget
	foreach {col row} [grid location $master $x $y] {}
	
	# If in column zero, we didn't hit anything
	if { $col == 0 } {
	    return ""
	}
    }
    # Scan the slave widgets in column 1. If a widget is found
    # in this row, return it. If a child widget is found instead,
    # do a recursive call to find the id in the child.
    foreach slave [grid slave $master -column 1] {
	set id $_id($slave)
	if { $_row($id) == $row } {
	    return $id
	} elseif { $_row($id) + 1 == $row } {
	    return [locate \
		    [expr $x - [winfo x $slave]] \
		    [expr $y - [winfo y $slave]] \
		    $id]
	} else {
	    error "Uh-oh: something's gone wrong..."
	}
    }
}

#####################################################################
#### names ?nodeid?
#
# Return the ids of children of a node. If _nodeid_ is not supplied, return
# the top-level ids; otherwise return ids of the children of the node.
#
body ::tycho::WidgetTree::names {{nodeid {}}} {
    if { $nodeid != "" } {
	_verifyNode $nodeid
    }
    return $_children($nodeid)
}

#####################################################################
#### open nodeid
#
# Open a node of the tree. If the node is already opened or is disabled,
# do nothing.
# If the node has no children and -createcommand is not null,
# append the id and evaluate at the global scope. If the node has
# children and -opencommand is not null, append the id and
# evaluate at the global scope. Raise an error if the node does not exist
# or if _nodeid_ is not a node.
#
body ::tycho::WidgetTree::open {nodeid} {
    _verifyNode $nodeid
    if { $_state($nodeid) == "open"  \
	    || [$itk_component(${nodeid}button) cget -state] == "disabled"} {
	return
    }

    # Set the state so that callbacks make new items display
    set _state($nodeid) open

    # If the containing frame doesn't exist, execute -createcommand.
    if ![::info exists itk_component($nodeid)] {
	if { $createcommand != "" } {
	    uplevel #0 $createcommand $nodeid
	}
    } else {
	# If it does exist, call -opencommand and then grid the contents
	if { $opencommand != "" } {
	    uplevel #0 $opencommand $nodeid
	}
	grid $itk_component($nodeid)
    }
    # Change the icon
    $itk_component(${nodeid}button) configure -image $itk_option(-openicon)
}

#####################################################################
#### toggle nodeid
#
# Toggle the state of a node: if it is open, close it; if it is closed,
# open it. If it is disabled, do nothing.
#
body ::tycho::WidgetTree::toggle {nodeid} {
    _verifyNode $nodeid
    if { [$itk_component(${nodeid}button) cget -state] != "disabled" } {
	if { $_state($nodeid) == "open" } {
	    close $nodeid
	} else {
	    open $nodeid
	}
    }
}

#####################################################################
#### traverse mode ?nodeid?
#
# Traverse the tree. This method is provided because Tk's default
# Tab traversal doesn't give the user enough control over where
# the traversal goes. The _mode_ controls the traversal operation,
# while the _nodeid_ is the id of the button that currently has the
# focus. If _nodeid_ is null or not supplied, then no button has the
# focus. To aid construction of call-backs, the _nodeid_ can either be
# a node id or a widget path. _mode_ is case-insensitive, and can be one
# of <b>down</b>, <b>return</b>, <b>up</b>, <b>left</b>, or <b>right</b>,
# with the effect described for the default bindings. It can also
# be <b>to</b>, which moves the focus to the given node.
# If the <b>-traversal</b> option is false, the method returns
# without doing anything.
#
body ::tycho::WidgetTree::traverse {mode {nodeid {}}} {
    if !$itk_option(-traversal) {
        return
    }
    set mode [string tolower $mode]
    set new {}
    # If no button has the focus, give it somewhere
    if { $nodeid == "" } {
        if { $mode == "up" } {
            set new [lindex [names] end]
        } elseif [regexp {^(down|left|right)$} $mode] {
            set new [lindex [names] 0]
        }
    } else {
        if { [regexp {(.+)button$} $nodeid _ t] \
                && [::info exists _id(${t}label)] } {
            # Got a widget path, so convert to id
            set nodeid $_id(${t}label)
        } elseif ![::info exists _type($nodeid)] {
            error "Unknown item: $nodeid"
        }
        # Decompose the id
        regexp {^((([^\.]+)?.*)?\.)?[^\.]+$} $nodeid _ _ parent

        # A button currently has the focus, so move the focus
        switch -exact $mode {
            "down" {
                set t [lsearch -exact $_children($parent) $nodeid]
                while { [incr t] < [llength $_children($parent)] } {
                    if { $_type([lindex $_children($parent) $t]) == "node" } {
                        set new [lindex $_children($parent) $t]
                        break
                    }
                }
            }
            "up" {
                set t [lsearch -exact $_children($parent) $nodeid]
                while { [incr t -1] >= 0 } {
                    if { $_type([lindex $_children($parent) $t]) == "node" } {
                        set new [lindex $_children($parent) $t]
                        break
                    }
                }
            }
            "left" {
                if { $parent != "" } {
                    set new $parent
                }
            }
            "to" {
                set new $nodeid
            }
            "right" {
                if { $_state($nodeid) == "closed" } {
                    open $nodeid
                }
                if { $_children($nodeid) != "" } {
                    set new [lindex $_children($nodeid) 0]
                }
            }
            "return" {
            	toggle $nodeid
            }
        }
    }
    if { $new != "" } {
        focus $itk_component(${new}button)
    }
}

#####################################################################
#### type id
#
# Return the type of _id_. The type will be value of the _type_
# argument passed to the <b>add</b> method when the item was created.
# Raise an error if the item does not exist.
#
body ::tycho::WidgetTree::type {id} {
    if ![::info exists _type($id)] {
	error "Unknown item: $id"
    }
    return $_type($id)
}

#####################################################################
#####################################################################
####                       private methods                       ####

#####################################################################
#### _mkNodeFrame
#
# Create the frame for an individual node. This method is called the
# first time an item is added to a node, not when the node itself
# is created. This strategy reduces the time to create each node
# of the tree.
#
body ::tycho::WidgetTree::_mkNodeFrame {id} {
    # Create the frame
    itk_component add $id {
	frame $itk_interior.$id
    }
    # Remember the reverse mapping
    set _id($itk_interior.$id) $id

    # If the node is open, display its contents
    if { $_state($id) == "open" } {
	grid $itk_interior.$id \
		-row [expr $_row($id)+1] \
		-column 1 \
		-sticky nw
    }
    # Configure internal column size and weight
    grid columnconfigure $itk_interior.$id 0 -minsize $indent -weight 0
    grid columnconfigure $itk_interior.$id 1 -weight 1
}

#####################################################################
#### _updateNodes
#
# Update node visuals in response to configuration update.
#
body ::tycho::WidgetTree::_updateNodes {} {
    foreach id [array names _state] {
        if { $_state($id) == "closed" } {
            $itk_component(${id}button) configure -image $itk_option(-closedicon)
        } else {
	    $itk_component(${id}button) configure -image $itk_option(-openicon)
	}
	if { [$itk_component(${id}button) cget -state] == "disabled" } {
	    $itk_component(${id}label) configure \
		    -foreground $itk_option(-disabledforeground)
    	} else {
	    $itk_component(${id}label) configure \
		    -foreground $itk_option(-foreground)
	}
    }
}

#####################################################################
#### _verifyNode id
#
# Raise an error if _id_ is not known or is not a node.
#
body ::tycho::WidgetTree::_verifyNode {id} {
    if ![::info exists _type($id)] {
	error "Unknown item: $id"
    }
    if { $_type($id) != "node" } {
	error "Item $id is not a node"
    }
}
