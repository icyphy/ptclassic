# Parse Java files to generate class models.
#
# @Author: Cliff Cordeiro
#
# @Contributor: Christopher Hylands
#
# @Version: @(#)JavaParser.itcl	1.23 1998
#
# @Copyright (c) 1996-1998 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### JavaParser
# Parses Java code, creates <a href = 
# "../../../../kernel/doc/codeDoc/Class.idoc">::tycho::ClassModel</a>
# models for each class found, and outputs those to the proper
# files. The most convenient interface
# is from <code>::tycho::ClassParser::generateIDoc</code>
# which will also generate Java documentation.
#
# The parsing is done in three main procedures. <code>readFile</code>
# reads in a file line by line, deletes irrelevant comments, and formats the
# doc comments in such a way
# as to allow easier parsing later. That is, by turning a comment like this:
# 
# <pre>
# // Blah blah
# /** blah */
# </pre>
# 
# Into:
#
# # {blah}
#
# This is also where other pre-formatting is done which allows the following
# Tcl list-based step to complete successfully. (Such as separating brackets
# and semicolons, which breaks lists.)
#
# The next method used is parseData, which sees the output of readFile as a Tcl 
# list. It goes through item by item, looking for keywords, and then calls 
# the appropriate method to add those into the Class model (or models, if 
# more than one Class is being defined).
#
# Finally, the classes are output as files by calling their write methods.
#
class ::tycho::JavaParser {
    inherit ::tycho::ClassParser

    constructor {args} {}
    destructor {}
    
    ###################################################################
    ####                         public methods                    ####

    # Output an IDoc representation of the loaded classes.
    method generateSingleFileIDoc {filename}

    # Read the current file, call parseData
    method readFile {filename}

    ###################################################################
    ####                      public procs                         ####

    # Return a list of all the classes defined in the given source file.
    public proc getClassesInSource {filename}

    # Check if the given file is newer than its related documentation.
    public proc modCheck {filename}

    ###################################################################
    ####                   protected methods                       ####

    ###################################################################
    ####                   private methods                         ####

    # Format and add the arguments in a form the class model understands
    private method addArgs {class context arguments}

    # Add the class comment by formatting it into HTML.
    private method addClassComment {class expl}

    # Add a method to the class model
    private method addMethod {class protection modifiers others comment} 

    # Add a variable to the class model
    private method addVariable {class protection modifiers others comment}

    # Parse the data within a class definition
    private method parseClass {class body}

    # Parse the data for a single file, creating and modifying classes as needed
    private method parseData {data}

    # Parse the extends/implements portion of the class definition
    private method parseExtends {class extends}

    # Parse the class modifiers (public/non-public, final, etc.)
    private method parseModifiers {class modifiers}

    # Parse the data relating to a method or variable declaration
    private method parseVarMeth {class protection modifiers others comment}

    # Split a tcl list of a Java method/variable declaration into Java tokens
    private method tokenize {list}

    # Return a signature for this method
    private method uniqueName {methodName arguments}

    ###################################################################
    ####                   private variables                       ####

    # Keywords that can appear in a class definition
    private common keywords "# public private protected static synchronized \
            abstract final native transient volatile"

}

#######################################################################
#### constructor
#
body ::tycho::JavaParser::constructor {args} {
}

    ###################################################################
    ###################################################################
    ####                      public methods                       ####

#####################################################################
#### generateSingleFileIDoc
# Output an IDoc representation of the loaded classes.
#
body ::tycho::JavaParser::generateSingleFileIDoc {filename} {
    # output class model data
    foreach {class model} [array get _classmodels] {
        # puts $class
        # puts [$_classmodels($class) describe]
        if {$class != "related"} {
            set docFile [namespaceTail $class].idoc
            ::tycho::rmIfNotWritable $docFile
            set fd [open $docFile "w"]
		    puts $fd [$model describe]
            close $fd
            # FIXME - ?
            delete object $_classmodels($class)
        } else {
            if {$_relatedModified} {
                # NOTE: goes by the filename, not the class name, for
                # related documentation
                if {[array size _classmodels] == 1} {
                    # Not related, ONLY docs
                    set relFile [file root $filename].javaclass
                } else {
                    # Related file
                    set relFile [file root $filename]_rel.idoc
                }
                ::tycho::rmIfNotWritable $relFile
                set fd [open $relFile "w"]
				puts $fd [$model describe]
                close $fd
            }
		    # FIXME - ?
            delete object $_classmodels(related)
        }
    }
}

#####################################################################
#### readFile 
# Read in a file line by line, identify comments, merge multi-line comments
# into one line, bracket protect the comment contents, and append to a 
# variable. Then parse that variable to distill class information.
#
body ::tycho::JavaParser::readFile {filename} {
    # Initialize single file data
    set _author ""
    set _contributor ""
    set _version ""
    set _copyright ""

    catch {unset _classmodels}

    # check extension (only java supported here)
    set ext [file extension $filename]
    switch $ext {
        .java {
            set _language Java
        }
        default {
            error "::tycho::JavaParser does not support $ext files"
        }
    }

    set inDocComment 0
    set inComment 0
    set _sourceFile $filename
    set currentComment ""
    set inEscapedComments ""
    set fd [open $filename]
    while {![eof $fd]} {
        # Keep the interface live.
        idocUpdate $this

        gets $fd lineIn

        if {!$inComment} {
            # Remove // comments
            # which begin the line
            if [ regexp {^[	 ]*\/\/.*$} $lineIn] {
                continue
            }
            # which follow code
            if [ regexp {^(.*)\/\/(.*)$} $lineIn dmy code comment] {
                set lineIn $code
            }

            # Remove line ending and single line /* */ comments
            if [ regexp {^(.*)\/\*[^*](.*)\*\/(.*)$} \
                    $lineIn d code comment code2] {
                # NOTE: only removes a single comment per line
                append code $code2
                if {[string trim $code " \t"] == ""} {
                    # No code
                    continue
                } {
                    # Code and comments
                    set lineIn $code
                }
            }
            
            # Check for multi-line comments
            if [ regexp {^[	 ]*(\/\*\*)(.*)$} $lineIn dmy dmy2 comment] {
                # Doc comment
                set inDocComment 1
                set inComment 1
                set lineIn $comment
            } elseif [ regexp {^[	 ]*(\/\*)(.*)$} \
                    $lineIn dmy dmy2 comment] {
                # C style comment
                set inComment 1
                set lineIn $comment
            }
        }
    
        if {$inDocComment} {
            if [ regexp {^(.*)(\*\/)(.*)$} $lineIn dmy comment dmy2 code] {
                # End of doc comment
                append commentText [string trimleft $comment "* \t"] " "
                append inEscapedComments "# \{$commentText\} \n"
                set commentText ""
                set lineIn $code
                set inComment 0
                set inDocComment 0
            } {
                # Doc comment continues
                append commentText [string trimleft $lineIn "* \t"] "\n"
                continue
            }
        } {
            if {$inComment} {
                if [ regexp {^(.*)(\*\/)(.*)$} $lineIn dmy comment dmy2 code] {
                    # End of C style comment
                    set lineIn $code
                    set inComment 0
                } {
                    # C style comment continues
                    continue
                }
            }
        }

        # Remove brace/quote followed by semi-colon errors (only one pass)
        if [ regexp {^(.*[\"\}]);(.*)$} $lineIn dmy first last] { ;# "
            set lineIn "$first ;$last"
        }
        # Remove open brace following anything errors (only one pass)
        if [ regexp "^(\[^\{\]+)\[\{\](.*)\$" $lineIn dmy first last] {
            set lineIn "$first \{$last"
        }

        append inEscapedComments "$lineIn \n"
    }

    close $fd

    parseData $inEscapedComments
}

    ###################################################################
    ###################################################################
    ####                      public procedures                    ####

#####################################################################
#### getClassesInSource
# Quickly scan the source file for the classes which are defined within.
# Not implemented for Java.
#
body ::tycho::JavaParser::getClassesInSource {filename} {
    error "Not implemented in Java."
}

#####################################################################
#### modCheck
# Checks if the current file is newer than its associated documentation.
# And returns 1 if so. Also returns 1 if the generator is newer than the
# documentation.
#
# This requires searching for all classes defined within the file and
# checking if those class's documents are older. JavaParser does not
# support inner classes (yet), so only checks if the file is newer than it's 
# top-level class' documentation.
#
# For non-java files, it silently returns 1 because it can't tell.
# NOTE: should normally be called through its parent, which will call the
# appropriate sibling for the file extension.
#
body ::tycho::JavaParser::modCheck {filename} {
    global env

    # Check to see if the generator itself has been changed.
    set javaGenTime [file mtime [file join $env(IDOC_LIBRARY) JavaParser.itcl]]
    set idocGenTime [file mtime [file join $env(IDOC_LIBRARY) ClassParser.itcl]]
    if {$javaGenTime > $idocGenTime} {
        set genTime $javaGenTime
    } {
        set genTime $idocGenTime
    }

    set sourceTime [file mtime $filename]
    set extension [file extension $filename]
    
    if {$extension != ".java"} {
        # Can't check other types of files
        return 1
    }
    # Takes advantage of Java's requirement that the classname and filename
    # be identical
    set docFile [file join doc codeDoc [file root $filename].javaclass]
        
    if ![file exists $docFile] {
        # There is no documentation.
        return 1
    }

    set docTime [file mtime $docFile]
    if {$sourceTime > $docTime} {
        # The source is newer.
        return 1
    }
    if {$genTime > $docTime} {
        # The generator is newer.
        return 1
    }

    # Not modified
    return 0   
}

    ###################################################################
    ###################################################################
    ####                     protected methods                     ####

    ###################################################################
    ###################################################################
    ####                      private methods                      ####

#####################################################################
#### addArgs
# Add the arguments to a method or Constructor to its associated entity in
# the class model. This involves reformatting from Java:
# <code>(int num1, String ding)</code> to the class model's preferred
# format: <code>parameter num1 -type int; parameter ding -type String</code>
#
body ::tycho::JavaParser::addArgs {class context arguments} {
    set classModel $_classmodels($class)
    set istype 1
    set type ""
    set name ""

    # Remove parentheses
    set lastarg [expr [llength $arguments] -2] 
    set arguments [lrange $arguments 1 $lastarg]

    # Add a comma to signal the end
    append arguments " ,"
    
    foreach token $arguments {
        if {$istype == 1} {
            set type $token
            set istype 0
        } {
            # Got the type, now check for [] and ,
            if {$token == "\["} {
                # part of the type
                append type \[
            } elseif {$token == "\]"} {
                # part of the type
                append type \]
            } elseif {$token == ","} {
                # next token is type
		$classModel add argument [concat $context $name] -type $type
                set type ""
                set name ""
                set istype 1
            } else {
                set name $token
            }
        }
    }
}

#####################################################################
#### addClassComment
# Add a class comment to the class model. 
#
# NOTE: What to do about see tags here?
#
body ::tycho::JavaParser::addClassComment {class expl} {
    set classModel $_classmodels($class)
    set expl [formatClassComment $expl]
    $classModel configure -explanation "$expl"
}

#####################################################################
#### addMethod 
# Add a method at the given protection, its arguments, and associated
# description to the class model for $class.
#
body ::tycho::JavaParser::addMethod {class protection modifiers others comment} {    
    set isConstructor 0
    set exceptions {}
    # Check if is static
    set static [lsearch -exact $modifiers "static"]
    if {$static != -1} {
        lreplace $modifiers $static $static
        set scope "class"
    } {
        set scope "instance"
    }

    # Break into tokens, pull out known information
    set others [tokenize $others]

    set type [lindex $others 0]
    set others [lreplace $others 0 0]

    # Pull out brackets if it is an array type
    while {[lindex $others 0] == "\["} {
        append type {[]}
        set others [lreplace $others 0 1]
    }

    set methodName [lindex $others 0]
    if {$methodName == "\("} {
        # It's a constructor and has no type
        set methodName $type
        set isConstructor 1
    } {
        set others [lreplace $others 0 0]
    }

    # Pull out the arguments, including ( and )
    set endparen [lsearch -exact $others ")"]
    if {$endparen != -1} {
        # There should always be an endparen
        set arguments [lrange $others 0 $endparen]
        set others [lreplace $others 0 $endparen]
    } else {
        error "::tycho::JavaParser::addMethod: no end parenthesis\
                found in $methodName."
    }
    
    if {$others != {}} {
        # Should be a "throws" at index 0
        if {[lindex $others 0] == "throws"} {
            set others [lreplace $others 0 0] 
            while {$others != {}} {
                lappend exceptions [lindex $others 0]
                # Assume element 1 is a comma
                set others [lreplace $others 0 1]
            }
        }
    }
    
    set classModel $_classmodels($class)
    set methodName [uniqueName $methodName $arguments]
    set desc [shortDesc $comment]
    set expl [formatExpl $comment]

    if {$isConstructor} {
        # Constructors are special
        $classModel add constructor $methodName \
		-protection $protection \
                -modifiers $modifiers \
                -exceptions $exceptions \
                -description $desc \
                -explanation $expl

    } elseif {$methodName == "finalize"} {
        # So are destructors
        $classModel add destructor $methodName \
		-protection $protection \
                -modifiers $modifiers \
                -exceptions $exceptions \
                -description $desc \
                -explanation $expl

    } else {
        $classModel add method m:$methodName \
		-protection $protection \
		-scope $scope \
                -type $type \
                -modifiers $modifiers \
                -exceptions $exceptions \
                -description $desc \
                -explanation $expl
	set methodName m:$methodName
    }
    addArgs $class $methodName $arguments
}

#####################################################################
#### addVariable 
# Add a variable at the given protection, its associated description,
# and its default (if any) to the class model for $class.
#
body ::tycho::JavaParser::addVariable \
		{class protection modifiers others comment} {

    set default {}

    # Check if is static
    set static [lsearch -exact $modifiers "static"]
    if {$static != -1} {
        lreplace $modifiers $static $static
        set scope "class"
    } {
        set scope "instance"
    }

    # Break into tokens, pull out known information (tokenizing done in caller)

    set type [lindex $others 0]
    set others [lreplace $others 0 0]

    # Pull out brackets if it is an array type
    while {[lindex $others 0] == "\["} {
        append type {[]}
        set others [lreplace $others 0 1]
    }

    set varName [lindex $others 0]
    set others [lreplace $others 0 0]

    # Pull out brackets if it is an array type (again)
    while {[lindex $others 0] == "\["} {
        append type {[]}
        set others [lreplace $others 0 1]
    }

    if {$others != {}} {
        # Assume there's an equals sign at index 0
        if {[lindex $others end] != ";"} {
            # The ; is munged to the last item
            set last [lindex $others end]
            set last [string trimright $last ";"]
            set default [lreplace $others end end $last]
            set default [lreplace $default 0 0]
        } {
            # The last item is a ;
            set enddef [expr [llength $others] -2]
            set default [lrange $others 1 $enddef]
            if {$default == "\{\"\"\}"} {
                # There's a null string default
                set default {""}
            }
        }
    }
    set classModel $_classmodels($class)
    
    # Create the variable entry
    set desc [shortDesc $comment]
    set expl [formatExpl $comment]
    $classModel add variable v:$varName \
	    -modifiers $modifiers \
	    -scope $scope \
            -protection $protection \
            -type $type \
            -default $default \
            -description $desc \
            -explanation $expl
}

#####################################################################
#### parseClass
# This method works in much the same way as parseBody. It takes the 
# contents of the class definition and looks for keywords. 
# These are then parsed according to Java syntax, relevant
# information added to the class model for this class. 
#
# Because there may be multiple classes in one Java file, each class
# model is stored in an array which is indexed by the name of the class
# the class models are created in this procedure while parsing the class
# definition.
#
# NOTE: Actually, this does not yet support Java 1.1 Inner classes.
#
body ::tycho::JavaParser::parseClass {class body} {
    set _classmodels($class) [::tycho::ClassModel [::tycho::autoName class]]
    $_classmodels($class) configure \
            -language $_language \
            -modelmode javaclass \
            -classname $class \
            -sourcefile [file join .. .. $_sourceFile] \
            -package $_package

    set lastComment {}
    set lastProtection {}
    set default {}
    set modifiers {}
    set others {}
    set inComment 0
    set inVarMeth 0
    set inStatic 0
    set inSynchronized 0

    # To assure every item is seen
    lappend body # #

    # Parse each item
    foreach construct $body {
        # Keep the interface live.
        idocUpdate $this

        # Ignore empty lines (is necessary?)
        if {$construct == "\n"} {
            continue
        }
        # First, check if we know what we're doing.
        if {$inComment} {
            # This is a comment. Save it.
            set lastComment "$construct"
            set inComment 0
            continue
        }
        if {$inStatic} {
            set construct [string trimright $construct " \t\n"]
            set inStatic 0
            if {[llength $construct] == 1} {
                # Heuristic says: not a static initializer
                lappend modifiers "static"
                # Keep parsing
            } {
                # Heuristic says: a static initializer, ignore
                continue
            }
        }
        if {$inSynchronized} {
            set construct [string trimright $construct " \t\n"]
            set inSynchronized 0
            if {[llength $construct] == 1} {
                # Heuristic says: a synchronized method
                lappend modifiers "synchronized" 
                # Keep parsing
            } {
                # Heuristic says: a synchronized block, ignore
                continue
            }
        }
        if {$inVarMeth} {
            # Default protection is package
            if {$lastProtection == {}} {
                set lastProtection package
            }
            parseVarMeth $class $lastProtection $modifiers $others $lastComment
            set inVarMeth 0
            set lastProtection {}
            set modifiers {}
            set others {}
            set lastComment {}
            # present construct is valid, continue
        }
        # Below, parse keywords
        switch -- $construct {
            "#" {
                set inComment 1
            }
            "private" {
                set lastProtection private
            }
            "protected" {
                set lastProtection protected
            }
            "public" {
                set lastProtection public
            }
            "static" {
                set inStatic 1
            }
            "synchronized" {
                set inSynchronized 1
            }
            "abstract" {
                lappend modifiers "abstract"
            }
            "final" {
                lappend modifiers "final"
            }
            "native" {
                lappend modifiers "native"
            }
            "transient" {
                lappend modifiers "transient"
            }
            "volatile" {
                lappend modifiers "volatile"
            }
            default {
                # Check if it is an end of command (;) or block {}
                set construct [string trimright $construct " \t\n"]
                set lastchar [string range $construct end end]
                if {$lastchar == "\}"} {
                    set inVarMeth 1
                    # Don't save the extra block, it's useless
                } elseif {$lastchar == ";"} {
                    set inVarMeth 1
                    # We might need the last block
                    lappend others $construct
                } else {
                    # Just a word, figure out what it is at the end.
                    lappend others $construct
                }
            }
        }
    }
}

#####################################################################
#### parseData
# Expects a modified source file, with adjacent doc comments all on one line,
# useless comments removed. This data is then gone through as a list,
# looking for toplevel keywords, such as "class" "package" and 
# "import". These are parsed according to Java's syntax and information
# of note is added to the class model.
#
# Comments are saved until the next keyword and are assumed to belong to
# that entity. The class comment, then, must be the comment directly
# preceding the class definition, ignoring initialization code that 
# JavaParser does not recognize.
#
# NOTE: @ tags are only recognized in the class comment, the comment 
# that appears before the class definition.
#
body ::tycho::JavaParser::parseData {data} {
    set inClass 0
    set inComment 0
    set inPackage 0
    set inImport 0
    set import ""
    set extends ""
    set modifiers ""
    set firstComment {}
    foreach construct $data {
        # Keep the interface live.
        idocUpdate $this

        if {$inComment} {
            # This is the class comment. Save it.
            # FIXME: assumes there is only one top-level doc comment
            set firstComment $construct
            set inComment 0
            continue
        }
        if {$construct == "\n"} {
            # Ignore empty lines outside of comments
            continue
        }
        if {$inPackage} {
            # This is part of a package statement.
            # package packagename;
            set _package [string trimright $construct " \t;"]
            set inPackage 0
            continue
        }
        if {$inImport} {
            # This is part of an import statement.
            # import package.class | import package.*
            append import [string trimright $construct " \t;"]
            set inImport 0
            continue
        }
        if {$inClass} {
            # This is part of a class statement. 
            # modifier+ class name extends?, implements* {definition}
            if {$inClass == 1} {
                set className $construct
                set inClass 2
            } elseif {$inClass == 2} {
                # Check if it is an end of command (;) or block {}
                # then it's the definition
                set construct [string trimright $construct " \t\n"]
                set lastchar [string range $construct end end]

                if {$lastchar == ";" || $lastchar == "\}" || $construct == {}} {
                    parseClass $className $construct
                    parseModifiers $className $modifiers
                    parseExtends $className $extends
                    addClassComment $className $firstComment
                    # FIXME: what to do with import?
                    set extends ""
                    set modifiers ""
                    set inClass 0
                } else {
                    # Or it's more parents
                    lappend extends $construct
                }
                continue
            }
        }
        if {[string index $construct 0] == "#"} {
            # A comment follows. NOTE: Is the index necessary?
            set inComment 1
            continue
        }
        if {$construct == "package"} {
            # A package name follows.
            set inPackage 1
            continue
        }
        if {$construct == "import"} {
            # An import statement follows.
            set inImport 1
            continue
        }
        if {$construct == "class"} {
            # A class follows.
            set inClass 1
            continue
        }
        if {[lsearch -exact $keywords $construct] != -1} {
            lappend modifiers $construct
            continue
        }
        # puts "UNCLASSIFIED: $construct"
    }
}


#####################################################################
#### parseExtends
# Parse the extends and implements clause of a Java class definition.
# Extends denotes a parent which supplies an implementation, implements
# denotes parents which supply an interface.
#
body ::tycho::JavaParser::parseExtends {class extends} {
    set classModel $_classmodels($class)

    if [regexp {^[ ]*extends[ ]+([A-Za-z_]+[A-Za-z0-9_.]*)[ ]+(.*)$} \
            $extends dmy parent other] {
        # Extends a class and more
        set inherits [$classModel cget -inherits]
        lappend inherits $parent
        $classModel configure -inherits $inherits
        set extends [string trimleft $other " \t"]
    } elseif [regexp {^[ ]*extends[ ]+([A-Za-z_]+[A-Za-z0-9_.]*)[ 	]*$} \
            $extends dmy parent] {
        # Extends a class and that's it
        set inherits [$classModel cget -inherits]
        lappend inherits $parent
        $classModel configure -inherits $inherits
        return
    }

    if [regexp {^[ ]*implements[ ]+(.*)$} $extends dmy parents] {
        # Implements a class
        while {$parents != ""} {
            if [regexp {^([A-Za-z_]+[A-Za-z0-9_.]*)[ ]*,[ ]*(.*)$} \
                    $parents dmy parent left] {
                # Pull out a parent interface
		        set implements [$classModel cget -implements]
        		lappend implements $parent
		        $classModel configure -implements $implements
                set parents [string trimleft $left " \t"]
            } else {
                # Last interface
                regexp {^([A-Za-z_]+[A-Za-z0-9_.]*)[ ]*(.*)$} \
                        $parents dmy parent left
                if {$parent != ""} {
			        set implements [$classModel cget -implements]
	        		lappend implements $parent
			        $classModel configure -implements $implements
                    set parents $left
                } 
                set extends $parents
                set parents ""
            }
        }
    }
    if [regexp {^[ ]*throws[ ]+(.*)$} $extends dmy throws] {
        # Throws an exception
        set exceptions ""
        while {$throws != ""} {
            if [regexp {^([A-Za-z_]+[A-Za-z0-9_.]*)[ ]*,[ ]*(.*)$} \
                    $throws dmy throw left] {
                # Pull out an exception
                lappend exceptions $throw
                set throws [string trimleft $left " \t"]
            } {
                # Last exception
                regexp {^([A-Za-z_]+[A-Za-z0-9_.]*)[ ]*(.*)$} \
                        $throws dmy throw left
                if {$throw != ""} {
                    lappend exceptions $throw
                    set throws $left
                } 
                set extends $throws
                set throws ""
            }
        }
        $classModel configure -exceptions $exceptions
    }
}

#####################################################################
#### parseModifiers
# Parse the class modifiers and protection and add them to the class 
# model. Examples of class modifiers are final and (?). FIXME: Does 
# not support other types of classes (inner classes in Java 1.1). 
#
body ::tycho::JavaParser::parseModifiers {class modifiers} {
    set classModel $_classmodels($class)
    if [regexp {^(.*)[	 ]*public[ 	]*(.*)$} $modifiers dmy other other2] {
        $classModel configure -protection public
        set modifiers [string trimleft "$other $other2" " \t"]
    } {
        $classModel configure -protection package
    }
    $classModel configure -modifiers $modifiers
}

#####################################################################
#### parseVarMeth
# Parse what may be a variable or method passed from parseClass. Decide
# which it is, then call addVariable or addMethod
#
body ::tycho::JavaParser::parseVarMeth {class protection modifiers\
        others comment} {
    set isMethod 0

    # Only methods may be native
    if {[lsearch -exact $modifiers "native"] != -1} {
        addMethod $class $protection $modifiers $others $comment
        return
    }
    # Or abstract
    if {[lsearch -exact $modifiers "abstract"] != -1} {
        addMethod $class $protection $modifiers $others $comment
        return
    }

    # Place spaces around interesting items
    if [ regexp {^([^(]+)[(](.*)$} $others dmy first last ] {
        set others "$first ( $last"
    }
    if [ regexp {^([^=]+)[=](.*)$} $others dmy first last ] {
        set others "$first = $last"
    }

    # Look for a "(" or an "="
    foreach token $others {
        if {$token == "("} {
            set isMethod 1
            break
        }
        if {$token == "="} {
            break
        }
    }

    if {$isMethod} {
        # Remove the method body, it is unneeded
        regexp "^(\[^)\]+\[)\]\[^\{\]*)\[\{\]" $others dmy others
        addMethod $class $protection $modifiers $others $comment
        return
    }

    # It's a variable if isMethod is unset
    
    # Break into tokens
    set others [tokenize $others]
    
    # Check for multiple declarations (int x=2,y,z=3;)
    set c_others {}
    foreach token $others {
        if {$token == "," || $token == ";"} {
            addVariable $class $protection $modifiers $c_others $comment
            # FIXME: won't work with: int[] x, y; will work with int x[],y[];
            set c_others [lindex $c_others 0]
        } else {
            lappend c_others $token
        }
    }
}

#####################################################################
#### tokenize
# Turn a tcl list into a list of Java tokens. Basically, a list element
# can either be an identifier followed by something else or a token
# followed by something else. tokenize breaks up each element, then 
# moves on to the next until all elements are broken into tokens.
#
body ::tycho::JavaParser::tokenize {list} {
    set retval ""

    # Remove extra braces so regexp works
#     foreach item $list {
#         append tlist " " $item
#     }
#     set list $tlist

    while {$list != {}} {
        # Pull off a token that's white space delimited
        set ltoken [lindex $list 0]
        set list [lreplace $list 0 0]
        
        while {$ltoken != {}} {
            # Either identifier-something 
            # NOTE: added numbers too, not complete
            if [regexp {^([A-Za-z_]+[A-Za-z0-9_.]*)(.*)$} \
                    $ltoken dmy id remain] {
                lappend retval $id
                set ltoken $remain
                # Or token-something
            } elseif [regexp {^([][,()=;])(.*)$} \
                    $ltoken dmy token remain] {
                lappend retval $token
                set ltoken $remain
                # Or a number constant-something
            } elseif [regexp {^([-]?[0-9]*[.]?[0-9]+[fFlL]?)(.*)$} \
                    $ltoken dmy token remain] {
                lappend retval $token
                set ltoken $remain
                # Or just a token
            } elseif {$ltoken != {}} {
                lappend retval $ltoken
                set ltoken {}
            }
        }
        
    }
    return $retval
}

#####################################################################
#### uniqueName
# Return a signature of the given method in the form: 
# methodName(arg1type,arg2type,arg3type)
# Substitutes <>s for brackets to avoid inconsistencies in the
# string translation.
#
# Try:
# <pre><tcl>string match {* b[] *} {a b[] c}</tcl></pre>
#
body ::tycho::JavaParser::uniqueName {methodName arguments} {
    set type 1
    
    append retval $methodName "("
    
    # Remove parentheses
    set lastarg [expr [llength $arguments] -2] 
    set arguments [lrange $arguments 1 $lastarg] 
    
    foreach token $arguments {
        if {$type == 1} {
            append retval $token
            set type 0
        } {
            # Got the type, now check for [] and ,
            if {$token == "\["} {
                # part of the type
                append retval \<
            } elseif {$token == "\]"} {
                # part of the type
                append retval \>
            } elseif {$token == ","} {
                # next token is type
                append retval ","
                set type 1
            }
        }
    }

    append retval ")"
    
    return $retval
}
