#! /bin/csh -f
# script to make directories and links for an architecture
# $Id$

if (! $?ARCH ) then
	echo The environment variable ARCH must be set.
	exit 1
endif

mkdir bin.$ARCH lib.$ARCH obj.$ARCH
cd obj.$ARCH
ln -s ../src/makefile .
foreach x (domains interp kernel pigiRpc tests preproc)
	mkdir $x
	cd $x
	ln -s ../../src/$x/makefile .
	if (-r ../../src/$x/make.template) ln -s ../../src/$x/make.template .
	cd ..
end

foreach x (xgraph filters)
	mkdir $x
	cd $x
	ln -s ../../src/$x/Makefile .
	cd ..
end

cd filters
foreach x (optfir wfir)
	mkdir $x
	cd $x
	ln -s ../../../src/filters/$x/Makefile .
	cd ..
end
cd ..

cd domains
foreach x (sdf ddf de capsim thor)
	mkdir $x
	cd $x
	ln -s ../../../src/domains/$x/makefile .
	foreach y (kernel stars)
		mkdir $y
		cd $y
		ln -s ../../../../src/domains/$x/$y/{make.template,makefile} .
		cd ..
	end
	cd ..
end

# Thor domain is a bit different
cd thor
foreach y (libarp pepp)
	mkdir $y
	cd $y
	ln -s ../../../../src/domains/thor/$y/{make.template,makefile} .
	cd ..
end

# thor/stars
cd stars
rm make.template
foreach x (analyzerX11 drvmon generic xxttl)
	mkdir $x
	cd $x
	ln -s ../../../../../src/domains/thor/stars/$x/{make.template,makefile} .
	cd ..
end

# This is for the makefile, go back to the top, from src/domains/thor/stars
cd ../../../..
touch obj.$ARCH
