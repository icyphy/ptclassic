#!/bin/csh -f
#
# Script to find the root (installation) Mathematica directory.  Used by
# makefiles to determine the location of the MathLink external interface
# library.  If the script finds a root Mathematica directory that contains
# the sub-directories Bin/MathLink and Source/Includes, then the script
# will return the root Mathematica directory.  Otherwise, it returns
# an empty string.
#
# Author:  Brian L. Evans
# Version: $Id$
#
# Copyright (c) 1990-%Q% The Regents of the University of California.
# All rights reserved.
# See the file $PTOLEMY/copyright for copyright notice,
# limitation of liability, and disclaimer of warranty provisions.
#
# This script takes no arguments.  It first searches for the script
# "math" on the path and if it is not found there, it searches on
# an alternative path of bin sub-directories in /usr.  If it finds
# the math script, then it extracts the value of the 'mathdir'
# shell variable.
#
#  Look for the Mathematica kernel startup script "math"
#
set searchfile = "math"
#
#  Search the path
#
foreach i ( $path )
  if ( -x $i/$searchfile && ! -d $i/$searchfile ) then
    set mmapath = $i/$searchfile
    break
  endif
end
#
#  Search an alternate path:  all bin sub-directories of /usr
#
if ( ! $?mmapath ) then
  if ( -x /usr/bin && -r /usr/bin && -d /usr/bin ) then
    set altpath = /usr/{bin,*/bin}
    foreach i ( $altpath )
      if ( -x $i/$searchfile && ! -d $i/$searchfile ) then
        set mmapath = $i/$searchfile
        break
      endif
    end
  endif
endif
#
#  If the math script is found, then
#  1. extract the mathdir setting from the "math" script
#  2. make sure that the mathdir setting is valid:  the mathdir should
#     have Source/Includes and Bin/MathLink sub-directories
#
if ( ! $?mmapath ) then
  echo ""
else
  # The following command to set newpath takes the following steps:
  # 1. eliminate all comments:           sed -e 's/#\(.*\)//g' 
  # 2. find the expression for mathdir:  grep "mathdir="
  # 3. extract the value of mathdir:     sed -e 's/mathdir=//g'
  set newpath = `sed -e 's/#\(.*\)//g' $mmapath | grep 'mathdir=' | sed -e 's/mathdir=//g'`
  # If the newpath is not an absolute path name, then append
  # newpath to the mmapath (mmapath is always guaranteed to be correct)
  if ( ! -e $newpath ) then
    set newdir = "$mmapath:h"
    set newpath = "$newdir/$newpath"
  endif
  set mathlinksource = $newpath/Source
  set mathlinkincdir = $mathlinksource/Includes
  if ( -x $mathlinksource && -r $mathlinksource && -d $mathlinksource && \
       -x $mathlinkincdir && -r $mathlinkincdir && -d $mathlinkincdir ) then
    set mathlinkbin = $newpath/Bin
    set mathlinklib = $mathlinkbin/MathLink
    if ( -x $mathlinkbin && -r $mathlinkbin && -d $mathlinkbin && \
         -x $mathlinklib && -r $mathlinklib && -d $mathlinklib ) then
        echo $newpath
    else
	echo ""
	# Installation error: no Bin/MathLink sub-directory
	ptErrorMessage "Found the Mathematica installation directory '$newpath', but it did not contain a Bin/MathLink sub-directory."
	exit 1
    endif
  else
    echo ""
  endif
endif
