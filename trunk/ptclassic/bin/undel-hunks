#!/bin/sh
#
#	$Id$
#
# 
# This script can be used to track down memory leaks in Ptolemy.  In
# theory if MEMORYLOG is defined during compile time, then LOG_NEW and
# LOG_DEL should produce memory usage log messages.  To define
# MEMORYLOG, add -DMEMORYLOG to GPPFLAGS in
# $PTOLEMY/mk/config-$PTARCH.
#
# At UC Berkeley, we use purify so we have not tested this script
# in sometime.  Note further that this script uses GNU awk, which
# is not part of the Ptolemy distribution.
# 
gawk '$1 == "new" {
	if ($3 == "Top") tag = $2 ":" $4;
	else tag = $2 ":" $3 ":" $4;
	where[$6] = tag;
	blocksiz[$6] = $5;
	idx[$6] = NR;
}

$1 == "del" {
	if ($5 in where) {
		delete where[$5];
	}
	else printf("No new for %s\n", $0);
}
END {
	print "Undeleted blocks:";
	for (loc in where) {
		print idx[loc], where[loc], blocksiz[loc], loc;
	}
}' $MEMORYLOG | sort -n
