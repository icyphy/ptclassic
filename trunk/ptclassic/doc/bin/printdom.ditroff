#! /bin/csh -f
# $Id$

# Print documentation for a Ptolemy domain.
# Assumed script is started in the "doc" sub-directory of a domain.
#
# Command line options:
#
#	-index		Generate an index only, print nothing.
#
#	-preview	Preview domain via xditview, or nroff if DISPLAY
#			environment variable isn't set.
#
#	-icons		Include postscript icons in the printout.
#			Postscript icons must already exist; see the script
#			$PTOLEMY/src/doc/makepsicons.  This can't be used
#			with the -preview option, and can really probably
#			only be used at Ptolemy Headquarters in Berkeley.
#
# Remaining arguments are taken to be files to print options.
# Command uses printer defined by environment variable $PRINTER
# User .cshrc file must contain "setenv PRINTER whatever"
#
# The documentation consists of a concatenation of the following files:
#
#   $PTOLEMY/doc/headers/shared - macro definitions
#   overall - introduction to the domain
#   targettitle - title page for the target section
#   targets/*.t - target troff documentation
#   startitle - title page for the stars section
#   stars/*.t - star troff documentation files
#   demotitle - title page for the demo section
#   demos/*.t- demo troff documentation.
#
# Any troff file may include other troff files with the .so command
#
# Index information is appended to the file $HOME/Almagest.index,
# or to whatever ALAMAGEST_INDEX is set to.

# On interrupt, remove temporary files
onintr catch

if (!($?PTOLEMY)) then
	setenv PTOLEMY ~ptolemy
endif

# File structure information
# -- If the menus don't exist when previewing the documentation, then you
#    are likely running the OpenWindows version of xditview without having
#    OpenWindows running: use "which xditview" to see which xditview it is
set dirheader = $PTOLEMY/doc/headers
set printcommand = ditroff
set printoptions = "-me -tbl -eqn -P$PRINTER"
set contentscmd = $PTOLEMY/doc/bin/contents
set previewcommand = "xditview -"
set psincludecommand = $PTOLEMY/doc/bin/ditpsinc
set tempdir = /tmp
set tempTroffFile = $tempdir/pt$$.t
set tempIndexFile = $tempdir/index$$
set tempPrintFile = $tempdir/prnr$$
set troffFileNames = "*.t"

# Variables
set indexonly = 0
set preview = 0
set icons = 0

if (!($?ALMAGEST_INDEX)) then
	setenv ALMAGEST_INDEX $HOME/Almagest.index
endif

while ($#argv)
	switch ($argv[1])
		case -index:
			set indexonly = 1
			shift
			breaksw
		case -preview:
			set preview = 1
			shift
			breaksw
		case -icons:
			set icons = 1
			shift
			breaksw
		case -*:
			set printoptions = ($printoptions $argv[1])
			shift
			breaksw
		default:
			cd $1
			shift
			breaksw
	endsw
end

if ($icons && $preview) then
	echo "printdom: -preview and -icons flags are mutually exclusive, sorry."
	exit 1
endif


soelim $dirheader/shared $dirheader/domain \
	init overall > $tempTroffFile

# Set troff icon printing flag: 0 if not printing icons, 1 if printing them.
if ($icons) then
	echo '.nr Ic 1' >> $tempTroffFile
else
	echo '.nr Ic 0' >> $tempTroffFile
endif

if ( -d targets ) then
        if (-e targettitle) then
                # set type for this section
                echo '.ds TY "target' >> $tempTroffFile
                cat targettitle >> $tempTroffFile
        endif
        cd targets
        # print all targets
        cat -s $troffFileNames >> $tempTroffFile
        cd ..
endif

if ( -d stars ) then
	if (-e starstitle) then
		# set type for this section
		echo '.ds TY "star' >> $tempTroffFile
		cat starstitle >> $tempTroffFile
	endif
	cd stars
	# print all stars, sorted in dictionary order
	foreach file (`ls -1 -d $troffFileNames | sort -f -d`)
		cat -s $file >> $tempTroffFile
	end
	cd ..
endif

if ( -d demos ) then
	if (-e demotitle) then
		# set type for this section
		echo '.ds TY "demo' >> $tempTroffFile
		cat demotitle >> $tempTroffFile
	endif
	cd demos
	# print all demos
	cat -s $troffFileNames >> $tempTroffFile
	cd ..
endif

if ($indexonly) then
	# if only generating index, add -t option and throw away the output
	($printcommand $printoptions -t $tempTroffFile > /dev/null) >& $tempIndexFile
else if ($preview) then
	# if previewing, use either X windows or nroff
	if ($?DISPLAY) then
		$printcommand $printoptions -t $tempTroffFile | $previewcommand
	else
		(eqn $tempTroffFile | tbl | nroff -me > /tmp/ptnr$$) >& /dev/null
		more /tmp/ptnr$$
	endif
else if ($icons) then
	# printing icons -- generates postscript
	($printcommand $printoptions -t $tempTroffFile | $psincludecommand > \
		/tmp/pt.$$.ps) >& $tempIndexFile
	echo "printdom: generated postscript in /tmp/pt.$$.ps"
else
	# otherwise just print it out
	$printcommand $printoptions $tempTroffFile >& $tempIndexFile
	$contentscmd $tempIndexFile
	$printcommand $printoptions $dirheader/shared title
endif
if (-r $tempIndexFile) cat $tempIndexFile >>! $ALMAGEST_INDEX
/bin/rm -f $tempTroffFile $tempIndexFile $tempPrintFile
exit 0

catch:
        # prevent the "no match" error message if there are no temporary files
        set nonomatch
	/bin/rm -f $tempTroffFile $tempIndexFile $tempPrintFile
        exit 1
