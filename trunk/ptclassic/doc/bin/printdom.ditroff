#! /bin/csh
# $Id$

# Print documentation for a Ptolemy domain

# Command uses printer defined by environmental variable $PRINTER
# User .cshrc file must contain "setenv PRINTER whatever"

# The optional argument is the directory of the domain documentation
# An implicit header file, ~ptolemy/doc/headers/shared, is prepended.

# The following files are assumed to present in the documentation directory:
#	overall	  - The main documentation section.
#		    It may source other files with .so.
#	startitle - The title page for the stars section.
#	stars	  - A directory with star documentation
#	demotitle - The title page for the demo section
#	demos	  - A directory with demo documentation.

if ($#argv == 1) then
	cd $1
endif

# On interrupt, remove temporary files
onintr catch

# File structure information:
set dirheader = ~ptolemy/doc/headers
set printcommand = ditroff
set printoptions = "-me -tbl -eqn -P$PRINTER"
set contentscmd = ~ptolemy/doc/bin/contents

soelim $dirheader/shared $dirheader/domain \
	init overall > /tmp/pt$$
if ( -d stars ) then
	if (-e starstitle) then
		cat starstitle >> /tmp/pt$$
	endif
	cd stars
	# The following is silent in case there is an SCCS directory
	cat -s * >> /tmp/pt$$
	cd ..
endif
if ( -d demos ) then
	if (-e demotitle) then
		cat demotitle >> /tmp/pt$$
	endif
	cd demos
	# The following is silent in case there is an SCCS directory
	cat -s * >> /tmp/pt$$
	cd ..
endif
/bin/rm -rf index
$printcommand $printoptions /tmp/pt$$ >& index
chmod g+w index
$contentscmd index
$printcommand $printoptions $dirheader/shared title
/bin/rm /tmp/pt$$
exit 0

catch:
        # prevent the "no match" error message if there are no temporary files
        set nomatch
        /bin/rm /tmp/pt$$
        exit 1
