#! /bin/csh
# $Id$

# Print documentation for a Ptolemy domain

# Command uses printer defined by environmental variable $PRINTER
# User .cshrc file must contain "setenv PRINTER whatever"

# The optional argument is the directory of the domain documentation
# An implicit header file, ~ptolemy/doc/headers/shared, is prepended.

# The following files are assumed to present in the documentation directory:
#	overall	  - The main documentation section.
#		    It may source other files with .so.
#	startitle - The title page for the stars section.
#	stars	  - A directory with star documentation
#	demotitle - The title page for the demo section
#	demos	  - A directory with demo documentation.

# On interrupt, remove temporary files
onintr catch

# File structure information:
set dirheader = ~ptolemy/doc/headers
set printcommand = ditroff
set printoptions = "-me -tbl -eqn -P$PRINTER"
set contentscmd = ~ptolemy/doc/bin/contents

while ($#argv)
	switch ($argv[1])
		case -*:
			set printoptions = ($printoptions $argv[1])
			shift
			breaksw
		default:
			cd $1
			shift
			breaksw
	endsw
end

soelim $dirheader/shared $dirheader/domain \
	init overall > /tmp/pt$$
if ( -d stars ) then
	if (-e starstitle) then
		cat starstitle >> /tmp/pt$$
	endif
	cd stars
	# print all "ordinary files"
	cat `find . -type f -print` >> /tmp/pt$$
	cd ..
endif
if ( -d demos ) then
	if (-e demotitle) then
		cat demotitle >> /tmp/pt$$
	endif
	cd demos
	# print all "ordinary files"
	cat `find . -type f -print` >> /tmp/pt$$
	cd ..
endif
/bin/rm -rf index
$printcommand $printoptions /tmp/pt$$ >& index
chmod g+w index
$contentscmd index
$printcommand $printoptions $dirheader/shared title
/bin/rm /tmp/pt$$
exit 0

catch:
        # prevent the "no match" error message if there are no temporary files
        set nonomatch
        /bin/rm /tmp/pt$$
        exit 1
