#! /bin/csh -f
# tycho - Ptolemy Tycho Brahe Graphical Interface
#
# Authors: Edward Lee, Christopher Hylands
# Version: $Id$
#
# Copyright (c) 1994-%Q% The Regents of the University of California.
# All rights reserved.
# See the file $PTOLEMY/copyright for copyright notice,
# limitation of liability, and disclaimer of warranty provisions.

set usagestring = "usage: tycho [-debug] [-ptiny] [-bin tychobin] [-bak] [scriptfile]"

# If 'tycho.ptiny' is a link to this script, then we just use tycho.ptiny
set progname = `basename $0`

if ( ! $?PTOLEMY ) setenv PTOLEMY ~ptolemy
if ( ! $?PTARCH ) then
    setenv PTARCH `$PTOLEMY/bin/arch`
endif
if ( ! $?USER ) then
    setenv USER $LOGNAME
endif

if ( ! $?PTX11DIR ) then
    switch ($PTARCH)
	case sol2:
	case sol2.cfront:
	    setenv PTX11DIR /usr/openwin
	    breaksw
	case *:
	    setenv PTX11DIR /usr/X11
	    breaksw
    endsw
endif

if ( ! $?LD_LIBRARY_PATH ) then
    setenv LD_LIBRARY_PATH ${PTOLEMY}/lib.${PTARCH}:/usr/lib:${PTX11DIR}/lib
endif

# FIXME: Hardwired in tk4.0
# See also $PTOLEMY/src/tycho/kernel/make.template
#	   $PTOLEMY/tycho/kernel/Lib.tcl
setenv TCL_LIBRARY $PTOLEMY/tcltk/tcl7.4p1/lib/tcl7.4
setenv TK_LIBRARY $PTOLEMY/tcltk/tk4.0p1/lib/tk4.0

# Not all shells and platforms set $PWD, so we use our own
setenv PTPWD `pwd`

# To get tcl/tk man pages before corresponding unix man pages,
# and to guess about a reasonable manpath if none is set.
if (! $?MANPATH ) then
    setenv MANPATH $PTOLEMY/tcltk/tk/man:$PTOLEMY/tcltk/tcl/man:$PTOLEMY/doc/man:/usr/local/man:/usr/man
else
    setenv MANPATH $PTOLEMY/tcltk/tk/man:$PTOLEMY/tcltk/tcl/man:$PTOLEMY/doc/man:$MANPATH
endif

while ($#argv)
	switch ($argv[1])
		case -help:
			echo "$usagestring"
			echo " -debug         Bring up in debug mode"
			echo " -ptiny         Smaller version, fewer domains"
			echo " -bin tychobin  Run the binary 'tychobin'"
			echo " -bak           Run a .bak binary"
			echo " scriptfile     Exec itcl(?) script"
			exit 0
		case -debug:
			set tychodebug
			breaksw
		case -ptiny:
			set tychoptiny
			breaksw
		case -bin:
			set prog = $argv[2]
			shift
			breaksw
		case -bak:
			set tychobak
			breaksw
		case -*:
			echo Bad option: $argv[1]
			echo "$usagestring"
			exit 1
			breaksw
		case *:
			set script=$1
			breaksw
	endsw
	shift
end

if ( ! $?DISPLAY ) then
    echo "${0}: Your DISPLAY environment variable must be set"
    exit 1
endif

# For now, look for the executable in obj.$PTARCH, rather than bin.$ARCH.
set bindir = $PTOLEMY/obj.$PTARCH

if ( ! $?prog ) then
	if ( "$progname" == "tycho.ptiny" || $?tychoptiny ) then
	    set prog = $PTOLEMY/obj.$PTARCH/tycho/kernel/tycho.ptiny
	else
	    set prog = $PTOLEMY/obj.$PTARCH/tycho/kernel/tycho
	endif
	if ( $?tychodebug ) then
	    set prog = $prog.debug
	endif
	if ( $?tychobak ) then
	    set prog = $prog.bak
	endif
endif

if ( "$1" =~ "-*" ) then
    echo "${0}: Bad option: $1"
    exit 1
endif

# FIXME: Should check starting palette/facet for validity.
# FIXME: Should make the starting facet if needed.

set path = ( $PTOLEMY/bin.$PTARCH $PTOLEMY/bin $path )

if (! -x $prog) then
    echo "Program does not exist: $prog"
else
    $prog
endif
