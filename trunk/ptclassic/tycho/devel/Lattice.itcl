##########################################################################
# Version: $Id$
# Author: John Reekie
#
# Copyright (c) 1997 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
#                                                         COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### Lattice
#
# The Lattice model is a model for arbitrary multi-dimensional lattices
# (latti?). It is intended to support applications such as
# Praveen Murthy's Generalized Multi-dimensional SDF (GMSSDF) and
# Thomson-CSFs Array-oriented Language (AOL). There is a corresponding
# view class for this model called EditLattice.
#
# The key attributes of this model are its dimensionality (default
# two) and its lattice-generating matrix. The format
# of this matrix is a list of lists -- for example, the
# default lattice generator, which creates a simple two-dimensional
# square grid, has the value *{{1 0} {0 1}}*.
#
# To access the lattice, create a _projection_ entity that
# projects onto two dimensions of the lattice. This projection
# can then be used to get a view of the lattice in those
# two dimensions.
#
class ::tycho::Lattice {
    inherit ::tycho::Model

    constructor {args} {}
    destructor {}
    
    #################################################################
    ####                        options                          ####

    # The dimensionality of this grid
    public variable dimensionality 2

    # The lattice generating matrix of this grid
    public variable lattice {{1 0} {0 1}}

    #################################################################
    ####                     public methods                      ####

    # Get the raw display coordinates of a projection
    public method getCoordinates {proj}

    # Get the pixel display dimensions of a projection.
    public method getDisplay {proj}

    # Get the viewed lattice extent of a projection
    public method getExtent {proj}

    # Get the lattice of a projection
    public method getLattice {proj}

    # Get the coordinates of the lattice points
    public method getLatticePoints {proj}

    # Read the pixel display dimensions of a projection
    public method getPixels {proj}

    # Make a projection in two dimensions
    public method projection {name dimensions args}

    # Delete a projection
    public method projectiondelete {name}

    # Set the viewed lattice extent of a projection
    public method setExtent {proj xlen ylen}

    # Set the pixel display dimensions of a projection
    public method setPixels {proj xpix ypix}

    #################################################################
    ####                  protected methods                      ####
    
    #################################################################
    ####                 protected variables                     ####
    
    # The dimensions of each projection
    protected variable pdimensions

    # The pixel display size of each projection
    protected variable ppixels

    # The lattice extent of each projection
    protected variable pextent

    # The raw coordinates of each projection
    protected variable pcoordinates

    #################################################################
    ####                   private methods                       ####

    # Evaluate code in this object's context
    private method evalInSpecificContext {args} {eval $args}
}

########################################################################
#### constructor
#
body ::tycho::Lattice::constructor {args} {	
    # This model is not hierarchical
    configure -separator ""

    # Evaluate options
    eval configure $args

    # The projection entity has a value (its dimensionality) and some
    # default attributes
    defineEntityType projection \
	    -valued 1 \
	    -pixels {256 256} \
	    -extent {2 2}
}

########################################################################
#### getCoordinates
#
# Get the raw display coordinates of a projection.
#
body ::tycho::Lattice::getCoordinates {proj} {
    _verify projection $proj
    ...
}

########################################################################
#### getDisplay
#
# Get the pixel display dimensions of a projection.
#
body ::tycho::Lattice::getDisplay {proj} {
    _verify projection $proj
    _cget projection $proj -pixels
}

########################################################################
#### getExtent
#
# Get the extent of a projection.
#
body ::tycho::Lattice::getExtent {proj} {
    _verify projection $proj
    _cget projection $proj -extent
}

########################################################################
#### getLattice
#
# Get the lattice generator of a projection. 
# For the projection P, each element of its lattice
# generator L is given by L(i,j) = V(P(i),P(j)).

body ::tycho::Lattice::getLattice {proj} {
    _verify projection $proj

    # For the projection P, each element of its lattice
    # generator L is given by L(i,j) = V(P(i),P(j)).
    set P [_get projection $proj]
    set L {}
    set indexes {0 1}
    foreach i $indexes {
	set row [lindex $lattice [lindex $P $i]]
	set l {}
    	foreach j $indexes {
	    lappend l [lindex $row [lindex $P $j]]
    	}
    	lappend L $l
    }
    return $L
}

########################################################################
#### getLatticeCoords
#
# Get the coordinates of the points of a projection.
#
body ::tycho::Lattice::getCoordinates {proj} {
    set L [getLattice $proj]
    set E [getExtent $proj]

    set xmax [expr [lindex $E 0] * \
            ([lindex [lindex $L 0] 0] + [lindex [lindex $L 0] 1])]
    set ymax [expr [lindex $E 1] * \
            ([lindex [lindex $L 1] 0] + [lindex [lindex $L 1] 1])]

    set disp [getDisplay $proj]
    set xfactor [expr [lindex $disp 0] / $xmax]
    set yfactor [expr [lindex $disp 1] / $ymax]

    set result {}
    set points [getLatticePoints $proj]
    foreach {x y} $points {
        lappend result [expr $x * $xfactor] [expr $y * $yfactor]
    }
    return $result
}

########################################################################
#### getLatticePoints
#
# Get the points of a projection.
#
body ::tycho::Lattice::getLatticePoints {proj} {
    _verify projection $proj

    set P [_get projection $proj]
    set L [getLattice $proj]
    set ext [getExtent $proj]

    set result {}

    # Here we go...
    set m [lindex $ext 0]
    set n [lindex $ext 1]
    set u0 [lindex [lindex $L 0] 0]
    set u1 [lindex [lindex $L 0] 1]
    set v0 [lindex [lindex $L 1] 0]
    set v1 [lindex [lindex $L 1] 1]
    set i 0
    set x0 0
    set y0 0
    while { $i <= $m } {
        set j 0
        set x $x0
        set y $y0
        while { $j <= $n } {
            lappend result $x $y
            incr j
            incr x $u0
            incr y $v0
        }
        incr i
        incr x0 $u1
        incr y0 $v1
    }
    return $result
}

########################################################################
#### projection
#
# Add a new projection to the model and return the inverse command. Its
# value is a two-list of the dimensions on which it projects. Neither
# value can be greater than the dimensionality of the lattice
# less one. Optional
# arguments are attributes of the projection, which can be any of:
# <ul>
# <li><b>-pixels</b>: The size of the display in pixels. This
# is a two-list. The default is a 256x256-pixel display.
#
# <li><b>-extent</b>: The extent of the grid in this projection.
# This is the amount of the grid that can be viewed, based on
# the underlying dimension implied by the lattice-generating
# matrix. The default is two unit grid elements in each
# direction.
# </ul>
#
body ::tycho::Lattice::projection {name dimensions args} {
    _verifynot projection $name

    # Check dimensionality
    set ndim [llength $dimensions]
    if { $ndim != 2 } {
	error "Projection ($dimensions) does not have two elements"
    }
    foreach d $dimensions {
	if { $d >= $dimensionality } {
	    error "Dimension $d is too high"
	}
    }

    # Create the projection and remember the inverse command
    set inverse [_entity projection {} $name \
            [concat [list $dimensions] $args]]
}

########################################################################
#### projectiondelete
#
# Delete a projection.
#
body ::tycho::Lattice::projectiondelete {name} {
    _verify projection $name
    _delete projection $name
}

########################################################################
#### setPixels
#
# Set the pixel display dimensions of a projection. An error
# is flagged if the number of arguments does not match the
# dimensionality of the projection.
#
body ::tycho::Lattice::setPixels {proj xpix ypix} {
    _verify projection $proj
    _configure projection $proj -pixels [list $xpix $ypix]
}

########################################################################
#### setExtent
#
# Set the extent of a projection. An error
# is flagged if the number of arguments does not match the
# dimensionality of the projection.
#
body ::tycho::Lattice::setExtent {proj xext yext} {
    _verify projection $proj
    _configure projection $proj -extent [list $xext $yext]
}
