#########################################################################
# @Version: $Id$
# @Author: John Reekie
#
# @Copyright (c) 1996 The Regents of the University of California.
# All rights reserved.
#  
# Permission is	hereby granted,	without	written	agreement and without
# license or royalty fees, to use, copy, modify, and distribute	this
# software and its documentation for any purpose, provided that	the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO	EVENT SHALL THE	UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT,	SPECIAL, INCIDENTAL, OR	CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN	ADVISED	OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES	OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.	THE SOFTWARE
# PROVIDED HEREUNDER IS	ON AN "AS IS" BASIS, AND THE UNIVERSITY	OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT,	UPDATES,
# ENHANCEMENTS,	OR MODIFICATIONS.
#							  COPYRIGHTENDKEY
##########################################################################


# Explicitly load the superclass. I don't really know why this is
# needed, but the common arrays get upset otherwise.
namespace ::tycho {
    if { [::info classes LabelledItem] == "" } {
        uplevel #0 {source $tychoslate/LabelledItem.itcl}
    }
}

##########################################################################
## LabelledRect
#
# <i>LabelledRect</i> is a rectangle with a text label and graphics.
# In all other respects, it can be treated like any other rectangle item.
#
# <p><b>Options</b>
#
# <dl>
# <dt><dd>
# <dl>
#
# <dt><b>-anchor</b>
# <dd>See <a href="LabelledItem.html">LabelledItem</a>.
#
# <dt><b>-fill</b>
# <dd>The color with which the rectangle is filled. By default, 
# it is "", which means that the rectangle is not filled.
#
# <dt><b>-graphics</b>
# <dd>See <a href="LabelledItem.html">LabelledItem</a>.
#
# <dt><b>-outline</b>.
# <dd>The color of the outline of the rectangle. If "", 
# the rectangle has no outline. By default, it is black.
#
# <dt><b>-text</b>
# <dd>See <a href="LabelledItem.html">LabelledItem</a>.
#
# <dt><b>-width</b>
# <dd>The width of the outline of the rectangle, in pixels. By 
# default, it is one.
#
# </dl></dl>
#
# <p><b>Bugs</b>: (none)
#
# <p><b>Example</b>: Create a labelled rectangle:
# <tcl quiet><pre>
#  source $tycho/editors/slate/doc/internals/newslate.itcl
#  $slate create LabelledRect 40 40 100 80 -fill blue -outline red &#92
#		-anchor nw -text Foo -tags moveable &#92
#		-graphics "line 0 0 100 100; line 0 100 100 0"
# </pre></tcl>
#
# <p><b>Enhancements</b>: (none)
#
# <p><b>Links</b>
# 
# <ul>
# <li><a href="../internals/howtoread.html">Reading <i>ComplexItem</i>
# documentation</a>.
# <li><a href="../internals/items.html">Complex item classes</a>.
# </ul>
#
class ::tycho::LabelledRect {
    inherit ::tycho::LabelledItem

    # Create a new item
    proc construct {id canvas slate tags x0 y0 x1 y1 args}

    ###################################################################
    ####                            options                        ####

    # The fill color of the rectangle
    common _fill

    # The outline color of the rectangle
    common _outline

    # The width of the rectangle outline
    common _width

    ###################################################################
    ####                     option update procs                   ####

    # Update the *-fill* option
    proc _fill {id canvas slate fill}

    # Update the *-outline* option
    proc _outline {id canvas slate outline}

    # Update the *-width* option
    proc _width {id canvas slate width}

    ###################################################################
    ####                         public procs                      ####

    # Set or get the item's coordinates
    proc coords {id canvas slate args}
    
    ###################################################################
    ####                         protected variables               ####

    # The virtual "method" table
    common methodtable

    # Default values of this item's options
    common optiondefault

    # Labelled rectangles are rectangular in shape
    common shape "rectangle"

    ###################################################################
    ####                       class initialization                ####

    #### Set method table
    array set methodtable [array get ::tycho::LabelledItem::methodtable]

    set methodtable(_fill)     ::tycho::LabelledRect::_fill
    set methodtable(_outline)  ::tycho::LabelledRect::_outline
    set methodtable(_width)    ::tycho::LabelledRect::_width

    set methodtable(construct) ::tycho::LabelledRect::construct
    set methodtable(coords)    ::tycho::LabelledRect::coords

    #### Set option defaults
    array set optiondefault [array get ::tycho::LabelledItem::optiondefault]

    set optiondefault(-fill)    ""
    set optiondefault(-outline) "black"
    set optiondefault(-width)   "1"
}

##########################################################################
## -fill option configuration
#
body ::tycho::LabelledRect::_fill {id canvas slate fill} {
    set _fill($id) $fill
    $canvas itemconfigure $primary($id) -fill $fill
}

##########################################################################
## -outline option configuration
#
body ::tycho::LabelledRect::_outline {id canvas slate outline} {
    set _outline($id) $outline
    $canvas itemconfigure $primary($id) -outline $outline
    if { $_text($id) != "" } {
	    $canvas itemconfigure $textitem($id) -fill $outline
    }
}

##########################################################################
## -width option configuration
#
body ::tycho::LabelledRect::_width {id canvas slate width} {
    set _width($id) $width
    $canvas itemconfigure $primary($id) -width $width
}

##########################################################################
#### construct
#
body ::tycho::LabelledRect::construct {id canvas slate tags x0 y0 x1 y1 args} {
    # Initialize the options
    foreach {option value} [concat [array get optiondefault] $args] {
	set _[string trimleft $option -]($id) $value
    }

    # Create the primary component. This is the displayed rectangle.
    set primary($id) [$canvas create rectangle $x0 $y0 $x1 $y1 \
	    -fill $_fill($id) -outline $_outline($id) \
	    -width $_width($id) -tags $tags]
    
    # Create the graphics and text label -- do not change the order!
    _text     $id $canvas $slate $_text($id)
    _graphics $id $canvas $slate $_graphics($id)
}

##########################################################################
#### coords
#
# Set or read the rectangle's coordinates
#
body ::tycho::LabelledRect::coords {id canvas slate args} {
    # No args: just return coordinates
    if { $args == "" } {
		return [$canvas coords $primary($id)]
    }

    # Move the rectangle itself
    eval $canvas coords $primary($id) $args

    # Move and scale the text and graphics FIXME: Inefficient
    _graphics $id $canvas $slate $_graphics($id)
    _anchor   $id $canvas $slate $_anchor($id)
}
