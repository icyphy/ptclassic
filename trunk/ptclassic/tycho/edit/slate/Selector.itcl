##########################################################################
# @Version: $Id$
# @Author: H. John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


#######################################################################
#### Selector
#
# A _Selector_ is an interactor that selects items. It can
# be configured to select items by clicking, to toggle items in and out
# of the selection set, and to support drag-selecting. One of its
# key functions is conditional activation of interactors -- for example,
# it could activate a Stepper interactor on the selected items, or an
# Editor interactor on a single selected item. A Slate should use
# a Selector when the simple binding mechanism provided by default
# in all interactors is not sophisticated enough.
#
# When an item is selected, it is given the tag "sel". Client code
# can use this to process selected items; note, however, that this tag is
# maintained by the Selector and the Slate, and must not be added to
# or removed from items -- use the Slate's select{} method instead.
# When an item is selected, it is highighted using the Slate's
# highlight{} method.
#
# The Selector is bound to to items and tags with its bind{} method,
# as usual for interactors. The Selector's method, however, has an
# additional argument that must be either "click", indicating that
# the binding applies to selection or deselection of items by clicking
# on them; or "drag," indicating that the bindings applies to selection
# and deselection by drag-selecting. The method accepts the usual
# *-button* and *-modifiers* options, and an additional option,
# *-toggle*, which causes the binding to toggle items into and out
# of the selection.
#
# The *-dragmode* option controls the style of drag-selection: if
# it is set to "overlapping," then items are selected (or toggled)
# if they overlap the drag outline at all; if it is "enclosed,"
# then the item is selected (or toggled) only if the drag box
# entirely encloses it. The *-dragcolor* option sets the color of the
# drag outline box and is by default set to some shade of grey.
# (Should the drag box be a dashed line?)
#
# Once an item is selected, the Selector can bind or activate another
# interactor on it: in the "immediate" mode an interactor
# that is to be activated on an item or the selection as soon as it
# is selected -- this is typically used for moving items, for example.
# This "delegation" can apply either to a single item or to the whole
# selection. In "subsequent" mode, an interactor is bound
# to an item or the selection when it is selected: selecting an item
# does not activate this interactor, but subsequent mouse events on it
# _will_ activate it. This is used, for example, to change
# a selected item into a live editing mode: select the item with say
# button 1, then use say button 2 to edit it.
#
# The Selector interactor is smart enough to know when these delegate
# interactors need to be unbound. It thus should be used if a Slate is
# being used for more than simple click-and-move and click-and-select
# interactions.
#
# <p><b>Options</b>
#
# <dl>
# <dt><dd>
#
# See also: <a href="Interactor.html">Interactor</a>.
#
# <dl>
# <dt><b>-dragmode</b>
# <dd>This can be <i>overlapping</i>, in which case an item 
# is selected if the drag-selector interactor has dragged out a region 
# that overlaps its bounding box, or <i>enclosed</i>, in which case an 
# item is selected only if the drag-selector has dragged out a region 
# that fully encloses its bounding box. The default is <i>overlapping</i>.
#
# <dt><b>-dragcolor</b>
# <dd>The color of the box used to mark the current 
# drag-selection region. The default color is "grey."
#
# </dl></dl>
#
# <p><b>Bugs</b>
# <ul>
# <li> The bind delegation does not work properly. There are
# all sorts of interactions between bindings that I haven't
# figured out yet...
# <li>
# <li>
# <li>
# </ul>
#
# <p><b>Enhancements</b> (none)
#
# <p><b>Links</b>
#
# <ul>
# <li><a href="../internals/selecting.html">More about the
# graphical selection</a>.
# <li><a href="../internals/interacting.html">How interactors work</a>.
# <li><a href="../internals/interactors.html">Interactor classes</a>.
# <li><a href="../internals/index.html">Infrastructure contents</a>
# <li><a href="index.html">Class documentation index</a>
# </ul>
#
class ::tycho::Selector {
    inherit ::tycho::Interactor

    constructor {{s {}} {c {}} args} {
	set slate $s
	set canvas $c

	eval configure $args

	# Create the selection handler
	::selection handle $slate "$this describe"
    }
    destructor {}

    ###################################################################
    ####                            options                        ####

    # The prefix used to forward selection clear events
    public variable clearprefix {}

    # The prefix used to forward deselection events
    public variable deselectprefix {}

    # The prefix used to forward selection events
    public variable selectprefix {}

    # The drag-select mode: "overlapping," or "enclosed"
    public variable dragmode "overlapping"

    # The color of the drag-select box
    public variable dragcolor "grey60"

    ###################################################################
    ####                         public methods                    ####

    # Activate the interactor for drag-selecting
    method activate {tag x y args}

    # Bind the selector to mouse events
    method bind {tagOrID args}

    # Start the drag-select interaction sequence.
    method click {x y args}

    # Remove the interactor from drag-selecting
    method deactivate {x y args}

    # Specify a delegate interactor to operate on selected items
    method delegate {interactor tag args}

    # Continue the drag-select interaction sequence
    method drag {x y}

    # Process a click on an item
    method itemclick {tag x y args}

    # Notify that the selection has been changed by someone else
    method refresh {}

    # Terminate the drag-select interaction sequence
    method release {}

    # Notify that the canvas size has changed
    method resize {}

    # Remove a mouse binding
    method unbind {tagOrID args}

    ###################################################################
    ####                        protected methods                  ####

    # Activate delegate interactors
    protected method _activate {id x y args}

    # Record a single delegate interactor.
    protected method _attach {tag opts detach}

    # Bind delegate interactors
    protected method _bind {}

    # Clear the selection
    protected method _clear {}

    # Deselect an item
    protected method _deselect {id}
    
    # Remove all delegate interactors.
    protected method _detach {}

    # Select an item
    protected method _select {id}

    ###################################################################
    ####                    private variables                      ####

    # The drag box
    private variable dragbox ""

    # The drag box
    private variable currentBindings ""

    # The current delgates and how to unbind or deactivate them
    private variable _attached

    # The delegate interactors
    private variable delegates

    # The items currently selected by dragging
    private variable covered ""

    # A flag saying whether we are toggling
    private variable toggling

    # The origin of the drag box
    private variable xOrigin
    private variable yOrigin
}

#######################################################################
#### activate
#
# Activate the drag-selector. Apart from the standard
# -button and -modifiers options, the method also responds to the
# *-toggle* option, which causes items to toggle in and out
# of the selection.
#
body ::tycho::Selector::activate {tag x y args} {
    # Process depending on whether this is toggle mode or not
    array set opt [concat -toggle 0 $args]
    set toggling $opt(-toggle)

    # Change raw coordinates to correct canvas coordinates
    # (needed if canvas has scrollbars)
    set x [$canvas canvasx $x]
    set y [$canvas canvasy $y]

    # Set the target
    $this target $tag

    # Set up the bindings to perform the moving and deactivation.
    $slate bind $tag \
	    [eval ::tycho::Interactor::event Motion $args] \
	    "$this drag %x %y; update idletasks"
    $slate bind $tag \
	    [eval ::tycho::Interactor::event Release $args] \
	    "$this release; $this deactivate %x %y $args"

    # Start the interaction sequence
    eval click $x $y $args
}

#######################################################################
#### bind
#
# Bind a selector interactor. This causes any items with the given _tag_
# to be selectable by either clicking on it or by dragging the mouse
# over it (thus allowing multiple items to be selected). When
# dragging, a grey rectangle is shown. By default, the item upon
# which drag-selecting can be performed as an "invisible" item
# on the slate called "#background." The *-background* option
# can be specified to give a different background item. By default,
# clicking on an item or the background deselects currently selected
# items. If, however, the
# *-toggle* option is specified and true, then clicking and
# drag-selecting will toggle items into and out of the selection.
# Other options can also be specified, being the *-buttons*
# and *-modifiers* options of the <code>::tycho::event</code>
# procedure. I recommend that a caller _always_ specifies a *-button*
# option, to reduce the chances of accidental
# interference between different interactors.
#
# FIXME: If the background item does not cover the whole slate,
# drag-selecting will need to be adjusted by the origin of that
# item.
#
body ::tycho::Selector::bind {tag args} {
    # Extract options
    array set opt [concat -background #background $args]
    set bgtag $opt(-background)
    unset opt(-background)
    set args [array get opt]

    # Remember the binding tag.
    set _bindings([concat $tag $args]) $args

    # Here's the event
    set event [eval ::tycho::Interactor::event Click $args]

    # Make the binding for clicking
    $slate bind $tag $event "$this itemclick $tag %x %y $args"

    # If using the default background and it doesn't exist, make it
    if { $bgtag == "#background" \
	    && [$canvas find withtag #background] == "" } {
	# Create the background item
	$canvas create rectangle \
		0 0 [winfo width $slate] [winfo height $slate] \
		-outline "" \
		-tags [list # #background]
	$canvas lower #background

	# If the slate resizes, we have to resize the background too
	::bind $slate <Configure> "$this resize"
    }
    
    # Make the drag-select binding
    $slate bind $bgtag $event "$this activate $bgtag %x %y $args"
}

#######################################################################
#### click
#
# Start an interaction sequence.
#
body ::tycho::Selector::click {x y args} {
    if !$toggling {
	_clear
    }

    # Detach delegate interactors
    _detach

    # Figure out which tags are valid for selection
    set currentBindings {}
    foreach b [array names _bindings] {
	if { [lreplace $b 0 0] == $args } {
	    lappend currentBindings [lindex $b 0]
	}
    }

    # Create the drag box and make it "invisible"
    set xOrigin $x
    set yOrigin $y
    
    set dragbox [$slate create rect $x $y $x $y \
	    -outline $dragcolor \
	    -tags "#"]
}

#######################################################################
#### deactivate
#
body ::tycho::Selector::deactivate {x y args} {
    # Remove bindings
    $slate bind $target \
	    [eval ::tycho::Interactor::event Motion $args] {}
    $slate bind $target \
	    [eval ::tycho::Interactor::event Release $args] {}

    # Update the screen. This appears to be necessary to ensure that
    # the "current" tag is updated correctly before processing another
    # event.
    update idletasks
}

#######################################################################
#### delegate
#
# Specify a "delegate" interactor -- that is, one which will
# be bound or activate by the Selector of the selected items
# meet the right conditions. The first two arguments are the
# interactor and the tag of the items on which it will operate.
# The option can be:
# <ul>
# <li><b>-mode</b> _mode_: The way in which the interactor is
# delegated. By default, it is "activate," which means that the
# interactor is activated if the selected items meets the conditions.
# Otherwise, it should be "bind," in which case the interactor is
# only bound to the selected items if they meet the right conditions.
# <li><b>-oneonly</b> _bool_: Specifies that the interactor will be
# activated or bound if there is only one selected item, and that
# item is tagged with
# _tag_. Otherwise, (the default), the interactor will be bound
# if all selected items are tagged with _tag_, regardless of how many
# are selected.
# <li><b>-button</b> _int_: the *-button* option that will be passed to
# the interactor when activated or bound.
# <li><b>-modifiers</b> _modifiers_: the *-modifiers* list that will be
# passed to the interactor when activated or bound.
# </ul>
body ::tycho::Selector::delegate {interactor tag args} {
    # Extract options
    array set opt [concat {-mode activate -oneonly 0} $args]
    set mode $opt(-mode)
    set oneonly $opt(-oneonly)
    unset opt(-mode)
    unset opt(-oneonly)
    set args [array get opt]

    # Remember the delegate
    set key [list $mode $oneonly $tag $args]
    if { [::info exists delegates($key)] } {
	eval lappend tag [lindex $delegates($key) 1]
    } else {
    	set tag [list $tag]
    }    
    set delegates($key) [list $interactor $tag $args]
    return
}

#######################################################################
#### drag
#
# Continue an interaction sequence with a mouse move. The behaviour
# depends on the current "mode" of the selector: in "select" mode,
# the drag box is moved and any newly covered items are selected;
# In "toggle" mode, an item that becomes covered or uncovered is
# toggled into or out of the selection. Whether an item
# is covered depends on the *-dragmode* option: if "overlapping" then
# an item is covered if the drag box overlaps it at all;
# if "enclosed" then the item is covered if the drag box
# entirely encloses it.
#
body ::tycho::Selector::drag {x y} {
    # Change raw coordinates to correct canvas coordinates
    # (needed if canvas has scrollbars)
    set x [$canvas canvasx $x]
    set y [$canvas canvasy $y]

    # Resize the drag box
    $slate coords $dragbox $xOrigin $yOrigin $x $y

    # Get the box again in "correct" coordinates. This is needed
    # because it may be that x < xOrigin or y < yOrigin.
    set coords [$slate coords $dragbox]

    # Get the newly covered and uncovered items
    set cu [eval $slate search $dragmode $this.covered $coords]
    set covered [lindex $cu 0]
    set uncovered [lindex $cu 1]

    # Process according to selection mode
    if !$toggling {
	# Add newly covered selectable objects. An object
	# is selectable if its name is in the binding set, or if
	# it has a tag that is in the binding set
	foreach {o tags} $covered {
	    if { [::tycho::lmember $currentBindings $o] \
		    || ! [::tycho::ldisjoint $currentBindings $tags] } {
		# Select it
		_select $o
	    }
	}
    	# Deselect selectable objects that are no longer covered
	foreach {o tags} $uncovered {
	    if { [::tycho::lmember $currentBindings $o] \
		    || ! [::tycho::ldisjoint $currentBindings $tags] } {
		# Deselect it
		_deselect $o
	    }
    	}
    } else {
	# Toggle any selectable objects that have changed state
    	foreach {o tags} [concat $covered $uncovered] {
	    # An object is selectable if its name is in the binding
	    # set or if it has a tag in the binding set.
	    if { [::tycho::lmember $currentBindings $o] \
		    || ! [::tycho::ldisjoint $currentBindings $tags] } {
		# Test if already selected or not, and act accordingly
		if { [$slate hastag $o "sel"] } {
		    # Deselect it
		    _deselect $o
		} else {
		    # Select it
		    _select $o
		}
	    }
	}
    }
    # Execute the client-specified update command
    if { $dragcommand != "" } {
        uplevel #0 $dragcommand $target $x $y
    }
}

#######################################################################
#### itemclick
#
# Process a click on an item.
#
body ::tycho::Selector::itemclick {tag x y args} {
    # Get the current object
    set current [$slate find withtag current]

    # Process depending on whether this is toggle mode or not
    array set opt [concat -toggle 0 $args]
    ::tycho::getflag toggle args

    if { ! $opt(-toggle) } {
	# Select it if it isn't already
	if { ! [$slate hastag $current "sel"] } {
	    # Deselect all currently selected objects
	    _clear

	    # Select and highlight the current object
	    _select $current
	}
	# Activate a delegate
	eval $this _activate $current $x $y $args

    } else {
	# Toggle the state of the clicked-on item
	if { [$slate hastag $current "sel"] } {
	    # The item is selected, so deselect it
	    _deselect $current
	} else {
	    # The item is not selected. Select and highlight it
	    _select $current

	    # Activate the delegate if there is one
	    eval $this _activate $current $x $y $args
	}
    }
    # Adjust delegate bindings
    $this _bind
}

#######################################################################
#### release
#
# Terminate an interaction sequence.
#
body ::tycho::Selector::release {} {
    # Delete the drag box
    $slate delete $dragbox

    # Finish the search
    $slate dtag $this.covered

    # Update delegate bindings for new selection set
    $this _bind
}

#######################################################################
#### resize
#
# Notify that the size of the canvas has changed.
#
body ::tycho::Selector::resize {} {
    # First test the scroll region: if not null, use that as the
    # size of the background item
    set region [$slate cget -scrollregion]
    if { $region != "" } {
	eval $slate coords #background $region
    } else {
	# Otherwise use the displayed size
	$slate coords #background 0 0 \
		[winfo width $slate] \
		[winfo height $slate]
    }
}

#######################################################################
#### unbind
#
# Remove the selector from the given background item or tag.
#
body ::tycho::Selector::unbind {tag args} {
    set event [eval ::tycho::Interactor::event Click $args]
	unset _bindings([concat $tag $args])
	$slate bind $tag $event {}
}

#######################################################################
#### refresh
#
# The selection has been changed by someone else. Update the
# delegate bindings.
#
body ::tycho::Selector::refresh {} {
    _detach
    _bind
}

#####################################################################
#####################################################################
####                     protected methods                       ####

#######################################################################
#### _activate
#
# Activate delegate interactors when a new item is added to
# the selection. This method is called whenever a new item
# is added to the selection by a mouse click.
#
body ::tycho::Selector::_activate {id x y args} {
    set selection [$slate find withtag "sel"]
    set selcount [llength $selection]

    # Process interactors that operate on a single item only
    if { $selcount == 1 } { 
	foreach index [array names delegates [list activate 1 * *]] {
	    # Activate the delegate interactor if that item is in the tags
	    # or if it has a tag that is in the tags
	    ::tycho::assign interactor tags options $delegates($index)
	    if { $selcount == 1 } {
		if { $args == $options } {
		    if { [::tycho::lmember $tags $id] \
			    || ! [::tycho::ldisjoint $tags [$slate gettags $id]] } {
			# Activate it
			_attach $id $options "$interactor deactivate 0 0 $options"
			eval $interactor activate $id $x $y $options
		    }
		}
	    }
	}
    }
    # Process interactors that operate on the whole selection
    foreach index [array names delegates [list activate 0 * *]] {
	# Activate a delegate interactor on the whole selection if
	# every item is either in the interactor's tags or if it
	# has a tag that is.
	::tycho::assign interactor tags options $delegates($index)
	if { $args == $options } {
	    set ok 1
	    foreach item $selection {
		if { ! [::tycho::lmember $tags $item] \
			&& [::tycho::ldisjoint $tags [$slate gettags $item]] } {
		    set ok 0
		    break
		}
	    }
	    if { $ok } {
		# Activate it
		_attach "sel" $options "$interactor deactivate 0 0 $options"
		eval $interactor activate "sel" $x $y $options
	    }
	}
    }
}

#######################################################################
#### _attach
#
# Record a single delegate interactor.
#
body ::tycho::Selector::_attach {tag opts detach} {
    set _attached([concat $tag $opts]) $detach
}

#######################################################################
#### _bind
#
# Bind or unbind delegate interactors.
#
# FIXME: If a delegate is bound, then do we have to unbind the selector
# from that item???
#
body ::tycho::Selector::_bind {} {
    set selection [$slate find withtag "sel"]
    set selcount [llength $selection]

    # Process interactors that operate on a single item only
    foreach index [array names delegates [list bind 1 * *]] {
	::tycho::assign interactor tags options $delegates($index)
	if { $selcount == 1 } {
	    set id [lindex $selection 0]
	    # If there is only one item and it's in that interactor's
	    # tags or has a tag in that interactor's tags, bind it
	    if { [::tycho::lmember $tags $id] \
		    || ! [::tycho::ldisjoint $tags [$slate gettags $id]] } {
		# Bind it
		_attach $id $options "$interactor unbind $id $options"
		eval $interactor bind $id $options
	    }
	}
    }

    # Process interactors that operate on the whole selection
    foreach index [array names delegates [list bind 0 * *]] {
	# Activate a delegate interactor on the whole selection if
	# every item is either in the interactor's tags or if it
	# has a tag that is.
	::tycho::assign interactor tags options $delegates($index)
	set ok 1
	foreach item $selection {
	    if { ! [::tycho::lmember $tags $item] \
		    && [::tycho::ldisjoint $tags [$slate gettags $item]] } {
		set ok 0
		break
	    }
	}
	if { $ok } {
	    # Bind it
	    _attach "sel" $options "$interactor unbind sel $options"
	    eval $interactor bind "sel" $options
	} 
    }
}

#######################################################################
#### _clear
#
body ::tycho::Selector::_clear {} {
    if { $clearprefix == "" } {
	$slate _select clear
    } else {
	eval $clearprefix
    }
}

#######################################################################
#### _deselect
#
body ::tycho::Selector::_deselect {id} {
    if { $deselectprefix == "" } {
	$slate _select remove $id
    } else {
	eval $deselectprefix $id
    }
}

#######################################################################
#### _detach
#
# Remove all delegate interactors.
#
body ::tycho::Selector::_detach {} {
    foreach {_ detach} [array get _attached] {
	uplevel #0 $detach
    }
    catch {unset _attached}
}

#######################################################################
#### _select
#
body ::tycho::Selector::_select {id} {
    if { $selectprefix == "" } {
	$slate _select add $id
    } else {
	eval $selectprefix $id
    }
}

