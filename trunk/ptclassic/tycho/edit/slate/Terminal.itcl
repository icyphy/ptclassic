#########################################################################
# @Version: $Id$
# @Author: H. John Reekie
#
# @Copyright (c) 1996 The Regents of the University of California.
# All rights reserved.
#  
# Permission is	hereby granted,	without	written	agreement and without
# license or royalty fees, to use, copy, modify, and distribute	this
# software and its documentation for any purpose, provided that	the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
# 
# IN NO	EVENT SHALL THE	UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT,	SPECIAL, INCIDENTAL, OR	CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN	ADVISED	OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES	OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.	THE SOFTWARE
# PROVIDED HEREUNDER IS	ON AN "AS IS" BASIS, AND THE UNIVERSITY	OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT,	UPDATES,
# ENHANCEMENTS,	OR MODIFICATIONS.
#							  COPYRIGHTENDKEY
##########################################################################


# Explicitly load the superclass. I don't really know why this is
# needed, but the common arrays get upset otherwise.
namespace ::tycho {
    if { [::info classes Picture] == "" } {
	uplevel #0 {source $tychoslate/kernel/Picture.itcl}
    }
}


#
# uplevel #0 {
#     if { [::info classes ::tycho::Picture] == "" } {
# 	source $tychoslate/kernel/Picture.itcl
#     }

#     array set Terminal [array get Picture]

#     set Terminal(construct)   ::tycho::Terminal::construct
#     set Terminal(coords)      ::tycho::Terminal::coords
#     set Terminal(direction)   ::tycho::Terminal::direction
#     set Terminal(fill)        ::tycho::Terminal::fill
#     set Terminal(length)      ::tycho::Terminal::length
#     set Terminal(style)       ::tycho::Terminal::style
#     set Terminal(type)        ::tycho::Terminal::type
# }


## Terminal
#
# A "terminal" for making connections to dataflow nodes and
# such. This kind of terminal is based on arrowed lines.
#
class ::tycho::Terminal {
    inherit ::tycho::Picture

    proc construct {id tags canvas x0 y0 args}


    ###################################################################
    ####                            options                        ####

    # The direction of the terminal away from its connection point,
    # as a unit vector pair
    common direction

    # The fill color of the terminal
    common fill

    # The length of the terminal line
    common length

    # The "style" of the terminal: "arrow," "doublearrow," or "blob"
    common style

    # The type of the terminal: "input" or "output"
    common type


    ###################################################################
    ####                         public procs                      ####

    proc coords    {id canvas args}
    proc direction {id canvas di}
    proc fill      {id canvas fi}
    proc length    {id canvas le}
    proc style     {id canvas ty}
    proc type      {id canvas ty}

    ###################################################################
    ####                         protected variables               ####

    # The additional item used to draw the terminal: blob or extra arrow
    common blobitem

    ###################################################################
    ####                       class initialization                ####

    #### Set method table
    array set methodtable [array get ::tycho::Picture::methodtable]

    set methodtable(construct)   ::tycho::Terminal::construct
    set methodtable(coords)      ::tycho::Terminal::coords
    set methodtable(direction)   ::tycho::Terminal::direction
    set methodtable(fill)        ::tycho::Terminal::fill
    set methodtable(length)      ::tycho::Terminal::length
    set methodtable(style)       ::tycho::Terminal::style
    set methodtable(type)        ::tycho::Terminal::type

    #### Set option defaults
    array set optiondefault [array get ::tycho::Picture::optiondefault]

    set optiondefault(-direction) {1 0}
    set optiondefault(-length)    25
    set optiondefault(-type)      input
    set optiondefault(-style)     arrow
    set optiondefault(-fill)      black
}


##########################################################################
#### construct
#
body ::tycho::Terminal::construct {id tags canvas x0 y0 args} {
    # Initialize the options
    foreach {option value} [concat [array get optiondefault] $args] {
	set [string trimleft $option -]($id) $value
    }

    # Figure the other end.
    set x1 [expr $x0 + [lindex $direction($id) 0] * $length($id)]
    set y1 [expr $y0 + [lindex $direction($id) 1] * $length($id)]

    # The primary component is also the line.
    set primary($id) [$canvas create line $x0 $y0 $x1 $y1 \
	    -tags $tags -fill $fill($id)]

    # Update the style, forcing an update.
    set st $style($id)
    set style($id) {}
    style $id $canvas $st
}


##########################################################################
#### coords
#
body ::tycho::Terminal::coords {id canvas args} {
    # No args: just return coordinates
    if { $args == "" } {
	return [$canvas coords $primary($id)]
    }

    # Move the primary
    set coords [$canvas coords $primary($id)]
    set moveX [expr [lindex $args 0] - [lindex $coords 0]]
    set moveY [expr [lindex $args 1] - [lindex $coords 1]] 

    $canvas move $id $moveX $moveY
}

##########################################################################
#### direction
#
body ::tycho::Terminal::direction {id canvas di} {
    set direction($id) $di
    set coords [$canvas coords $primary($id)]

    # Figure the other end.
    set x1 [expr [lindex $coords 0] \
	    + [lindex $direction($id) 0] * $length($id)]
    set y1 [expr [lindex $coords 1] \
	    + [lindex $direction($id) 1] * $length($id)]

    # Change the coordinates
    eval $canvas coords $primary($id) [lrange $coords 0 1] $x1 $y1

    # Get the right appearance by setting the style option. Force
    # redraw by setting style to nothing.
    set st $style($id)
    set style($id) {}
    style $id $canvas $st
}

##########################################################################
#### fill
#
body ::tycho::Terminal::fill {id canvas fi} {
    set fill($id) $fi

    $canvas itemconfigure $primary($id) -fill $fill($id)
    if { [::info exists blobitem($id)] } {
	$canvas itemconfigure $blobitem($id) -fill $fill($id)
    }
}

##########################################################################
#### length
#
body ::tycho::Terminal::length {id canvas le} {
    set length($id) $le
    set coords [$canvas coords $primary($id)]

    # Figure the other end.
    set x1 [expr [lindex $coords 0] \
	    + [lindex $direction($id) 0] * $length($id)]
    set y1 [expr [lindex $coords 1] \
	    + [lindex $direction($id) 1] * $length($id)]

    # Change the coordinates
    eval $canvas coords $primary($id) $coords $x1 $y1

    # Get the right appearance by setting the style option. Force
    # redraw by setting style to nothing.
    set st $style($id)
    set style($id) {}
    style $id $canvas $st
}

##########################################################################
#### style
#
body ::tycho::Terminal::style {id canvas st} {
    # Check direction of arrow
    if { $st == "arrow" } {
	if { $type($id) == "input" } {
	    $canvas itemconfigure $primary($id) -arrow first
	} else {
	    $canvas itemconfigure $primary($id) -arrow last
	}
	if { [::info exists blobitem($id)] } {
	    $canvas delete $blobitem($id)
	    unset blobitem($id)
	}
    } elseif { $st == "doublearrow" && $style($id) != "doublearrow" } {
	if { [::info exists blobitem($id)] } {
	    $canvas delete $blobitem($id)
	}

	set coords [$canvas coords $primary($id)]
	set xdir [lindex $direction($id) 0]
	set ydir [lindex $direction($id) 1]

	if { $type($id) == "input" } {
	    $canvas itemconfigure $primary($id) -arrow first
	    set blobitem($id) [$canvas create line \
		    [expr [lindex $coords 0] + $xdir * 5] \
		    [expr [lindex $coords 1] + $ydir * 5] \
		    [expr [lindex $coords 0] + $xdir * 6] \
		    [expr [lindex $coords 1] + $ydir * 6] \
		    -fill $fill($id) \
		    -arrow first \
		    -tags [$canvas gettags $id]]
	} else {
	    $canvas itemconfigure $primary($id) -arrow last
	    set blobitem($id) [$canvas create line \
		    [expr [lindex $coords 2] - $xdir * 5] \
		    [expr [lindex $coords 3] - $ydir * 5] \
		    [expr [lindex $coords 2] - $xdir * 6] \
		    [expr [lindex $coords 3] - $ydir * 6] \
		    -fill $fill($id) \
		    -arrow first \
		    -tags [$canvas gettags $id]]
	}
    } elseif { $st == "blob" && $style($id) != "blob" } {
	$canvas itemconfigure $primary($id) -arrow none
	if { [::info exists blobitem($id)] } {
	    $canvas delete $blobitem($id)
	}

	set coords [$canvas coords $primary($id)]
	set blobitem($id) [eval $canvas create oval \
		[rectStretch [list \
		    [lindex $coords 2] [lindex $coords 3] \
		    [lindex $coords 2] [lindex $coords 3]] \
		    3] \
		-fill $fill($id) -outline $fill($id) \
		-tags [list [$canvas gettags $id]]]
    }
    set style($id) $st
}

##########################################################################
#### type
#
body ::tycho::Terminal::type {id canvas ty} {
    set type($id) $ty

    # Get the right appearance by setting the style option. Force
    # redraw by setting style to nothing.
    set st $style($id)
    set style($id) {}
    style $id $canvas $st
}
