# Test ICanvas methods
#
# @Author: John Reekie
# 
# @Version: @(#)testIncrCanvas.itcl	1.15 07/29/98
#
# @Copyright (c) 1997-1998 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

# Make sure that the test package is loaded
package require slate.test

############################################################################
#### Comments on these tests
#
# These tests test out only the methods in ICanvas. For tests
# that apply to individual item types, shapes, or interactors,
# see the individual test files. For test of the methods
# in Slate.itcl, see testSlate.itcl.
#
# The test are in two groups: the first test basic operations
# like construction, item creation, and tagging, which need to
# be functioning for the remaining tests. Following tests are
# on a per-method basis. Most methods have (at least) four cases:
# pass a simple item by ID, pass a tag that will match a simple
# item, pass a complex item by ID, and pass a tag that will match
# a complex item.
#
# 

#set VERBOSE 1

############################################################################
#### Construction tests
#
test IncrCanvas-constructor.1 {Simplest construction test} {
   catch {itcl::delete object .s}
   ::tycho::icanvas .s
} {.s}
itcl::delete object .s

# FIXME: add more tests with different options

############################################################################
#### Basic tests, needed for following tests to work
#
_clean
test IncrCanvas-basic.1 {Create a simple item} {
    set r [.s create rectangle 40 40 80 80 -fill blue -tags foo]
    list \
	    [regexp {[0-9]+} $r] \
	    [string compare [.s find all] $r] \
	    [string compare [.s.canvas find all] $r]
} {1 0 0}
_update

test IncrCanvas-basic.2 {Find simple item by tag} {
    string compare [.s find withtag foo] $r
} {0}
_update

test IncrCanvas-basic.3 {Delete simple item} {
    .s delete $r
    list \
	    [.s find all] \
	    [.s.canvas find all]
} {{} {}}
_update

test IncrCanvas-basic.11 {Create a complex item} {
    set f [.s create Frame 140 140 180 180 -color green -tags bar]
    list \
	    [regexp {_[0-9]+} $f] \
	    [string compare [.s find all] $f]
} {1 0}
_update

test IncrCanvas-basic.12 {Find complex item by tag} {
    string compare [.s find withtag bar] $f
} {0}
_update

test IncrCanvas-basic.13 {Delete complex item} {
    .s delete $f
    list \
	    [.s find all] \
	    [.s.canvas find all]
} {{} {}}
_update

test IncrCanvas-basic.21 {Delete all items} {
    .s create rectangle 40 40 80 80 -fill blue -tags foo
    .s create Frame 140 140 180 180 -color green -tags bar
    .s delete all
    list \
	    [.s find all] \
	    [.s.canvas find all]
} {{} {}}

############################################################################
#### addtag{}
#
_twoitems
test IncrCanvas-addtag.1 {Pass simple item by ID} {
    .s addtag new withtag $r
    list [.s find withtag new] [lsort [.s itemcget $r -tags]] \
} [list $r {foo new} ]

test IncrCanvas-addtag.2 {Pass simple item by tag} {
    .s addtag zoo withtag foo
    list [.s find withtag zoo] [lsort [.s itemcget $r -tags]]
} [list $r {foo new zoo} ]

test IncrCanvas-addtag.11 {Pass complex item by ID} {
    .s addtag moo withtag $f
    list [.s find withtag moo] [lsort [.s itemcget $f -tags]]
} [list $f {bar moo} ]

test IncrCanvas-addtag.12 {Pass complex item by tag} {
    .s addtag roo withtag bar
    list [.s find withtag roo] [lsort [.s itemcget $f -tags]]
} [list $f {bar moo roo} ]

# Tests on hierarchical items
test IncrCanvas-addtag.21 {Add to the actual root} {
    _hieritem
    .s addtag foo withtag $h
    # The items tagged with foo are the items in h minus the items in j
    string compare \
	    [lsort [.s.canvas find withtag foo]] \
	    [lsort [::tycho::lsubtract [.s.canvas find all] \
	       [.s.canvas find withtag $j]]]
} {0}

############################################################################
#### bbox{}
#
_twoitems

test IncrCanvas-bbox.1 {Pass simple item by ID} {
    .s bbox $r
} {39 39 81 81}

test IncrCanvas-bbox.2 {Pass simple item by tag} {
    .s bbox foo
} {39 39 81 81}

test IncrCanvas-bbox.11 {Pass complex item by ID} {
    .s bbox $f
} {138 138 183 183}

test IncrCanvas-bbox.12 {Pass complex item by tag} {
    .s bbox bar
} {138 138 183 183}

############################################################################
#### bind{}
#
_twoitems
test IncrCanvas-bind.1 {Pass simple item by ID} {
    .s bind $r <Button-1> {puts !}
    .s.canvas bind $r <Button-1>
} {puts !}

test IncrCanvas-bind.1a {Read bindings of simple item} {
    list \
	    [.s bind $r] \
	    [.s bind $r <Button-1>]
} {<Button-1> {puts !}}

test IncrCanvas-bind.2 {Pass complex item by ID} {
    .s bind $f <Button-1> {puts !}
    list \
	    [.s.canvas bind !$f <Button-1>] \
	    [.s.canvas bind $f]
} {{puts !} {}}

test IncrCanvas-bind.2a {Read bindings of complex item} {
    list \
	    [.s bind $f] \
	    [.s bind $f <Button-1>]
} {<Button-1> {puts !}}

test IncrCanvas-bind.3 {Pass a tag} {
    .s bind foo <Button-2> {puts !}
    .s.canvas bind foo <Button-2>
} {puts !}

test IncrCanvas-bind.3a {Read bindings of tag} {
    list \
	    [.s bind foo] \
	    [.s bind foo <Button-2>]
} {<Button-2> {puts !}}

############################################################################
#### call{}
# FIXME

############################################################################
#### children{}
#
_hieritem
test IncrCanvas-children.1 {Get children by ID} {
    string compare [lsort [.s children $h]] [lsort [list $i $j]]
} {0}

test IncrCanvas-children.2 {Get children by tag} {
    .s addtag foo withtag $h
    string compare [lsort [.s children foo]] [lsort [list $i $j]]
} {0}

test IncrCanvas-children.3 {Get children -- none} {
    list [.s children $i] [.s children $j]
} {{} {}}

_twoitems
test IncrCanvas-children.4 {Get children -- none} {
    .s children $r
} {}

############################################################################
#### component{}
# FIXME

############################################################################
#### coords{}
#
_twoitems

test IncrCanvas-coords.1 {Pass simple item by ID} {
    .s coords $r
} {40.0 40.0 80.0 80.0}
_update

test IncrCanvas-coords.2 {Pass simple item by tag} {
    .s coords foo
} {40.0 40.0 80.0 80.0}
_update

test IncrCanvas-coords.3 {Pass simple item by ID to change coords} {
    .s coords $r 30 30 90 90
    .s coords $r
} {30.0 30.0 90.0 90.0}
_update

test IncrCanvas-coords.4 {Pass simple item by tag to change coords} {
    .s coords foo 40 40 80 80
    .s coords foo
} {40.0 40.0 80.0 80.0}
_update

test IncrCanvas-coords.11 {Pass complex item by ID} {
    .s coords $f
} {140.0 140.0 180.0 180.0}
_update

test IncrCanvas-coords.12 {Pass complex item by tag} {
    .s coords bar
} {140.0 140.0 180.0 180.0}
_update

test IncrCanvas-coords.13 {Pass complex item by ID to change coords} {
    .s coords $f 130 130 190 190
    .s coords $f
} {130.0 130.0 190.0 190.0}
_update

test IncrCanvas-coords.14 {Pass complex item by tag to change coords} {
    .s coords bar 140 140 180 180
    .s coords bar
} {140.0 140.0 180.0 180.0}
_update

############################################################################
#### create {}
#
_clean

test IncrCanvas-create.1 {Test create and delete of simple items} {
    image create photo .img \
	    -file [file join $env(SLATE_LIBRARY) img mantra.gif]
    set result 1
    # FIXME: Add other item types
    foreach item {
	{rectangle 100 100 200 200 -fill blue}
	{oval 100 100 200 200 -outline green}
	{line 100 100 200 200 -arrow last}
	{polygon 100 100 200 200 100 200 -fill red}
	{arc 100 100 200 200 -width 6}
	{text 150 150 -text Foo}
	{image 150 150 -image .img}
	{bitmap 150 150 -bitmap \
		@[file join $env(SLATE_LIBRARY) img arrowclose.xbm]}
    } {
	set i [eval .s create $item]
	_update
	# This makes sure that an integer was returned
	set result [expr $result && [regexp {[0-9]+} $i]]
    }
    .s delete all
    set result
} {1}
_update

############################################################################
#### create*{}
# FIXME

############################################################################
#### createchild{}
# FIXME

############################################################################
#### createchild*{}
# FIXME

############################################################################
#### createrootchild{}
# FIXME

############################################################################
#### createrootchild*{}
# FIXME

############################################################################
#### delete{}
#
_twoitems
test IncrCanvas-delete.1 {Delete simple item by ID} {
    .s delete $r
    .s.canvas find withtag $r
} {}
_update

test IncrCanvas-delete.2 {Delete complex item by ID} {
    .s delete $f
    .s.canvas find withtag $f
} {}
_update

_twoitems
test IncrCanvas-delete.11 {Delete simple item by tag} {
    .s delete foo
    .s.canvas find withtag $r
} {}
_update

test IncrCanvas-delete.12 {Delete complex item by tag} {
    .s delete bar
    .s.canvas find withtag $f
} {}
_update

_twoitems
test IncrCanvas-delete.21 {Delete multiple items by tag} {
    .s addtag foo all
    .s delete foo
    .s.canvas find all
} {}
_update

############################################################################
#### dtag{}
# FIXME

############################################################################
#### find{}
# FIXME
 
############################################################################
#### focusin{}
# FIXME

############################################################################
#### gettags{}
# FIXME

############################################################################
#### getchildtags{}
# FIXME

############################################################################
#### getrootchildtags{}
# FIXME

############################################################################
#### hastag{}
# FIXME

############################################################################
#### itemcget{}
# FIXME

#############################################################################
#### itemconfigure{}
# FIXME

#############################################################################
#### lower{}
# FIXME

###########################################################################
#### move{}
#
_twoitems

test IncrCanvas-move.1 {Pass simple item by ID} {
    .s move $r 10 10
    .s coords $r
} {50.0 50.0 90.0 90.0}
_update

test IncrCanvas-move.2 {Pass simple item by tag} {
    .s move foo 10 10
    .s coords foo
} {60.0 60.0 100.0 100.0}
_update

test IncrCanvas-move.11 {Pass complex item by ID} {
    .s move $f 10 10
    .s coords $f
} {150.0 150.0 190.0 190.0}
_update

test IncrCanvas-move.12 {Pass complex item by tag} {
    .s move bar 10 10
    .s coords bar
} {160.0 160.0 200.0 200.0}
_update

############################################################################
#### parent{}
#
_hieritem
test IncrCanvas-parent.1 {Get parent by ID} {
    .s parent $h
} {}

test IncrCanvas-parent.2 {Get parent by tag} {
    .s addtag foo withtag $h
    .s parent foo
} {}

test IncrCanvas-parent.3 {Get parent by ID} {
    string compare [.s parent $i] $h
} {0}

test IncrCanvas-parent.4 {Get parent by tag} {
    .s addtag bar withtag $i
    string compare [.s parent bar] $h
} {0}

test IncrCanvas-parent.5 {Get parent by ID} {
    string compare [.s parent $j] $h
} {0}

test IncrCanvas-parent.6 {Get parent by tag} {
    .s addtag baz withtag $j
    string compare [.s parent baz] $h
} {0}

_twoitems
test IncrCanvas-parent.7 {Get parent by ID} {
    .s parent $r
} {}

test IncrCanvas-parent.8 {Get parent by tag} {
    .s parent foo
} {}

#############################################################################
#### raise{}
# FIXME

############################################################################
#### root{}
#
_hieritem
test IncrCanvas-root.1 {Get root by ID} {
    string compare [.s root $h] $h
} {0}

test IncrCanvas-root.2 {Get root by tag -- error} {
    .s addtag foo withtag $h
    catch {.s root foo} result
    set result
} {Argument (foo) is not an item ID}

test IncrCanvas-root.3 {Get root of non-root} {
    string compare [.s root $i] $h
} {0}

test IncrCanvas-root.4 {Get root of child-root} {
    string compare [.s root $j] $j
} {0}

_twoitems
test IncrCanvas-root.5 {Get root of simple item} {
    string compare [.s root $r] $r
} {0}

############################################################################
#### scale{}
#
_twoitems

test IncrCanvas-scale.1 {Pass simple item by ID} {
    .s scale $r 20 20 0.5 2.0
    .s coords $r
} {30.0 60.0 50.0 140.0}
_update

test IncrCanvas-scale.2 {Pass simple item by tag} {
    .s scale foo 20 20 2.0 0.5
    .s coords foo
} {40.0 40.0 80.0 80.0}
_update

test IncrCanvas-scale.11 {Pass complex item by ID} {
    .s scale $f 120 120 0.5 2.0
    .s coords $f
} {130.0 160.0 150.0 240.0}
_update

test IncrCanvas-scale.12 {Pass complex item by tag} {
    .s scale bar 120 120 2.0 0.5
    .s coords bar
} {140.0 140.0 180.0 180.0}
_update

#############################################################################
#### search{}
# FIXME

#############################################################################
#### select{}
# FIXME

############################################################################
#### type{}
#
_twoitems
test IncrCanvas-type.1 {Simple item by ID} {
    .s type $r
} {rectangle}

test IncrCanvas-type.2 {Complex item by ID} {
    .s type $f
} {Frame}

test IncrCanvas-type.3 {Simple item by tag} {
    .s type foo
} {rectangle}

test IncrCanvas-type.4 {Complex item by tag} {
    .s type bar
} {Frame}

test IncrCanvas-type.5 {No match} {
    .s type nothing
} {}

############################################################################
####
# Delete the last canvas and iconify main window
_update
catch {itcl::delete object .s}
wm iconify .
