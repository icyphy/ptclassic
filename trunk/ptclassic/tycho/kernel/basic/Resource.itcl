# The abstract superclass of file-like "resources"
# 
# @Author: John Reekie
#
# @Version: $Id$
# 
# @Copyright (c) 1997 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

#####################################################################
#### resourceClass
# Get the class that deals with a resource.
# With just the path argument, return the name of the
# class that deals with resources with that path. If the
# second argument is supplied, call the proc with that name
# in the resource class, passing any additional arguments
# to it, and return the proc's result.
#
# FIXME: This needs to be extended to use a registration mechanism
# as we have for file suffixes and the like.
#
proc ::tycho::resourceClass {path {procname {}} args} {
    # ::puts $path
    # ::puts [regexp {^([a-z]+:/+)} $path protocol]
    if { ! [regexp {^([a-z]+:/+)} $path protocol] } {
	# puts $path
	set rclass ::tycho::ResourceFile
    } else {
	switch -exact -- $protocol {
	    http:// {
		set rclass ::tycho::ResourceHTTP
	    }
	    ftp:// {
		error "Ftp access not supported yet"
	    }
	    file:/ {
		set rclass ::tycho::ResourceFile
	    }
	    weld:// {
		set rclass ::tycho::ResourceWELD
	    }
	    default {
		error "Unknown protocol \"$protocol\""
	    }
	}
    }
    if { $procname == "" } {
	return $rclass
    } else {
	# ::puts -nonewline "$rclass::$procname $path $args -> "
	set result [eval [list $rclass::$procname $path] $args]
	# ::puts $result
	return $result
    }
}

#######################################################################
#### ::tycho::resource
#
# Access a resource by URL-style name. This procedure provides options
# that are similar to many of the options of the Tcl file{}
# command. (See also the procudre <a 
# href="Path.html"><code>:tycho::url</code></a>,
# which provides syntactic manipulation of URL-style names.) The options
# are:
# <dl>
# <dt><b>cachefile</d> _path_
# <dd> Return the name of a cached copy of the file. This is provided
# solely for the purpose of working around Tk's lack of support
# for binary data, and should not be used unless absolutely necessary.
# <i>Do not delete or write to the cache file.</i> An error will be
# triggered if this is not a streamable resource.
# <dt><b>contenttype</d> 
# <dd> Return the type of the content (binary, text/*, etc...)
# <dt><b>chmod</d> _path_ _mode_
# <dd> Change access permissions of a resource. The exact values of
# _mode_ has not yet been determined but is probably a subset of the
# first argument to the UNIX *chmod* command.
# <dt><b>delete</d> _path_
# <dd> Delete the given file. Will trigger an error if the file
# does not exist or is not writable.
# <dt><b>dirlist</d> _path_
# <dd> Get a listing of a directory. Sub-directories have names
# ending in a slash; files do not.
# <dt><b>executable</d> _path_
# <dd> Test if a resource is executable. <b>FIXME</b>: what does this
# mean for directories -- should be adopt the bogus UNIX convention?
# <dt><b>exists</d> _path_ ?_timeout_?
# <dd> Test if a resource exists. If the _timeout_ argument is given
# and the resource is remote, this procedure will return zero when that
# time expires.
# <dt><b>isdirectory</d> _path_
# <dd> Test if a resource exists and is a directory.
# <dt><b>isfile</d> _path_
# <dd> Test if a resource exists and is a file.
# <dt><b>isremote</d> _path_
# <dd> Test if a resource is remote. (It may not exist.)
# <dt><b>mtime</d> _path_
# <dd> Get the last-modified time of the resource (what format?)
# <dt><b>new</d> _path_ ?_option value_ ...?
# <dd> Create a new resource object, passing any options to its constructor.
# The connect{} method will be called on the new object.
# <dt><b>read</d> _path_ ?_timeout_?
# <dd> Return the contents of a resource. An error is signalled if the
# resurce is not streamable. An error is also signalled if the resource
# is remote and access times out on the optional _timeout_ argument.
# <dt><b>readable</d> _path_
# <dd> Test if a resource exists and is readable.
# <dt><b>size</d> _path_
# <dd> Get the size in bytes of a resource.
# <dt><b>source</d> _path_ ?_timeout_?
# <dd> Source the contents of a resource as a Tcl script. An error is
# signalled if the
# resource is not streamable. An error is also signalled if the resource
# is remote and access times out on the optional _timeout_ argument.
# <dd> Get the status of a resource into the array _varName_. The
# indexes of the array may vary, but <code>mtime</code> and <code>size</code>
# are guaranteed.
# <dt><b>writable</d> _path_
# <dd> Test if a resource exists and is writable.
# <dt><b>write</d> _path_ _data_ ?_timeout_?
# <dd> Write data to a resource. An error is signalled if the
# resurce is not streamable. An error is also signalled if the resource
# is remote and access times out on the optional _timeout_ argument.
# </dl>
#
ensemble ::tycho::resource {
    # Get a cached copy of the file
    option cachefile {path} {
	set resource [::tycho::resource new $path]
	if ![$resource streamable] {
	    error "$path is not streamable"
	}
	$resource open "r"
	set result [$resource cachefile]
	$resource close
	return $result
    }
    # Change access permissions of a resource
    option chmod {path mode} {
	::tycho::resourceClass $path chmod $mode
    }
    # Return "text*", "directory*", or others
    option contenttype {path} {
        ::tycho::resourceClass $path contenttype
    }
    # Delete a given resource
    option delete {path} {
	if ![::tycho::resource exists $path] {
	    error "File \"$path\" does not exist"
	}
	if ![::tycho::resource writable $path] {
	    error "File \"$path\" is not writable"
	}
	::tycho::resourceClass $path delete
    }
    # Get a directory listing in ls -F style format
    option dirlist {path} {
	::tycho::resourceClass $path dirlist
    }
    # Test if a resource path is executable
    option executable {path} {
	::tycho::resourceClass $path query executable
    }
    # Test if a resource exists.
    option exists {path} {
	::tycho::resourceClass $path exists
    }
    # Test if a resource path is a directory
    option isdirectory {path} {
	::tycho::resourceClass $path query isdirectory
    }
    # Test if a resource path is a file
    option isfile {path} {
	::tycho::resourceClass $path query isfile
    }
    # Get the last-modified time
    option mtime {path} {
	::tycho::resource stat $path results
	return $results(mtime)
    }
    # Get a new resource object
    option new {path args} {
	set obj [eval ::tycho::resourceClass [list $path] new $args]
	$obj connect $path
	return $obj
    }
    # Read from a resource.
    option read {path} {
	set resource [::tycho::resource new $path]
	if ![$resource streamable] {
	    error "$path is not streamable"
	}
	$resource open "r"
	set result [$resource read]
	$resource close
	delete object $resource
	return $result
    }
    # Test if a resource is readable
    option readable {path} {
	::tycho::resourceClass $path query readable
    }
    # Test if a resource is remote
    option isremote {path} {
	::tycho::resourceClass $path query isremote
    }
    # Get the size
    option size {path} {
	::tycho::resource stat $path results
	return $results(size)
    }
    # Source a streamable resource as Tcl
    option source {path} {
	set resource [::tycho::resource new $path]
	if ![$resource streamable] {
	    error "$path is not streamable"
	}
	$resource open "r"
	set result [$resource source]
	$resource close
	delete object $resource
	return $result
    }
    # Get resource status
    option stat {path varName} {
	upvar $varName v
	# Can't pass the proc name (stat) to resource class
	# because of the need to do an upvar. Do it the long way.
	set rclass [::tycho::resourceClass $path]
	$rclass::stat $path v
    }
    # Test if a resource is writable
    option writable {path} {
	::tycho::resourceClass $path query writable
    }
    # Write data to a resource file.
    option write {path data} {
	set resource [::tycho::resource new $path]
	if ![$resource streamable] {
	    error "$path is not streamable"
	}
	$resource open "w+"
	set result [$resource write $data]
	$resource close
	delete object $resource
	return $result
    }
}

#######################################################################
#### Resource
#
# The Resource class abstracts the notion of files and (to some extent)
# a file system. Subclass of this one provide the concrete
# implementation of accessing difference kinds of resource.
# There is both a procedural interface to resources (through
# <code>::tycho::resource</code> and a method interface to individual
# resource objects. These interfaces are approximately equivalent
# to the notions of file system and file respectively. All types
# of resource are accessed via a URL-ilke naming scheme, in which
# names begin with a protocol or resource type, such as <code>http</code>
# or <code>file</code>. (Names that do not being this way are
# taken to be local files.).
#
# There are two key qualities of different resource types. 
# The first is whether or not a resource is _streamable_. This
# is indicated by the streamable{} method of resource objects.
# Streamable resources are like files in that they can only
# be read or written sequentially. The second quality is whether
# the resource is _local_ or _remote_. This is indicated by the
# isremote{} option of <code>::tycho::resource</code> procedure.
# The key distinction between local and remote resources is that
# remote resource may a) take a long time to access and b) be
# unreliable. Remote resources support non-blocking access
# via the *-donecommand*, the *-statusbar* option (for
# progress updates), and the *-timeout* option.
#
# Streamable and non-streamable resource support two different
# read-write interfaces, and clients must be prepared to support
# both (or at least check that a resource is the type they are
# prepared to use.) The interface that must be supported by the
# two resource types is as follows. (Tcl doesn't support any
# better way of doing this; in a stricter language, these would be
# captured in an interface ar abstract subclasses.) Note that
# these methods are in addition to those defined in this superclass.
#
# <b>Streamable resource interface</b>
#
# <dl>
# <dt><b>flush</b>
# <dd> Flush any buffered output to the physical resource device.
# <dt><b>gets</b> ?_varName_?
# <dd> Read a line from the file. If the _varName_ argument is
# supplied and not null, write the line into that variable
# and return the number of characters read.
# <dt><b>puts</b> string
# <dd> Write the string to the file and append a newline character.
# <dt><b>read</b>
# <dd> Read the whole contents of the resource and return it.
# <dt><b>write</b> _string_
# <dd> Write the string to the file, without appending a newline character.
# </dl>
#
# <b>Non-streamable resource interface</b>
# (Still being worked out.)
# <dl>
# <dt><b>flush</b>
# <dd>
# <dt><b>load</b>
# <dd> 
# <dt><b>read</b>
# <dd> 
# <dt><b>save</b>
# <dd> 
# <dt><b>write</b>
# <dd> 
# </dl>
#
class ::tycho::Resource {
    inherit ::tycho::Object

    constructor {args} {}
    destructor {}

    #####################################################################
    ####                          options                            ####

    # A command to execute when the current access is done
    public variable donecommand {}
    
    # A statusbar to keep updated of progress (remote resources)
    public variable statusbar {}
    
    # Remote acess timeout, in ms. -1 means no timeout.
    public variable timeout -1
    
    ###################################################################
    ####                         public methods                    ####

    # Get a cached copy of the data
    method cachefile {}

    # Close the resource
    method close {}

    # Connect a resource to a path name
    method connect {path}

    # Test if the resource is currently open
    method isopen {}

    # Open the resource
    method open {{mode "r"}}

    # Get the current full resource path
    method path {}

    # Source the resource as Tcl
    method source {}

    # Return 1 if this resource is streamable
    method streamable {}

    ###################################################################
    ####                           public procs                    ####

    # Change the permissions of a resource
    proc chmod {path permissions}

    # Return the content type of the resource
    proc contenttype {path}

    # Delete a resource
    proc delete {path}

    # Get a directory listing
    proc dirlist {pattern}

    # Test if a given resource exists
    proc exists {path}

    # Test if a given resource is remote
    proc isremote {path}

    # Create a new directory
    proc mkdir {path}

    # Create a new resource object
    proc new {path args}

    # Query properties of a file
    proc query {path option}

    # Get the status of a resource
    proc stat {path varName}

    # Return the type of a given file
    proc type {path}

    ###################################################################
    ####                         protected methods                 ####

    ###################################################################
    ####                        protected variables                ####

    ###################################################################
    ####                         private variables               ####

    # The full name of the current file (or directory)
    private variable currentFile ""
}


#######################################################################
#### constructor
#
body ::tycho::Resource::constructor {args} {
    eval configure $args
}

#######################################################################
#### destructor
#
body ::tycho::Resource::destructor {} {
    # close
}

#########################################################################
#########################################################################
####                      public methods                             ####

#####################################################################
#### cachefile
# Get a cached copy of the contents of the file. This is provided
# to work around Tcl's lack of binary data handling.
body ::tycho::Resource::cachefile {} {
    return $currentFile
}

#####################################################################
#### close
body ::tycho::Resource::close {} {
    ::tycho::abstractMethod close
}

#####################################################################
#### connect
# Connect a resource to a name. Subclasses can override if they need
# to check for existence or similar things. Subclasses that override
# this method may also need to override path{}. This method is
# called by the <code>::tycho::resource new</code> procedure. It will
# trigger an error{} if this object cannot handle the new path.
#
body ::tycho::Resource::connect {path} {
    set p [path]
    if { $p != "" && \
	    [::tycho::url protocol $path] != [::tycho::url protocol $p] } {
	error "Cannot change path from $p to $path"
    }
    if [isopen] {
	close
    }
    set currentFile $path
}

#####################################################################
#### isopen
# Test if the resource is currently open.
#
body ::tycho::Resource::isopen {} {
    ::tycho::abstractMethod isopen
}

#####################################################################
#### open
# Open the resource to which this object is connected. The optional
# argument defaults to "r", which opens it for reading. The
# legal value of ths argument are the same as for the Tcl open{}
# command.
body ::tycho::Resource::open {{mode "r"}} {
    ::tycho::abstractMethod open
}

#####################################################################
#### path
# Return the path of this object. Subclasses that override
# connect{} may need to override this method too.
#
body ::tycho::Resource::path {} {
    return $currentFile
}

#####################################################################
#### source
# Source the resource as Tcl. It must be streamable and already
# open. Subclasses only need to override this method if they
# have a better way of doing it.
#
body ::tycho::Resource::source {} {
    if ![streamable] {
	error "File [path] is not streamable"
    }
    if ![isopen] {
    	error "File [path] is not open"
    }
    uplevel [read]
}

#####################################################################
#### streamable
# Return one if this object supports the streamable interface
# (as documented in the class comment), and zero otherwise.
#
body ::tycho::Resource::streamable {} {
    ::tycho::abstractMethod streamable
}

#########################################################################
#########################################################################
####                          public procedures                      ####

#####################################################################
#### chmod
body ::tycho::Resource::chmod {permissions path} {
    ::tycho::abstractProc mkdir
}

#####################################################################
#### contenttype
# Return the content type of the open file.
# In the very base class, look at the file extension only
#
body ::tycho::Resource::contenttype {path} {
    set ext [string tolower [file extension $path]]
    if       { $ext == ".html" || $string == ".htm" } {
        return "text/html"
    } elseif { $ext == ".gif" } {
        return "image/gif"
    } elseif { $ext == ".jpg" || $ext == ".jpeg" } {
        return "image/jpg"
    } else {
        return "text/plain"
    }
}

#####################################################################
#### delete
body ::tycho::Resource::delete {path} {
    ::tycho::abstractProc delete
}

#####################################################################
#### dirlist
# Return a list of files at the given _path_. The list should be
# in a format similar to <code>ls -F</code>, with directories
# having a "/" appended. The list should also be sorted. If the
# resource is remote, it should generate a Tcl error{} if the
# time given by *-timeout* expires. 
#
body ::tycho::Resource::dirlist {path} {
    ::tycho::abstractProc dirlist
}

#####################################################################
#### exists
# Test if a resource exists. If the resource is remote, it must
# return zero if the time given by the *-timeout* option is exceeded
# when first accessing the resource.
#
body ::tycho::Resource::exists {path} {
    ::tycho::abstractProc exists
}

#####################################################################
#### isremote
# Test is a path represents a remote resource. Most subclasses
# can define this proc to just return zero or one.
#
body ::tycho::Resource::isremote {path} {
    ::tycho::abstractProc isremote
}

#####################################################################
#### mkdir
body ::tycho::Resource::mkdir {path} {
    ::tycho::abstractProc mkdir
}

#####################################################################
#### new
# Create a new resource object, and connect{} it to the given
# path.
body ::tycho::Resource::new {path args} {
    ::tycho::abstractProc new
}

#####################################################################
#### query
body ::tycho::Resource::query {path option} {
    ::tycho::abstractProc query
}

#####################################################################
#### stat
body ::tycho::Resource::stat {path varName} {
    ::tycho::abstractProc stat
}
