# Dialog Box to build derived files in Tycho, such as HTML documentation
#
# @Author: Christopher Hylands
#
# @Version: $Id$
#
# @Copyright (c) 1997 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################


#######################################################################
#### BuilderDialog
# The BuilderDialog brings up a Dialog box that allows the user to
# build Tycho documentation files and indexes.
#
# FIXME: There should be a way to stop a build.
# FIXME: The way we view the doc files is ugly, perhaps we
# should use a widget with a menubar?

# <tcl><pre>
# set bd [::tycho::autoName .builderDialog]
# ::tycho::BuilderDialog $bd
# $bd centerOnScreen
# </pre></tcl>
#
# BuilderDialog is a graphical front end for the Builder class.
#
class ::tycho::BuilderDialog {
    inherit ::tycho::Query

    constructor {args} {}
    
    ###################################################################
    ####                         public methods                    ####

    # Run what ever Builder proc the user asked for.
    method _runBuilder {}

    # Bring up a dialog that allows the user to display Tycho documents
    method _showDocs  {}

    ###################################################################
    ####                         public procs                      ####

    # Check to see what files need to be built and bring up a dialog
    proc checkFiles {}}

#######################################################################
#### constructor
#
body ::tycho::BuilderDialog::constructor {args} {
    configure -text "Tycho Build Manager" \
            -title "Tycho Build Manager"
    radio builderchoice {Build Operation: } \
            { {Full (10 Minutes)} \
	    {Glimpse Only} \
	    {Itcl HTML Only (4 Minutes)} \
            {Indices Only} \
            {Class Diagram Only} \
        } {Full (10 Minutes)}

    addButton Build -text "Build" \
            -command "$this execCommand -okcommand"

    #addButton Stop -text "Stop" \
    #        -command "::tycho::silentError"

    addButton Stop -text "View Docs" \
            -command "$this _showDocs"


    addButton help -text "Help" \
            -command "::tycho::File::openContext \
            [file join \$TYCHO kernel doc usersGuides BuilderDialogHelp.html]"

    configure -okcommand "$this _runBuilder"
}

#######################################################################
#### _runBuilder
# Run what ever Builder proc the user asked for
#
body ::tycho::BuilderDialog::_runBuilder {} {
    switch [get builderchoice] {
	{Full (10 Minutes)} { ::tycho::Builder::build }
	{Glimpse Only} { ::tycho::Builder::glimpse }
	{Itcl HTML Only (4 Minutes)} { ::tycho::Builder::itcldocs }
	{Indices Only} { ::tycho::Builder::indices }
	{Class Diagram Only} { ::tycho::Builder::classDiagram }
        default {error "Internal Error: ::tycho::BuilderDialog::_runBuilder \
                got `[get builderchoice]', don't know what to do."}
    }
}


#######################################################################
#### _showDocs
# Bring up a dialog that allows the user to display a document.
#
body ::tycho::BuilderDialog::_showDocs {} {
    set choice [::tycho::query {Open a document} {
        {radio docChoice {Document to open} { \
                {Tycho Concept Index} \
                {Tycho Code Index} \
                {Tycho Class Diagram}} \
            {Tycho Class Diagram}}}]

    switch [lindex $choice 1] {
	{Tycho Concept Index} {
	    ::tycho::File::openContext [file join \$TYCHO lib idx tycho.idx]
        }
        {Tycho Code Index} {
	    ::tycho::File::openContext [file join \$TYCHO lib idx codeDoc.idx]
	}
	{Tycho Class Diagram} {
	    ::tycho::File::openContext \
		    [file join \$TYCHO doc tychoClasses.dag]

        }
    }
}


    ###################################################################
    ###################################################################
    ####                      public procs                       ####

#######################################################################
#### checkFiles
# Check to see what files need to be built and bring up a dialog asking
# the user if they want to build.
# 
body ::tycho::BuilderDialog::checkFiles {} {
    global TYCHO
    set origmsg "The following Tycho files do not exist:" 
    set msg $origmsg
    if ![file exists [file join $TYCHO kernel doc codeDoc File.html]] {
	set msg "$msg\n The Tycho Itcl HTML documentation might not be \
		built, for example the file:\n\
		[file join $TYCHO kernel doc codeDoc File.html] \
		does not exist."
    } 
    if ![file exists [file join $TYCHO doc tychoClasses.dag]] {
	set msg "$msg\n The Tycho Class Diagram at \
		[file join $TYCHO doc tychoClasses.dag]"
    } 
    if ![file exists [file join $TYCHO lib idx tycho.idx]] {
	set msg "$msg\n The Tycho Concept Index at \
		[file join $TYCHO lib idx tycho.idx]"
    }
    if ![file exists [file join $TYCHO lib idx codeDoc.idx]] {
	set msg "$msg\n The Tycho Code Index at \
		[file join $TYCHO lib idx codeDoc.idx]"
    } 

    if {$msg != $origmsg} {
	# Something is missing, so bring up the dialog
	set msg "$msg\n\nBring up Build Dialog to build these files?"

	if [::tycho::askuser $msg] {
	    set bd [::tycho::autoName .builderDialog]
	    ::tycho::BuilderDialog $bd
	    $bd centerOnScreen
	    # Return the name of the window so the test suite can get at it.
	    return $bd
	}
    }
}

