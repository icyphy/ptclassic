# Definition of a popup menu widget
# 
# @Author: John Reekie
#
# @Version: $Id$
# 
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### PopupMenu
# This class defines a popup menu widget. The method overrides
# those in the MenuSupport class so that, if no menu name is
# specified, "top" is used.
#
class ::tycho::PopupMenu {
    inherit ::tycho::MenuSupport

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                            options                        ####

    # The command to call before posting the menu
    itk_option define -command command Command {}

    ###################################################################
    ####                         public methods                    ####

    # Add a menu entry to a menu
    method add {name inMenu args}

    # Add a separator to a menu
    method addSeparator {args}

    # Clear a menu
    method clear {{menu {}}}

    # Get an option of a menu
    method menucget {name option}

    # Set options of a menu
    method menuconfigure {name args}

    # "Post" the popup
    method post {x y}
}


#######################################################################
#### constructor
#
body ::tycho::PopupMenu::constructor {args} {
    itk_component add top {
	menu $itk_interior.top -tearoff 0
    } {
	keep -font -activebackground -cursor -activeforeground \
		-background -foreground
    }

    # Record its "path"
    set menupath(top) $itk_component(top)

    eval itk_initialize $args
    eval preference exclude $this $args
}

#######################################################################
#### destructor
#
body ::tycho::PopupMenu::destructor {} {
}

###################################################################
###################################################################
####                      public methods                       ####


#####################################################################
#### add
#
body ::tycho::PopupMenu::add {name args} {
    if { ! [string match {-*} [lindex $args 0]] } {
	set inMenu [lindex $args 0]
	set args [lrange $args 1 end]
    } else {
	set inMenu "top"
    }
    eval ::tycho::MenuSupport::add [list $name] [list $inMenu] $args
}


#####################################################################
#### addSeparator
#
body ::tycho::PopupMenu::addSeparator {args} {
    if { ! [string match {-*} [lindex $args 0]] } {
	set inMenu [lindex $args 0]
	set args [lrange $args 1 end]
    } else {
	set inMenu "top"
    }
    eval ::tycho::MenuSupport::addSeparator [list $inMenu] $args
}


#####################################################################
#### clear
#
body ::tycho::PopupMenu::clear {{menu {}}} {
    if { $menu == "" } {
	set menu "top"
    }
    ::tycho::MenuSupport::clear $menu
}


#####################################################################
#### menucget
# Get a configuration option of a menu.
#
body ::tycho::PopupMenu::menucget {name {option {}}} {
    if { [string match {-*} $name] } {
	set option $name
	set name "top"
    }
    ::tycho::MenuSupport::menucget $name $option
}

#####################################################################
#### menuconfigure
# Get or configure options of a menu.
#
body ::tycho::PopupMenu::menuconfigure {args} {
    if { ! [string match {-*} [lindex $args 0]] } {
	set name [lindex $args 0]
	set args [lrange $args 1 end]
    } else {
	set name "top"
    }
    eval ::tycho::MenuSupport::menuconfigure [list $name] $args
}

#####################################################################
#### post
# Post the popup.
#
body ::tycho::PopupMenu::post {x y} {
    if { $itk_option(-command) != "" } {
	uplevel #0 $itk_option(-command)
    }
    tk_popup $itk_component(top) $x $y
}
