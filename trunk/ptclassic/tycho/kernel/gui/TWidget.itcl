# Base class for Tycho widgets.
# 
# @Author: Edward A. Lee
#
# @Version: $Id$
# 
# @Copyright (c) 1995-%Q% The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

# NOTE:  It would be nice to be able to name this "Widget".
# In theory, this should be possible since it is in a different namespace
# from ::itk::Widget.  In practice, however, very strange behavior
# (with core dumps) results if that name is used.

#######################################################################
#### TWidget
# This class is the base class for most widgets in Tycho. It provides a
# set of useful methods.
#
class ::tycho::TWidget {
    inherit ::itk::Widget

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                         public methods                    ####

    # Change the cursor to the "watch" and execute the arguments.
    method busy {args}

    # Configure options of this widget
    method configure {args}

    # Invoke a command on the context-sensitive popup menu.
    method contextmenu {cmd args}

    # Take the focus.
    method focusin {}

    # Open a file and go to its internal point identified by name.
    method hyperJump {file {point {}}}

    # Raise the top-level window.
    method raiseWindow {}

    # Find and make visible a point in the display.
    method seeFragment {point}

    # Access stylesheets of this widget
    method stylesheet {mode args}

    # Return the name of my window.
    method windowName {} {return $itk_component(hull)}

    ###################################################################
    ####                         public procs                      ####

    # If the named object exists, evaluate the arguments.
    proc evalIfExists {command args} {}

    # Get the current contents of the Tk clipboard.
    proc getClipboard {} {}

    # Set the current contents of the local and Tk clipboards.
    proc setClipboard {text} {}

    ###################################################################
    ####                         protected methods                 ####

    # Configure the popup menu depending on the current context
    protected method configureContextMenu {}

    # Remember explicitly-set options and then call the built-in
    # itk_initialize
    protected method itk_initialize {args}

    # Prevent destruction of this object while evaluating the arguments.
    protected method safeEval {args}

    # Register code to be evaluated when the widget has been constructed.
    protected method whenConstructed {command}

    # Register a command to execute when a component is mapped
    protected method whenMapped {component command}

    ###################################################################
    ####                         protected variables               ####

    # A local Tycho clipboard that is mirrored in the Tk clipboard.
    # protected common clipboard {}

    # Name of my context-sensitive popup menu
    protected variable myContextmenu {}

    # The prefix is just a shorthand for $itk_component(hull).
    protected variable prefix

    ###################################################################
    ####                         private methods                   ####

    # Execute code waiting for the status bar to be mapped
    private method _notifymapped {component}

    ###################################################################
    ####                         private variables                 ####

    # If non-zero, disable destruction.
    private variable disableDestruction 0

    # A flag saying whether this widget is already constructed
    private variable _isConstructed 0

    # An array of options subscribed by "stylesheet subscribe"
    private variable _subscribedOptions

    # An array of subscribed style-sheets
    private variable _styleSheets

    # An array of options overridden by itk_initialize or configure
    private variable _overriddenOptions

    # Commands to execute when the widget has been constructed
    private variable _whenConstructed {}

    # A array of lists of commands to execute when a component
    # is mapped. The array is indexed by the component name
    private variable _whenMapped
}

#######################################################################
#### constructor
#
body ::tycho::TWidget::constructor {args} {
    set prefix $itk_component(hull)

    eval itk_initialize $args

    # This is not a good idea because some widgets don't want the focus:
    # ::bind $itk_component(hull) <Enter> "$this focusin"

    # There are a few bindings that we want to work everywhere
    bind $itk_component(hull) <<Open>> {::tycho::File::openWin %W; break}
}

#######################################################################
#### destructor
# Destruction will succeed only if we are not within a <code>safeEval</code>
# call.  If we are, then destruction is canceled with a silent error, and
# scheduled to be attempted again after 1 second.
#
body ::tycho::TWidget::destructor {} {
    if $disableDestruction {
        # Schedule a future attempt at destruction.
        after 1000 "$this prevWindow; delete object $this"
        ::tycho::silentError
    }
    # Delete the context menu if there is one
    if { $myContextmenu != "" } {
	delete object $myContextmenu
    }
    # Delete myself from stylesheets
    stylesheet remove

    # Unsubscribe from all preferences
    # preference unsubscribeoptions $this
}


    ###################################################################
    ###################################################################
    ####                      public methods                       ####

#####################################################################
#### busy
# Change the cursor in the interior of this widget to the "watch"
# cursor (to indicate that the object is busy), execute the arguments
# and then change the cursor back to whatever it was before. In order
# for the cursor to actually change, the method calls "update", during
# which time the object may be deleted. If the object is deleted, this
# method never returns (it unwinds the stack by triggering a silent
# error). A typical invocation is as follows:
# <pre>
#     busy $this methodname arg1 arg2
# </pre>
# Notice that, unlike <code>safeEval</code>, this method does not prevent
# destruction of the object.  Instead, if the object is destroyed during
# the update, the method never returns.
#
body ::tycho::TWidget::busy {args} {
    set oldCursor [lindex [config -cursor] end]
    config -cursor {watch}
    ::tycho::safeUpdate $prefix
    # If we get here, then the object $this still exists.
    # Catch errors so that we can reset the cursor.
    if {[catch {eval $args} msg] != 0} {
        config -cursor $oldCursor
        global ::errorInfo
        set saveInfo $errorInfo
	error $msg $saveInfo
    }
    config -cursor $oldCursor
    return $msg
}

#####################################################################
#### configure
# Configure a widget. This method overrides the default in itk::Archetype.
# Any given options are unsubscribed from any style sheets that they
# are subscribed to. This ensures that the widget keeps the value
# set by this method -- that is, setting options with this method
# "overrides" the preferences.
#
# This is actually rather slow, since every time a megawidget
# creates a component that is a megawidget, this method
# will get called from within the keep{} and rename{}
# statements. To speed it up, we should keep a record of which
# options have been subscribed and which haven't...
#
# If this method is modified, be sure to also update the
# *itk_initialize{} method, and the same methods in ::tycho::TopLevel.
#
body ::tycho::TWidget::configure {args} {
    # As long as there are two or more args...
    if { [llength $args] >= 2 } {
        # If any options are subscribed, unsubscribe them
        foreach {option _} $args {
            if [::info exists _subscribedOptions($option)] {
                ::tycho::stylesheet unsubscribe $this \
                        $_subscribedOptions($option) $option
                unset _subscribedOptions($option)
            }
        }
        # Remember the over-ridden options
        array set _overriddenOptions $args
    }
    # Configure the options
    eval ::itk::Archetype::configure $args
}

#########################################################################
#### contextmenu
# If there is a context-sensitive popup menu associated with this widget,
# use the arguments as a command to issue to the PopupMenu widget,
# Menubar, and return whatever is returned by the command.  Otherwise,
# return an empty string.
#
# The command "create" is treated as a special case:
# the context menu is created. The second argument, if present.
# is the widget to add the binding for posting the menu. When the
# menu is posted, the method configureContextMenu{} is called.
#
body ::tycho::TWidget::contextmenu {cmd args} {
    if { $cmd == "create" } {
	set myContextmenu [::tycho::PopupMenu .#auto \
		-command "@scope [info class] \
		    [list $this configureContextMenu]"]
	if { $args != "" } {
	    bind [lindex $args 0] <Button-3> \
		    "$this contextmenu post %X %Y"
	}
    } elseif {$myContextmenu != {}} {
        return [eval $myContextmenu $cmd $args]
    } else {
        return {}
    }
}

#########################################################################
#### focusin
# Grab the focus and change the Displayer menu, if necessary. This
# should be redefined in derived classes to give the focus to the
# appropriate subwindow.
#
body ::tycho::TWidget::focusin {} {
    focus $itk_component(hull)
}

#####################################################################
#### hyperJump
# Open a file and go to its internal point identified by name. If the
# filename is the empty string, just call <code>seeFragment</code>,
# passing it the second argument. Otherwise, the
# file is opened  with <code>::tycho::File::openContext</code>,
# which will choose an editor based on the filename extension.
# Invoke <code>seeFragment</code> to view the specified point within the
# file. The format for the <i>point</i> argument depends on the type of
# file being opened. For HTML files, it will normally be the name of an
# anchor in the file. For text files, it will normally be either "{line
# <i>linenumber</i>}" or "{range <i>start</i> <i>end</i>}", where
# <i>start</i> and <i>end</i> have the form
# <i>linenumber.characternumber</i>. The seeFragment{} method of the
# target widget is responsible for making sure that the <i>point</i>
# is displayed properly, using the whenMapped{} method if necessary to
# defer the jump until after the widget is mapped to the screen
# (see Edit.itcl for an example).
# 
body ::tycho::TWidget::hyperJump {file {point {}}} {
    if {$file != {}} {
        set obj [uplevel #0 [list ::tycho::File::openContext $file]]
        if {$point != {}} {
            # The seeFragment{} method is called directly here,
            # on the assumption that $obj works properly.
            $obj seeFragment $point
        }
    } {
        seeFragment $point
    }
}

#####################################################################
#### raiseWindow
# Bring the corresponding top-level window to the foreground, and
# make sure it is deiconified.
#
body ::tycho::TWidget::raiseWindow {} {
    set tl [winfo toplevel $itk_component(hull)]
    wm deiconify $tl
    raise $tl
    focusin
}

#####################################################################
#### seeFragment
# In derived classes, this method will go to the point in the display
# identified by the name argument.  What this means depends on what
# type of data being displayed.  In this base class, do nothing.
#
body ::tycho::TWidget::seeFragment {point} {
}

#####################################################################
#### stylesheet
# Access stylesheets of this widget. This method should be used
# to set widget option preferences instead of direct access using the
# *::tycho::stylesheet* procedure, since it ensures that user-set
# options are not overridden by style-sheets. The method has several
# forms, depending on the value of the _mode_ argument:
#
# <dl>
# <dt><b>get</b> _stylesheet_ _parameter_
# <dd>
# Get the value of the given _parameter_ of _stylesheet_,
# where _parameter_ is parameter name with a style of the
# style-sheet. The style user is the correct style for this
# particular widget, based upon user's style prefeernces.
# Color and font values are converted to raw X values
# before returning.
#
# <dt><b>list</b>
# <dd>
# Return a list of subscribed stylesheets.
#
# <dt><b>remove</b>
# <dd>
# Remove myself from all stylesheets. To be called prior
# to destruction.
# 
# <dt><b>subscribe</b> ?_option_ _stylesheet_ _parameter_ ...?
# <dd>
# Subscribe each names _option_ to the given _stylesheet_
# _parameter_. Each _option_ that has not been explicitly
# overridden by supplying a value to the widget's constructor
# will be set to the current value of _parameter_, and any
# future changes to _parameter_ will update the option.
#
# </dl>
#
body ::tycho::TWidget::stylesheet {mode args} {
    switch -exact $mode {
        "get" {
	    set sheet [lindex $args 0]
	    set param [lindex $args 1]
		    
            # Get my style
            set style [::tycho::userprofile getstyle $sheet $this]

            # Get the value
            ::tycho::stylesheet get $sheet $style $param
        }
        "list" {
	    array names _styleSheets
        }
        "remove" {
	    foreach sheet [array names _styleSheets] {
		::tycho::stylesheet style $sheet $this "delete"
	    }
        }
	"subscribe" {
	    # Pre-digest arguments
	    foreach {option sheet param} $args {
		if ![::info exists _overriddenOptions($option)] {
		    # Make lists of options per sheet
		    lappend lists($sheet) $option $param

		    # If an option is already subscribed, then
		    # remove that subscription so the new one will work
		    if [::info exists _subscribedOptions($option)] {
			::tycho::stylesheet unsubscribe $this \
				$_subscribedOptions($option) $option
		    }

		    # Remember the subscribed options
		    set _subscribedOptions($option) $sheet
		}
	    }
	    # Call the stylesheets to do the work
	    foreach {sheet opts} [array get lists] {
		# Tell the style manager what style I prefer
		if ![::info exists _styleSheets($sheet)] {
		    set style [::tycho::userprofile getstyle $sheet $this]
		    ::tycho::stylesheet style $sheet $this $style
		    set _styleSheets($sheet) $sheet
		}
		# Subscribe to it
		eval ::tycho::stylesheet subscribe $this $sheet $opts
	    }
	}
    }
}

#####################################################################
#####################################################################
####                   public procedures                         ####


#####################################################################
#### evalIfExists
# If the command exists, evaluate the arguments.
# This is useful for evaluating something in the background where you are
# not sure that the widget will still exist when Tcl gets around to doing
# the evaluation.  It should be used, for example, when invoking an object
# method using "after".  For example, within an object, you can say:
# <pre>
#    after idle [code evalIfExists $this methodname]
# </pre>
#
body ::tycho::TWidget::evalIfExists {command args} {
    if {[info which -command $command] != {}} {
        eval $command $args
    }
}

#####################################################################
#### getClipboard
# Get the current contents of the Tk clipboard.
#
body ::tycho::TWidget::getClipboard {} {
    # return $clipboard
    if ![catch {selection get -selection CLIPBOARD} cb] {
        return $cb
    } else {
        return ""
    }
}

#####################################################################
#### setClipboard
# Set the current contents of the local and Tk clipboards.
#
body ::tycho::TWidget::setClipboard {text} {
    # set clipboard $text
    clipboard clear
    clipboard append -- $text
}

#####################################################################
#####################################################################
####                   protected methods                         ####

#####################################################################
#### configureContextMenu
# Configure the popup menu according to context. By default, this
# menu simply contains an entry that offers information about
# the widget.
#
# Subclasses that create a context menu (by calling "contextmenu create"
# on themselves) should override this method.
#
body ::tycho::TWidget::configureContextMenu {} {
    # Clear the menu
    contextmenu clear

    contextmenu command usersguide \
            -label "Guide to the [info class] widget" \
	    -command "$this help"

    # Entries for developer information
    # FIXME: could be much more useful
    contextmenu command widgetname \
            -label "Name of this [info class] widget..." \
            -command "::tycho::post $this"
}

#####################################################################
#### itk_initialize
# Initialize options of a widget. This method overrides the
# default itk_initialize{} provided in ::itk::Archetype to
# remember the options that have been explicitly set by the
# user. This is used by the preferences manager so that these
# options "override" any option subscriptions.
#
# If this method is modified, be sure to also update the
# *configure{} method, and the same methods in ::tycho::TopLevel.
#
body ::tycho::TWidget::itk_initialize {args} {
    # If any options are subscribed, unsubscribe them
    foreach {option _} $args {
	if [::info exists _subscribedOptions($option)] {
	    ::tycho::stylesheet unsubscribe $this \
		    $_subscribedOptions($option) $option
	    unset _subscribedOptions($option)
	}
    }
    # Remember the over-ridden options
    array set _overriddenOptions $args

    # Configure the options
    uplevel ::itk::Archetype::itk_initialize $args

    # If called from the most specific class, execute the
    # post-construction commands.
    if { [uplevel info context] == [info class] } {
        foreach {scope cmd} $_whenConstructed {
            @scope $scope $cmd
        }
	set _whenConstructed {}
	set _isConstructed 1
    }
}

#####################################################################
#### safeEval
# Prevent destruction of this object while evaluating the arguments.
# Return the result of evaluating the arguments.
# Destruction will be cancelled by invoking a silent error, and will
# be scheduled for another attempt in the future.
# <p>
# This method is a workaround for a defect in Itcl. If "update",
# "update idletasks", or "tkwait" is called directly or indirectly from
# within an Itcl method, and during the update the Itcl object is
# destroyed, then when update returns, you may get a catastrophic failure
# of the application (a core dump).
#
body ::tycho::TWidget::safeEval {args} {
    set disableDestruction 1
    set retval {}
    if [catch {set retval [eval $args]} msg] {
        global ::errorInfo
        set saveInfo $errorInfo
        set disableDestruction 0
        error $saveInfo
    }
    set disableDestruction 0
    return $retval
}

########################################################################
#### whenConstructed
#
# Register a command to be executed when the last constructor in the
# chain of constructor executes. The command will be executed
# in the scope of the class from which this method was called,
# at the completion of the call to itk_initialize. The purpose of
# this method is to delay evaluation of code until construction
# of the widget is complete.
#
# If the widget is already constructed, the command will be
# executed immediately in the caller's context -- this method
# can therefore be called from methods that will only sometimes
# be executed within an object's constructor.
#
body ::tycho::TWidget::whenConstructed {command} {
    set w [string trimleft $this :]
    if $_isConstructed {
        uplevel $command
    } else {
        lappend _whenConstructed [uplevel info context] $command
    }
}

#######################################################################
#### whenMapped
#
# Register a command to be executed when a component of the mega-widget
# is mapped. Any number of commands can be registered for each component.
# If the component is already mapped, the command will be executed
# immediately in the caller's scope; if not, a binding will be made
# to the component for the &lt;Map&gt; event and the pending commands
# executed within the caller's scope when the binding is triggered.
# The purpose of this method is to support code that depends on the
# screen appearance or size of the component.
#
# Note that it is necessary to specify the deferred commands
# on a component basis (rather than just for the whole mega-widget),
# since Tk maps containing widgets before contained widgets.
#
body ::tycho::TWidget::whenMapped {component command} {
    set w [component $component]
    if [winfo ismapped $w] {
        uplevel $command
    } else {
        lappend _whenMapped($component) [uplevel info context] $command

        # Make the binding to the component
        bind $w <Map> "@scope [info context] $this _notifymapped $component"
    }
}

#######################################################################
#### _notifymapped
#
# This method is a callback to be executed when the <Map>
# event is triggered on a component. If the component is not
# already mapped, return without doing anything. Otherwise,
# evaluate the commands registered with the *whenMapped* method
# and remove the binding that triggered this callback.
#
body ::tycho::TWidget::_notifymapped {component} {
    set w [component $component]

    # Just check this to make sure...
    if ![winfo ismapped $w] {
        # Not mapped: return
        return
    } elseif [::info exists _whenMapped($component)] {
        # Mapped: evaluate commands and then remove the binding
        foreach {scope cmd} $_whenMapped($component) {
            @scope $scope $cmd
        }
        unset _whenMapped($component)
        if { [::info which -command $w] != "" } {
            bind $w <Map> {}
        }
    }
}
