# Test suite for ButtonBox
#
# @Author: John Reekie
#
# @Version: $Id$
#
# @Copyright (c) 1997 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
#######################################################################

if {[string compare test [info procs test]] == 1} then { 
    source [file join $TYCHO kernel test testDefs.tcl]
} {}

#set VERBOSE 1

############################################################################
#### Construction tests
#
test ButtonBox-1.1 {Simplest construction test} {
   catch {delete object .bb}
   ::tycho::ButtonBox .bb
} {.bb}

test ButtonBox-1.2 {Test construction with sttausbar} {
   catch {delete object .bb}
   catch {delete object .sb}
   ::tycho::StatusBar .sb
   ::tycho::ButtonBox .bb -statusbar .sb
} {.bb}

# FIXME: add more tests with different options

############################################################################
#### Basic button operations
#
catch {delete object .bb}
::tycho::ButtonBox .bb
pack .bb
wm deiconify .
raise .

update
test ButtonBox-2.1 {Add a button} {
    .bb add foo -text Foo -command {string range {Some text} 0 end}
    .bb add bar -text Bar
    .bb exists foo
} {1}

update
test ButtonBox-2.2 {Invoke a button} {
    .bb invoke foo
} {Some text}

update
test ButtonBox-2.3 {Delete a button} {
    .bb delete foo
    .bb exists foo
} {0}

############################################################################
#### Basic button configuration
#
catch {delete object .bb}
::tycho::ButtonBox .bb
.bb add foo -text Foo -command {string range {Some text} 0 end}
.bb add bar -text Bar
pack .bb
wm deiconify .
raise .

update
test ButtonBox-3.1 {Read default configuration options} {
    set result {}
    foreach o {row column description borderwidth \
	    command text relief state} {
	lappend result [.bb buttoncget bar -$o]
    }
    set result
} {0 1 {} 2 {} Bar raised normal}

update
test ButtonBox-3.2 {Change Tk button options} {
    set result {}
    foreach {o v} {borderwidth 4 command {puts Bar!} text Bar! \
	    relief sunken state disabled} {
	.bb buttonconfigure bar -$o $v
	lappend result [.bb buttoncget bar -$o]
    }
    set result
} {4 {puts Bar!} Bar! sunken disabled}

update
test ButtonBox-3.3 {Test button disable} {
    .bb disable foo
    .bb buttoncget foo -state
} {disabled}

update
test ButtonBox-3.4 {Test button enable} {
    .bb enable foo
    .bb buttoncget foo -state
} {normal}


############################################################################
#### Test the -description option and statusbar interface
#
catch {delete object .bb}
catch {delete object .sb}
::tycho::StatusBar .sb
::tycho::ButtonBox .bb -statusbar .sb
.bb add foo -text Foo -description {The Foo button}
.bb add bar -text Bar
pack .bb
pack .sb -fill x -expand yes
wm deiconify .
raise .

update
test ButtonBox-4.1 {Confirm configuration initialization} {
    .bb buttoncget foo -description
} {The Foo button}

update
test ButtonBox-4.2 {Configure description string and confirm} {
    .bb buttonconfigure bar -description {The Bar button}
    .bb buttoncget bar -description
} {The Bar button}

update
test ButtonBox-4.3 {Enter button and check status bar} {
    event generate [.bb component buttonfoo] <Enter>
    .sb get
} {The Foo button}

update
test ButtonBox-4.4 {Leave the button} {
    event generate [.bb component buttonfoo] <Leave>
    .sb get
} {}

############################################################################
#### Test button placement and movement
#
catch {delete object .bb}
::tycho::ButtonBox .bb
pack .bb
wm deiconify .
raise .

update
test ButtonBox-5.1 {Check horizontal layout} {
    .bb add foo -text Foo
    .bb add bar -text Bar
    .bb add baz -text Baz
    list \
	    [.bb buttoncget foo -row] [.bb buttoncget foo -column] \
	    [.bb buttoncget bar -row] [.bb buttoncget bar -column] \
	    [.bb buttoncget baz -row] [.bb buttoncget baz -column]
} {0 0 0 1 0 2}

update
test ButtonBox-5.2 {Check vertical layout} {
    .bb delete foo
    .bb delete bar
    .bb delete baz
    .bb reset
    .bb configure -columns 1
    .bb add foo -text Foo
    .bb add bar -text Bar
    .bb add baz -text Baz
    list \
	    [.bb buttoncget foo -row] [.bb buttoncget foo -column] \
	    [.bb buttoncget bar -row] [.bb buttoncget bar -column] \
	    [.bb buttoncget baz -row] [.bb buttoncget baz -column]
} {0 0 1 0 2 0}

update
test ButtonBox-5.3 {Move a button} {
    .bb buttonconfigure baz -row 0 -column 1
    array set temp [grid info [.bb component framebaz]]
    list $temp(-row) $temp(-column)
} {0 1}

############################################################################
#### Test the default ring
#
catch {delete object .bb}
::tycho::ButtonBox .bb
.bb add foo -text Foo
.bb add bar -text Bar
pack .bb
wm deiconify .
raise .

update
test ButtonBox-6.1 {Initially no button has the default ring} {
    list \
	    [[.bb component framefoo] cget -relief] \
	    [[.bb component framebar] cget -relief]
} {flat flat}

update
test ButtonBox-6.2 {Make a button the default} {
    .bb default foo
    list \
	    [[.bb component framefoo] cget -relief] \
	    [[.bb component framebar] cget -relief]
} {sunken flat}

update
test ButtonBox-6.3 {Make some other button the default} {
    .bb default bar
    list \
	    [[.bb component framefoo] cget -relief] \
	    [[.bb component framebar] cget -relief]
} {flat sunken}

############################################################################
####
# Delete the last button box and iconify main window
update
after 200
catch {delete object .bb}
catch {delete object .sb}
wm iconify .
