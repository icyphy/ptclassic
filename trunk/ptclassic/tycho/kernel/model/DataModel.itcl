##########################################################################
# @Version: $Id$
# @Author: John Reekie
#
# @Copyright (c) 1996-%Q% The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY
##########################################################################


##########################################################################
#### DataModel
#
# DataModel extends the Model class to support data models and the
# TIM interchange format.  TIM is a simple meta-data format that
# encourages a simple and clean representation of data, both in
# in-memory objects and in an external file representation. See 
# <a href="../../../doc/tim.html">the TIM documentation</a> for more 
# details.
#
# The data stored in a DataModel object is hierarchical. Each item is
# referenced by its name, which consists of a list of name segments.
# A name segment is the item type and name, separated by a colon. For
# example, a top-level item may have the name <code>group:foo</code>,
# where _group_ is a type defined for this particular model. An item
# contained within that item may have the name
# <code>group:foo icon:bar</code>, where _icon_ is another type defined
# for this model. Item names are unique within each type -- for example,
# I could also have an item named <code>icon:foo</code>.
#
# The methods of _DataModel_ fall into several categories:
# <dl>
# <dt>Initialization and setup
# <dd>
# _DataModel_ is designed to be subclassed to create application-specific
# models. The constructor of each subclass must call the <b>type define</b>
# method to define the types in that model. Clients and subclasses
# can also use options such as <b>type cget</b> and <b>type configure</b>
# to access types.
#
# <dt>Loading and saving
# <dd>
# The data in a model can be loaded into and out of a readable string
# format. The <b>parse</b> method loads data into the model, while the
# <b>describe</b> method produces a description of its data.
#
# <dt>Data modification
# <dd>
# There are several methods that modify the data in the model:
# <b>add</b>, <b>assign</b>, <b>clear</b>, <b>delete</b>, and
# <b>itemconfigure</b>`. All of these, with the exception of
# <b>clear</b>, return an undo command, so they can be recorded
# and/or published with methods in the _Model_ superclass.
#
# <dt>Data query
# <dd>
# There are several methods that provide ways of accessing the
# data in a model. <b>itemcget</b> gets data from a single item,
# while <b>names</b> and <b>get</b> perform glob-style matching
# on item names. The <b>listing</b> method returns an ordered
# list of child items within a given context, while <b>sort</b>
# allows that ordering to be changed. <b>search</b> provides an
# efficient searching mechanism over model data, and <b>exists</b>
# provides a simple test whether data is present.
#
# </dl>
#
class ::tycho::DataModel {
    inherit ::tycho::Model

    constructor {args} {}
    destructor {}

    #################################################################
    ####                        options                          ####

    #################################################################
    ####                     public methods                      ####

    # Add a new item
    method add {name args}

    # Assign an attribute list to an item
    method assign {name value}

    # Remove all data
    method clear {}

    # Delete an item
    method delete {name}

    # Describe the data as a string in TIM format
    method describe {{name {}} {prefix {}}}

    # Check if an item exists
    method exists {name}

    # Get the names and attributes of each item matching a pattern
    method get {{pattern *}}

    # Get an attribute or attributes of an item
    method itemcget {name {attribute {}}}

    # Configure attributes of an item
    method itemconfigure {name args}

    # Get a list children of a given type
    method listing {type {context {}}}

    # Get the names of each item matching a pattern
    method names {{pattern *}}

    # Load data from a TIM string
    method parse {data}
    
    # Search for items with regexp or glob-syle matching
    method search {mode args}

    # Sort items in a given context
    method sort {type {context {}} args}

    # Define or configure item types
    method type {mode {type {}} args}

    #################################################################
    ####                   protected methods                     ####

    # Return a description of a single item
    protected method _describeitem {name {prefix {}}}
    
    # Create a header string
    protected method _header {}

    # Update the cache if it's out of date
    protected method _update

    # Do a cache update
    protected method _updatecache

    # Manipulate the data cache
    protected method cache {mode args}

    #################################################################
    ####                  protected variables                    ####

    # The path of the currently-parsed model
    protected common _object_ {}

    # The name of the currently-parsed model
    protected common _current_ {}

    # The context currently being parsed in
    protected common _context_ {}

    #################################################################
    ####                    private methods                      ####

    # Generate a parsing procedure
    private method _mkparseproc {type}

    # Verify that an item exists
    private method _verify {name}

    #################################################################
    ####               private common variables                  ####

    private common defaulttypeconfig {
        :leafonly 0
        :override 0
    }

    # The safe interpreter provided for safe parsing
    private common safeinterp [uplevel #0 interp create]

    # The types defined in each scopem indexed by scope
    private common _types

    # The types configuration of each type, indexed by $scope:$type
    private common _typeconfig

    #################################################################
    ####                  private variables                    ####

    # The scope in which we store data
    private variable scope

    # Default attributes, indexed by type
    private variable _defaults

    # Local copy of the :leafonly flag, indexed by type
    private variable _leafonly
 
    # Local copy of the :override flag, indexed by type
    private variable _override
 
    # Search data
    private variable _searchdata
 
    # Search mode
    private variable _searchtype
 
    # Search expression
    private variable _searchexp
 
    # Debugging only
    method debug {args} {
        eval $args
    }
}

########################################################################
#### constructor
#
body ::tycho::DataModel::constructor {args} {

    # Set the scope variable
    set scope [info class]

    # Make sure the namespaces exist in the safe interpreter
    $safeinterp eval namespace ::tycho
    $safeinterp eval namespace $scope

    # Evaluate options
    eval configure $args

    # Initialize the model attributes
    @scope $scope set [set modelname]() {}
}

########################################################################
#### destructor
#
body ::tycho::DataModel::destructor {} {
    # Clear data so as not to leak...
    clear
}

########################################################################
#### add
#
# Add a new item. _name_ is its full name including types. Following
# arguments are attribute-value pairs. If the item already exists,
# raise an error. Return the undo command.
#
body ::tycho::DataModel::add {name args} {
    if [@scope $scope ::info exists [set modelname]($name)] {
        error "An item \"$name\" already exists in $modelname"
    }

    # Invalidate cache
    @scope $scope set [set modelname].cachevalid 0

    # Extract type and tail
    regexp {(.+):(.+)} [lindex $name end] _ type tail
    set context [lreplace $name end end]

    # Extract contents
    if { !$_leafonly($type) && ([llength $args] & 1) } {
    	set contents [lindex $args end]
    	set args [lreplace $args end end]
    } else {
    	set contents {}
    }

    # Store the data of this item
    @scope $scope set [set modelname]($name) $args

    # Add it to the list of children of its parent
    @scope $scope lappend [set modelname]:[set type]($context) $tail

    # Parse contents
    if { $contents != "" } {
        set _context_ $name
        $safeinterp eval [list @scope $scope $contents]
    }
    # Return undo command
    list delete $name
}

########################################################################
#### assign
#
# Set the value of the item named _name_. The special item name
# <b>{}</b> denotes the model itself. The value must be a list of
# attribute name-value pairs. If the item does not exist, raise an
# error. Return the undo command.
#
body ::tycho::DataModel::assign {name value} {
    _verify $name

    # Invalidate cache
    @scope $scope set [set modelname].cachevalid 0

    # Remember the old data
    set data [@scope $scope set [set modelname]($name)]

    # Write the new data
    @scope $scope set [set modelname]($name) $args

    # Return undo command
    list assign $name $data
}

########################################################################
#### clear
#
# Clear all data from the model and invalidate the cache. The top-level
# model attributes will not be deleted. This method is not undoable
# because of the potential size of the undo command. (Clients can still
# generate an undo command by calling <b>describe</b> and
# <b>history replace</b> prior to calling this method.
#
body ::tycho::DataModel::clear {} {
    # Invalidate the cache
    cache invalidate

    # Remember the top-level attributes
    set attributes [@scope $scope set [set modelname]()]

    # Unset the data of all items and the children of each type
    catch {
        @scope $scope unset $modelname
        @scope $scope unset $modelname.cachevalid
        foreach type $_types($scope) {
            @scope $scope unset $modelname:$type
        }
    }
    # Restore the top-level attributes
    @scope $scope set [set modelname]() $attributes

    # Return null since this is not undoable.
    return ""
}

########################################################################
#### delete
#
# Delete the item specified by _name_. Raise an error if
# the item does not exist.  The item and all child items will
# be deleted. Return the undo command.
#
body ::tycho::DataModel::delete {name} {
    _verify $name
    regexp {(.+):(.+)} [lindex $name end] _ type tail
    set context [lreplace $name end end]

    # Invalidate cache
    @scope $scope set [set modelname].cachevalid 0

    # Get a description for the undo command
    # FIXME: this is horrendously inefficient. We need to
    # merge the generation of the undo command with the
    # deletion of children.
    set desc [_describeitem $name]

    # Remove from the parent list
    @scope $scope set [set modelname]:[set type]($context) \
            [::tycho::ldelete \
            [@scope $scope set [set modelname]:[set type]($context)] $tail]

    # Delete attributes
    @scope $scope unset [set modelname]($name)

    # Delete children
    # FIXME: Generate undo command here.
    if !$_leafonly($type) {
        foreach t $_types($scope) {
            foreach child [listing $t $name] {
                delete [concat $name $t:$child]
            }
        }
    }

    # Return the undo command
    list parse $desc
}

########################################################################
#### describe
#
# Generate a TIM description. If no name is supplied, generate a
# description of all the data. The description starts with an emacs-style
# header string that contains the mode, class, and version attributes,
# and the current date. If the _name_ is supplied, generate a description
# of just that item. If the _prefix_ argument is also
# supplied, prepend the prefix to each line produced.
#
body ::tycho::DataModel::describe {{name {}} {prefix {}}} {
    if { $name!= "" } {
        _verify $name
        _describeitem $name $prefix
	
    } else {
        set string "# -*- [_header] -*-\n"
        foreach type $_types($scope) {
            # Get the root items of this type
            set items [listing $type]

            # Append a description of each to the result string
            foreach n $items {
                append string [_describeitem $type:$n $prefix]
            }
        }
        return $string
    }
}

########################################################################
#### exists
#
# Test if the item given by _name_ is present in the model, returning
# 1 if it does and 0 if not.
#
body ::tycho::DataModel::exists {name} {
    @scope $scope ::info exists [set modelname]($name)
}

########################################################################
#### get
#
# Return the names and data of items with names that match _pattern_.
# If _pattern_ is not supplied, get the names and data of all items.
# Note that this method matches item names against a pattern, so
# that a pattern such as <code>item:foo.*</code> will return all
# descendents, not just children, of <code>item:foo</code>. The returned
# list will not be ordered.
#
body ::tycho::DataModel::get {{pattern *}} {
    @scope $scope array get $modelname $pattern
}

########################################################################
#### itemcget
#
# Get an attribute or attributes of the item specified by
# _name_. The special item name <b>{}</b> denotes top-level attributes
# of the model. If the _attribute_ argument is supplied, return the
# value of that attribute. Otherwise return a list containing
# all attributes and their values. Raise an error if the item
# does not exist or if the attribute is unknown.
#
body ::tycho::DataModel::itemcget {name {attribute {}}} {
    _verify $name

    # If an item name is supplied, merge with defaults
    if { $name != "" } {
        regexp {(.+):} [lindex $name end] _ type
        array set data $_defaults($type)
    }
    array set data [@scope $scope set [set modelname]($name)]

    if { $attribute == "" } {
        # Get the list of attributes
	array get data
    } else {
        # Get a single attribute
        if { ! [::info exists data($attribute)] } {
            error "Unknown attribute $attribute in $modelname"
        }
        return $data($attribute)
    }
}

########################################################################
#### itemconfigure
#
# Configure attributes of the item specified by _name_. The special
# item name <b>{}</b> denotes top-level attributes of the model. Following
# arguments are an attribute-value list. The new attributes are
# merged with the existing attributes. Raise an error if the item
# does not exist. Return the undo command.
#
body ::tycho::DataModel::itemconfigure {name args} {
    _verify $name

    # Invalidate cache
    @scope $scope set [set modelname].cachevalid 0

    # Get the old data
    set data [@scope $scope set [set modelname]($name)]

    # Merge the new data
    array set attrs $data
    array set attrs $args
    @scope $scope set [set modelname]($name) [array get attrs]

    # Return the undo command
    list assign $name $data
}

########################################################################
#### listing
#
# Return a list of child items of the given _type_. If the
# _context_ is given, return children of that item; otherwise
# return top-level items. The returned names are the names
# within the given context only. For example, if the model
# contains items with full names *item:foo* and *item:bar*,
# then the call
# <pre>
#     $model listing item
# </pre>
# will return <code>{foo bar}</code>. (In comparison, the names{}
# method will return <code>{item:foo item:bar}</code>.) Children
# are returned in the same order in which they were added to
# the model -- the order of children of a given type can be altered
# with the <b>sort</b> method.
#
body ::tycho::DataModel::listing {type {context {}}} {
    if [@scope $scope ::info exists [set modelname]:[set type]($context)] {
        @scope $scope set [set modelname]:[set type]($context)
    } else {
        return {}
    }
}

########################################################################
#### names
#
# Return item names that match _pattern_. If _pattern_ is not supplied
# get the names of all items in the model. Note that this method matches
# item names against a pattern, so that a pattern such as
# <code>item:foo.*</code> will return all descendants, not just children
# of <code>item:foo</code>. Use the listing{} method to get just
# children. The returned list will not be ordered.
#
body ::tycho::DataModel::names {{pattern *}} {
    @scope $scope array names $modelname $pattern
}

########################################################################
#### parse
#
# Read data from a string containing a TIM representation of a
# model. Each non-comment line must start with the name of a type
# defined for this model. The item name follows, then attribute-value
# pairs, and then nested items within curly braces. If any item
# already exists, then an error is raised. _This method cannot
# be recorded._
#
body ::tycho::DataModel::parse {data} {
    global _current_ _context_ _object_
    set _current_ $modelname
    set _context_ {}
    set _object_ $this
    
    $safeinterp eval [list @scope $scope $data]

    # Can't record this method.
    return ""
}

########################################################################
#### search
#
# Search the model for data. This method has several forms, depending
# on the _mode_ argument:
#
# <dl>
# <dt><b>done</b> _index_
# <dd>
# Terminate a search. _index_ must be a value returned by a previous
# call to <b>search glob</b> or <b>search regexp</b>.
#
# <dt><b>glob</b> _attribute_ _glob_ ?_pattern_?
# <dd>
# Start a glob-style expression search and return an index to be
# used in future calls to the <b>next</b> and <b>done</b> options.
# The glob-style pattern _glob_ will be matched against
# the values of each attribute named _attribute_. _attribute_
# is itself a glob-style pattern. The glob-style patterns
# are as for the Tcl <b>string match</b> command. If _pattern_
# is supplied, it is a glob-style pattern that is matched against
# the item names -- this can be used to limit the items through
# which the search is performed.
#
# <dt><b>next</b> _index_
# <dd>
# Return the name of the next item that matches the search 
# criteria given by _index_. _index_ must be a value returned
# by a previous call to <b>search glob</b> or <b>search regexp</b>.
# If no item is found, return null. Note that the search it somewhat
# optimistic: it is possible for an item to be returned that does
# not in fact match the search criteria. Clients should therefore
# verify that a proper match occurred by performing a <b>regexp</b>
# or <b>string match</b> on the attributes of found items.
#
# <dt><b>regexp</b> _attribute_ _regexp_ ?_pattern_?
# <dd>
# Start a regular expression search and return an index to be
# used in future calls to the <b>next</b> and <b>done</b> options.
# The regular expression _regexp_ will be matched against
# the values of each attribute named _attribute_. _attribute_
# is itself a regular expression. The regular expressions are as
# for the Tcl <b>regexp</b> command, but cannot contain an initial
# <b>^</b> or final <b>$</b> character. If _pattern_ is supplied,
# it is a glob-style pattern that is matched against the item
# names -- this can be used to limit the items through which the
# search is performed.
#
# </dl>
#
body ::tycho::DataModel::search {mode args} {
    switch -exact $mode {
        "done" {
            if { [llength $args] != 1 } {
		error "wrong # args: should be \"search done index\""
            }
            set index [lindex $args 0]
            if ![::info exists _searchdata($index)] {
                error "invalid search index \"$index\""
            }
           unset _searchdata($index)
           unset _searchtype($index)
            unset _searchexp($index)
        }
        "glob" -
        "regexp" {
	    if { [llength $args] < 2 || [llength $args] > 3 } {
		error "wrong # args: should be \"search $mode \
                        attribute $mode ?pattern?\""
	    }
            set index [::tycho::autoName search]
            set attribute [lindex $args 0]
            set expression [lindex $args 1]
            if { $mode == "glob" } {
                set exp "*$attribute $expression*"
            } else {
                set exp "$attribute $expression"
            }

            set _searchtype($index) $mode
            set _searchexp($index) $exp
            if { [llength $args] == 2 } {
                set _searchdata($index) [get]
            } else {
                set _searchdata($index) [get $pattern]
            }
            return $index
	}
       "next" {
	    if { [llength $args] != 1 } {
		error "wrong # args: should be \"search next index\""
	    }
            set index [lindex $args 0]
            if ![::info exists _searchdata($index)] {
                error "invalid search index \"$index\""
            }
            set data $_searchdata($index)
            set t [lsearch -$_searchtype($index) \
                    $data \
                    $_searchexp($index)]

            if { $t == -1 } {
                # Nothing found
                set item ""
                set data ""
            } elseif { !($t & 1) } {
                # Oops, matched against item name...
                set item [lindex $data $t]
                set data [lreplace $data 0 [expr $t+1]]
            } else {
                # Matched against data
                set item [lindex $data [expr $t-1]]
                set data [lreplace $data 0 $t]
            }
            set _searchdata($index) $data
            return $item
        }
        default {
            error "Invalid option \"$mode\": must be done, \
                    glob, regexp, or next"
        }
    }
}

########################################################################
#### sort
#
# Sort the items in a given context. The items within _context_ are
# sorted by name using the Tcl <b>lsort</b> procedure, with the
# optional arguments passed to <b>lsort</b>.
#
body ::tycho::DataModel::sort {type {context {}} args} {
    set children [@scope $scope set [set modelname]:[set type]($context)]
    set children [eval lsort $args [list $children]]
    @scope $scope set [set modelname]:[set type]($context) $children
}

########################################################################
#### type
#
# Manipulate item types. This command has several forms, depending
# on the _mode_ argument:
#
# <dl>
# <dt><b>cget</b> _type_ ?_attribute_?
# <dd>
# Get the value of the _attribute_ attribute of _type_. If
# _attribute_ is not supplied, get the default attributes of
# this type.
#
# <dt><b>configure</b> _type_ ?_attribute_ _value_ ...?
# <dd>
# Configure attributes of _type_.
# 
# <dt><b>defaults</b>
# <dd>
# Return a list of attributes and their default values for this type.
#
# <dt><b>define</b> _type_ ?_attribute_ _value_... ?
# <dd>
# Define a new type. The new type will be defined only the first
# time this option is called for a given class. The attributes and
# values set default attributes of the type.
#
# <dt><b>names</b>
# <dd>
# Return a list of types defined for this class.
#
# </dl>
#
# The following special attributes specify information about the
# type rather than default attributes.  These special attributes
# being with a leading colon rather than a dash, and apply across
# all models of a given class:
#
# <dl>
# <dt><b>:leafonly</b> _bool_
# <dd>
# If true, items of this type most be a leaf item -- that is,
# they cannot contain nested items. This should be set in any
# situation where a type can only be a leaf, since parsing is more
# efficient. The default is false.
#
# <dt><b>:override</b> _bool_
# <dd>
# If true, items of this type will be parsed by calling a method
# with the same name as _type_ on the target object. This can be
# used if models need to do additional processing when items are
# parsed.
#
# </dl>
#
# For example, the call
# <pre>
#     $datamodel type define foo :leafonly 1 -bar hello
# </pre>
# will create the type <b>foo</b> and declare it to only be a leaf item.
# In addition, any item of type <b>foo</b> will have the default
# value "hello" of the option <b>-bar</b>.
#
body ::tycho::DataModel::type {mode {type {}} args} {
    switch -exact $mode {
	"cget" {
            array set attrs _typeconfig($type)
            array set attrs _defaults($type)
	    if { $args == "" } {
		array get attrs
	    } else {
		return $attrs([lindex $args 0])
	    }
	}
	"configure" {
	    array set opts $args
            if { [array names opts :*] != "" } {
                error "Reconfiguring type-control attribute not yet supported"
	    }
	    array set attrs $_defaults($type)
	    array set attrs $args
	    set _defaults($type) [array get attrs]
	}
	"define" {
	    # Regenerate the parsing proc if this is true
	    set regenerate 0

	    if { ![::info exists _typeconfig($scope:$type)] } {
		# This is the first time this type has been defined in this
		# scope. Get default type options.
		array set opts $defaulttypeconfig
		array set opts $args

		# Remember the type and the type configuration
		lappend _types($scope) $type
		set _typeconfig($scope:$type) [array get opts :*]
		set regenerate 1
	    } else {
		# This type has previously been defined. Get default
		# type options and default attributes from before.
		array set opts $_typeconfig($scope:$type)
		array set opts $args

		# If the type configuration differs, update it
		set config [array get opts :*]
		if { [lsort $_typeconfig($scope:$type)] != [lsort $config] } {
		    set _typeconfig($scope:$type) $config
		    set regenerate 1
		}
	    }
            # Remember or update the attribute defaults for this object
            if [::info exists _defaults($type)] {
                array set t $_defaults($type)
            }
            array set t $args
            set _defaults($type) [array get t -*]

            # Local copies of configuration
            foreach {cfg val} $_typeconfig($scope:$type) {
                set _[string trimleft $cfg :]($type) $val
            }

	    # Regenerate the parsing procedure if configuration changed
	    if $regenerate {
		# Create and define the parsing proc
                @scope $scope eval [_mkparseproc $type]

		# Link the proc into the safe interpreter
		$safeinterp alias $scope::$type $scope::__parse_$type
	    }
	}
	"defaults" {
	    return $_defaults($type)
        }
	"names" {
	    return $_types($scope)
	}
	default {
	    error "unknown option: must be one of \
		    \"cget, configure, define, or names\""
        }
    }
}


#####################################################################
#####################################################################
####                     protected methods                       ####

########################################################################
#### _describeitem
#
# Return a string describing the item given by _name_
# in the TIM format. This method performs no error-checking, so
# the item must exist or an obscure Tcl error will be raised.
# The _prefix_, if supplied, is prepended to each line.
# Subclasses can override this method if they don't like the
# way that items are printed.
#
body ::tycho::DataModel::_describeitem {name {prefix {}}} {
    regexp {(.+):(.+)} [lindex $name end] _ type tail
    set context [lreplace $name end end]

    # Start string with type followed by local name
    set string $prefix$type
    lappend string $tail

    # Get default attributes
    array set dflt $_defaults($type)

    # Print attributes that differ from the default (if any)
    foreach {attr val} [@scope $scope set [set modelname]($name)] {
        if { ! [::info exists dflt($attr)] || $dflt($attr) != $val } {
            lappend string $attr $val
            # Add line wrapping here?
        }
    }

    # Print children if I have any
    if !$_leafonly($type) {
        set haschildren 0
        foreach t $_types($scope) {
            if [@scope $scope ::info exists [set modelname]:[set t]($name)] {
                set haschildren 1
                break
            }
        }

        # If I have children, print enclosing braces and child items
        if $haschildren {
            set childprefix $prefix
            append childprefix "    "
            append string " \{\n"
            foreach t $_types($scope) {
                foreach child [listing $t $name] {
                    append string \
			    [_describeitem \
			    [concat $name $t:$child] \
			    $childprefix]
                }
            }
            append string "$prefix\}"
        }
    }
    return $string\n
}

########################################################################
#### _header
#
# Return a header string for this model. The returned string contains
# only the field name-value pairs, so that subclasses can chain
# and add additional fields. This method
# returns a string containing the current values of the mode, version,
# and class attributes, and the current time as the mtime field.
#
body ::tycho::DataModel::_header {} {
    set header ""
    # append header "mode: [modelcget -mode]; "
    # append header "class: [modelcget -class]; "
    # append header "version: [modelcget -version] "
    append header "mtime: [clock format [clock seconds]] "
    return $header
}

########################################################################
#### _update
#
# Build an updated version of the cache if it's out of date.
# This is short-cut version of <b>cache update</b>.
#
body ::tycho::DataModel::_update {} {
    if ![@scope $scope set [set modelname].cachevalid] {
        _updatecache
    }
}

########################################################################
#### _updatecache
#
# Build an updated version of the cache. This method should
# be overridden by subclasses that maintain internal data derived
# from the loaded data. The overriding method should update its
# cached data, then chain to this method.
#
body ::tycho::DataModel::_updatecache {} {
    @scope $scope set [set modelname].cachevalid 1
}

########################################################################
#### cache
#
# Manipulate the cache state flag. Models often create internal
# data derived from the data loaded into them, and this method
# is provided as a means for method to keep this derived information
# up-to-date.
# 
# <dl>
# <dt><b>invalidate</b>
# <dd>
# Declare that the cache data is not valid. This option is called
# any time data is added to or deleted from the model.
#
# <dt><b>update</b>
# <dd>
# Update the cache data. If the cache is not up to date, the
# protected method </b>updatecache</b> will be called. Subclasses
# that rely on cached data should call this (or the <b>_update</b>
# short-cut) before executing code that relies on cached data.
#
# <dt><b>valid</b>
# <dd>
# If the cache data is valid, return true, otherwise false.
#
# </dl>
#
body ::tycho::DataModel::cache {mode args} {
    switch -exact $mode {
        "invalidate" {
	    if { $args != "" } {
		error "wrong # args: should be \"cache invalidate\""
	    }
	    @scope $scope set [set modelname].cachevalid 0
        }
        "update" {
 	    if { $args != "" } {
		error "wrong # args: should be \"cache update\""
	    }
            _update
        }
        "valid" {
	    if { $args != "" } {
		error "wrong # args: should be \"cache valid\""
	    }
	    @scope $scope set [set modelname].cachevalid
	}
	default {
	    error "bad option \"$mode\": should be invalidate, \
		    update, or valid"
	}
    }
}

#####################################################################
#####################################################################
####                       private methods                       ####

########################################################################
#### _mkparseproc
#
# Create a proc to parse an item
#
body ::tycho::DataModel::_mkparseproc {type} {
    # Proc header
    set proc "proc $scope::__parse_$type "
    if $_override($type) {
        append proc "{args}"
    } else {
        append proc "{tail args}"
    }
    append proc " \{\n"

    # Proc body
    append proc "    set type $type\n"


    # If the override option is set, call a method on the
    # object instead of generating code to load its data.
    if $_override($type) {
        append proc {
            global _object_
            global $_object_
            eval $_object_ $type $args
        }
    } else {
        # Declare various data global
        append proc {
            global _current_ _context_
            global $_current_
            global $_current_:$type
            global $_current_.cachevalid
        }
        # Generate the full name of the item
        append proc {
            set name [concat $_context_ $type:$tail]
        }
        # Check whether the item already exists
        append proc {
            if [::info exists [set _current_]($name)] {
                error "An item \"$name\" already exists in $_current_"
            }
        }
        # Extract contents if the item is not always a leaf
        if !$_leafonly($type) {
            append proc {
                if { [llength $args] & 1 } {
                    set contents [lindex $args end]
                    set args [lreplace $args end end]
                } else {
                    set contents {}
                }
            }
        }
        # Set the data of this item
        append proc {
            set [set _current_]($name) $args
	}
        # Add the item tail to the list of children of its parent
        append proc {
            lappend [set _current_]:[set type]($_context_) $tail
        }
        # Clear the cache valid flag
        append proc {
            set $_current_.cachevalid 0
        }
        # Parse contents if the item is not a leaf. Use the safe
	# interpreter to ensure safety.
        if !$_leafonly($type) {
            append proc {if { $contents != "" }}
            set t {
                set saved $_context_
                set _context_ [concat $_context_ $type:$name]
            }
            append t "$safeinterp eval @scope $scope \$contents\n"
            append t {
                set _context_ $saved
            }
            append proc " \{$t\}"
        }
    }
    # Complete the proc and return it
    append proc "\}"
}

########################################################################
#### _verify
#
# Verify that the item given by _name_ exists, raising an
# error if it does not.
#
body ::tycho::DataModel::_verify {name} {
    if ![@scope $scope ::info exists [set modelname]($name)] {
    	error "Unknown item \"$name\" in $modelname"
    }
}
