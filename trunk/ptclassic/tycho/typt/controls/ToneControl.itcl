# A control sub-panel with three tone control sliders.
#
# @Author: John Reekie and Sunil Bhave
#
# @Version: $Id$
#
# @Copyright (c) 1996-1997 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################


#######################################################################
#### ToneControl
# A small panel that contains three sliders for low, mid, and high
# frequency bands. The names of the stars controlled can be set by the
# *-stars* option -- this must be set before calling the control
# panel add{} method. The default is *{left right}*.
#
class ::tycho::ToneControl {
    inherit ::tycho::ControlSubpanel

    constructor {args} {}
    destructor {}
    
    ###################################################################
    ####                            options                       ####

    # The stars to control
    itk_option define -stars stars Stars {left right}

    ###################################################################
    ####                         public methods                    ####

    # Request the widget to connect its controls
    method requestConnect {}
}

#######################################################################
#### constructor
#
body ::tycho::ToneControl::constructor {args} {
    # Create a frame so we can have nice reliefs
    itk_component add frame {
        frame $itk_interior.frame \
                -borderwidth 2 \
                -relief sunken
    }
    pack $itk_component(frame)
    
    # Create the bank of sliders for low, mid, and high gain
    foreach {tag label} {low Low mid Mid high High} {
    	itk_component add $tag {
                ::tycho::Slider $itk_interior.$tag \
                -label $label \
                -from -10 -to 10 \
                -resolution 1 \
                -digits 0
        } {
                keep -cursor -background
    	}
        $itk_component($tag) set 0
        pack $itk_component($tag) -side left -in $itk_component(frame)
    }

    # Initialise options.
    eval itk_initialize $args
}

######################################################################
#### requestConnect
#
# Connect all controls to the control panel.
#
body ::tycho::ToneControl::requestConnect {} {
    foreach star $itk_option(-stars) {
        foreach band {low mid high} {
            connect $band $star[::tycho::capitalize $band].gain 5.0 50
        }
    }
}
