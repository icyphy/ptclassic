# A viewer for the idocumentation.
#
# @Author: Cliff Cordeiro
#
# @Version: $Id$
#
# @Copyright (c) 1995-1996 The Regents of the University of California.
# All rights reserved.
#
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the above
# copyright notice and the following two paragraphs appear in all copies
# of this software.
#
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
#                                        PT_COPYRIGHT_VERSION_2
#                                        COPYRIGHTENDKEY
#######################################################################

#######################################################################
#### Idoc
# This class defines a combination of an HTML widget and a text widget
# which will eventually be used for more efficient class browsing.
#  
# Data is stored in an array of lists. The array is indexed by type
# (Protected Variable, Private Method, etc.) and the lists are arranged in
# name/content pairs (link {click here} altlink {shift-click here} etc {and
# more}). 
#  
# Eventually I would like to somehow add the methods of parent classes into
# the text widget and ultimately I think a better design would inherit from
# the HTML widget and add on to it. In a perfect world, this functionality 
# could be implemented with frames. (Except for the shift-click, which is  
# not well defined anyway.)
#  
# John suggested something about right clicking on the methods to allow
# switching to the idoc for the parent class or something. I think a link to
# the parent class(es) would be useful, but I would like to first document  
# all the methods that are available to a user and to a maintainer of this  
# particular class, which includes public and protected methods from the   
# parent classes which are not redefined.
#
# Here's an example of how to use this class:
#
# <tcl><pre>
# catch {destroy .u}
# ::tycho::Displayer .u
# ::tycho::Idoc .u.i
# .u showView .u.i
# .u centerOnScreen
# .u.i configure -file  $TYCHO/lib/tydoc/test/Idoc.idoc
# .u.i reload
# </tcl></pre>
#
class ::tycho::Idoc {
    inherit ::tycho::File

    ##########################################################################
    ####                            options                               ####

    # The color of Itcl options.
    itk_option define -optioncolor optionColor OptionColor white {
    }
  
    # The color of public constructs.
    itk_option define -publiccolor publicColor PublicColor white {
    }
    
    # The color of protected constructs.
    itk_option define -protectedcolor protectedColor ProtectedColor white {
    }
    
    # The color of private constructs.
    itk_option define -privatecolor privateColor PrivateColor white {
    }

    # The default head font -- currently an X font, but should
    # be made symbolic in Tk 8.0
    itk_option define -headfont headFont Font [::tycho::font \
            {New Century Schoolbook} 18 Roman]

    # The default body font -- currently an X font, but should
    # be made symbolic in Tk 8.0
    itk_option define -bodyfont bodyFont Font [::tycho::font Hevetica 12 Bold]

    constructor {args} {}
    destructor {}

    ###################################################################
    ####                      public methods                       ####
    
    # Insert the specified data.
    public method insertData {data}
    
    # Insert HTML into myHtml widget
    public method putHtml {data}

    ###################################################################
    ####                      protected variables                  ####
  
    # Current document description
    protected variable myIdoc
 
    # Current class name and html description
    protected variable myClass
   
    # Internal contents widget
    protected variable myContents

    # Internal HTML widget
    protected variable myHtml

    ###################################################################
    ####                      private methods                      ####

    # Add a record to the array of lists which describes the current idoc
    private method add {args}

    # Set the name of the current class.
    private method setClass {args}
    
    # Display the contents of the myIdoc variable in text widget.
    private method display {}
    
    # Verify that the argument is a properly formatted list.
    private method verifycontents {contents}

}
#########################################################################
#### -headfont configuration
# Parse the given X font to figure out what font family, size,
# and style to use for the display font
#
configbody ::tycho::Idoc::headfont {

    set f $itk_option(-headfont)
    foreach panel [$myContents panels] {
        $myContents subconfig $panel -font $f
    }
    # must be caught because class may not exist
    catch {$myContents subconfig class -font $f}
}
#########################################################################
#### -bodyfont configuration
# Parse the given X font to figure out what font family, size,
# and style to use for the display font
#
configbody ::tycho::Idoc::bodyfont {

    set f $itk_option(-bodyfont)
    foreach label [$myContents labels] {
        if {$label != "class"} {    
            $myContents subconfig $label -font $f
        }
    }
}
###################################################################
#
body ::tycho::Idoc::constructor {args} {

    # Setup a frame for the HTML and contents widgets
    itk_component add mainFrame {
        frame $itk_interior.mainFrame
    } 
 
    itk_component add html {
        ::tycho::HTML $itk_interior.html
    } {
        rename -textwidth -htmlwidth htmlWidth HtmlWidth
        rename -textheight -height height Height
    }

    set myHtml $itk_component(html)
  
    itk_component add contents {
        ::tycho::MultiPanel $itk_interior.contents
    } {
       keep -height
       rename -width -contentswidth contentsWidth ContentsWidth
    }

    set myContents $itk_component(contents)

    pack $itk_component(contents) -side left -anchor e  -fill y \
            -in $itk_interior.mainFrame

    pack $itk_component(html) -side left -expand 1 -fill both \
            -anchor n -in $itk_interior.mainFrame
    
    pack $itk_component(mainFrame) -side top -expand 1 -fill both

    # Insert items in the menubar, if there is one.
    if {$myMenubar != {}} {
        $myMenubar disable {Undo/Redo}
        $myMenubar disable {Cut}
        $myMenubar disable {Paste}
        $myMenubar disable "Font..."
        $myMenubar delete "Fill Region"
        $myMenubar delete "Right Fill Column"
        $myMenubar delete "Set Fill Prefix"
        $myMenubar delete "Lower Case"
        $myMenubar delete "Upper Case"
        $myMenubar delete "Capitalize"
        $myMenubar delete "Insert..."
        $myMenubar delete "Save"
        $myMenubar delete "SaveAs..."
        $myMenubar delete "Evaluate"
        $myMenubar delete "Revision Control..."
    }

    eval itk_initialize $args

    # Subscribe to the preference set
    preference subscribeoptions $this \
            -headfont displayFont \
            -bodyfont entryFont

    # Something annoying inherited insists on creating this thing
    pack forget $itk_interior.childsite
    }
###########################################################################
#### add
# add a record to the array of lists which describes the current
# idoc. myIdoc is arranged by type and contains a list of 
# name contents pairs
body ::tycho::Idoc::add {type name contents} {
    # if it doesn't exist, lappend will still work
    catch {set tmp [set myIdoc($type)]}
    lappend tmp "$name $contents"
    set myIdoc($type) $tmp   
}

###########################################################################
#### setClass
# set the (single) name and HTML description of the current
# class. Subsequent calls overwrite $myClass
body ::tycho::Idoc::setClass {name html} {
    set myClass(name) $name
    set myClass(html) $html
}

###########################################################################
#### display
# display the contents of the myidoc variable in text widget
body ::tycho::Idoc::display {} {
    
    # clear the contents widget
    $myContents clear

    # insert the class name, if one exists
    if {![catch {set myClass(name)}]} {
        $myContents appendlabel class \
                -text $myClass(name) \
                -font [$this cget -headfont]

        $myContents bind class <Double-Button-1> \
                [code "$this putHtml {$myClass(html)}"]
    }

    # put in each type (Protected Method, etc.) by array order
    foreach type [array names myIdoc] {

        set subnames {}

        foreach item [set myIdoc($type)] {
            set name [lindex $item 0]
            set contents [lrange $item 1 end]
            
            # keep track of names in a list for subpanel
            lappend subnames $name
            
            # insert displayed text
            $myContents appendlabel $name \
                    -text $name \
                    -font [$this cget -bodyfont]
            switch -glob $type {
                Protected* {$myContents subconfig $name -fg \
                        [$this cget -protectedcolor]}
                Private* {$myContents subconfig $name -fg \
                        [$this cget -privatecolor]}
                Public* {$myContents subconfig $name -fg \
                        [$this cget -publiccolor]}
                *Option {$myContents subconfig $name -fg \
                        [$this cget -optioncolor]}
            } 

            # bind <enter> text
            foreach {attr value} $contents {
                if {$attr == "link"} {
                    # When the pointer is over the tag
                    $myContents bind $name <Enter> \
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <KeyRelease-Shift_L> \        
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <KeyRelease-Shift_R> \
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <Leave> \
                            [code "$this putStatus {}"]
                }

                if {$attr == "altlink"} {
                    # Pointer is over and shift is pressed
                    $myContents bind $name <Shift-Enter> \
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <KeyPress-Shift_L> \
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <KeyPress-Shift_R> \
                            [code "$this putStatus {$value}"]
                    $myContents bind $name <Leave> \
                            [code "$this putStatus {}"]
                }
                
                if {$attr == "html"} {
                    # HTML to display after double click
                    $myContents bind $name <Double-Button-1> \
                            [code "$this putHtml {$value}"]
                }
            }
        }
        $myContents append $type "$subnames" \
                -text $type \
                -font [$this cget -headfont] 
    }

}

#######################################################################
#### verifycontents
# Verify that the argument is a properly formatted list.
#
body ::tycho::Idoc::verifycontents {contents} {
    if {$contents != {}} {
        if [catch {array set foo $contents}] {
            error "Contents are not formatted as a \
                    {keyword item keyword item ...} list: $contents"
        }
    }
}

#######################################################################
#### insertData
# Append the specified data to the existing display.
# The data should be a list of items of the form <i>methodname args</i>,
# where <i>methodname</i> is a method of this class (i.e. any class
# derived from <code>Idoc</code>), and <i>args</i> is the
# set of arguments appropriate for that method.
#
body ::tycho::Idoc::insertData {data} {
    if [getReadOnly] {bell; return}
    catch {unset myIdoc}
    foreach command $data {
        if [catch {
            eval $this $command
        } msg] {
            global ::errorInfo
            set saveErr $errorInfo
            error "Invalid item: $data\n$msg" $saveErr
        }
    }
    display
    putHtml $myClass(html)
}

#########################################################################
#### putHtml
# Load a portion of HTML into the $myHtml widget
body ::tycho::Idoc::putHtml {data} {
    $myHtml clear
    $myHtml insertData $data
}
